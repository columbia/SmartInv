{{
  "language": "Solidity",
  "sources": {
    "contracts/PrimitiveManager.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity 0.8.6;\r\n\r\nimport \"@primitivefi/rmm-core/contracts/interfaces/engine/IPrimitiveEngineView.sol\";\r\nimport \"./interfaces/IPrimitiveManager.sol\";\r\nimport \"./base/Multicall.sol\";\r\nimport \"./base/CashManager.sol\";\r\nimport \"./base/SelfPermit.sol\";\r\nimport \"./base/PositionManager.sol\";\r\nimport \"./base/SwapManager.sol\";\r\nimport \"./libraries/TransferHelper.sol\";\r\n\r\n/// @title   PrimitiveManager contract\r\n/// @author  Primitive\r\n/// @notice  Interacts with Primitive Engine contracts\r\ncontract PrimitiveManager is IPrimitiveManager, Multicall, CashManager, SelfPermit, PositionManager, SwapManager {\r\n    using TransferHelper for IERC20;\r\n    using Margin for Margin.Data;\r\n\r\n    /// EFFECT FUNCTIONS ///\r\n\r\n    /// @param factory_             Address of a PrimitiveFactory\r\n    /// @param WETH9_               Address of WETH9\r\n    /// @param positionDescriptor_  Address of PositionDescriptor\r\n    constructor(\r\n        address factory_,\r\n        address WETH9_,\r\n        address positionDescriptor_\r\n    ) ManagerBase(factory_, WETH9_, positionDescriptor_) {}\r\n\r\n    /// @inheritdoc IPrimitiveManager\r\n    function create(\r\n        address risky,\r\n        address stable,\r\n        uint128 strike,\r\n        uint32 sigma,\r\n        uint32 maturity,\r\n        uint32 gamma,\r\n        uint256 riskyPerLp,\r\n        uint256 delLiquidity\r\n    )\r\n        external\r\n        payable\r\n        override\r\n        lock\r\n        returns (\r\n            bytes32 poolId,\r\n            uint256 delRisky,\r\n            uint256 delStable\r\n        )\r\n    {\r\n        address engine = EngineAddress.computeAddress(factory, risky, stable);\r\n        if (engine.code.length == 0) revert EngineAddress.EngineNotDeployedError();\r\n\r\n        if (delLiquidity == 0) revert ZeroLiquidityError();\r\n\r\n        CallbackData memory callbackData = CallbackData({risky: risky, stable: stable, payer: msg.sender});\r\n\r\n        (poolId, delRisky, delStable) = IPrimitiveEngineActions(engine).create(\r\n            strike,\r\n            sigma,\r\n            maturity,\r\n            gamma,\r\n            riskyPerLp,\r\n            delLiquidity,\r\n            abi.encode(callbackData)\r\n        );\r\n\r\n        // Mints {delLiquidity - MIN_LIQUIDITY} of liquidity tokens\r\n        uint256 MIN_LIQUIDITY = IPrimitiveEngineView(engine).MIN_LIQUIDITY();\r\n        _allocate(msg.sender, engine, poolId, delLiquidity - MIN_LIQUIDITY);\r\n\r\n        emit Create(msg.sender, engine, poolId, strike, sigma, maturity, gamma, delLiquidity - MIN_LIQUIDITY);\r\n    }\r\n\r\n    address private _engine;\r\n\r\n    /// @inheritdoc IPrimitiveManager\r\n    function allocate(\r\n        address recipient,\r\n        bytes32 poolId,\r\n        address risky,\r\n        address stable,\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bool fromMargin,\r\n        uint256 minLiquidityOut\r\n    ) external payable override lock returns (uint256 delLiquidity) {\r\n        _engine = EngineAddress.computeAddress(factory, risky, stable);\r\n        if (_engine.code.length == 0) revert EngineAddress.EngineNotDeployedError();\r\n\r\n        if (delRisky == 0 && delStable == 0) revert ZeroLiquidityError();\r\n\r\n        (delLiquidity) = IPrimitiveEngineActions(_engine).allocate(\r\n            poolId,\r\n            address(this),\r\n            delRisky,\r\n            delStable,\r\n            fromMargin,\r\n            abi.encode(CallbackData({risky: risky, stable: stable, payer: msg.sender}))\r\n        );\r\n\r\n        if (delLiquidity < minLiquidityOut) revert MinLiquidityOutError();\r\n\r\n        if (fromMargin) margins[msg.sender][_engine].withdraw(delRisky, delStable);\r\n\r\n        // Mints {delLiquidity} of liquidity tokens\r\n        _allocate(recipient, _engine, poolId, delLiquidity);\r\n\r\n        emit Allocate(msg.sender, recipient, _engine, poolId, delLiquidity, delRisky, delStable, fromMargin);\r\n\r\n        _engine = address(0);\r\n    }\r\n\r\n    /// @inheritdoc IPrimitiveManager\r\n    function remove(\r\n        address engine,\r\n        bytes32 poolId,\r\n        uint256 delLiquidity,\r\n        uint256 minRiskyOut,\r\n        uint256 minStableOut\r\n    ) external override lock returns (uint256 delRisky, uint256 delStable) {\r\n        if (delLiquidity == 0) revert ZeroLiquidityError();\r\n\r\n        (delRisky, delStable) = IPrimitiveEngineActions(engine).remove(poolId, delLiquidity);\r\n        if (delRisky < minRiskyOut || delStable < minStableOut) revert MinRemoveOutError();\r\n\r\n        _remove(msg.sender, poolId, delLiquidity);\r\n        margins[msg.sender][engine].deposit(delRisky, delStable);\r\n\r\n        emit Remove(msg.sender, engine, poolId, delLiquidity, delRisky, delStable);\r\n    }\r\n\r\n    /// CALLBACK IMPLEMENTATIONS ///\r\n\r\n    /// @inheritdoc IPrimitiveCreateCallback\r\n    function createCallback(\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bytes calldata data\r\n    ) external override {\r\n        CallbackData memory decoded = abi.decode(data, (CallbackData));\r\n\r\n        address engine = EngineAddress.computeAddress(factory, decoded.risky, decoded.stable);\r\n        if (msg.sender != engine) revert NotEngineError();\r\n\r\n        if (delRisky != 0) pay(decoded.risky, decoded.payer, msg.sender, delRisky);\r\n        if (delStable != 0) pay(decoded.stable, decoded.payer, msg.sender, delStable);\r\n    }\r\n\r\n    /// @inheritdoc IPrimitiveLiquidityCallback\r\n    function allocateCallback(\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bytes calldata data\r\n    ) external override {\r\n        CallbackData memory decoded = abi.decode(data, (CallbackData));\r\n\r\n        address engine = EngineAddress.computeAddress(factory, decoded.risky, decoded.stable);\r\n        if (msg.sender != engine) revert NotEngineError();\r\n\r\n        if (delRisky != 0) pay(decoded.risky, decoded.payer, msg.sender, delRisky);\r\n        if (delStable != 0) pay(decoded.stable, decoded.payer, msg.sender, delStable);\r\n    }\r\n}\r\n"
    },
    "@primitivefi/rmm-core/contracts/interfaces/engine/IPrimitiveEngineView.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.5.0;\r\n\r\n/// @title  View functions of the Primitive Engine contract\r\n/// @author Primitive\r\ninterface IPrimitiveEngineView {\r\n    // ===== View =====\r\n\r\n    /// @notice             Fetches the current invariant, notation is usually `k`, based on risky and stable token reserves of pool with `poolId`\r\n    /// @param  poolId      Keccak256 hash of the engine address, strike, sigma, maturity, and gamma\r\n    /// @return invariant   Signed fixed point 64.64 number, invariant of `poolId`\r\n    function invariantOf(bytes32 poolId) external view returns (int128 invariant);\r\n\r\n    // ===== Constants =====\r\n\r\n    /// @return Precision units to scale to when doing token related calculations\r\n    function PRECISION() external view returns (uint256);\r\n\r\n    /// @return Amount of seconds after pool expiry which allows swaps, no swaps after buffer\r\n    function BUFFER() external view returns (uint256);\r\n\r\n    // ===== Immutables =====\r\n\r\n    /// @return Amount of liquidity burned on `create()` calls\r\n    function MIN_LIQUIDITY() external view returns (uint256);\r\n\r\n    //// @return Factory address which deployed this engine contract\r\n    function factory() external view returns (address);\r\n\r\n    //// @return Risky token address, a more accurate name is the underlying token\r\n    function risky() external view returns (address);\r\n\r\n    /// @return Stable token address, a more accurate name is the quote token\r\n    function stable() external view returns (address);\r\n\r\n    /// @return Multiplier to scale amounts to/from, equal to 10^(18 - riskyDecimals)\r\n    function scaleFactorRisky() external view returns (uint256);\r\n\r\n    /// @return Multiplier to scale amounts to/from, equal to 10^(18 - stableDecimals)\r\n    function scaleFactorStable() external view returns (uint256);\r\n\r\n    // ===== Pool State =====\r\n\r\n    /// @notice                      Fetches the global reserve state for a pool with `poolId`\r\n    /// @param  poolId               Keccak256 hash of the engine address, strike, sigma, maturity, and gamma\r\n    /// @return reserveRisky         Risky token balance in the reserve\r\n    /// @return reserveStable        Stable token balance in the reserve\r\n    /// @return liquidity            Total supply of liquidity for the curve\r\n    /// @return blockTimestamp       Timestamp when the cumulative reserve values were last updated\r\n    /// @return cumulativeRisky      Cumulative sum of risky token reserves of the previous update\r\n    /// @return cumulativeStable     Cumulative sum of stable token reserves of the previous update\r\n    /// @return cumulativeLiquidity  Cumulative sum of total supply of liquidity of the previous update\r\n    function reserves(bytes32 poolId)\r\n        external\r\n        view\r\n        returns (\r\n            uint128 reserveRisky,\r\n            uint128 reserveStable,\r\n            uint128 liquidity,\r\n            uint32 blockTimestamp,\r\n            uint256 cumulativeRisky,\r\n            uint256 cumulativeStable,\r\n            uint256 cumulativeLiquidity\r\n        );\r\n\r\n    /// @notice                 Fetches `Calibration` pool parameters\r\n    /// @param  poolId          Keccak256 hash of the engine address, strike, sigma, maturity, and gamma\r\n    /// @return strike          Marginal price of the pool's risky token at maturity, with the same decimals as the stable token, valid [0, 2^128-1]\r\n    /// @return sigma           AKA Implied Volatility in basis points, determines the price impact of swaps, valid for (1, 10_000_000)\r\n    /// @return maturity        Timestamp which starts the BUFFER countdown until swaps will cease, in seconds, valid for (block.timestamp, 2^32-1]\r\n    /// @return lastTimestamp   Last timestamp used to calculate time until expiry, aka \"tau\"\r\n    /// @return gamma           Multiplied against swap in amounts to apply fee, equal to 1 - fee % but units are in basis points, valid for (9_000, 10_000)\r\n    function calibrations(bytes32 poolId)\r\n        external\r\n        view\r\n        returns (\r\n            uint128 strike,\r\n            uint32 sigma,\r\n            uint32 maturity,\r\n            uint32 lastTimestamp,\r\n            uint32 gamma\r\n        );\r\n\r\n    /// @notice             Fetches position liquidity an account address and poolId\r\n    /// @param  poolId      Keccak256 hash of the engine address, strike, sigma, maturity, and gamma\r\n    /// @return liquidity   Liquidity owned by `account` in `poolId`\r\n    function liquidity(address account, bytes32 poolId) external view returns (uint256 liquidity);\r\n\r\n    /// @notice                 Fetches the margin balances of `account`\r\n    /// @param  account         Margin account to fetch\r\n    /// @return balanceRisky    Balance of the risky token\r\n    /// @return balanceStable   Balance of the stable token\r\n    function margins(address account) external view returns (uint128 balanceRisky, uint128 balanceStable);\r\n}\r\n"
    },
    "contracts/interfaces/IPrimitiveManager.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.8.6;\r\n\r\nimport \"@primitivefi/rmm-core/contracts/interfaces/callback/IPrimitiveCreateCallback.sol\";\r\nimport \"@primitivefi/rmm-core/contracts/interfaces/callback/IPrimitiveLiquidityCallback.sol\";\r\n\r\n/// @title   Interface of PrimitiveManager contract\r\n/// @author  Primitive\r\ninterface IPrimitiveManager is IPrimitiveCreateCallback, IPrimitiveLiquidityCallback {\r\n    /// ERRORS ///\r\n\r\n    /// @notice  Thrown when trying to add or remove zero liquidity\r\n    error ZeroLiquidityError();\r\n\r\n    /// @notice  Thrown when the received liquidity is lower than the expected\r\n    error MinLiquidityOutError();\r\n\r\n    /// @notice  Thrown when the received risky / stable amounts are lower than the expected\r\n    error MinRemoveOutError();\r\n\r\n    /// EVENTS ///\r\n\r\n    /// @notice               Emitted when a new pool is created\r\n    /// @param payer          Payer sending liquidity\r\n    /// @param engine         Primitive Engine where the pool is created\r\n    /// @param poolId         Id of the new pool\r\n    /// @param strike         Strike of the new pool\r\n    /// @param sigma          Sigma of the new pool\r\n    /// @param maturity       Maturity of the new pool\r\n    /// @param gamma          Gamma of the new pool\r\n    /// @param delLiquidity   Amount of liquidity allocated (minus the minimum liquidity)\r\n    event Create(\r\n        address indexed payer,\r\n        address indexed engine,\r\n        bytes32 indexed poolId,\r\n        uint128 strike,\r\n        uint32 sigma,\r\n        uint32 maturity,\r\n        uint32 gamma,\r\n        uint256 delLiquidity\r\n    );\r\n\r\n    /// @notice              Emitted when liquidity is allocated\r\n    /// @param payer         Payer sending liquidity\r\n    /// @param recipient     Address that receives minted ERC-1155 Primitive liquidity tokens\r\n    /// @param engine        Primitive Engine receiving liquidity\r\n    /// @param poolId        Id of the pool receiving liquidity\r\n    /// @param delLiquidity  Amount of liquidity allocated\r\n    /// @param delRisky      Amount of risky tokens allocated\r\n    /// @param delStable     Amount of stable tokens allocated\r\n    /// @param fromMargin    True if liquidity was paid from margin\r\n    event Allocate(\r\n        address payer,\r\n        address indexed recipient,\r\n        address indexed engine,\r\n        bytes32 indexed poolId,\r\n        uint256 delLiquidity,\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bool fromMargin\r\n    );\r\n\r\n    /// @notice              Emitted when liquidity is removed\r\n    /// @param payer         Payer receiving liquidity\r\n    /// @param engine        Engine where liquidity is removed from\r\n    /// @param poolId        Id of the pool where liquidity is removed from\r\n    /// @param delLiquidity  Amount of liquidity removed\r\n    /// @param delRisky      Amount of risky tokens allocated\r\n    /// @param delStable     Amount of stable tokens allocated\r\n    event Remove(\r\n        address indexed payer,\r\n        address indexed engine,\r\n        bytes32 indexed poolId,\r\n        uint256 delLiquidity,\r\n        uint256 delRisky,\r\n        uint256 delStable\r\n    );\r\n\r\n    /// EFFECT FUNCTIONS ///\r\n\r\n    /// @notice              Creates a new pool using the specified parameters\r\n    /// @param risky         Address of the risky asset\r\n    /// @param stable        Address of the stable asset\r\n    /// @param strike        Strike price of the pool to calibrate to, with the same decimals as the stable token\r\n    /// @param sigma         Volatility to calibrate to as an unsigned 256-bit integer w/ precision of 1e4, 10000 = 100%\r\n    /// @param maturity      Maturity timestamp of the pool, in seconds\r\n    /// @param gamma         Multiplied against swap in amounts to apply fee, equal to 1 - fee %, an unsigned 32-bit integer, w/ precision of 1e4, 10000 = 100%\r\n    /// @param riskyPerLp    Risky reserve per liq. with risky decimals, = 1 - N(d1), d1 = (ln(S/K)+(r*sigma^2/2))/sigma*sqrt(tau)\r\n    /// @param delLiquidity  Amount of liquidity to allocate to the curve, wei value with 18 decimals of precision\r\n    /// @return poolId       Id of the new created pool (Keccak256 hash of the engine address, maturity, sigma and strike)\r\n    /// @return delRisky     Amount of risky tokens allocated into the pool\r\n    /// @return delStable    Amount of stable tokens allocated into the pool\r\n    function create(\r\n        address risky,\r\n        address stable,\r\n        uint128 strike,\r\n        uint32 sigma,\r\n        uint32 maturity,\r\n        uint32 gamma,\r\n        uint256 riskyPerLp,\r\n        uint256 delLiquidity\r\n    )\r\n        external\r\n        payable\r\n        returns (\r\n            bytes32 poolId,\r\n            uint256 delRisky,\r\n            uint256 delStable\r\n        );\r\n\r\n    /// @notice               Allocates liquidity into a pool\r\n    /// @param recipient      Address that receives minted ERC-1155 Primitive liquidity tokens\r\n    /// @param poolId         Id of the pool\r\n    /// @param risky          Address of the risky asset\r\n    /// @param stable         Address of the stable asset\r\n    /// @param delRisky       Amount of risky tokens to allocate\r\n    /// @param delStable      Amount of stable tokens to allocate\r\n    /// @param fromMargin     True if the funds of the sender should be used\r\n    /// @return delLiquidity  Amount of liquidity allocated into the pool\r\n    function allocate(\r\n        address recipient,\r\n        bytes32 poolId,\r\n        address risky,\r\n        address stable,\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bool fromMargin,\r\n        uint256 minLiquidityOut\r\n    ) external payable returns (uint256 delLiquidity);\r\n\r\n    /// @notice              Removes liquidity from a pool\r\n    /// @param engine        Address of the engine\r\n    /// @param poolId        Id of the pool\r\n    /// @param delLiquidity  Amount of liquidity to remove\r\n    /// @param minRiskyOut   Minimum amount of risky tokens expected to be received\r\n    /// @param minStableOut  Minimum amount of stable tokens expected to be received\r\n    /// @return delRisky     Amount of risky tokens removed from the pool\r\n    /// @return delStable    Amount of stable tokens removed from the pool\r\n    function remove(\r\n        address engine,\r\n        bytes32 poolId,\r\n        uint256 delLiquidity,\r\n        uint256 minRiskyOut,\r\n        uint256 minStableOut\r\n    ) external returns (uint256 delRisky, uint256 delStable);\r\n}\r\n"
    },
    "contracts/base/Multicall.sol": {
      "content": "// SPDX-License-Identifier: GPL-2.0-or-later\r\npragma solidity 0.8.6;\r\n\r\nimport \"../interfaces/IMulticall.sol\";\r\n\r\n/// @title   Multicall contract\r\n/// @author  https://github.com/Uniswap/v3-periphery/blob/main/contracts/base/Multicall.sol\r\n/// @notice  Enables calling multiple methods in a single call to the contract\r\nabstract contract Multicall is IMulticall {\r\n    /// @inheritdoc IMulticall\r\n    function multicall(bytes[] calldata data) external payable override returns (bytes[] memory results) {\r\n        results = new bytes[](data.length);\r\n        for (uint256 i = 0; i < data.length; i++) {\r\n            (bool success, bytes memory result) = address(this).delegatecall(data[i]);\r\n\r\n            if (!success) {\r\n                // Next 5 lines from https://ethereum.stackexchange.com/a/83577\r\n                if (result.length < 68) revert();\r\n                assembly {\r\n                    result := add(result, 0x04)\r\n                }\r\n                revert(abi.decode(result, (string)));\r\n            }\r\n\r\n            results[i] = result;\r\n        }\r\n    }\r\n}\r\n"
    },
    "contracts/base/CashManager.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity 0.8.6;\r\n\r\nimport \"../interfaces/ICashManager.sol\";\r\nimport \"../base/ManagerBase.sol\";\r\nimport \"../libraries/TransferHelper.sol\";\r\nimport \"../interfaces/external/IWETH9.sol\";\r\n\r\n/// @title   CashManager contract\r\n/// @author  Primitive\r\n/// @notice  Utils contract to manage ETH and token balances\r\nabstract contract CashManager is ICashManager, ManagerBase {\r\n    /// @notice Only WETH9 can send ETH to this contract\r\n    receive() external payable {\r\n        if (msg.sender != WETH9) revert OnlyWETHError();\r\n    }\r\n\r\n    /// @inheritdoc ICashManager\r\n    function wrap(uint256 value) external payable override {\r\n        if (address(this).balance < value) {\r\n            revert BalanceTooLowError(address(this).balance, value);\r\n        }\r\n\r\n        IWETH9(WETH9).deposit{value: value}();\r\n        IWETH9(WETH9).transfer(msg.sender, value);\r\n    }\r\n\r\n    /// @inheritdoc ICashManager\r\n    function unwrap(uint256 amountMin, address recipient) external payable override {\r\n        uint256 balance = IWETH9(WETH9).balanceOf(address(this));\r\n\r\n        if (balance < amountMin) revert BalanceTooLowError(balance, amountMin);\r\n\r\n        if (balance != 0) {\r\n            IWETH9(WETH9).withdraw(balance);\r\n            TransferHelper.safeTransferETH(recipient, balance);\r\n        }\r\n    }\r\n\r\n    /// @inheritdoc ICashManager\r\n    function sweepToken(\r\n        address token,\r\n        uint256 amountMin,\r\n        address recipient\r\n    ) external payable override {\r\n        uint256 balance = IERC20(token).balanceOf(address(this));\r\n        if (balance < amountMin) revert BalanceTooLowError(balance, amountMin);\r\n\r\n        if (balance != 0) {\r\n            TransferHelper.safeTransfer(token, recipient, balance);\r\n        }\r\n    }\r\n\r\n    /// @inheritdoc ICashManager\r\n    function refundETH() external payable override {\r\n        if (address(this).balance != 0) TransferHelper.safeTransferETH(msg.sender, address(this).balance);\r\n    }\r\n\r\n    /// @dev              Pays `value` of `token` to `recipient` from `payer` wallet\r\n    /// @param token      Token to transfer as payment\r\n    /// @param payer      Account that pays\r\n    /// @param recipient  Account that receives payment\r\n    /// @param value      Amount to pay\r\n    function pay(\r\n        address token,\r\n        address payer,\r\n        address recipient,\r\n        uint256 value\r\n    ) internal {\r\n        if (token == WETH9 && address(this).balance >= value) {\r\n            IWETH9(WETH9).deposit{value: value}();\r\n            IWETH9(WETH9).transfer(recipient, value);\r\n        } else if (payer == address(this)) {\r\n            TransferHelper.safeTransfer(token, recipient, value);\r\n        } else {\r\n            TransferHelper.safeTransferFrom(token, payer, recipient, value);\r\n        }\r\n    }\r\n}\r\n"
    },
    "contracts/base/SelfPermit.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity 0.8.6;\r\n\r\nimport \"@openzeppelin/contracts/token/ERC20/extensions/draft-IERC20Permit.sol\";\r\nimport \"../interfaces/external/IERC20.sol\";\r\nimport \"../interfaces/external/IERC20PermitAllowed.sol\";\r\nimport \"../interfaces/ISelfPermit.sol\";\r\n\r\n/// @title   SelfPermit contract\r\n/// @author  https://github.com/Uniswap/v3-periphery/blob/main/contracts/base/SelfPermit.sol\r\n/// @notice  Functionality to call permit on any EIP-2612-compliant token\r\n/// @dev     These functions are expected to be embedded in multicalls to allow EOAs to approve a contract and call a function\r\n///          that requires an approval in a single transaction\r\nabstract contract SelfPermit is ISelfPermit {\r\n    /// @inheritdoc ISelfPermit\r\n    function selfPermit(\r\n        address token,\r\n        uint256 value,\r\n        uint256 deadline,\r\n        uint8 v,\r\n        bytes32 r,\r\n        bytes32 s\r\n    ) public payable override {\r\n        IERC20Permit(token).permit(msg.sender, address(this), value, deadline, v, r, s);\r\n    }\r\n\r\n    /// @inheritdoc ISelfPermit\r\n    function selfPermitIfNecessary(\r\n        address token,\r\n        uint256 value,\r\n        uint256 deadline,\r\n        uint8 v,\r\n        bytes32 r,\r\n        bytes32 s\r\n    ) external payable override {\r\n        if (IERC20(token).allowance(msg.sender, address(this)) < value) selfPermit(token, value, deadline, v, r, s);\r\n    }\r\n\r\n    /// @inheritdoc ISelfPermit\r\n    function selfPermitAllowed(\r\n        address token,\r\n        uint256 nonce,\r\n        uint256 expiry,\r\n        uint8 v,\r\n        bytes32 r,\r\n        bytes32 s\r\n    ) public payable override {\r\n        IERC20PermitAllowed(token).permit(msg.sender, address(this), nonce, expiry, true, v, r, s);\r\n    }\r\n\r\n    /// @inheritdoc ISelfPermit\r\n    function selfPermitAllowedIfNecessary(\r\n        address token,\r\n        uint256 nonce,\r\n        uint256 expiry,\r\n        uint8 v,\r\n        bytes32 r,\r\n        bytes32 s\r\n    ) external payable override {\r\n        if (IERC20(token).allowance(msg.sender, address(this)) < type(uint256).max)\r\n            selfPermitAllowed(token, nonce, expiry, v, r, s);\r\n    }\r\n}\r\n"
    },
    "contracts/base/PositionManager.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity 0.8.6;\r\n\r\nimport \"./ERC1155Permit.sol\";\r\nimport \"../base/ManagerBase.sol\";\r\nimport \"../interfaces/IPositionDescriptor.sol\";\r\n\r\n/// @title   PositionManager contract\r\n/// @author  Primitive\r\n/// @notice  Wraps the positions into ERC1155 tokens\r\nabstract contract PositionManager is ManagerBase, ERC1155Permit {\r\n    /// @dev  Ties together pool ids with engine addresses, this is necessary because\r\n    ///       there is no way to get the Primitive Engine address from a pool id\r\n    mapping(uint256 => address) private cache;\r\n\r\n    /// @dev  Empty variable to pass to the _mint function\r\n    bytes constant private _empty = \"\";\r\n\r\n    /// @notice         Returns the metadata of a token\r\n    /// @param tokenId  Token id to look for (same as pool id)\r\n    /// @return         Metadata of the token as a string\r\n    function uri(uint256 tokenId) public view override returns (string memory) {\r\n        return IPositionDescriptor(positionDescriptor).getMetadata(cache[tokenId], tokenId);\r\n    }\r\n\r\n    /// @notice         Allocates {amount} of {poolId} liquidity to {account} balance\r\n    /// @param account  Recipient of the liquidity\r\n    /// @param engine   Address of the Primitive Engine\r\n    /// @param poolId   Id of the pool\r\n    /// @param amount   Amount of liquidity to allocate\r\n    function _allocate(\r\n        address account,\r\n        address engine,\r\n        bytes32 poolId,\r\n        uint256 amount\r\n    ) internal {\r\n        _mint(account, uint256(poolId), amount, _empty);\r\n\r\n        if (cache[uint256(poolId)] == address(0)) cache[uint256(poolId)] = engine;\r\n    }\r\n\r\n    /// @notice         Removes {amount} of {poolId} liquidity from {account} balance\r\n    /// @param account  Account to remove from\r\n    /// @param poolId   Id of the pool\r\n    /// @param amount   Amount of liquidity to remove\r\n    function _remove(\r\n        address account,\r\n        bytes32 poolId,\r\n        uint256 amount\r\n    ) internal {\r\n        _burn(account, uint256(poolId), amount);\r\n    }\r\n}\r\n"
    },
    "contracts/base/SwapManager.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity 0.8.6;\r\n\r\nimport \"@primitivefi/rmm-core/contracts/interfaces/engine/IPrimitiveEngineActions.sol\";\r\nimport \"@primitivefi/rmm-core/contracts/interfaces/engine/IPrimitiveEngineView.sol\";\r\nimport \"@primitivefi/rmm-core/contracts/libraries/ReplicationMath.sol\";\r\n\r\nimport \"../interfaces/ISwapManager.sol\";\r\nimport \"../interfaces/external/IERC20.sol\";\r\nimport \"./MarginManager.sol\";\r\nimport \"./CashManager.sol\";\r\n\r\n/// @title   SwapManager contract\r\n/// @author  Primitive\r\n/// @dev     Manages the swaps\r\nabstract contract SwapManager is ISwapManager, CashManager, MarginManager {\r\n    using TransferHelper for IERC20;\r\n    using Margin for Margin.Data;\r\n\r\n    /// @notice Reverts the transaction is the deadline is reached\r\n    modifier checkDeadline(uint256 deadline) {\r\n        if (block.timestamp > deadline) revert DeadlineReachedError();\r\n        _;\r\n    }\r\n\r\n    /// EFFECT FUNCTIONS ///\r\n\r\n    /// @inheritdoc ISwapManager\r\n    function swap(SwapParams calldata params) external payable override lock checkDeadline(params.deadline) {\r\n        CallbackData memory callbackData = CallbackData({\r\n            payer: msg.sender,\r\n            risky: params.risky,\r\n            stable: params.stable\r\n        });\r\n\r\n        address engine = EngineAddress.computeAddress(factory, params.risky, params.stable);\r\n        if (engine.code.length == 0) revert EngineAddress.EngineNotDeployedError();\r\n\r\n        IPrimitiveEngineActions(engine).swap(\r\n            params.toMargin ? address(this) : params.recipient,\r\n            params.poolId,\r\n            params.riskyForStable,\r\n            params.deltaIn,\r\n            params.deltaOut,\r\n            params.fromMargin,\r\n            params.toMargin,\r\n            abi.encode(callbackData)\r\n        );\r\n\r\n        if (params.fromMargin) {\r\n            margins[msg.sender][engine].withdraw(\r\n                params.riskyForStable ? params.deltaIn : 0,\r\n                params.riskyForStable ? 0 : params.deltaIn\r\n            );\r\n        }\r\n\r\n        if (params.toMargin) {\r\n            margins[params.recipient][engine].deposit(\r\n                params.riskyForStable ? 0 : params.deltaOut,\r\n                params.riskyForStable ? params.deltaOut : 0\r\n            );\r\n        }\r\n\r\n        emit Swap(\r\n            msg.sender,\r\n            params.recipient,\r\n            engine,\r\n            params.poolId,\r\n            params.riskyForStable,\r\n            params.deltaIn,\r\n            params.deltaOut,\r\n            params.fromMargin,\r\n            params.toMargin\r\n        );\r\n    }\r\n\r\n    /// CALLBACK IMPLEMENTATIONS ///\r\n\r\n    /// @inheritdoc IPrimitiveSwapCallback\r\n    function swapCallback(\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bytes calldata data\r\n    ) external override {\r\n        CallbackData memory decoded = abi.decode(data, (CallbackData));\r\n\r\n        address engine = EngineAddress.computeAddress(factory, decoded.risky, decoded.stable);\r\n        if (msg.sender != engine) revert NotEngineError();\r\n\r\n        if (delRisky != 0) pay(decoded.risky, decoded.payer, msg.sender, delRisky);\r\n        if (delStable != 0) pay(decoded.stable, decoded.payer, msg.sender, delStable);\r\n    }\r\n}\r\n"
    },
    "contracts/libraries/TransferHelper.sol": {
      "content": "// SPDX-License-Identifier: GPL-2.0-or-later\r\npragma solidity 0.8.6;\r\n\r\nimport \"../interfaces/external/IERC20.sol\";\r\n\r\n/// @author  https://github.com/Uniswap/v3-periphery/blob/main/contracts/libraries/TransferHelper.sol\r\n/// @notice  Utils functions to transfer tokens and ETH\r\nlibrary TransferHelper {\r\n    /// ERRORS ///\r\n\r\n    /// @notice Thrown when a transfer reverts\r\n    error TransferError();\r\n\r\n    /// @notice Thrown when an approval reverts\r\n    error ApproveError();\r\n\r\n    /// FUNCTIONS ///\r\n\r\n    /// @notice       Transfers tokens from the targeted address to the given destination\r\n    /// @param token  Contract address of the token to be transferred\r\n    /// @param from   Originating address from which the tokens will be transferred\r\n    /// @param to     Destination address of the transfer\r\n    /// @param value  Amount to be transferred\r\n    function safeTransferFrom(\r\n        address token,\r\n        address from,\r\n        address to,\r\n        uint256 value\r\n    ) internal {\r\n        (bool success, bytes memory data) = token.call(\r\n            abi.encodeWithSelector(IERC20.transferFrom.selector, from, to, value)\r\n        );\r\n\r\n        if (!(success && (data.length == 0 || abi.decode(data, (bool))))) revert TransferError();\r\n    }\r\n\r\n    /// @notice       Transfers tokens from msg.sender to a recipient\r\n    /// @param token  Contract address of the token which will be transferred\r\n    /// @param to     Recipient of the transfer\r\n    /// @param value  Value of the transfer\r\n    function safeTransfer(\r\n        address token,\r\n        address to,\r\n        uint256 value\r\n    ) internal {\r\n        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(IERC20.transfer.selector, to, value));\r\n        if (!(success && (data.length == 0 || abi.decode(data, (bool))))) revert TransferError();\r\n    }\r\n\r\n    /// @notice       Approves the stipulated contract to spend the given allowance in the given token\r\n    /// @param token  Contract address of the token to be approved\r\n    /// @param to     Target of the approval\r\n    /// @param value  Amount of the given token the target will be allowed to spend\r\n    function safeApprove(\r\n        address token,\r\n        address to,\r\n        uint256 value\r\n    ) internal {\r\n        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(IERC20.approve.selector, to, value));\r\n        if (!(success && (data.length == 0 || abi.decode(data, (bool))))) revert ApproveError();\r\n    }\r\n\r\n    /// @notice       Transfers ETH to the recipient address\r\n    /// @param to     Destination of the transfer\r\n    /// @param value  Value to be transferred\r\n    function safeTransferETH(address to, uint256 value) internal {\r\n        (bool success, ) = to.call{value: value}(new bytes(0));\r\n        if (success == false) revert TransferError();\r\n    }\r\n}\r\n"
    },
    "@primitivefi/rmm-core/contracts/interfaces/callback/IPrimitiveCreateCallback.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.5.0;\r\n\r\n/// @title  Primitive Create Callback\r\n/// @author Primitive\r\ninterface IPrimitiveCreateCallback {\r\n    /// @notice              Triggered when creating a new pool for an Engine\r\n    /// @param  delRisky     Amount of risky tokens required to initialize risky reserve\r\n    /// @param  delStable    Amount of stable tokens required to initialize stable reserve\r\n    /// @param  data         Calldata passed on create function call\r\n    function createCallback(\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bytes calldata data\r\n    ) external;\r\n}\r\n"
    },
    "@primitivefi/rmm-core/contracts/interfaces/callback/IPrimitiveLiquidityCallback.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.5.0;\r\n\r\n/// @title  Primitive Liquidity Callback\r\n/// @author Primitive\r\ninterface IPrimitiveLiquidityCallback {\r\n    /// @notice              Triggered when providing liquidity to an Engine\r\n    /// @param  delRisky     Amount of risky tokens required to provide to risky reserve\r\n    /// @param  delStable    Amount of stable tokens required to provide to stable reserve\r\n    /// @param  data         Calldata passed on allocate function call\r\n    function allocateCallback(\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bytes calldata data\r\n    ) external;\r\n}\r\n"
    },
    "contracts/interfaces/IMulticall.sol": {
      "content": "// SPDX-License-Identifier: GPL-2.0-or-later\r\npragma solidity >=0.8.6;\r\n\r\n/// @title   Interface of Multicall contract\r\n/// @author  https://github.com/Uniswap/v3-periphery/blob/main/contracts/interfaces/IMulticall.sol\r\ninterface IMulticall {\r\n    /// @notice          Call multiple functions in the current contract and return the data from all of them if they all succeed\r\n    /// @dev             `msg.value` should not be trusted for any method callable from Multicall\r\n    /// @param data      Encoded function data for each of the calls to make to this contract\r\n    /// @return results  Results from each of the calls passed in via data\r\n    function multicall(bytes[] calldata data) external payable returns (bytes[] memory results);\r\n}\r\n"
    },
    "contracts/interfaces/ICashManager.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.8.6;\r\n\r\n/// @title   Interface of CashManager contract\r\n/// @author  Primitive\r\ninterface ICashManager {\r\n    /// ERRORS ///\r\n\r\n    /// @notice  Thrown when the sender is not WETH\r\n    error OnlyWETHError();\r\n\r\n    /// @notice                Thrown when the amount required is above balance\r\n    /// @param balance         Actual ETH or token balance of the contract\r\n    /// @param requiredAmount  ETH or token amount required by the user\r\n    error BalanceTooLowError(uint256 balance, uint256 requiredAmount);\r\n\r\n    /// EFFECT FUNCTIONS ///\r\n\r\n    /// @notice       Wraps ETH into WETH and transfers to the msg.sender\r\n    /// @param value  Amount of ETH to wrap\r\n    function wrap(uint256 value) external payable;\r\n\r\n    /// @notice           Unwraps WETH to ETH and transfers to a recipient\r\n    /// @param amountMin  Minimum amount to unwrap\r\n    /// @param recipient  Address of the recipient\r\n    function unwrap(uint256 amountMin, address recipient) external payable;\r\n\r\n    /// @notice           Transfers the tokens in the contract to a recipient\r\n    /// @param token      Address of the token to sweep\r\n    /// @param amountMin  Minimum amount to transfer\r\n    /// @param recipient  Recipient of the tokens\r\n    function sweepToken(\r\n        address token,\r\n        uint256 amountMin,\r\n        address recipient\r\n    ) external payable;\r\n\r\n    /// @notice  Transfers the ETH balance of the contract to the caller\r\n    function refundETH() external payable;\r\n}\r\n"
    },
    "contracts/base/ManagerBase.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity 0.8.6;\r\n\r\nimport \"../interfaces/IManagerBase.sol\";\r\nimport \"../interfaces/IPrimitiveManager.sol\";\r\nimport \"./Reentrancy.sol\";\r\nimport \"../libraries/EngineAddress.sol\";\r\n\r\n/// @title   ManagerBase contract\r\n/// @author  Primitive\r\n/// @notice  Base contract of the Manager\r\nabstract contract ManagerBase is IManagerBase, Reentrancy {\r\n    /// @notice Data struct reused by callbacks\r\n    struct CallbackData {\r\n        address payer;\r\n        address risky;\r\n        address stable;\r\n    }\r\n\r\n    /// @inheritdoc IManagerBase\r\n    address public immutable override factory;\r\n\r\n    /// @inheritdoc IManagerBase\r\n    address public immutable override WETH9;\r\n\r\n    /// @inheritdoc IManagerBase\r\n    address public immutable override positionDescriptor;\r\n\r\n    /// @param factory_  Address of a PrimitiveFactory\r\n    /// @param WETH9_    Address of WETH9\r\n    /// @param positionDescriptor_    Address of the position renderer\r\n    constructor(\r\n        address factory_,\r\n        address WETH9_,\r\n        address positionDescriptor_\r\n    ) {\r\n        if (factory_ == address(0) || WETH9_ == address(0) || positionDescriptor_ == address(0))\r\n            revert WrongConstructorParametersError();\r\n\r\n        factory = factory_;\r\n        WETH9 = WETH9_;\r\n        positionDescriptor = positionDescriptor_;\r\n    }\r\n}\r\n"
    },
    "contracts/interfaces/external/IWETH9.sol": {
      "content": "// SPDX-License-Identifier: GPL-2.0-or-later\r\npragma solidity >=0.8.6;\r\n\r\nimport \"./IERC20.sol\";\r\n\r\n/// @title   Interface for WETH9\r\n/// @author  Primitive\r\ninterface IWETH9 is IERC20 {\r\n    /// @notice Wraps ETH into WETH\r\n    function deposit() external payable;\r\n\r\n    /// @notice Unwraps WETH into ETH\r\n    function withdraw(uint256) external;\r\n}\r\n"
    },
    "contracts/interfaces/IManagerBase.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.8.6;\r\n\r\n/// @title   Interface of ManagerBase contract\r\n/// @author  Primitive\r\ninterface IManagerBase {\r\n    /// ERRORS ///\r\n\r\n    /// @notice Thrown when the sender is not a Primitive Engine contract\r\n    error NotEngineError();\r\n\r\n    /// @notice Thrown when the constructor parameters are wrong\r\n    error WrongConstructorParametersError();\r\n\r\n    /// VIEW FUNCTIONS ///\r\n\r\n    /// @notice Returns the address of the factory\r\n    function factory() external view returns (address);\r\n\r\n    /// @notice Returns the address of WETH9\r\n    function WETH9() external view returns (address);\r\n\r\n    /// @notice Returns the address of the PositionDescriptor\r\n    function positionDescriptor() external view returns (address);\r\n}\r\n"
    },
    "contracts/base/Reentrancy.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity 0.8.6;\r\n\r\n/// @title   Reentrancy contract\r\n/// @author  Primitive\r\n/// @notice  Prevents reentrancy\r\ncontract Reentrancy {\r\n    /// @notice  Thrown when a call to the contract is made during a locked state\r\n    error LockedError();\r\n\r\n    /// @dev Reentrancy guard initialized to state\r\n    uint256 private _locked = 1;\r\n\r\n    /// @notice  Locks the contract to prevent reentrancy\r\n    modifier lock() {\r\n        if (_locked != 1) revert LockedError();\r\n\r\n        _locked = 2;\r\n        _;\r\n        _locked = 1;\r\n    }\r\n}\r\n"
    },
    "contracts/libraries/EngineAddress.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.8.6;\r\n\r\n/// @author  Primitive\r\n/// @notice  Small library to compute the address of the engines\r\nlibrary EngineAddress {\r\n    /// @notice Thrown when the target Engine is not deployed\r\n    error EngineNotDeployedError();\r\n\r\n    /// @notice Hash of the bytecode of the PrimitiveEngine\r\n    bytes32 internal constant ENGINE_INIT_CODE_HASH =\r\n        0xf8e63833fcbf190bd32bc64a1c450bd59b356b4b7cf79cad298a369fb369aaae;\r\n\r\n    /// @notice         Computes the address of an engine\r\n    /// @param factory  Address of the factory\r\n    /// @param risky    Address of the risky token\r\n    /// @param stable   Address of the stable token\r\n    /// @return engine  Computed address of the engine\r\n    function computeAddress(\r\n        address factory,\r\n        address risky,\r\n        address stable\r\n    ) internal pure returns (address engine) {\r\n        engine = address(\r\n            uint160(\r\n                uint256(\r\n                    keccak256(\r\n                        abi.encodePacked(hex\"ff\", factory, keccak256(abi.encode(risky, stable)), ENGINE_INIT_CODE_HASH)\r\n                    )\r\n                )\r\n            )\r\n        );\r\n    }\r\n}\r\n"
    },
    "contracts/interfaces/external/IERC20.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.8.6;\r\n\r\n/// @title   ERC20 Interface\r\n/// @author  Primitive\r\ninterface IERC20 {\r\n    function totalSupply() external view returns (uint256);\r\n\r\n    function balanceOf(address account) external view returns (uint256);\r\n\r\n    function transfer(address recipient, uint256 amount) external returns (bool);\r\n\r\n    function allowance(address owner, address spender) external view returns (uint256);\r\n\r\n    function approve(address spender, uint256 amount) external returns (bool);\r\n\r\n    function transferFrom(\r\n        address sender,\r\n        address recipient,\r\n        uint256 amount\r\n    ) external returns (bool);\r\n\r\n    event Transfer(address indexed from, address indexed to, uint256 value);\r\n\r\n    event Approval(address indexed owner, address indexed spender, uint256 value);\r\n}\r\n"
    },
    "@openzeppelin/contracts/token/ERC20/extensions/draft-IERC20Permit.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/draft-IERC20Permit.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Interface of the ERC20 Permit extension allowing approvals to be made via signatures, as defined in\n * https://eips.ethereum.org/EIPS/eip-2612[EIP-2612].\n *\n * Adds the {permit} method, which can be used to change an account's ERC20 allowance (see {IERC20-allowance}) by\n * presenting a message signed by the account. By not relying on {IERC20-approve}, the token holder account doesn't\n * need to send a transaction, and thus is not required to hold Ether at all.\n */\ninterface IERC20Permit {\n    /**\n     * @dev Sets `value` as the allowance of `spender` over ``owner``'s tokens,\n     * given ``owner``'s signed approval.\n     *\n     * IMPORTANT: The same issues {IERC20-approve} has related to transaction\n     * ordering also apply here.\n     *\n     * Emits an {Approval} event.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     * - `deadline` must be a timestamp in the future.\n     * - `v`, `r` and `s` must be a valid `secp256k1` signature from `owner`\n     * over the EIP712-formatted function arguments.\n     * - the signature must use ``owner``'s current nonce (see {nonces}).\n     *\n     * For more information on the signature format, see the\n     * https://eips.ethereum.org/EIPS/eip-2612#specification[relevant EIP\n     * section].\n     */\n    function permit(\n        address owner,\n        address spender,\n        uint256 value,\n        uint256 deadline,\n        uint8 v,\n        bytes32 r,\n        bytes32 s\n    ) external;\n\n    /**\n     * @dev Returns the current nonce for `owner`. This value must be\n     * included whenever a signature is generated for {permit}.\n     *\n     * Every successful call to {permit} increases ``owner``'s nonce by one. This\n     * prevents a signature from being used multiple times.\n     */\n    function nonces(address owner) external view returns (uint256);\n\n    /**\n     * @dev Returns the domain separator used in the encoding of the signature for {permit}, as defined by {EIP712}.\n     */\n    // solhint-disable-next-line func-name-mixedcase\n    function DOMAIN_SEPARATOR() external view returns (bytes32);\n}\n"
    },
    "contracts/interfaces/external/IERC20PermitAllowed.sol": {
      "content": "// SPDX-License-Identifier: GPL-2.0-or-later\r\npragma solidity >=0.8.6;\r\n\r\n/// @title   Interface for permit\r\n/// @author  https://github.com/Uniswap/v3-periphery/blob/main/contracts/interfaces/external/IERC20PermitAllowed.sol\r\n/// @notice  Interface used by DAI/CHAI for permit\r\ninterface IERC20PermitAllowed {\r\n    /// @notice         Approve the spender to spend some tokens via the holder signature\r\n    /// @dev            This is the permit interface used by DAI and CHAI\r\n    /// @param holder   Address of the token holder, the token owner\r\n    /// @param spender  Address of the token spender\r\n    /// @param nonce    Holder's nonce, increases at each call to permit\r\n    /// @param expiry   Timestamp at which the permit is no longer valid\r\n    /// @param allowed  Boolean that sets approval amount, true for type(uint256).max and false for 0\r\n    /// @param v        Must produce valid secp256k1 signature from the holder along with `r` and `s`\r\n    /// @param r        Must produce valid secp256k1 signature from the holder along with `v` and `s`\r\n    /// @param s        Must produce valid secp256k1 signature from the holder along with `r` and `v`\r\n    function permit(\r\n        address holder,\r\n        address spender,\r\n        uint256 nonce,\r\n        uint256 expiry,\r\n        bool allowed,\r\n        uint8 v,\r\n        bytes32 r,\r\n        bytes32 s\r\n    ) external;\r\n}\r\n"
    },
    "contracts/interfaces/ISelfPermit.sol": {
      "content": "// SPDX-License-Identifier: GPL-2.0-or-later\r\npragma solidity >=0.8.6;\r\n\r\n/// @title   Interface of SelfPermit contract\r\n/// @author  https://github.com/Uniswap/v3-periphery/blob/main/contracts/interfaces/ISelfPermit.sol\r\ninterface ISelfPermit {\r\n    /// @notice          Permits this contract to spend a given token from `msg.sender`\r\n    /// @dev             `owner` is always msg.sender and the `spender` is always address(this)\r\n    /// @param token     Address of the token spent\r\n    /// @param value     Amount that can be spent of token\r\n    /// @param deadline  A timestamp, the current blocktime must be less than or equal to this timestamp\r\n    /// @param v         Must produce valid secp256k1 signature from the holder along with `r` and `s`\r\n    /// @param r         Must produce valid secp256k1 signature from the holder along with `v` and `s`\r\n    /// @param s         Must produce valid secp256k1 signature from the holder along with `r` and `v`\r\n    function selfPermit(\r\n        address token,\r\n        uint256 value,\r\n        uint256 deadline,\r\n        uint8 v,\r\n        bytes32 r,\r\n        bytes32 s\r\n    ) external payable;\r\n\r\n    /// @notice          Permits this contract to spend a given token from `msg.sender`\r\n    /// @dev             `owner` is always msg.sender and the `spender` is always address(this)\r\n    ///                  Can be used instead of #selfPermit to prevent calls from failing due to a frontrun of a call to #selfPermit\r\n    /// @param token     Address of the token spent\r\n    /// @param value     Amount that can be spent of token\r\n    /// @param deadline  A timestamp, the current blocktime must be less than or equal to this timestamp\r\n    /// @param v         Must produce valid secp256k1 signature from the holder along with `r` and `s`\r\n    /// @param r         Must produce valid secp256k1 signature from the holder along with `v` and `s`\r\n    /// @param s         Must produce valid secp256k1 signature from the holder along with `r` and `v`\r\n    function selfPermitIfNecessary(\r\n        address token,\r\n        uint256 value,\r\n        uint256 deadline,\r\n        uint8 v,\r\n        bytes32 r,\r\n        bytes32 s\r\n    ) external payable;\r\n\r\n    /// @notice        Permits this contract to spend the sender's tokens for permit signatures that have the `allowed` parameter\r\n    /// @dev           `owner` is always msg.sender and the `spender` is always address(this)\r\n    /// @param token   Address of the token spent\r\n    /// @param nonce   Current nonce of the owner\r\n    /// @param expiry  Timestamp at which the permit is no longer valid\r\n    /// @param v       Must produce valid secp256k1 signature from the holder along with `r` and `s`\r\n    /// @param r       Must produce valid secp256k1 signature from the holder along with `v` and `s`\r\n    /// @param s       Must produce valid secp256k1 signature from the holder along with `r` and `v`\r\n    function selfPermitAllowed(\r\n        address token,\r\n        uint256 nonce,\r\n        uint256 expiry,\r\n        uint8 v,\r\n        bytes32 r,\r\n        bytes32 s\r\n    ) external payable;\r\n\r\n    /// @notice        Permits this contract to spend the sender's tokens for permit signatures that have the `allowed` parameter\r\n    /// @dev           `owner` is always msg.sender and the `spender` is always address(this)\r\n    ///                Can be used instead of #selfPermitAllowed to prevent calls from failing due to a frontrun of a call to #selfPermitAllowed\r\n    /// @param token   Address of the token spent\r\n    /// @param nonce   Current nonce of the owner\r\n    /// @param expiry  Timestamp at which the permit is no longer valid\r\n    /// @param v       Must produce valid secp256k1 signature from the holder along with `r` and `s`\r\n    /// @param r       Must produce valid secp256k1 signature from the holder along with `v` and `s`\r\n    /// @param s       Must produce valid secp256k1 signature from the holder along with `r` and `v`\r\n    function selfPermitAllowedIfNecessary(\r\n        address token,\r\n        uint256 nonce,\r\n        uint256 expiry,\r\n        uint8 v,\r\n        bytes32 r,\r\n        bytes32 s\r\n    ) external payable;\r\n}\r\n"
    },
    "contracts/base/ERC1155Permit.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity 0.8.6;\r\n\r\nimport \"@openzeppelin/contracts/utils/cryptography/draft-EIP712.sol\";\r\nimport \"./ERC1155.sol\";\r\nimport \"../interfaces/IERC1155Permit.sol\";\r\n\r\n/// @title   ERC1155Permit contract\r\n/// @author  Primitive\r\n/// @notice  ERC1155 contract with permit extension allowing approvals to be made via signatures\r\ncontract ERC1155Permit is ERC1155, IERC1155Permit, EIP712 {\r\n    /// @inheritdoc IERC1155Permit\r\n    mapping(address => uint256) public override nonces;\r\n\r\n    /// @dev Typehash of the permit function\r\n    bytes32 private immutable _PERMIT_TYPEHASH =\r\n        keccak256(\"Permit(address owner,address operator,bool approved,uint256 nonce,uint256 deadline)\");\r\n\r\n    constructor() ERC1155(\"\") EIP712(\"PrimitiveManager\", \"1\") {}\r\n\r\n    /// @inheritdoc IERC1155Permit\r\n    function permit(\r\n        address owner,\r\n        address operator,\r\n        bool approved,\r\n        uint256 deadline,\r\n        uint8 v,\r\n        bytes32 r,\r\n        bytes32 s\r\n    ) external override {\r\n        if (block.timestamp > deadline) revert SigExpiredError();\r\n\r\n        unchecked {\r\n            bytes32 structHash = keccak256(\r\n                abi.encode(_PERMIT_TYPEHASH, owner, operator, approved, nonces[owner]++, deadline)\r\n            );\r\n\r\n            bytes32 hash = _hashTypedDataV4(structHash);\r\n            address signer = ECDSA.recover(hash, v, r, s);\r\n\r\n            if (signer != owner) revert InvalidSigError();\r\n        }\r\n\r\n        _setApprovalForAll(owner, operator, approved);\r\n    }\r\n\r\n    /// @inheritdoc IERC1155Permit\r\n    function DOMAIN_SEPARATOR() external view override returns (bytes32) {\r\n        return _domainSeparatorV4();\r\n    }\r\n}\r\n"
    },
    "contracts/interfaces/IPositionDescriptor.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.8.6;\r\n\r\n/// @title   Interface of PositionDescriptor contract\r\n/// @author  Primitive\r\ninterface IPositionDescriptor {\r\n    /// VIEW FUNCTIONS ///\r\n\r\n    /// @notice  Returns the address of the PositionRenderer contract\r\n    function positionRenderer() external view returns (address);\r\n\r\n    /// @notice         Returns the metadata of a position token\r\n    /// @param engine   Address of the PrimitiveEngine contract\r\n    /// @param tokenId  Id of the position token (pool id)\r\n    /// @return         Metadata as a base64 encoded JSON string\r\n    function getMetadata(address engine, uint256 tokenId) external view returns (string memory);\r\n}\r\n"
    },
    "@openzeppelin/contracts/utils/cryptography/draft-EIP712.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/cryptography/draft-EIP712.sol)\n\npragma solidity ^0.8.0;\n\nimport \"./ECDSA.sol\";\n\n/**\n * @dev https://eips.ethereum.org/EIPS/eip-712[EIP 712] is a standard for hashing and signing of typed structured data.\n *\n * The encoding specified in the EIP is very generic, and such a generic implementation in Solidity is not feasible,\n * thus this contract does not implement the encoding itself. Protocols need to implement the type-specific encoding\n * they need in their contracts using a combination of `abi.encode` and `keccak256`.\n *\n * This contract implements the EIP 712 domain separator ({_domainSeparatorV4}) that is used as part of the encoding\n * scheme, and the final step of the encoding to obtain the message digest that is then signed via ECDSA\n * ({_hashTypedDataV4}).\n *\n * The implementation of the domain separator was designed to be as efficient as possible while still properly updating\n * the chain id to protect against replay attacks on an eventual fork of the chain.\n *\n * NOTE: This contract implements the version of the encoding known as \"v4\", as implemented by the JSON RPC method\n * https://docs.metamask.io/guide/signing-data.html[`eth_signTypedDataV4` in MetaMask].\n *\n * _Available since v3.4._\n */\nabstract contract EIP712 {\n    /* solhint-disable var-name-mixedcase */\n    // Cache the domain separator as an immutable value, but also store the chain id that it corresponds to, in order to\n    // invalidate the cached domain separator if the chain id changes.\n    bytes32 private immutable _CACHED_DOMAIN_SEPARATOR;\n    uint256 private immutable _CACHED_CHAIN_ID;\n    address private immutable _CACHED_THIS;\n\n    bytes32 private immutable _HASHED_NAME;\n    bytes32 private immutable _HASHED_VERSION;\n    bytes32 private immutable _TYPE_HASH;\n\n    /* solhint-enable var-name-mixedcase */\n\n    /**\n     * @dev Initializes the domain separator and parameter caches.\n     *\n     * The meaning of `name` and `version` is specified in\n     * https://eips.ethereum.org/EIPS/eip-712#definition-of-domainseparator[EIP 712]:\n     *\n     * - `name`: the user readable name of the signing domain, i.e. the name of the DApp or the protocol.\n     * - `version`: the current major version of the signing domain.\n     *\n     * NOTE: These parameters cannot be changed except through a xref:learn::upgrading-smart-contracts.adoc[smart\n     * contract upgrade].\n     */\n    constructor(string memory name, string memory version) {\n        bytes32 hashedName = keccak256(bytes(name));\n        bytes32 hashedVersion = keccak256(bytes(version));\n        bytes32 typeHash = keccak256(\n            \"EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)\"\n        );\n        _HASHED_NAME = hashedName;\n        _HASHED_VERSION = hashedVersion;\n        _CACHED_CHAIN_ID = block.chainid;\n        _CACHED_DOMAIN_SEPARATOR = _buildDomainSeparator(typeHash, hashedName, hashedVersion);\n        _CACHED_THIS = address(this);\n        _TYPE_HASH = typeHash;\n    }\n\n    /**\n     * @dev Returns the domain separator for the current chain.\n     */\n    function _domainSeparatorV4() internal view returns (bytes32) {\n        if (address(this) == _CACHED_THIS && block.chainid == _CACHED_CHAIN_ID) {\n            return _CACHED_DOMAIN_SEPARATOR;\n        } else {\n            return _buildDomainSeparator(_TYPE_HASH, _HASHED_NAME, _HASHED_VERSION);\n        }\n    }\n\n    function _buildDomainSeparator(\n        bytes32 typeHash,\n        bytes32 nameHash,\n        bytes32 versionHash\n    ) private view returns (bytes32) {\n        return keccak256(abi.encode(typeHash, nameHash, versionHash, block.chainid, address(this)));\n    }\n\n    /**\n     * @dev Given an already https://eips.ethereum.org/EIPS/eip-712#definition-of-hashstruct[hashed struct], this\n     * function returns the hash of the fully encoded EIP712 message for this domain.\n     *\n     * This hash can be used together with {ECDSA-recover} to obtain the signer of a message. For example:\n     *\n     * ```solidity\n     * bytes32 digest = _hashTypedDataV4(keccak256(abi.encode(\n     *     keccak256(\"Mail(address to,string contents)\"),\n     *     mailTo,\n     *     keccak256(bytes(mailContents))\n     * )));\n     * address signer = ECDSA.recover(digest, signature);\n     * ```\n     */\n    function _hashTypedDataV4(bytes32 structHash) internal view virtual returns (bytes32) {\n        return ECDSA.toTypedDataHash(_domainSeparatorV4(), structHash);\n    }\n}\n"
    },
    "contracts/base/ERC1155.sol": {
      "content": "// SPDX-License-Identifier: MIT\r\n\r\npragma solidity ^0.8.0;\r\n\r\nimport \"@openzeppelin/contracts/token/ERC1155/IERC1155.sol\";\r\nimport \"@openzeppelin/contracts/token/ERC1155/IERC1155Receiver.sol\";\r\nimport \"@openzeppelin/contracts/token/ERC1155/extensions/IERC1155MetadataURI.sol\";\r\nimport \"@openzeppelin/contracts/utils/Address.sol\";\r\nimport \"@openzeppelin/contracts/utils/Context.sol\";\r\nimport \"@openzeppelin/contracts/utils/introspection/ERC165.sol\";\r\n\r\n/**\r\n * @dev Implementation of the basic standard multi-token.\r\n * See https://eips.ethereum.org/EIPS/eip-1155\r\n * Originally based on code by Enjin: https://github.com/enjin/erc-1155\r\n *\r\n * _Available since v3.1._\r\n */\r\ncontract ERC1155 is Context, ERC165, IERC1155, IERC1155MetadataURI {\r\n    using Address for address;\r\n\r\n    // Mapping from token ID to account balances\r\n    mapping(uint256 => mapping(address => uint256)) private _balances;\r\n\r\n    // Mapping from account to operator approvals\r\n    mapping(address => mapping(address => bool)) private _operatorApprovals;\r\n\r\n    // Used as the URI for all token types by relying on ID substitution, e.g. https://token-cdn-domain/{id}.json\r\n    string private _uri;\r\n\r\n    /**\r\n     * @dev See {_setURI}.\r\n     */\r\n    constructor(string memory uri_) {\r\n        _setURI(uri_);\r\n    }\r\n\r\n    /**\r\n     * @dev See {IERC165-supportsInterface}.\r\n     */\r\n    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {\r\n        return\r\n            interfaceId == type(IERC1155).interfaceId ||\r\n            interfaceId == type(IERC1155MetadataURI).interfaceId ||\r\n            super.supportsInterface(interfaceId);\r\n    }\r\n\r\n    /**\r\n     * @dev See {IERC1155MetadataURI-uri}.\r\n     *\r\n     * This implementation returns the same URI for *all* token types. It relies\r\n     * on the token type ID substitution mechanism\r\n     * https://eips.ethereum.org/EIPS/eip-1155#metadata[defined in the EIP].\r\n     *\r\n     * Clients calling this function must replace the `\\{id\\}` substring with the\r\n     * actual token type ID.\r\n     */\r\n    function uri(uint256) public view virtual override returns (string memory) {\r\n        return _uri;\r\n    }\r\n\r\n    /**\r\n     * @dev See {IERC1155-balanceOf}.\r\n     *\r\n     * Requirements:\r\n     *\r\n     * - `account` cannot be the zero address.\r\n     */\r\n    function balanceOf(address account, uint256 id) public view virtual override returns (uint256) {\r\n        require(account != address(0), \"ERC1155: balance query for the zero address\");\r\n        return _balances[id][account];\r\n    }\r\n\r\n    /**\r\n     * @dev See {IERC1155-balanceOfBatch}.\r\n     *\r\n     * Requirements:\r\n     *\r\n     * - `accounts` and `ids` must have the same length.\r\n     */\r\n    function balanceOfBatch(address[] memory accounts, uint256[] memory ids)\r\n        public\r\n        view\r\n        virtual\r\n        override\r\n        returns (uint256[] memory)\r\n    {\r\n        require(accounts.length == ids.length, \"ERC1155: accounts and ids length mismatch\");\r\n\r\n        uint256[] memory batchBalances = new uint256[](accounts.length);\r\n\r\n        for (uint256 i = 0; i < accounts.length; ++i) {\r\n            batchBalances[i] = balanceOf(accounts[i], ids[i]);\r\n        }\r\n\r\n        return batchBalances;\r\n    }\r\n\r\n    /**\r\n     * @dev See {IERC1155-setApprovalForAll}.\r\n     */\r\n    function setApprovalForAll(address operator, bool approved) public virtual override {\r\n        require(_msgSender() != operator, \"ERC1155: setting approval status for self\");\r\n\r\n        _operatorApprovals[_msgSender()][operator] = approved;\r\n        emit ApprovalForAll(_msgSender(), operator, approved);\r\n    }\r\n\r\n    /**\r\n     * @dev See {IERC1155-isApprovedForAll}.\r\n     */\r\n    function isApprovedForAll(address account, address operator) public view virtual override returns (bool) {\r\n        return _operatorApprovals[account][operator];\r\n    }\r\n\r\n    /**\r\n     * @dev See {IERC1155-safeTransferFrom}.\r\n     */\r\n    function safeTransferFrom(\r\n        address from,\r\n        address to,\r\n        uint256 id,\r\n        uint256 amount,\r\n        bytes memory data\r\n    ) public virtual override {\r\n        require(\r\n            from == _msgSender() || isApprovedForAll(from, _msgSender()),\r\n            \"ERC1155: caller is not owner nor approved\"\r\n        );\r\n        _safeTransferFrom(from, to, id, amount, data);\r\n    }\r\n\r\n    /**\r\n     * @dev See {IERC1155-safeBatchTransferFrom}.\r\n     */\r\n    function safeBatchTransferFrom(\r\n        address from,\r\n        address to,\r\n        uint256[] memory ids,\r\n        uint256[] memory amounts,\r\n        bytes memory data\r\n    ) public virtual override {\r\n        require(\r\n            from == _msgSender() || isApprovedForAll(from, _msgSender()),\r\n            \"ERC1155: transfer caller is not owner nor approved\"\r\n        );\r\n        _safeBatchTransferFrom(from, to, ids, amounts, data);\r\n    }\r\n\r\n    /**\r\n     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.\r\n     *\r\n     * Emits a {TransferSingle} event.\r\n     *\r\n     * Requirements:\r\n     *\r\n     * - `to` cannot be the zero address.\r\n     * - `from` must have a balance of tokens of type `id` of at least `amount`.\r\n     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the\r\n     * acceptance magic value.\r\n     */\r\n    function _safeTransferFrom(\r\n        address from,\r\n        address to,\r\n        uint256 id,\r\n        uint256 amount,\r\n        bytes memory data\r\n    ) internal virtual {\r\n        require(to != address(0), \"ERC1155: transfer to the zero address\");\r\n\r\n        address operator = _msgSender();\r\n\r\n        _beforeTokenTransfer(operator, from, to, _asSingletonArray(id), _asSingletonArray(amount), data);\r\n\r\n        uint256 fromBalance = _balances[id][from];\r\n        require(fromBalance >= amount, \"ERC1155: insufficient balance for transfer\");\r\n        unchecked {\r\n            _balances[id][from] = fromBalance - amount;\r\n        }\r\n        _balances[id][to] += amount;\r\n\r\n        emit TransferSingle(operator, from, to, id, amount);\r\n\r\n        _doSafeTransferAcceptanceCheck(operator, from, to, id, amount, data);\r\n    }\r\n\r\n    /**\r\n     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_safeTransferFrom}.\r\n     *\r\n     * Emits a {TransferBatch} event.\r\n     *\r\n     * Requirements:\r\n     *\r\n     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the\r\n     * acceptance magic value.\r\n     */\r\n    function _safeBatchTransferFrom(\r\n        address from,\r\n        address to,\r\n        uint256[] memory ids,\r\n        uint256[] memory amounts,\r\n        bytes memory data\r\n    ) internal virtual {\r\n        require(ids.length == amounts.length, \"ERC1155: ids and amounts length mismatch\");\r\n        require(to != address(0), \"ERC1155: transfer to the zero address\");\r\n\r\n        address operator = _msgSender();\r\n\r\n        _beforeTokenTransfer(operator, from, to, ids, amounts, data);\r\n\r\n        for (uint256 i = 0; i < ids.length; ++i) {\r\n            uint256 id = ids[i];\r\n            uint256 amount = amounts[i];\r\n\r\n            uint256 fromBalance = _balances[id][from];\r\n            require(fromBalance >= amount, \"ERC1155: insufficient balance for transfer\");\r\n            unchecked {\r\n                _balances[id][from] = fromBalance - amount;\r\n            }\r\n            _balances[id][to] += amount;\r\n        }\r\n\r\n        emit TransferBatch(operator, from, to, ids, amounts);\r\n\r\n        _doSafeBatchTransferAcceptanceCheck(operator, from, to, ids, amounts, data);\r\n    }\r\n\r\n    /**\r\n     * @dev Sets a new URI for all token types, by relying on the token type ID\r\n     * substitution mechanism\r\n     * https://eips.ethereum.org/EIPS/eip-1155#metadata[defined in the EIP].\r\n     *\r\n     * By this mechanism, any occurrence of the `\\{id\\}` substring in either the\r\n     * URI or any of the amounts in the JSON file at said URI will be replaced by\r\n     * clients with the token type ID.\r\n     *\r\n     * For example, the `https://token-cdn-domain/\\{id\\}.json` URI would be\r\n     * interpreted by clients as\r\n     * `https://token-cdn-domain/000000000000000000000000000000000000000000000000000000000004cce0.json`\r\n     * for token type ID 0x4cce0.\r\n     *\r\n     * See {uri}.\r\n     *\r\n     * Because these URIs cannot be meaningfully represented by the {URI} event,\r\n     * this function emits no events.\r\n     */\r\n    function _setURI(string memory newuri) internal virtual {\r\n        _uri = newuri;\r\n    }\r\n\r\n    /**\r\n     * @dev Creates `amount` tokens of token type `id`, and assigns them to `account`.\r\n     *\r\n     * Emits a {TransferSingle} event.\r\n     *\r\n     * Requirements:\r\n     *\r\n     * - `account` cannot be the zero address.\r\n     * - If `account` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the\r\n     * acceptance magic value.\r\n     */\r\n    function _mint(\r\n        address account,\r\n        uint256 id,\r\n        uint256 amount,\r\n        bytes memory data\r\n    ) internal virtual {\r\n        require(account != address(0), \"ERC1155: mint to the zero address\");\r\n\r\n        address operator = _msgSender();\r\n\r\n        _beforeTokenTransfer(operator, address(0), account, _asSingletonArray(id), _asSingletonArray(amount), data);\r\n\r\n        _balances[id][account] += amount;\r\n        emit TransferSingle(operator, address(0), account, id, amount);\r\n\r\n        _doSafeTransferAcceptanceCheck(operator, address(0), account, id, amount, data);\r\n    }\r\n\r\n    /**\r\n     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_mint}.\r\n     *\r\n     * Requirements:\r\n     *\r\n     * - `ids` and `amounts` must have the same length.\r\n     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the\r\n     * acceptance magic value.\r\n     */\r\n    function _mintBatch(\r\n        address to,\r\n        uint256[] memory ids,\r\n        uint256[] memory amounts,\r\n        bytes memory data\r\n    ) internal virtual {\r\n        require(to != address(0), \"ERC1155: mint to the zero address\");\r\n        require(ids.length == amounts.length, \"ERC1155: ids and amounts length mismatch\");\r\n\r\n        address operator = _msgSender();\r\n\r\n        _beforeTokenTransfer(operator, address(0), to, ids, amounts, data);\r\n\r\n        for (uint256 i = 0; i < ids.length; i++) {\r\n            _balances[ids[i]][to] += amounts[i];\r\n        }\r\n\r\n        emit TransferBatch(operator, address(0), to, ids, amounts);\r\n\r\n        _doSafeBatchTransferAcceptanceCheck(operator, address(0), to, ids, amounts, data);\r\n    }\r\n\r\n    /**\r\n     * @dev Destroys `amount` tokens of token type `id` from `account`\r\n     *\r\n     * Requirements:\r\n     *\r\n     * - `account` cannot be the zero address.\r\n     * - `account` must have at least `amount` tokens of token type `id`.\r\n     */\r\n    function _burn(\r\n        address account,\r\n        uint256 id,\r\n        uint256 amount\r\n    ) internal virtual {\r\n        require(account != address(0), \"ERC1155: burn from the zero address\");\r\n\r\n        address operator = _msgSender();\r\n\r\n        _beforeTokenTransfer(operator, account, address(0), _asSingletonArray(id), _asSingletonArray(amount), \"\");\r\n\r\n        uint256 accountBalance = _balances[id][account];\r\n        require(accountBalance >= amount, \"ERC1155: burn amount exceeds balance\");\r\n        unchecked {\r\n            _balances[id][account] = accountBalance - amount;\r\n        }\r\n\r\n        emit TransferSingle(operator, account, address(0), id, amount);\r\n    }\r\n\r\n    /**\r\n     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {_burn}.\r\n     *\r\n     * Requirements:\r\n     *\r\n     * - `ids` and `amounts` must have the same length.\r\n     */\r\n    function _burnBatch(\r\n        address account,\r\n        uint256[] memory ids,\r\n        uint256[] memory amounts\r\n    ) internal virtual {\r\n        require(account != address(0), \"ERC1155: burn from the zero address\");\r\n        require(ids.length == amounts.length, \"ERC1155: ids and amounts length mismatch\");\r\n\r\n        address operator = _msgSender();\r\n\r\n        _beforeTokenTransfer(operator, account, address(0), ids, amounts, \"\");\r\n\r\n        for (uint256 i = 0; i < ids.length; i++) {\r\n            uint256 id = ids[i];\r\n            uint256 amount = amounts[i];\r\n\r\n            uint256 accountBalance = _balances[id][account];\r\n            require(accountBalance >= amount, \"ERC1155: burn amount exceeds balance\");\r\n            unchecked {\r\n                _balances[id][account] = accountBalance - amount;\r\n            }\r\n        }\r\n\r\n        emit TransferBatch(operator, account, address(0), ids, amounts);\r\n    }\r\n\r\n    function _setApprovalForAll(\r\n        address owner,\r\n        address operator,\r\n        bool approved\r\n    ) internal {\r\n        _operatorApprovals[owner][operator] = approved;\r\n        emit ApprovalForAll(owner, operator, approved);\r\n    }\r\n\r\n    /**\r\n     * @dev Hook that is called before any token transfer. This includes minting\r\n     * and burning, as well as batched variants.\r\n     *\r\n     * The same hook is called on both single and batched variants. For single\r\n     * transfers, the length of the `id` and `amount` arrays will be 1.\r\n     *\r\n     * Calling conditions (for each `id` and `amount` pair):\r\n     *\r\n     * - When `from` and `to` are both non-zero, `amount` of ``from``'s tokens\r\n     * of token type `id` will be  transferred to `to`.\r\n     * - When `from` is zero, `amount` tokens of token type `id` will be minted\r\n     * for `to`.\r\n     * - when `to` is zero, `amount` of ``from``'s tokens of token type `id`\r\n     * will be burned.\r\n     * - `from` and `to` are never both zero.\r\n     * - `ids` and `amounts` have the same, non-zero length.\r\n     *\r\n     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].\r\n     */\r\n    function _beforeTokenTransfer(\r\n        address operator,\r\n        address from,\r\n        address to,\r\n        uint256[] memory ids,\r\n        uint256[] memory amounts,\r\n        bytes memory data\r\n    ) internal virtual {}\r\n\r\n    function _doSafeTransferAcceptanceCheck(\r\n        address operator,\r\n        address from,\r\n        address to,\r\n        uint256 id,\r\n        uint256 amount,\r\n        bytes memory data\r\n    ) private {\r\n        if (to.code.length != 0) {\r\n            try IERC1155Receiver(to).onERC1155Received(operator, from, id, amount, data) returns (bytes4 response) {\r\n                if (response != IERC1155Receiver.onERC1155Received.selector) {\r\n                    revert(\"ERC1155: ERC1155Receiver rejected tokens\");\r\n                }\r\n            } catch Error(string memory reason) {\r\n                revert(reason);\r\n            } catch {\r\n                revert(\"ERC1155: transfer to non ERC1155Receiver implementer\");\r\n            }\r\n        }\r\n    }\r\n\r\n    function _doSafeBatchTransferAcceptanceCheck(\r\n        address operator,\r\n        address from,\r\n        address to,\r\n        uint256[] memory ids,\r\n        uint256[] memory amounts,\r\n        bytes memory data\r\n    ) private {\r\n        if (to.code.length != 0) {\r\n            try IERC1155Receiver(to).onERC1155BatchReceived(operator, from, ids, amounts, data) returns (\r\n                bytes4 response\r\n            ) {\r\n                if (response != IERC1155Receiver.onERC1155BatchReceived.selector) {\r\n                    revert(\"ERC1155: ERC1155Receiver rejected tokens\");\r\n                }\r\n            } catch Error(string memory reason) {\r\n                revert(reason);\r\n            } catch {\r\n                revert(\"ERC1155: transfer to non ERC1155Receiver implementer\");\r\n            }\r\n        }\r\n    }\r\n\r\n    function _asSingletonArray(uint256 element) private pure returns (uint256[] memory) {\r\n        uint256[] memory array = new uint256[](1);\r\n        array[0] = element;\r\n\r\n        return array;\r\n    }\r\n}\r\n"
    },
    "contracts/interfaces/IERC1155Permit.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.8.6;\r\n\r\nimport \"@openzeppelin/contracts/token/ERC1155/IERC1155.sol\";\r\n\r\n/// @title   Interface of ERC1155Permit contract\r\n/// @author  Primitive\r\ninterface IERC1155Permit is IERC1155 {\r\n    /// ERRORS ///\r\n\r\n    /// @notice Thrown when the signature has expired\r\n    error SigExpiredError();\r\n\r\n    /// @notice Thrown when the signature is invalid\r\n    error InvalidSigError();\r\n\r\n    /// EFFECT FUNCTIONS ///\r\n\r\n    /// @notice          Grants or revokes the approval for an operator to transfer any of the owner's\r\n    ///                  tokens using their signature\r\n    /// @param owner     Address of the owner\r\n    /// @param operator  Address of the operator\r\n    /// @param approved  True if the approval should be granted, false if revoked\r\n    /// @param deadline  Expiry of the signature, as a timestamp\r\n    /// @param v         Must produce valid secp256k1 signature from the holder along with `r` and `s`\r\n    /// @param r         Must produce valid secp256k1 signature from the holder along with `v` and `s`\r\n    /// @param s         Must produce valid secp256k1 signature from the holder along with `r` and `v`\r\n    function permit(\r\n        address owner,\r\n        address operator,\r\n        bool approved,\r\n        uint256 deadline,\r\n        uint8 v,\r\n        bytes32 r,\r\n        bytes32 s\r\n    ) external;\r\n\r\n    /// VIEW FUNCTIONS ///\r\n\r\n    /// @notice Returns the current nonce of an address\r\n    /// @param owner Address to inspect\r\n    /// @return Current nonce of an address\r\n    function nonces(address owner) external view returns (uint256);\r\n\r\n    /// @notice Returns the domain separator\r\n    /// @return Hash of the domain separator\r\n    function DOMAIN_SEPARATOR() external view returns (bytes32);\r\n}\r\n"
    },
    "@openzeppelin/contracts/utils/cryptography/ECDSA.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.5.0) (utils/cryptography/ECDSA.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../Strings.sol\";\n\n/**\n * @dev Elliptic Curve Digital Signature Algorithm (ECDSA) operations.\n *\n * These functions can be used to verify that a message was signed by the holder\n * of the private keys of a given address.\n */\nlibrary ECDSA {\n    enum RecoverError {\n        NoError,\n        InvalidSignature,\n        InvalidSignatureLength,\n        InvalidSignatureS,\n        InvalidSignatureV\n    }\n\n    function _throwError(RecoverError error) private pure {\n        if (error == RecoverError.NoError) {\n            return; // no error: do nothing\n        } else if (error == RecoverError.InvalidSignature) {\n            revert(\"ECDSA: invalid signature\");\n        } else if (error == RecoverError.InvalidSignatureLength) {\n            revert(\"ECDSA: invalid signature length\");\n        } else if (error == RecoverError.InvalidSignatureS) {\n            revert(\"ECDSA: invalid signature 's' value\");\n        } else if (error == RecoverError.InvalidSignatureV) {\n            revert(\"ECDSA: invalid signature 'v' value\");\n        }\n    }\n\n    /**\n     * @dev Returns the address that signed a hashed message (`hash`) with\n     * `signature` or error string. This address can then be used for verification purposes.\n     *\n     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:\n     * this function rejects them by requiring the `s` value to be in the lower\n     * half order, and the `v` value to be either 27 or 28.\n     *\n     * IMPORTANT: `hash` _must_ be the result of a hash operation for the\n     * verification to be secure: it is possible to craft signatures that\n     * recover to arbitrary addresses for non-hashed data. A safe way to ensure\n     * this is by receiving a hash of the original message (which may otherwise\n     * be too long), and then calling {toEthSignedMessageHash} on it.\n     *\n     * Documentation for signature generation:\n     * - with https://web3js.readthedocs.io/en/v1.3.4/web3-eth-accounts.html#sign[Web3.js]\n     * - with https://docs.ethers.io/v5/api/signer/#Signer-signMessage[ethers]\n     *\n     * _Available since v4.3._\n     */\n    function tryRecover(bytes32 hash, bytes memory signature) internal pure returns (address, RecoverError) {\n        // Check the signature length\n        // - case 65: r,s,v signature (standard)\n        // - case 64: r,vs signature (cf https://eips.ethereum.org/EIPS/eip-2098) _Available since v4.1._\n        if (signature.length == 65) {\n            bytes32 r;\n            bytes32 s;\n            uint8 v;\n            // ecrecover takes the signature parameters, and the only way to get them\n            // currently is to use assembly.\n            assembly {\n                r := mload(add(signature, 0x20))\n                s := mload(add(signature, 0x40))\n                v := byte(0, mload(add(signature, 0x60)))\n            }\n            return tryRecover(hash, v, r, s);\n        } else if (signature.length == 64) {\n            bytes32 r;\n            bytes32 vs;\n            // ecrecover takes the signature parameters, and the only way to get them\n            // currently is to use assembly.\n            assembly {\n                r := mload(add(signature, 0x20))\n                vs := mload(add(signature, 0x40))\n            }\n            return tryRecover(hash, r, vs);\n        } else {\n            return (address(0), RecoverError.InvalidSignatureLength);\n        }\n    }\n\n    /**\n     * @dev Returns the address that signed a hashed message (`hash`) with\n     * `signature`. This address can then be used for verification purposes.\n     *\n     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:\n     * this function rejects them by requiring the `s` value to be in the lower\n     * half order, and the `v` value to be either 27 or 28.\n     *\n     * IMPORTANT: `hash` _must_ be the result of a hash operation for the\n     * verification to be secure: it is possible to craft signatures that\n     * recover to arbitrary addresses for non-hashed data. A safe way to ensure\n     * this is by receiving a hash of the original message (which may otherwise\n     * be too long), and then calling {toEthSignedMessageHash} on it.\n     */\n    function recover(bytes32 hash, bytes memory signature) internal pure returns (address) {\n        (address recovered, RecoverError error) = tryRecover(hash, signature);\n        _throwError(error);\n        return recovered;\n    }\n\n    /**\n     * @dev Overload of {ECDSA-tryRecover} that receives the `r` and `vs` short-signature fields separately.\n     *\n     * See https://eips.ethereum.org/EIPS/eip-2098[EIP-2098 short signatures]\n     *\n     * _Available since v4.3._\n     */\n    function tryRecover(\n        bytes32 hash,\n        bytes32 r,\n        bytes32 vs\n    ) internal pure returns (address, RecoverError) {\n        bytes32 s = vs & bytes32(0x7fffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff);\n        uint8 v = uint8((uint256(vs) >> 255) + 27);\n        return tryRecover(hash, v, r, s);\n    }\n\n    /**\n     * @dev Overload of {ECDSA-recover} that receives the `r and `vs` short-signature fields separately.\n     *\n     * _Available since v4.2._\n     */\n    function recover(\n        bytes32 hash,\n        bytes32 r,\n        bytes32 vs\n    ) internal pure returns (address) {\n        (address recovered, RecoverError error) = tryRecover(hash, r, vs);\n        _throwError(error);\n        return recovered;\n    }\n\n    /**\n     * @dev Overload of {ECDSA-tryRecover} that receives the `v`,\n     * `r` and `s` signature fields separately.\n     *\n     * _Available since v4.3._\n     */\n    function tryRecover(\n        bytes32 hash,\n        uint8 v,\n        bytes32 r,\n        bytes32 s\n    ) internal pure returns (address, RecoverError) {\n        // EIP-2 still allows signature malleability for ecrecover(). Remove this possibility and make the signature\n        // unique. Appendix F in the Ethereum Yellow paper (https://ethereum.github.io/yellowpaper/paper.pdf), defines\n        // the valid range for s in (301): 0 < s < secp256k1n  2 + 1, and for v in (302): v  {27, 28}. Most\n        // signatures from current libraries generate a unique signature with an s-value in the lower half order.\n        //\n        // If your library generates malleable signatures, such as s-values in the upper range, calculate a new s-value\n        // with 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1 and flip v from 27 to 28 or\n        // vice versa. If your library also generates signatures with 0/1 for v instead 27/28, add 27 to v to accept\n        // these malleable signatures as well.\n        if (uint256(s) > 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0) {\n            return (address(0), RecoverError.InvalidSignatureS);\n        }\n        if (v != 27 && v != 28) {\n            return (address(0), RecoverError.InvalidSignatureV);\n        }\n\n        // If the signature is valid (and not malleable), return the signer address\n        address signer = ecrecover(hash, v, r, s);\n        if (signer == address(0)) {\n            return (address(0), RecoverError.InvalidSignature);\n        }\n\n        return (signer, RecoverError.NoError);\n    }\n\n    /**\n     * @dev Overload of {ECDSA-recover} that receives the `v`,\n     * `r` and `s` signature fields separately.\n     */\n    function recover(\n        bytes32 hash,\n        uint8 v,\n        bytes32 r,\n        bytes32 s\n    ) internal pure returns (address) {\n        (address recovered, RecoverError error) = tryRecover(hash, v, r, s);\n        _throwError(error);\n        return recovered;\n    }\n\n    /**\n     * @dev Returns an Ethereum Signed Message, created from a `hash`. This\n     * produces hash corresponding to the one signed with the\n     * https://eth.wiki/json-rpc/API#eth_sign[`eth_sign`]\n     * JSON-RPC method as part of EIP-191.\n     *\n     * See {recover}.\n     */\n    function toEthSignedMessageHash(bytes32 hash) internal pure returns (bytes32) {\n        // 32 is the length in bytes of hash,\n        // enforced by the type signature above\n        return keccak256(abi.encodePacked(\"\\x19Ethereum Signed Message:\\n32\", hash));\n    }\n\n    /**\n     * @dev Returns an Ethereum Signed Message, created from `s`. This\n     * produces hash corresponding to the one signed with the\n     * https://eth.wiki/json-rpc/API#eth_sign[`eth_sign`]\n     * JSON-RPC method as part of EIP-191.\n     *\n     * See {recover}.\n     */\n    function toEthSignedMessageHash(bytes memory s) internal pure returns (bytes32) {\n        return keccak256(abi.encodePacked(\"\\x19Ethereum Signed Message:\\n\", Strings.toString(s.length), s));\n    }\n\n    /**\n     * @dev Returns an Ethereum Signed Typed Data, created from a\n     * `domainSeparator` and a `structHash`. This produces hash corresponding\n     * to the one signed with the\n     * https://eips.ethereum.org/EIPS/eip-712[`eth_signTypedData`]\n     * JSON-RPC method as part of EIP-712.\n     *\n     * See {recover}.\n     */\n    function toTypedDataHash(bytes32 domainSeparator, bytes32 structHash) internal pure returns (bytes32) {\n        return keccak256(abi.encodePacked(\"\\x19\\x01\", domainSeparator, structHash));\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Strings.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/Strings.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev String operations.\n */\nlibrary Strings {\n    bytes16 private constant _HEX_SYMBOLS = \"0123456789abcdef\";\n\n    /**\n     * @dev Converts a `uint256` to its ASCII `string` decimal representation.\n     */\n    function toString(uint256 value) internal pure returns (string memory) {\n        // Inspired by OraclizeAPI's implementation - MIT licence\n        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol\n\n        if (value == 0) {\n            return \"0\";\n        }\n        uint256 temp = value;\n        uint256 digits;\n        while (temp != 0) {\n            digits++;\n            temp /= 10;\n        }\n        bytes memory buffer = new bytes(digits);\n        while (value != 0) {\n            digits -= 1;\n            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));\n            value /= 10;\n        }\n        return string(buffer);\n    }\n\n    /**\n     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.\n     */\n    function toHexString(uint256 value) internal pure returns (string memory) {\n        if (value == 0) {\n            return \"0x00\";\n        }\n        uint256 temp = value;\n        uint256 length = 0;\n        while (temp != 0) {\n            length++;\n            temp >>= 8;\n        }\n        return toHexString(value, length);\n    }\n\n    /**\n     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.\n     */\n    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {\n        bytes memory buffer = new bytes(2 * length + 2);\n        buffer[0] = \"0\";\n        buffer[1] = \"x\";\n        for (uint256 i = 2 * length + 1; i > 1; --i) {\n            buffer[i] = _HEX_SYMBOLS[value & 0xf];\n            value >>= 4;\n        }\n        require(value == 0, \"Strings: hex length insufficient\");\n        return string(buffer);\n    }\n}\n"
    },
    "@openzeppelin/contracts/token/ERC1155/IERC1155.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (token/ERC1155/IERC1155.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../../utils/introspection/IERC165.sol\";\n\n/**\n * @dev Required interface of an ERC1155 compliant contract, as defined in the\n * https://eips.ethereum.org/EIPS/eip-1155[EIP].\n *\n * _Available since v3.1._\n */\ninterface IERC1155 is IERC165 {\n    /**\n     * @dev Emitted when `value` tokens of token type `id` are transferred from `from` to `to` by `operator`.\n     */\n    event TransferSingle(address indexed operator, address indexed from, address indexed to, uint256 id, uint256 value);\n\n    /**\n     * @dev Equivalent to multiple {TransferSingle} events, where `operator`, `from` and `to` are the same for all\n     * transfers.\n     */\n    event TransferBatch(\n        address indexed operator,\n        address indexed from,\n        address indexed to,\n        uint256[] ids,\n        uint256[] values\n    );\n\n    /**\n     * @dev Emitted when `account` grants or revokes permission to `operator` to transfer their tokens, according to\n     * `approved`.\n     */\n    event ApprovalForAll(address indexed account, address indexed operator, bool approved);\n\n    /**\n     * @dev Emitted when the URI for token type `id` changes to `value`, if it is a non-programmatic URI.\n     *\n     * If an {URI} event was emitted for `id`, the standard\n     * https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[guarantees] that `value` will equal the value\n     * returned by {IERC1155MetadataURI-uri}.\n     */\n    event URI(string value, uint256 indexed id);\n\n    /**\n     * @dev Returns the amount of tokens of token type `id` owned by `account`.\n     *\n     * Requirements:\n     *\n     * - `account` cannot be the zero address.\n     */\n    function balanceOf(address account, uint256 id) external view returns (uint256);\n\n    /**\n     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {balanceOf}.\n     *\n     * Requirements:\n     *\n     * - `accounts` and `ids` must have the same length.\n     */\n    function balanceOfBatch(address[] calldata accounts, uint256[] calldata ids)\n        external\n        view\n        returns (uint256[] memory);\n\n    /**\n     * @dev Grants or revokes permission to `operator` to transfer the caller's tokens, according to `approved`,\n     *\n     * Emits an {ApprovalForAll} event.\n     *\n     * Requirements:\n     *\n     * - `operator` cannot be the caller.\n     */\n    function setApprovalForAll(address operator, bool approved) external;\n\n    /**\n     * @dev Returns true if `operator` is approved to transfer ``account``'s tokens.\n     *\n     * See {setApprovalForAll}.\n     */\n    function isApprovedForAll(address account, address operator) external view returns (bool);\n\n    /**\n     * @dev Transfers `amount` tokens of token type `id` from `from` to `to`.\n     *\n     * Emits a {TransferSingle} event.\n     *\n     * Requirements:\n     *\n     * - `to` cannot be the zero address.\n     * - If the caller is not `from`, it must be have been approved to spend ``from``'s tokens via {setApprovalForAll}.\n     * - `from` must have a balance of tokens of type `id` of at least `amount`.\n     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155Received} and return the\n     * acceptance magic value.\n     */\n    function safeTransferFrom(\n        address from,\n        address to,\n        uint256 id,\n        uint256 amount,\n        bytes calldata data\n    ) external;\n\n    /**\n     * @dev xref:ROOT:erc1155.adoc#batch-operations[Batched] version of {safeTransferFrom}.\n     *\n     * Emits a {TransferBatch} event.\n     *\n     * Requirements:\n     *\n     * - `ids` and `amounts` must have the same length.\n     * - If `to` refers to a smart contract, it must implement {IERC1155Receiver-onERC1155BatchReceived} and return the\n     * acceptance magic value.\n     */\n    function safeBatchTransferFrom(\n        address from,\n        address to,\n        uint256[] calldata ids,\n        uint256[] calldata amounts,\n        bytes calldata data\n    ) external;\n}\n"
    },
    "@openzeppelin/contracts/token/ERC1155/IERC1155Receiver.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC1155/IERC1155Receiver.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../../utils/introspection/IERC165.sol\";\n\n/**\n * @dev _Available since v3.1._\n */\ninterface IERC1155Receiver is IERC165 {\n    /**\n     * @dev Handles the receipt of a single ERC1155 token type. This function is\n     * called at the end of a `safeTransferFrom` after the balance has been updated.\n     *\n     * NOTE: To accept the transfer, this must return\n     * `bytes4(keccak256(\"onERC1155Received(address,address,uint256,uint256,bytes)\"))`\n     * (i.e. 0xf23a6e61, or its own function selector).\n     *\n     * @param operator The address which initiated the transfer (i.e. msg.sender)\n     * @param from The address which previously owned the token\n     * @param id The ID of the token being transferred\n     * @param value The amount of tokens being transferred\n     * @param data Additional data with no specified format\n     * @return `bytes4(keccak256(\"onERC1155Received(address,address,uint256,uint256,bytes)\"))` if transfer is allowed\n     */\n    function onERC1155Received(\n        address operator,\n        address from,\n        uint256 id,\n        uint256 value,\n        bytes calldata data\n    ) external returns (bytes4);\n\n    /**\n     * @dev Handles the receipt of a multiple ERC1155 token types. This function\n     * is called at the end of a `safeBatchTransferFrom` after the balances have\n     * been updated.\n     *\n     * NOTE: To accept the transfer(s), this must return\n     * `bytes4(keccak256(\"onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)\"))`\n     * (i.e. 0xbc197c81, or its own function selector).\n     *\n     * @param operator The address which initiated the batch transfer (i.e. msg.sender)\n     * @param from The address which previously owned the token\n     * @param ids An array containing ids of each token being transferred (order and length must match values array)\n     * @param values An array containing amounts of each token being transferred (order and length must match ids array)\n     * @param data Additional data with no specified format\n     * @return `bytes4(keccak256(\"onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)\"))` if transfer is allowed\n     */\n    function onERC1155BatchReceived(\n        address operator,\n        address from,\n        uint256[] calldata ids,\n        uint256[] calldata values,\n        bytes calldata data\n    ) external returns (bytes4);\n}\n"
    },
    "@openzeppelin/contracts/token/ERC1155/extensions/IERC1155MetadataURI.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (token/ERC1155/extensions/IERC1155MetadataURI.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../IERC1155.sol\";\n\n/**\n * @dev Interface of the optional ERC1155MetadataExtension interface, as defined\n * in the https://eips.ethereum.org/EIPS/eip-1155#metadata-extensions[EIP].\n *\n * _Available since v3.1._\n */\ninterface IERC1155MetadataURI is IERC1155 {\n    /**\n     * @dev Returns the URI for token type `id`.\n     *\n     * If the `\\{id\\}` substring is present in the URI, it must be replaced by\n     * clients with the actual token type ID.\n     */\n    function uri(uint256 id) external view returns (string memory);\n}\n"
    },
    "@openzeppelin/contracts/utils/Address.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.5.0) (utils/Address.sol)\n\npragma solidity ^0.8.1;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary Address {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * [IMPORTANT]\n     * ====\n     * It is unsafe to assume that an address for which this function returns\n     * false is an externally-owned account (EOA) and not a contract.\n     *\n     * Among others, `isContract` will return false for the following\n     * types of addresses:\n     *\n     *  - an externally-owned account\n     *  - a contract in construction\n     *  - an address where a contract will be created\n     *  - an address where a contract lived, but was destroyed\n     * ====\n     *\n     * [IMPORTANT]\n     * ====\n     * You shouldn't rely on `isContract` to protect against flash loan attacks!\n     *\n     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets\n     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract\n     * constructor.\n     * ====\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies on extcodesize/address.code.length, which returns 0\n        // for contracts in construction, since the code is only stored at the end\n        // of the constructor execution.\n\n        return account.code.length > 0;\n    }\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        require(address(this).balance >= amount, \"Address: insufficient balance\");\n\n        (bool success, ) = recipient.call{value: amount}(\"\");\n        require(success, \"Address: unable to send value, recipient may have reverted\");\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain `call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason, it is bubbled up by this\n     * function (like regular Solidity function calls).\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionCall(target, data, \"Address: low-level call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with\n     * `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(\n        address target,\n        bytes memory data,\n        uint256 value\n    ) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, value, \"Address: low-level call with value failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but\n     * with `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(\n        address target,\n        bytes memory data,\n        uint256 value,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        require(address(this).balance >= value, \"Address: insufficient balance for call\");\n        require(isContract(target), \"Address: call to non-contract\");\n\n        (bool success, bytes memory returndata) = target.call{value: value}(data);\n        return verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {\n        return functionStaticCall(target, data, \"Address: low-level static call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal view returns (bytes memory) {\n        require(isContract(target), \"Address: static call to non-contract\");\n\n        (bool success, bytes memory returndata) = target.staticcall(data);\n        return verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionDelegateCall(target, data, \"Address: low-level delegate call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        require(isContract(target), \"Address: delegate call to non-contract\");\n\n        (bool success, bytes memory returndata) = target.delegatecall(data);\n        return verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the\n     * revert reason using the provided one.\n     *\n     * _Available since v4.3._\n     */\n    function verifyCallResult(\n        bool success,\n        bytes memory returndata,\n        string memory errorMessage\n    ) internal pure returns (bytes memory) {\n        if (success) {\n            return returndata;\n        } else {\n            // Look for revert reason and bubble it up if present\n            if (returndata.length > 0) {\n                // The easiest way to bubble the revert reason is using memory via assembly\n\n                assembly {\n                    let returndata_size := mload(returndata)\n                    revert(add(32, returndata), returndata_size)\n                }\n            } else {\n                revert(errorMessage);\n            }\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Context.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Provides information about the current execution context, including the\n * sender of the transaction and its data. While these are generally available\n * via msg.sender and msg.data, they should not be accessed in such a direct\n * manner, since when dealing with meta-transactions the account sending and\n * paying for execution may not be the actual sender (as far as an application\n * is concerned).\n *\n * This contract is only required for intermediate, library-like contracts.\n */\nabstract contract Context {\n    function _msgSender() internal view virtual returns (address) {\n        return msg.sender;\n    }\n\n    function _msgData() internal view virtual returns (bytes calldata) {\n        return msg.data;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/introspection/ERC165.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)\n\npragma solidity ^0.8.0;\n\nimport \"./IERC165.sol\";\n\n/**\n * @dev Implementation of the {IERC165} interface.\n *\n * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check\n * for the additional interface id that will be supported. For example:\n *\n * ```solidity\n * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {\n *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);\n * }\n * ```\n *\n * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.\n */\nabstract contract ERC165 is IERC165 {\n    /**\n     * @dev See {IERC165-supportsInterface}.\n     */\n    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {\n        return interfaceId == type(IERC165).interfaceId;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/introspection/IERC165.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Interface of the ERC165 standard, as defined in the\n * https://eips.ethereum.org/EIPS/eip-165[EIP].\n *\n * Implementers can declare support of contract interfaces, which can then be\n * queried by others ({ERC165Checker}).\n *\n * For an implementation, see {ERC165}.\n */\ninterface IERC165 {\n    /**\n     * @dev Returns true if this contract implements the interface defined by\n     * `interfaceId`. See the corresponding\n     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]\n     * to learn more about how these ids are created.\n     *\n     * This function call must use less than 30 000 gas.\n     */\n    function supportsInterface(bytes4 interfaceId) external view returns (bool);\n}\n"
    },
    "@primitivefi/rmm-core/contracts/interfaces/engine/IPrimitiveEngineActions.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.5.0;\r\n\r\n/// @title  Action functions for the Primitive Engine contract\r\n/// @author Primitive\r\ninterface IPrimitiveEngineActions {\r\n    // ===== Pool Updates =====\r\n\r\n    /// @notice             Updates the time until expiry of the pool by setting its last timestamp value\r\n    /// @param  poolId      Keccak256 hash of engine address, strike, sigma, maturity, and gamma\r\n    /// @return lastTimestamp Timestamp loaded into the state of the pool's Calibration.lastTimestamp\r\n    function updateLastTimestamp(bytes32 poolId) external returns (uint32 lastTimestamp);\r\n\r\n    /// @notice             Initializes a curve with parameters in the `calibrations` storage mapping in the Engine\r\n    /// @param  strike      Marginal price of the pool's risky token at maturity, with the same decimals as the stable token, valid [0, 2^128-1]\r\n    /// @param  sigma       AKA Implied Volatility in basis points, determines the price impact of swaps, valid for (1, 10_000_000)\r\n    /// @param  maturity    Timestamp which starts the BUFFER countdown until swaps will cease, in seconds, valid for (block.timestamp, 2^32-1]\r\n    /// @param  gamma       Multiplied against swap in amounts to apply fee, equal to 1 - fee % but units are in basis points, valid for (9_000, 10_000)\r\n    /// @param  riskyPerLp  Risky reserve per liq. with risky decimals, = 1 - N(d1), d1 = (ln(S/K)+(r*^2/2))/, valid for [0, 1e^(risky token decimals))\r\n    /// @param  delLiquidity Amount of liquidity units to allocate to the curve, wei value with 18 decimals of precision\r\n    /// @param  data        Arbitrary data that is passed to the createCallback function\r\n    /// @return poolId      Keccak256 hash of engine address, strike, sigma, maturity, and gamma\r\n    /// @return delRisky    Total amount of risky tokens provided to reserves\r\n    /// @return delStable   Total amount of stable tokens provided to reserves\r\n    function create(\r\n        uint128 strike,\r\n        uint32 sigma,\r\n        uint32 maturity,\r\n        uint32 gamma,\r\n        uint256 riskyPerLp,\r\n        uint256 delLiquidity,\r\n        bytes calldata data\r\n    )\r\n        external\r\n        returns (\r\n            bytes32 poolId,\r\n            uint256 delRisky,\r\n            uint256 delStable\r\n        );\r\n\r\n    // ===== Margin ====\r\n\r\n    /// @notice             Adds risky and/or stable tokens to a `recipient`'s internal balance account\r\n    /// @param  recipient   Recipient margin account of the deposited tokens\r\n    /// @param  delRisky    Amount of risky tokens to deposit\r\n    /// @param  delStable   Amount of stable tokens to deposit\r\n    /// @param  data        Arbitrary data that is passed to the depositCallback function\r\n    function deposit(\r\n        address recipient,\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bytes calldata data\r\n    ) external;\r\n\r\n    /// @notice             Removes risky and/or stable tokens from a `msg.sender`'s internal balance account\r\n    /// @param  recipient   Address that tokens are transferred to\r\n    /// @param  delRisky    Amount of risky tokens to withdraw\r\n    /// @param  delStable   Amount of stable tokens to withdraw\r\n    function withdraw(\r\n        address recipient,\r\n        uint256 delRisky,\r\n        uint256 delStable\r\n    ) external;\r\n\r\n    // ===== Liquidity =====\r\n\r\n    /// @notice             Allocates risky and stable tokens to a specific curve with `poolId`\r\n    /// @param  poolId      Keccak256 hash of engine address, strike, sigma, maturity, and gamma\r\n    /// @param  recipient   Address to give the allocated liquidity to\r\n    /// @param  delRisky    Amount of risky tokens to add\r\n    /// @param  delStable   Amount of stable tokens to add\r\n    /// @param  fromMargin  Whether the `msg.sender` pays with their margin balance, or must send tokens\r\n    /// @param  data        Arbitrary data that is passed to the allocateCallback function\r\n    /// @return delLiquidity Amount of liquidity given to `recipient`\r\n    function allocate(\r\n        bytes32 poolId,\r\n        address recipient,\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bool fromMargin,\r\n        bytes calldata data\r\n    ) external returns (uint256 delLiquidity);\r\n\r\n    /// @notice               Unallocates risky and stable tokens from a specific curve with `poolId`\r\n    /// @param  poolId        Keccak256 hash of engine address, strike, sigma, maturity, and gamma\r\n    /// @param  delLiquidity  Amount of liquidity to remove\r\n    /// @return delRisky      Amount of risky tokens received from removed liquidity\r\n    /// @return delStable     Amount of stable tokens received from removed liquidity\r\n    function remove(bytes32 poolId, uint256 delLiquidity) external returns (uint256 delRisky, uint256 delStable);\r\n\r\n    // ===== Swaps =====\r\n\r\n    /// @notice             Swaps between `risky` and `stable` tokens\r\n    /// @param  recipient   Address that receives output token `deltaOut` amount\r\n    /// @param  poolId      Keccak256 hash of engine address, strike, sigma, maturity, and gamma\r\n    /// @param  riskyForStable If true, swap risky to stable, else swap stable to risky\r\n    /// @param  deltaIn     Amount of tokens to swap in\r\n    /// @param  deltaOut    Amount of tokens to swap out\r\n    /// @param  fromMargin  Whether the `msg.sender` uses their margin balance, or must send tokens\r\n    /// @param  toMargin    Whether the `deltaOut` amount is transferred or deposited into margin\r\n    /// @param  data        Arbitrary data that is passed to the swapCallback function\r\n    function swap(\r\n        address recipient,\r\n        bytes32 poolId,\r\n        bool riskyForStable,\r\n        uint256 deltaIn,\r\n        uint256 deltaOut,\r\n        bool fromMargin,\r\n        bool toMargin,\r\n        bytes calldata data\r\n    ) external;\r\n}\r\n"
    },
    "@primitivefi/rmm-core/contracts/libraries/ReplicationMath.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity ^0.8.4;\r\n\r\nimport \"./ABDKMath64x64.sol\";\r\nimport \"./CumulativeNormalDistribution.sol\";\r\nimport \"./Units.sol\";\r\n\r\n/// @title   Replication Math\r\n/// @author  Primitive\r\n/// @notice  Alex Evans, Guillermo Angeris, and Tarun Chitra. Replicating Market Makers.\r\n///          https://stanford.edu/~guillean/papers/rmms.pdf\r\nlibrary ReplicationMath {\r\n    using ABDKMath64x64 for int128;\r\n    using ABDKMath64x64 for uint256;\r\n    using CumulativeNormalDistribution for int128;\r\n    using Units for int128;\r\n    using Units for uint256;\r\n\r\n    int128 internal constant ONE_INT = 0x10000000000000000;\r\n\r\n    /// @notice         Normalizes volatility with respect to square root of time until expiry\r\n    /// @param   sigma  Unsigned 256-bit percentage as an integer with precision of 1e4, 10000 = 100%\r\n    /// @param   tau    Time until expiry in seconds as an unsigned 256-bit integer\r\n    /// @return  vol    Signed fixed point 64.64 number equal to sigma * sqrt(tau)\r\n    function getProportionalVolatility(uint256 sigma, uint256 tau) internal pure returns (int128 vol) {\r\n        int128 sqrtTauX64 = tau.toYears().sqrt();\r\n        int128 sigmaX64 = sigma.percentageToX64();\r\n        vol = sigmaX64.mul(sqrtTauX64);\r\n    }\r\n\r\n    /// @notice                 Uses riskyPerLiquidity and invariant to calculate stablePerLiquidity\r\n    /// @dev                    Converts unsigned 256-bit values to fixed point 64.64 numbers w/ decimals of precision\r\n    /// @param   invariantLastX64   Signed 64.64 fixed point number. Calculated w/ same `tau` as the parameter `tau`\r\n    /// @param   scaleFactorRisky   Unsigned 256-bit integer scaling factor for `risky`, 10^(18 - risky.decimals())\r\n    /// @param   scaleFactorStable  Unsigned 256-bit integer scaling factor for `stable`, 10^(18 - stable.decimals())\r\n    /// @param   riskyPerLiquidity  Unsigned 256-bit integer of Pool's risky reserves *per liquidity*, 0 <= x <= 1\r\n    /// @param   strike         Unsigned 256-bit integer value with precision equal to 10^(18 - scaleFactorStable)\r\n    /// @param   sigma          Volatility of the Pool as an unsigned 256-bit integer w/ precision of 1e4, 10000 = 100%\r\n    /// @param   tau            Time until expiry in seconds as an unsigned 256-bit integer\r\n    /// @return  stablePerLiquidity = K*CDF(CDF^-1(1 - riskyPerLiquidity) - sigma*sqrt(tau)) + invariantLastX64 as uint\r\n    function getStableGivenRisky(\r\n        int128 invariantLastX64,\r\n        uint256 scaleFactorRisky,\r\n        uint256 scaleFactorStable,\r\n        uint256 riskyPerLiquidity,\r\n        uint256 strike,\r\n        uint256 sigma,\r\n        uint256 tau\r\n    ) internal pure returns (uint256 stablePerLiquidity) {\r\n        int128 strikeX64 = strike.scaleToX64(scaleFactorStable);\r\n        int128 riskyX64 = riskyPerLiquidity.scaleToX64(scaleFactorRisky); // mul by 2^64, div by precision\r\n        int128 oneMinusRiskyX64 = ONE_INT.sub(riskyX64);\r\n        if (tau != 0) {\r\n            int128 volX64 = getProportionalVolatility(sigma, tau);\r\n            int128 phi = oneMinusRiskyX64.getInverseCDF();\r\n            int128 input = phi.sub(volX64);\r\n            int128 stableX64 = strikeX64.mul(input.getCDF()).add(invariantLastX64);\r\n            stablePerLiquidity = stableX64.scaleFromX64(scaleFactorStable);\r\n        } else {\r\n            stablePerLiquidity = (strikeX64.mul(oneMinusRiskyX64).add(invariantLastX64)).scaleFromX64(\r\n                scaleFactorStable\r\n            );\r\n        }\r\n    }\r\n\r\n    /// @notice                 Calculates the invariant of a curve\r\n    /// @dev                    Per unit of replication, aka per unit of liquidity\r\n    /// @param   scaleFactorRisky   Unsigned 256-bit integer scaling factor for `risky`, 10^(18 - risky.decimals())\r\n    /// @param   scaleFactorStable  Unsigned 256-bit integer scaling factor for `stable`, 10^(18 - stable.decimals())\r\n    /// @param   riskyPerLiquidity  Unsigned 256-bit integer of Pool's risky reserves *per liquidity*, 0 <= x <= 1\r\n    /// @param   stablePerLiquidity Unsigned 256-bit integer of Pool's stable reserves *per liquidity*, 0 <= x <= strike\r\n    /// @return  invariantX64       = stablePerLiquidity - K * CDF(CDF^-1(1 - riskyPerLiquidity) - sigma * sqrt(tau))\r\n    function calcInvariant(\r\n        uint256 scaleFactorRisky,\r\n        uint256 scaleFactorStable,\r\n        uint256 riskyPerLiquidity,\r\n        uint256 stablePerLiquidity,\r\n        uint256 strike,\r\n        uint256 sigma,\r\n        uint256 tau\r\n    ) internal pure returns (int128 invariantX64) {\r\n        uint256 output = getStableGivenRisky(\r\n            0,\r\n            scaleFactorRisky,\r\n            scaleFactorStable,\r\n            riskyPerLiquidity,\r\n            strike,\r\n            sigma,\r\n            tau\r\n        );\r\n        int128 outputX64 = output.scaleToX64(scaleFactorStable);\r\n        int128 stableX64 = stablePerLiquidity.scaleToX64(scaleFactorStable);\r\n        invariantX64 = stableX64.sub(outputX64);\r\n    }\r\n}\r\n"
    },
    "contracts/interfaces/ISwapManager.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.8.6;\r\n\r\nimport \"@primitivefi/rmm-core/contracts/interfaces/callback/IPrimitiveSwapCallback.sol\";\r\n\r\n/// @title   Interface of SwapManager contract\r\n/// @author  Primitive\r\ninterface ISwapManager is IPrimitiveSwapCallback {\r\n    /// @notice                Parameters for the swap function\r\n    /// @param recipient       Address of the recipient\r\n    /// @param risky           Address of the risky token\r\n    /// @param stable          Address of the stable token\r\n    /// @param poolId          Id of the pool\r\n    /// @param riskyForStable  True if swapping risky for stable\r\n    /// @param deltaIn         Exact amount to send\r\n    /// @param deltaOut        Exact amount to receive\r\n    /// @param fromMargin      True if the sent amount should be taken from the margin\r\n    /// @param toMargin        True if the received amount should be sent to the margin\r\n    /// @param deadline        Transaction will revert above this deadline\r\n    struct SwapParams {\r\n        address recipient;\r\n        address risky;\r\n        address stable;\r\n        bytes32 poolId;\r\n        bool riskyForStable;\r\n        uint256 deltaIn;\r\n        uint256 deltaOut;\r\n        bool fromMargin;\r\n        bool toMargin;\r\n        uint256 deadline;\r\n    }\r\n\r\n    /// ERRORS ///\r\n\r\n    /// @notice Thrown when the deadline is reached\r\n    error DeadlineReachedError();\r\n\r\n    /// EVENTS ///\r\n\r\n    /// @notice                Emitted when a swap occurs\r\n    /// @param payer           Address of the payer\r\n    /// @param recipient       Address of the recipient\r\n    /// @param engine          Address of the engine\r\n    /// @param poolId          Id of the pool\r\n    /// @param riskyForStable  True if swapping risky for stable\r\n    /// @param deltaIn         Sent amount\r\n    /// @param deltaOut        Received amount\r\n    /// @param fromMargin      True if the sent amount is taken from the margin\r\n    /// @param toMargin        True if the received amount is sent to the margin\r\n    event Swap(\r\n        address indexed payer,\r\n        address recipient,\r\n        address indexed engine,\r\n        bytes32 indexed poolId,\r\n        bool riskyForStable,\r\n        uint256 deltaIn,\r\n        uint256 deltaOut,\r\n        bool fromMargin,\r\n        bool toMargin\r\n    );\r\n\r\n    /// EFFECTS FUNCTIONS ///\r\n\r\n    /// @notice        Swaps an exact amount of risky OR stable tokens for some risky OR stable tokens\r\n    /// @dev           Funds are swapped from a specific pool located into a specific engine\r\n    /// @param params  A struct of type SwapParameters\r\n    function swap(SwapParams calldata params) external payable;\r\n}\r\n"
    },
    "contracts/base/MarginManager.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity 0.8.6;\r\n\r\nimport \"@primitivefi/rmm-core/contracts/interfaces/engine/IPrimitiveEngineActions.sol\";\r\nimport \"@primitivefi/rmm-core/contracts/interfaces/engine/IPrimitiveEngineView.sol\";\r\nimport \"../interfaces/IMarginManager.sol\";\r\nimport \"./CashManager.sol\";\r\nimport \"../libraries/TransferHelper.sol\";\r\nimport \"../libraries/Margin.sol\";\r\n\r\n/// @title   MarginManager contract\r\n/// @author  Primitive\r\n/// @notice  Manages the margins\r\nabstract contract MarginManager is IMarginManager, CashManager {\r\n    using TransferHelper for IERC20;\r\n    using Margin for Margin.Data;\r\n\r\n    /// @inheritdoc IMarginManager\r\n    mapping(address => mapping(address => Margin.Data)) public override margins;\r\n\r\n    /// EFFECT FUNCTIONS ///\r\n\r\n    /// @inheritdoc IMarginManager\r\n    function deposit(\r\n        address recipient,\r\n        address risky,\r\n        address stable,\r\n        uint256 delRisky,\r\n        uint256 delStable\r\n    ) external payable override lock {\r\n        if (delRisky == 0 && delStable == 0) revert ZeroDelError();\r\n\r\n        address engine = EngineAddress.computeAddress(factory, risky, stable);\r\n        if (engine.code.length == 0) revert EngineAddress.EngineNotDeployedError();\r\n\r\n        IPrimitiveEngineActions(engine).deposit(\r\n            address(this),\r\n            delRisky,\r\n            delStable,\r\n            abi.encode(CallbackData({payer: msg.sender, risky: risky, stable: stable}))\r\n        );\r\n\r\n        margins[recipient][engine].deposit(delRisky, delStable);\r\n\r\n        emit Deposit(msg.sender, recipient, engine, risky, stable, delRisky, delStable);\r\n    }\r\n\r\n    /// @inheritdoc IMarginManager\r\n    function withdraw(\r\n        address recipient,\r\n        address engine,\r\n        uint256 delRisky,\r\n        uint256 delStable\r\n    ) external override lock {\r\n        if (delRisky == 0 && delStable == 0) revert ZeroDelError();\r\n\r\n        // Reverts the call early if margins are insufficient\r\n        margins[msg.sender][engine].withdraw(delRisky, delStable);\r\n\r\n        // Setting address(0) as the recipient will result in the tokens\r\n        // being sent into the contract itself, useful to unwrap WETH for example\r\n        IPrimitiveEngineActions(engine).withdraw(\r\n            recipient == address(0) ? address(this) : recipient,\r\n            delRisky,\r\n            delStable\r\n        );\r\n\r\n        emit Withdraw(\r\n            msg.sender,\r\n            recipient == address(0) ? msg.sender : recipient,\r\n            engine,\r\n            IPrimitiveEngineView(engine).risky(),\r\n            IPrimitiveEngineView(engine).stable(),\r\n            delRisky,\r\n            delStable\r\n        );\r\n    }\r\n\r\n    /// CALLBACK IMPLEMENTATIONS ///\r\n\r\n    /// @inheritdoc IPrimitiveDepositCallback\r\n    function depositCallback(\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bytes calldata data\r\n    ) external override {\r\n        CallbackData memory decoded = abi.decode(data, (CallbackData));\r\n\r\n        address engine = EngineAddress.computeAddress(factory, decoded.risky, decoded.stable);\r\n        if (msg.sender != engine) revert NotEngineError();\r\n\r\n        if (delStable != 0) pay(decoded.stable, decoded.payer, msg.sender, delStable);\r\n        if (delRisky != 0) pay(decoded.risky, decoded.payer, msg.sender, delRisky);\r\n    }\r\n}\r\n"
    },
    "@primitivefi/rmm-core/contracts/libraries/ABDKMath64x64.sol": {
      "content": "// SPDX-License-Identifier: BSD-4-Clause\r\n/*\r\n * ABDK Math 64.64 Smart Contract Library.  Copyright  2019 by ABDK Consulting.\r\n * Author: Mikhail Vladimirov <mikhail.vladimirov@gmail.com>\r\n */\r\npragma solidity ^0.8.0;\r\n\r\n/**\r\n * Smart contract library of mathematical functions operating with signed\r\n * 64.64-bit fixed point numbers.  Signed 64.64-bit fixed point number is\r\n * basically a simple fraction whose numerator is signed 128-bit integer and\r\n * denominator is 2^64.  As long as denominator is always the same, there is no\r\n * need to store it, thus in Solidity signed 64.64-bit fixed point numbers are\r\n * represented by int128 type holding only the numerator.\r\n */\r\nlibrary ABDKMath64x64 {\r\n    /*\r\n     * Minimum value signed 64.64-bit fixed point number may have.\r\n     */\r\n    int128 private constant MIN_64x64 = -0x80000000000000000000000000000000;\r\n\r\n    /*\r\n     * Maximum value signed 64.64-bit fixed point number may have.\r\n     */\r\n    int128 private constant MAX_64x64 = 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF;\r\n\r\n    /**\r\n     * Convert signed 256-bit integer number into signed 64.64-bit fixed point\r\n     * number.  Revert on overflow.\r\n     *\r\n     * @param x signed 256-bit integer number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function fromInt(int256 x) internal pure returns (int128) {\r\n        unchecked {\r\n            require(x >= -0x8000000000000000 && x <= 0x7FFFFFFFFFFFFFFF);\r\n            return int128(x << 64);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Convert signed 64.64 fixed point number into signed 64-bit integer number\r\n     * rounding down.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @return signed 64-bit integer number\r\n     */\r\n    function toInt(int128 x) internal pure returns (int64) {\r\n        unchecked {\r\n            return int64(x >> 64);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Convert unsigned 256-bit integer number into signed 64.64-bit fixed point\r\n     * number.  Revert on overflow.\r\n     *\r\n     * @param x unsigned 256-bit integer number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function fromUInt(uint256 x) internal pure returns (int128) {\r\n        unchecked {\r\n            require(x <= 0x7FFFFFFFFFFFFFFF);\r\n            return int128(int256(x << 64));\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Convert signed 64.64 fixed point number into unsigned 64-bit integer\r\n     * number rounding down.  Revert on underflow.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @return unsigned 64-bit integer number\r\n     */\r\n    function toUInt(int128 x) internal pure returns (uint64) {\r\n        unchecked {\r\n            require(x >= 0);\r\n            return uint64(uint128(x >> 64));\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Convert signed 128.128 fixed point number into signed 64.64-bit fixed point\r\n     * number rounding down.  Revert on overflow.\r\n     *\r\n     * @param x signed 128.128-bin fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function from128x128(int256 x) internal pure returns (int128) {\r\n        unchecked {\r\n            int256 result = x >> 64;\r\n            require(result >= MIN_64x64 && result <= MAX_64x64);\r\n            return int128(result);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Convert signed 64.64 fixed point number into signed 128.128 fixed point\r\n     * number.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @return signed 128.128 fixed point number\r\n     */\r\n    function to128x128(int128 x) internal pure returns (int256) {\r\n        unchecked {\r\n            return int256(x) << 64;\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate x + y.  Revert on overflow.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @param y signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function add(int128 x, int128 y) internal pure returns (int128) {\r\n        unchecked {\r\n            int256 result = int256(x) + y;\r\n            require(result >= MIN_64x64 && result <= MAX_64x64);\r\n            return int128(result);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate x - y.  Revert on overflow.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @param y signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function sub(int128 x, int128 y) internal pure returns (int128) {\r\n        unchecked {\r\n            int256 result = int256(x) - y;\r\n            require(result >= MIN_64x64 && result <= MAX_64x64);\r\n            return int128(result);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate x * y rounding down.  Revert on overflow.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @param y signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function mul(int128 x, int128 y) internal pure returns (int128) {\r\n        unchecked {\r\n            int256 result = (int256(x) * y) >> 64;\r\n            require(result >= MIN_64x64 && result <= MAX_64x64);\r\n            return int128(result);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate x * y rounding towards zero, where x is signed 64.64 fixed point\r\n     * number and y is signed 256-bit integer number.  Revert on overflow.\r\n     *\r\n     * @param x signed 64.64 fixed point number\r\n     * @param y signed 256-bit integer number\r\n     * @return signed 256-bit integer number\r\n     */\r\n    function muli(int128 x, int256 y) internal pure returns (int256) {\r\n        unchecked {\r\n            if (x == MIN_64x64) {\r\n                require(\r\n                    y >= -0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF &&\r\n                        y <= 0x1000000000000000000000000000000000000000000000000\r\n                );\r\n                return -y << 63;\r\n            } else {\r\n                bool negativeResult = false;\r\n                if (x < 0) {\r\n                    x = -x;\r\n                    negativeResult = true;\r\n                }\r\n                if (y < 0) {\r\n                    y = -y; // We rely on overflow behavior here\r\n                    negativeResult = !negativeResult;\r\n                }\r\n                uint256 absoluteResult = mulu(x, uint256(y));\r\n                if (negativeResult) {\r\n                    require(absoluteResult <= 0x8000000000000000000000000000000000000000000000000000000000000000);\r\n                    return -int256(absoluteResult); // We rely on overflow behavior here\r\n                } else {\r\n                    require(absoluteResult <= 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF);\r\n                    return int256(absoluteResult);\r\n                }\r\n            }\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate x * y rounding down, where x is signed 64.64 fixed point number\r\n     * and y is unsigned 256-bit integer number.  Revert on overflow.\r\n     *\r\n     * @param x signed 64.64 fixed point number\r\n     * @param y unsigned 256-bit integer number\r\n     * @return unsigned 256-bit integer number\r\n     */\r\n    function mulu(int128 x, uint256 y) internal pure returns (uint256) {\r\n        unchecked {\r\n            if (y == 0) return 0;\r\n\r\n            require(x >= 0);\r\n\r\n            uint256 lo = (uint256(int256(x)) * (y & 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF)) >> 64;\r\n            uint256 hi = uint256(int256(x)) * (y >> 128);\r\n\r\n            require(hi <= 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF);\r\n            hi <<= 64;\r\n\r\n            require(hi <= 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF - lo);\r\n            return hi + lo;\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate x / y rounding towards zero.  Revert on overflow or when y is\r\n     * zero.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @param y signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function div(int128 x, int128 y) internal pure returns (int128) {\r\n        unchecked {\r\n            require(y != 0);\r\n            int256 result = (int256(x) << 64) / y;\r\n            require(result >= MIN_64x64 && result <= MAX_64x64);\r\n            return int128(result);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate x / y rounding towards zero, where x and y are signed 256-bit\r\n     * integer numbers.  Revert on overflow or when y is zero.\r\n     *\r\n     * @param x signed 256-bit integer number\r\n     * @param y signed 256-bit integer number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function divi(int256 x, int256 y) internal pure returns (int128) {\r\n        unchecked {\r\n            require(y != 0);\r\n\r\n            bool negativeResult = false;\r\n            if (x < 0) {\r\n                x = -x; // We rely on overflow behavior here\r\n                negativeResult = true;\r\n            }\r\n            if (y < 0) {\r\n                y = -y; // We rely on overflow behavior here\r\n                negativeResult = !negativeResult;\r\n            }\r\n            uint128 absoluteResult = divuu(uint256(x), uint256(y));\r\n            if (negativeResult) {\r\n                require(absoluteResult <= 0x80000000000000000000000000000000);\r\n                return -int128(absoluteResult); // We rely on overflow behavior here\r\n            } else {\r\n                require(absoluteResult <= 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF);\r\n                return int128(absoluteResult); // We rely on overflow behavior here\r\n            }\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate x / y rounding towards zero, where x and y are unsigned 256-bit\r\n     * integer numbers.  Revert on overflow or when y is zero.\r\n     *\r\n     * @param x unsigned 256-bit integer number\r\n     * @param y unsigned 256-bit integer number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function divu(uint256 x, uint256 y) internal pure returns (int128) {\r\n        unchecked {\r\n            require(y != 0);\r\n            uint128 result = divuu(x, y);\r\n            require(result <= uint128(MAX_64x64));\r\n            return int128(result);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate -x.  Revert on overflow.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function neg(int128 x) internal pure returns (int128) {\r\n        unchecked {\r\n            require(x != MIN_64x64);\r\n            return -x;\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate |x|.  Revert on overflow.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function abs(int128 x) internal pure returns (int128) {\r\n        unchecked {\r\n            require(x != MIN_64x64);\r\n            return x < 0 ? -x : x;\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate 1 / x rounding towards zero.  Revert on overflow or when x is\r\n     * zero.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function inv(int128 x) internal pure returns (int128) {\r\n        unchecked {\r\n            require(x != 0);\r\n            int256 result = int256(0x100000000000000000000000000000000) / x;\r\n            require(result >= MIN_64x64 && result <= MAX_64x64);\r\n            return int128(result);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate arithmetics average of x and y, i.e. (x + y) / 2 rounding down.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @param y signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function avg(int128 x, int128 y) internal pure returns (int128) {\r\n        unchecked {\r\n            return int128((int256(x) + int256(y)) >> 1);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate geometric average of x and y, i.e. sqrt (x * y) rounding down.\r\n     * Revert on overflow or in case x * y is negative.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @param y signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function gavg(int128 x, int128 y) internal pure returns (int128) {\r\n        unchecked {\r\n            int256 m = int256(x) * int256(y);\r\n            require(m >= 0);\r\n            require(m < 0x4000000000000000000000000000000000000000000000000000000000000000);\r\n            return int128(sqrtu(uint256(m)));\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate x^y assuming 0^0 is 1, where x is signed 64.64 fixed point number\r\n     * and y is unsigned 256-bit integer number.  Revert on overflow.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @param y uint256 value\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function pow(int128 x, uint256 y) internal pure returns (int128) {\r\n        unchecked {\r\n            bool negative = x < 0 && y & 1 == 1;\r\n\r\n            uint256 absX = uint128(x < 0 ? -x : x);\r\n            uint256 absResult;\r\n            absResult = 0x100000000000000000000000000000000;\r\n\r\n            if (absX <= 0x10000000000000000) {\r\n                absX <<= 63;\r\n                while (y != 0) {\r\n                    if (y & 0x1 != 0) {\r\n                        absResult = (absResult * absX) >> 127;\r\n                    }\r\n                    absX = (absX * absX) >> 127;\r\n\r\n                    if (y & 0x2 != 0) {\r\n                        absResult = (absResult * absX) >> 127;\r\n                    }\r\n                    absX = (absX * absX) >> 127;\r\n\r\n                    if (y & 0x4 != 0) {\r\n                        absResult = (absResult * absX) >> 127;\r\n                    }\r\n                    absX = (absX * absX) >> 127;\r\n\r\n                    if (y & 0x8 != 0) {\r\n                        absResult = (absResult * absX) >> 127;\r\n                    }\r\n                    absX = (absX * absX) >> 127;\r\n\r\n                    y >>= 4;\r\n                }\r\n\r\n                absResult >>= 64;\r\n            } else {\r\n                uint256 absXShift = 63;\r\n                if (absX < 0x1000000000000000000000000) {\r\n                    absX <<= 32;\r\n                    absXShift -= 32;\r\n                }\r\n                if (absX < 0x10000000000000000000000000000) {\r\n                    absX <<= 16;\r\n                    absXShift -= 16;\r\n                }\r\n                if (absX < 0x1000000000000000000000000000000) {\r\n                    absX <<= 8;\r\n                    absXShift -= 8;\r\n                }\r\n                if (absX < 0x10000000000000000000000000000000) {\r\n                    absX <<= 4;\r\n                    absXShift -= 4;\r\n                }\r\n                if (absX < 0x40000000000000000000000000000000) {\r\n                    absX <<= 2;\r\n                    absXShift -= 2;\r\n                }\r\n                if (absX < 0x80000000000000000000000000000000) {\r\n                    absX <<= 1;\r\n                    absXShift -= 1;\r\n                }\r\n\r\n                uint256 resultShift = 0;\r\n                while (y != 0) {\r\n                    require(absXShift < 64);\r\n\r\n                    if (y & 0x1 != 0) {\r\n                        absResult = (absResult * absX) >> 127;\r\n                        resultShift += absXShift;\r\n                        if (absResult > 0x100000000000000000000000000000000) {\r\n                            absResult >>= 1;\r\n                            resultShift += 1;\r\n                        }\r\n                    }\r\n                    absX = (absX * absX) >> 127;\r\n                    absXShift <<= 1;\r\n                    if (absX >= 0x100000000000000000000000000000000) {\r\n                        absX >>= 1;\r\n                        absXShift += 1;\r\n                    }\r\n\r\n                    y >>= 1;\r\n                }\r\n\r\n                require(resultShift < 64);\r\n                absResult >>= 64 - resultShift;\r\n            }\r\n            int256 result = negative ? -int256(absResult) : int256(absResult);\r\n            require(result >= MIN_64x64 && result <= MAX_64x64);\r\n            return int128(result);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate sqrt (x) rounding down.  Revert if x < 0.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function sqrt(int128 x) internal pure returns (int128) {\r\n        unchecked {\r\n            require(x >= 0);\r\n            return int128(sqrtu(uint256(int256(x)) << 64));\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate binary logarithm of x.  Revert if x <= 0.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function log_2(int128 x) internal pure returns (int128) {\r\n        unchecked {\r\n            require(x > 0);\r\n\r\n            int256 msb = 0;\r\n            int256 xc = x;\r\n            if (xc >= 0x10000000000000000) {\r\n                xc >>= 64;\r\n                msb += 64;\r\n            }\r\n            if (xc >= 0x100000000) {\r\n                xc >>= 32;\r\n                msb += 32;\r\n            }\r\n            if (xc >= 0x10000) {\r\n                xc >>= 16;\r\n                msb += 16;\r\n            }\r\n            if (xc >= 0x100) {\r\n                xc >>= 8;\r\n                msb += 8;\r\n            }\r\n            if (xc >= 0x10) {\r\n                xc >>= 4;\r\n                msb += 4;\r\n            }\r\n            if (xc >= 0x4) {\r\n                xc >>= 2;\r\n                msb += 2;\r\n            }\r\n            if (xc >= 0x2) msb += 1; // No need to shift xc anymore\r\n\r\n            int256 result = (msb - 64) << 64;\r\n            uint256 ux = uint256(int256(x)) << uint256(127 - msb);\r\n            for (int256 bit = 0x8000000000000000; bit > 0; bit >>= 1) {\r\n                ux *= ux;\r\n                uint256 b = ux >> 255;\r\n                ux >>= 127 + b;\r\n                result += bit * int256(b);\r\n            }\r\n\r\n            return int128(result);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate natural logarithm of x.  Revert if x <= 0.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function ln(int128 x) internal pure returns (int128) {\r\n        unchecked {\r\n            require(x > 0);\r\n\r\n            return int128(int256((uint256(int256(log_2(x))) * 0xB17217F7D1CF79ABC9E3B39803F2F6AF) >> 128));\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate binary exponent of x.  Revert on overflow.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function exp_2(int128 x) internal pure returns (int128) {\r\n        unchecked {\r\n            require(x < 0x400000000000000000); // Overflow\r\n\r\n            if (x < -0x400000000000000000) return 0; // Underflow\r\n\r\n            uint256 result = 0x80000000000000000000000000000000;\r\n\r\n            if (x & 0x8000000000000000 > 0) result = (result * 0x16A09E667F3BCC908B2FB1366EA957D3E) >> 128;\r\n            if (x & 0x4000000000000000 > 0) result = (result * 0x1306FE0A31B7152DE8D5A46305C85EDEC) >> 128;\r\n            if (x & 0x2000000000000000 > 0) result = (result * 0x1172B83C7D517ADCDF7C8C50EB14A791F) >> 128;\r\n            if (x & 0x1000000000000000 > 0) result = (result * 0x10B5586CF9890F6298B92B71842A98363) >> 128;\r\n            if (x & 0x800000000000000 > 0) result = (result * 0x1059B0D31585743AE7C548EB68CA417FD) >> 128;\r\n            if (x & 0x400000000000000 > 0) result = (result * 0x102C9A3E778060EE6F7CACA4F7A29BDE8) >> 128;\r\n            if (x & 0x200000000000000 > 0) result = (result * 0x10163DA9FB33356D84A66AE336DCDFA3F) >> 128;\r\n            if (x & 0x100000000000000 > 0) result = (result * 0x100B1AFA5ABCBED6129AB13EC11DC9543) >> 128;\r\n            if (x & 0x80000000000000 > 0) result = (result * 0x10058C86DA1C09EA1FF19D294CF2F679B) >> 128;\r\n            if (x & 0x40000000000000 > 0) result = (result * 0x1002C605E2E8CEC506D21BFC89A23A00F) >> 128;\r\n            if (x & 0x20000000000000 > 0) result = (result * 0x100162F3904051FA128BCA9C55C31E5DF) >> 128;\r\n            if (x & 0x10000000000000 > 0) result = (result * 0x1000B175EFFDC76BA38E31671CA939725) >> 128;\r\n            if (x & 0x8000000000000 > 0) result = (result * 0x100058BA01FB9F96D6CACD4B180917C3D) >> 128;\r\n            if (x & 0x4000000000000 > 0) result = (result * 0x10002C5CC37DA9491D0985C348C68E7B3) >> 128;\r\n            if (x & 0x2000000000000 > 0) result = (result * 0x1000162E525EE054754457D5995292026) >> 128;\r\n            if (x & 0x1000000000000 > 0) result = (result * 0x10000B17255775C040618BF4A4ADE83FC) >> 128;\r\n            if (x & 0x800000000000 > 0) result = (result * 0x1000058B91B5BC9AE2EED81E9B7D4CFAB) >> 128;\r\n            if (x & 0x400000000000 > 0) result = (result * 0x100002C5C89D5EC6CA4D7C8ACC017B7C9) >> 128;\r\n            if (x & 0x200000000000 > 0) result = (result * 0x10000162E43F4F831060E02D839A9D16D) >> 128;\r\n            if (x & 0x100000000000 > 0) result = (result * 0x100000B1721BCFC99D9F890EA06911763) >> 128;\r\n            if (x & 0x80000000000 > 0) result = (result * 0x10000058B90CF1E6D97F9CA14DBCC1628) >> 128;\r\n            if (x & 0x40000000000 > 0) result = (result * 0x1000002C5C863B73F016468F6BAC5CA2B) >> 128;\r\n            if (x & 0x20000000000 > 0) result = (result * 0x100000162E430E5A18F6119E3C02282A5) >> 128;\r\n            if (x & 0x10000000000 > 0) result = (result * 0x1000000B1721835514B86E6D96EFD1BFE) >> 128;\r\n            if (x & 0x8000000000 > 0) result = (result * 0x100000058B90C0B48C6BE5DF846C5B2EF) >> 128;\r\n            if (x & 0x4000000000 > 0) result = (result * 0x10000002C5C8601CC6B9E94213C72737A) >> 128;\r\n            if (x & 0x2000000000 > 0) result = (result * 0x1000000162E42FFF037DF38AA2B219F06) >> 128;\r\n            if (x & 0x1000000000 > 0) result = (result * 0x10000000B17217FBA9C739AA5819F44F9) >> 128;\r\n            if (x & 0x800000000 > 0) result = (result * 0x1000000058B90BFCDEE5ACD3C1CEDC823) >> 128;\r\n            if (x & 0x400000000 > 0) result = (result * 0x100000002C5C85FE31F35A6A30DA1BE50) >> 128;\r\n            if (x & 0x200000000 > 0) result = (result * 0x10000000162E42FF0999CE3541B9FFFCF) >> 128;\r\n            if (x & 0x100000000 > 0) result = (result * 0x100000000B17217F80F4EF5AADDA45554) >> 128;\r\n            if (x & 0x80000000 > 0) result = (result * 0x10000000058B90BFBF8479BD5A81B51AD) >> 128;\r\n            if (x & 0x40000000 > 0) result = (result * 0x1000000002C5C85FDF84BD62AE30A74CC) >> 128;\r\n            if (x & 0x20000000 > 0) result = (result * 0x100000000162E42FEFB2FED257559BDAA) >> 128;\r\n            if (x & 0x10000000 > 0) result = (result * 0x1000000000B17217F7D5A7716BBA4A9AE) >> 128;\r\n            if (x & 0x8000000 > 0) result = (result * 0x100000000058B90BFBE9DDBAC5E109CCE) >> 128;\r\n            if (x & 0x4000000 > 0) result = (result * 0x10000000002C5C85FDF4B15DE6F17EB0D) >> 128;\r\n            if (x & 0x2000000 > 0) result = (result * 0x1000000000162E42FEFA494F1478FDE05) >> 128;\r\n            if (x & 0x1000000 > 0) result = (result * 0x10000000000B17217F7D20CF927C8E94C) >> 128;\r\n            if (x & 0x800000 > 0) result = (result * 0x1000000000058B90BFBE8F71CB4E4B33D) >> 128;\r\n            if (x & 0x400000 > 0) result = (result * 0x100000000002C5C85FDF477B662B26945) >> 128;\r\n            if (x & 0x200000 > 0) result = (result * 0x10000000000162E42FEFA3AE53369388C) >> 128;\r\n            if (x & 0x100000 > 0) result = (result * 0x100000000000B17217F7D1D351A389D40) >> 128;\r\n            if (x & 0x80000 > 0) result = (result * 0x10000000000058B90BFBE8E8B2D3D4EDE) >> 128;\r\n            if (x & 0x40000 > 0) result = (result * 0x1000000000002C5C85FDF4741BEA6E77E) >> 128;\r\n            if (x & 0x20000 > 0) result = (result * 0x100000000000162E42FEFA39FE95583C2) >> 128;\r\n            if (x & 0x10000 > 0) result = (result * 0x1000000000000B17217F7D1CFB72B45E1) >> 128;\r\n            if (x & 0x8000 > 0) result = (result * 0x100000000000058B90BFBE8E7CC35C3F0) >> 128;\r\n            if (x & 0x4000 > 0) result = (result * 0x10000000000002C5C85FDF473E242EA38) >> 128;\r\n            if (x & 0x2000 > 0) result = (result * 0x1000000000000162E42FEFA39F02B772C) >> 128;\r\n            if (x & 0x1000 > 0) result = (result * 0x10000000000000B17217F7D1CF7D83C1A) >> 128;\r\n            if (x & 0x800 > 0) result = (result * 0x1000000000000058B90BFBE8E7BDCBE2E) >> 128;\r\n            if (x & 0x400 > 0) result = (result * 0x100000000000002C5C85FDF473DEA871F) >> 128;\r\n            if (x & 0x200 > 0) result = (result * 0x10000000000000162E42FEFA39EF44D91) >> 128;\r\n            if (x & 0x100 > 0) result = (result * 0x100000000000000B17217F7D1CF79E949) >> 128;\r\n            if (x & 0x80 > 0) result = (result * 0x10000000000000058B90BFBE8E7BCE544) >> 128;\r\n            if (x & 0x40 > 0) result = (result * 0x1000000000000002C5C85FDF473DE6ECA) >> 128;\r\n            if (x & 0x20 > 0) result = (result * 0x100000000000000162E42FEFA39EF366F) >> 128;\r\n            if (x & 0x10 > 0) result = (result * 0x1000000000000000B17217F7D1CF79AFA) >> 128;\r\n            if (x & 0x8 > 0) result = (result * 0x100000000000000058B90BFBE8E7BCD6D) >> 128;\r\n            if (x & 0x4 > 0) result = (result * 0x10000000000000002C5C85FDF473DE6B2) >> 128;\r\n            if (x & 0x2 > 0) result = (result * 0x1000000000000000162E42FEFA39EF358) >> 128;\r\n            if (x & 0x1 > 0) result = (result * 0x10000000000000000B17217F7D1CF79AB) >> 128;\r\n\r\n            result >>= uint256(int256(63 - (x >> 64)));\r\n            require(result <= uint256(int256(MAX_64x64)));\r\n\r\n            return int128(int256(result));\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate natural exponent of x.  Revert on overflow.\r\n     *\r\n     * @param x signed 64.64-bit fixed point number\r\n     * @return signed 64.64-bit fixed point number\r\n     */\r\n    function exp(int128 x) internal pure returns (int128) {\r\n        unchecked {\r\n            require(x < 0x400000000000000000); // Overflow\r\n\r\n            if (x < -0x400000000000000000) return 0; // Underflow\r\n\r\n            return exp_2(int128((int256(x) * 0x171547652B82FE1777D0FFDA0D23A7D12) >> 128));\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate x / y rounding towards zero, where x and y are unsigned 256-bit\r\n     * integer numbers.  Revert on overflow or when y is zero.\r\n     *\r\n     * @param x unsigned 256-bit integer number\r\n     * @param y unsigned 256-bit integer number\r\n     * @return unsigned 64.64-bit fixed point number\r\n     */\r\n    function divuu(uint256 x, uint256 y) private pure returns (uint128) {\r\n        unchecked {\r\n            require(y != 0);\r\n\r\n            uint256 result;\r\n\r\n            if (x <= 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF) result = (x << 64) / y;\r\n            else {\r\n                uint256 msb = 192;\r\n                uint256 xc = x >> 192;\r\n                if (xc >= 0x100000000) {\r\n                    xc >>= 32;\r\n                    msb += 32;\r\n                }\r\n                if (xc >= 0x10000) {\r\n                    xc >>= 16;\r\n                    msb += 16;\r\n                }\r\n                if (xc >= 0x100) {\r\n                    xc >>= 8;\r\n                    msb += 8;\r\n                }\r\n                if (xc >= 0x10) {\r\n                    xc >>= 4;\r\n                    msb += 4;\r\n                }\r\n                if (xc >= 0x4) {\r\n                    xc >>= 2;\r\n                    msb += 2;\r\n                }\r\n                if (xc >= 0x2) msb += 1; // No need to shift xc anymore\r\n\r\n                result = (x << (255 - msb)) / (((y - 1) >> (msb - 191)) + 1);\r\n                require(result <= 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF);\r\n\r\n                uint256 hi = result * (y >> 128);\r\n                uint256 lo = result * (y & 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF);\r\n\r\n                uint256 xh = x >> 192;\r\n                uint256 xl = x << 64;\r\n\r\n                if (xl < lo) xh -= 1;\r\n                xl -= lo; // We rely on overflow behavior here\r\n                lo = hi << 128;\r\n                if (xl < lo) xh -= 1;\r\n                xl -= lo; // We rely on overflow behavior here\r\n\r\n                assert(xh == hi >> 128);\r\n\r\n                result += xl / y;\r\n            }\r\n\r\n            require(result <= 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF);\r\n            return uint128(result);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * Calculate sqrt (x) rounding down, where x is unsigned 256-bit integer\r\n     * number.\r\n     *\r\n     * @param x unsigned 256-bit integer number\r\n     * @return unsigned 128-bit integer number\r\n     */\r\n    function sqrtu(uint256 x) private pure returns (uint128) {\r\n        unchecked {\r\n            if (x == 0) return 0;\r\n            else {\r\n                uint256 xx = x;\r\n                uint256 r = 1;\r\n                if (xx >= 0x100000000000000000000000000000000) {\r\n                    xx >>= 128;\r\n                    r <<= 64;\r\n                }\r\n                if (xx >= 0x10000000000000000) {\r\n                    xx >>= 64;\r\n                    r <<= 32;\r\n                }\r\n                if (xx >= 0x100000000) {\r\n                    xx >>= 32;\r\n                    r <<= 16;\r\n                }\r\n                if (xx >= 0x10000) {\r\n                    xx >>= 16;\r\n                    r <<= 8;\r\n                }\r\n                if (xx >= 0x100) {\r\n                    xx >>= 8;\r\n                    r <<= 4;\r\n                }\r\n                if (xx >= 0x10) {\r\n                    xx >>= 4;\r\n                    r <<= 2;\r\n                }\r\n                if (xx >= 0x8) {\r\n                    r <<= 1;\r\n                }\r\n                r = (r + x / r) >> 1;\r\n                r = (r + x / r) >> 1;\r\n                r = (r + x / r) >> 1;\r\n                r = (r + x / r) >> 1;\r\n                r = (r + x / r) >> 1;\r\n                r = (r + x / r) >> 1;\r\n                r = (r + x / r) >> 1; // Seven iterations should be enough\r\n                uint256 r1 = x / r;\r\n                return uint128(r < r1 ? r : r1);\r\n            }\r\n        }\r\n    }\r\n}\r\n"
    },
    "@primitivefi/rmm-core/contracts/libraries/CumulativeNormalDistribution.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity ^0.8.4;\r\n\r\nimport \"./ABDKMath64x64.sol\";\r\n\r\n/// @title   Cumulative Normal Distribution Math Library\r\n/// @author  Primitive\r\nlibrary CumulativeNormalDistribution {\r\n    using ABDKMath64x64 for int128;\r\n    using ABDKMath64x64 for uint256;\r\n\r\n    /// @notice Thrown on passing an arg that is out of the input range for these math functions\r\n    error InverseOutOfBounds(int128 value);\r\n\r\n    int128 public constant ONE_INT = 0x10000000000000000;\r\n    int128 public constant TWO_INT = 0x20000000000000000;\r\n    int128 public constant CDF0 = 0x53dd02a4f5ee2e46;\r\n    int128 public constant CDF1 = 0x413c831bb169f874;\r\n    int128 public constant CDF2 = -0x48d4c730f051a5fe;\r\n    int128 public constant CDF3 = 0x16a09e667f3bcc908;\r\n    int128 public constant CDF4 = -0x17401c57014c38f14;\r\n    int128 public constant CDF5 = 0x10fb844255a12d72e;\r\n\r\n    /// @notice Uses Abramowitz and Stegun approximation:\r\n    ///         https://en.wikipedia.org/wiki/Abramowitz_and_Stegun\r\n    /// @dev    Maximum error: 3.15x10-3\r\n    /// @return Standard Normal Cumulative Distribution Function of `x`\r\n    function getCDF(int128 x) internal pure returns (int128) {\r\n        int128 z = x.div(CDF3);\r\n        int128 t = ONE_INT.div(ONE_INT.add(CDF0.mul(z.abs())));\r\n        int128 erf = getErrorFunction(z, t);\r\n        if (z < 0) {\r\n            erf = erf.neg();\r\n        }\r\n        int128 result = (HALF_INT).mul(ONE_INT.add(erf));\r\n        return result;\r\n    }\r\n\r\n    /// @notice Uses Abramowitz and Stegun approximation:\r\n    ///         https://en.wikipedia.org/wiki/Error_function\r\n    /// @dev    Maximum error: 1.5107\r\n    /// @return Error Function for approximating the Standard Normal CDF\r\n    function getErrorFunction(int128 z, int128 t) internal pure returns (int128) {\r\n        int128 step1 = t.mul(CDF3.add(t.mul(CDF4.add(t.mul(CDF5)))));\r\n        int128 step2 = CDF1.add(t.mul(CDF2.add(step1)));\r\n        int128 result = ONE_INT.sub(t.mul(step2.mul((z.mul(z).neg()).exp())));\r\n        return result;\r\n    }\r\n\r\n    int128 public constant HALF_INT = 0x8000000000000000;\r\n    int128 public constant INVERSE0 = 0x26A8F3C1F21B336E;\r\n    int128 public constant INVERSE1 = -0x87C57E5DA70D3C90;\r\n    int128 public constant INVERSE2 = 0x15D71F5721242C787;\r\n    int128 public constant INVERSE3 = 0x21D0A04B0E9B94F1;\r\n    int128 public constant INVERSE4 = -0xC2BF5D74C724E53F;\r\n\r\n    int128 public constant LOW_TAIL = 0x666666666666666; // 0.025\r\n    int128 public constant HIGH_TAIL = 0xF999999999999999; // 0.975\r\n\r\n    /// @notice  Returns the inverse CDF, or quantile function of `p`.\r\n    /// @dev     Source: https://arxiv.org/pdf/1002.0567.pdf\r\n    ///          Maximum error of central region is 1.16x104\r\n    /// @return  fcentral(p) = q * (a2 + (a1r + a0) / (r^2 + b1r +b0))\r\n    function getInverseCDF(int128 p) internal pure returns (int128) {\r\n        if (p >= ONE_INT || p <= 0) revert InverseOutOfBounds(p);\r\n        // Short circuit for the central region, central region inclusive of tails\r\n        if (p <= HIGH_TAIL && p >= LOW_TAIL) {\r\n            return central(p);\r\n        } else if (p < LOW_TAIL) {\r\n            return tail(p);\r\n        } else {\r\n            int128 negativeTail = -tail(ONE_INT.sub(p));\r\n            return negativeTail;\r\n        }\r\n    }\r\n\r\n    /// @dev    Maximum error: 1.16x104\r\n    /// @return Inverse CDF around the central area of 0.025 <= p <= 0.975\r\n    function central(int128 p) internal pure returns (int128) {\r\n        int128 q = p.sub(HALF_INT);\r\n        int128 r = q.mul(q);\r\n        int128 result = q.mul(\r\n            INVERSE2.add((INVERSE1.mul(r).add(INVERSE0)).div((r.mul(r).add(INVERSE4.mul(r)).add(INVERSE3))))\r\n        );\r\n        return result;\r\n    }\r\n\r\n    int128 public constant C0 = 0x10E56D75CE8BCE9FAE;\r\n    int128 public constant C1 = -0x2CB2447D36D513DAE;\r\n    int128 public constant C2 = -0x8BB4226952BD69EDF;\r\n    int128 public constant C3 = -0x1000BF627FA188411;\r\n    int128 public constant C0_D = 0x10AEAC93F55267A9A5;\r\n    int128 public constant C1_D = 0x41ED34A2561490236;\r\n    int128 public constant C2_D = 0x7A1E70F720ECA43;\r\n    int128 public constant D0 = 0x72C7D592D021FB1DB;\r\n    int128 public constant D1 = 0x8C27B4617F5F800EA;\r\n\r\n    /// @dev    Maximum error: 2.458x10-5\r\n    /// @return Inverse CDF of the tail, defined for p < 0.0465, used with p < 0.025\r\n    function tail(int128 p) internal pure returns (int128) {\r\n        int128 r = ONE_INT.div(p.mul(p)).ln().sqrt();\r\n        int128 step0 = C3.mul(r).add(C2_D);\r\n        int128 numerator = C1_D.mul(r).add(C0_D);\r\n        int128 denominator = r.mul(r).add(D1.mul(r)).add(D0);\r\n        int128 result = step0.add(numerator.div(denominator));\r\n        return result;\r\n    }\r\n}\r\n"
    },
    "@primitivefi/rmm-core/contracts/libraries/Units.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity ^0.8.0;\r\n\r\nimport \"./ABDKMath64x64.sol\";\r\n\r\n/// @title   Units library\r\n/// @author  Primitive\r\n/// @notice  Utility functions for unit conversions\r\nlibrary Units {\r\n    using ABDKMath64x64 for int128;\r\n    using ABDKMath64x64 for uint256;\r\n\r\n    uint256 internal constant YEAR = 31556952; // 365.24219 ephemeris day = 1 year, in seconds\r\n    uint256 internal constant PRECISION = 1e18; // precision to scale to\r\n    uint256 internal constant PERCENTAGE = 1e4; // precision of percentages\r\n\r\n    // ===== Unit Conversion =====\r\n\r\n    /// @notice             Scales a wei value to a precision of 1e18 using the scaling factor\r\n    /// @param   value      Unsigned 256-bit wei amount to convert with native decimals\r\n    /// @param   factor     Scaling factor to multiply by, i.e. 10^(18 - value.decimals())\r\n    /// @return  y          Unsigned 256-bit wei amount scaled to a precision of 1e18\r\n    function scaleUp(uint256 value, uint256 factor) internal pure returns (uint256 y) {\r\n        y = value * factor;\r\n    }\r\n\r\n    /// @notice             Scales a wei value from a precision of 1e18 to 10^(18 - precision)\r\n    /// @param   value      Unsigned 256-bit wei amount with 18 decimals\r\n    /// @param   factor     Scaling factor to divide by, i.e. 10^(18 - value.decimals())\r\n    /// @return  y          Unsigned 256-bit wei amount scaled to 10^(18 - factor)\r\n    function scaleDown(uint256 value, uint256 factor) internal pure returns (uint256 y) {\r\n        y = value / factor;\r\n    }\r\n\r\n    /// @notice             Converts unsigned 256-bit wei value into a fixed point 64.64 number\r\n    /// @param   value      Unsigned 256-bit wei amount, in native precision\r\n    /// @param   factor     Scaling factor for `value`, used to calculate decimals of `value`\r\n    /// @return  y          Signed 64.64 fixed point number scaled from native precision\r\n    function scaleToX64(uint256 value, uint256 factor) internal pure returns (int128 y) {\r\n        uint256 scaleFactor = PRECISION / factor;\r\n        y = value.divu(scaleFactor);\r\n    }\r\n\r\n    /// @notice             Converts signed fixed point 64.64 number into unsigned 256-bit wei value\r\n    /// @param   value      Signed fixed point 64.64 number to convert from precision of 10^18\r\n    /// @param   factor     Scaling factor for `value`, used to calculate decimals of `value`\r\n    /// @return  y          Unsigned 256-bit wei amount scaled to native precision of 10^(18 - factor)\r\n    function scaleFromX64(int128 value, uint256 factor) internal pure returns (uint256 y) {\r\n        uint256 scaleFactor = PRECISION / factor;\r\n        y = value.mulu(scaleFactor);\r\n    }\r\n\r\n    /// @notice         Converts denormalized percentage integer to a fixed point 64.64 number\r\n    /// @dev            Convert unsigned 256-bit integer number into signed 64.64 fixed point number\r\n    /// @param denorm   Unsigned percentage integer with precision of 1e4\r\n    /// @return         Signed 64.64 fixed point percentage with precision of 1e4\r\n    function percentageToX64(uint256 denorm) internal pure returns (int128) {\r\n        return denorm.divu(PERCENTAGE);\r\n    }\r\n\r\n    /// @notice         Converts unsigned seconds integer into years as a signed 64.64 fixed point number\r\n    /// @dev            Convert unsigned 256-bit integer number into signed 64.64 fixed point number\r\n    /// @param s        Unsigned 256-bit integer amount of seconds to convert into year units\r\n    /// @return         Fixed point 64.64 number of years equal to `seconds`\r\n    function toYears(uint256 s) internal pure returns (int128) {\r\n        return s.divu(YEAR);\r\n    }\r\n}\r\n"
    },
    "@primitivefi/rmm-core/contracts/interfaces/callback/IPrimitiveSwapCallback.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.5.0;\r\n\r\n/// @title  Primitive Swap Callback\r\n/// @author Primitive\r\ninterface IPrimitiveSwapCallback {\r\n    /// @notice              Triggered when swapping tokens in an Engine\r\n    /// @param  delRisky     Amount of risky tokens required to pay the swap with\r\n    /// @param  delStable    Amount of stable tokens required to pay the swap with\r\n    /// @param  data         Calldata passed on swap function call\r\n    function swapCallback(\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bytes calldata data\r\n    ) external;\r\n}\r\n"
    },
    "contracts/interfaces/IMarginManager.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.8.6;\r\n\r\nimport \"@primitivefi/rmm-core/contracts/interfaces/callback/IPrimitiveDepositCallback.sol\";\r\n\r\n/// @title   Interface of MarginManager contract\r\n/// @author  Primitive\r\ninterface IMarginManager is IPrimitiveDepositCallback {\r\n    /// ERRORS ///\r\n\r\n    /// @notice Thrown when trying to deposit or withdraw 0 risky and stable\r\n    error ZeroDelError();\r\n\r\n    /// EVENTS ///\r\n\r\n    /// @notice           Emitted when funds are deposited\r\n    /// @param payer      Address depositing the funds\r\n    /// @param recipient  Address receiving the funds in their margin\r\n    /// @param engine     Engine receiving the funds\r\n    /// @param risky      Address of the risky token\r\n    /// @param stable     Address of the stable token\r\n    /// @param delRisky   Amount of deposited risky\r\n    /// @param delStable  Amount of deposited stable\r\n    event Deposit(\r\n        address indexed payer,\r\n        address indexed recipient,\r\n        address indexed engine,\r\n        address risky,\r\n        address stable,\r\n        uint256 delRisky,\r\n        uint256 delStable\r\n    );\r\n\r\n    /// @notice           Emitted when funds are withdrawn\r\n    /// @param payer      Address withdrawing the funds\r\n    /// @param recipient  Address receiving the funds in their wallet\r\n    /// @param engine     Engine where the funds are withdrawn from\r\n    /// @param risky      Address of the risky token\r\n    /// @param stable     Address of the stable token\r\n    /// @param delRisky   Amount of withdrawn risky\r\n    /// @param delStable  Amount of withdrawn stable\r\n    event Withdraw(\r\n        address indexed payer,\r\n        address indexed recipient,\r\n        address indexed engine,\r\n        address risky,\r\n        address stable,\r\n        uint256 delRisky,\r\n        uint256 delStable\r\n    );\r\n\r\n    /// EFFECT FUNCTIONS ///\r\n\r\n    /// @notice           Deposits funds into the margin of a Primitive Engine\r\n    /// @dev              Since the PrimitiveManager contract keeps track of the margins, it\r\n    ///                   will deposit the funds into the Primitive Engine using its own address\r\n    /// @param recipient  Address receiving the funds in their margin\r\n    /// @param risky      Address of the risky token\r\n    /// @param stable     Address of the stable token\r\n    /// @param delRisky   Amount of risky token to deposit\r\n    /// @param delStable  Amount of stable token to deposit\r\n    function deposit(\r\n        address recipient,\r\n        address risky,\r\n        address stable,\r\n        uint256 delRisky,\r\n        uint256 delStable\r\n    ) external payable;\r\n\r\n    /// @notice           Withdraws funds from the margin of a Primitive Engine\r\n    /// @param recipient  Address receiving the funds in their wallet\r\n    /// @param engine     Primitive Engine to withdraw from\r\n    /// @param delRisky   Amount of risky token to withdraw\r\n    /// @param delStable  Amount of stable token to withdraw\r\n    function withdraw(\r\n        address recipient,\r\n        address engine,\r\n        uint256 delRisky,\r\n        uint256 delStable\r\n    ) external;\r\n\r\n    /// VIEW FUNCTIONS ///\r\n\r\n    /// @notice                Returns the margin of an account for a specific Primitive Engine\r\n    /// @param account         Address of the account\r\n    /// @param engine          Address of the engine\r\n    /// @return balanceRisky   Balance of risky in the margin of the user\r\n    /// @return balanceStable  Balance of stable in the margin of the user\r\n    function margins(address account, address engine)\r\n        external\r\n        view\r\n        returns (uint128 balanceRisky, uint128 balanceStable);\r\n}\r\n"
    },
    "contracts/libraries/Margin.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.8.6;\r\n\r\nimport \"@primitivefi/rmm-core/contracts/libraries/SafeCast.sol\";\r\n\r\n/// @author  Primitive\r\n/// @notice  Utils functions to manage margins\r\n/// @dev     Uses a data struct with two uint128s to optimize for one storage slot\r\nlibrary Margin {\r\n    using SafeCast for uint256;\r\n\r\n    struct Data {\r\n        uint128 balanceRisky; // Balance of the risky token, aka underlying asset\r\n        uint128 balanceStable; // Balance of the stable token, aka \"quote\" asset\r\n    }\r\n\r\n    /// @notice             Adds to risky and stable token balances\r\n    /// @param  margin      Margin data of an account in storage to manipulate\r\n    /// @param  delRisky    Amount of risky tokens to add to margin\r\n    /// @param  delStable   Amount of stable tokens to add to margin\r\n    function deposit(\r\n        Data storage margin,\r\n        uint256 delRisky,\r\n        uint256 delStable\r\n    ) internal {\r\n        if (delRisky != 0) margin.balanceRisky += delRisky.toUint128();\r\n        if (delStable != 0) margin.balanceStable += delStable.toUint128();\r\n    }\r\n\r\n    /// @notice             Removes risky and stable token balance from an internal margin account\r\n    /// @param  margin      Margin data of an account in storage to manipulate\r\n    /// @param  delRisky    Amount of risky tokens to subtract from margin\r\n    /// @param  delStable   Amount of stable tokens to subtract from margin\r\n    function withdraw(\r\n        Data storage margin,\r\n        uint256 delRisky,\r\n        uint256 delStable\r\n    ) internal {\r\n        if (delRisky != 0) margin.balanceRisky -= delRisky.toUint128();\r\n        if (delStable != 0) margin.balanceStable -= delStable.toUint128();\r\n    }\r\n}\r\n"
    },
    "@primitivefi/rmm-core/contracts/interfaces/callback/IPrimitiveDepositCallback.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.5.0;\r\n\r\n/// @title  Primitive Deposit Callback\r\n/// @author Primitive\r\ninterface IPrimitiveDepositCallback {\r\n    /// @notice              Triggered when depositing tokens to an Engine\r\n    /// @param  delRisky     Amount of risky tokens required to deposit to risky margin balance\r\n    /// @param  delStable    Amount of stable tokens required to deposit to stable margin balance\r\n    /// @param  data         Calldata passed on deposit function call\r\n    function depositCallback(\r\n        uint256 delRisky,\r\n        uint256 delStable,\r\n        bytes calldata data\r\n    ) external;\r\n}\r\n"
    },
    "@primitivefi/rmm-core/contracts/libraries/SafeCast.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-only\r\npragma solidity >=0.5.0;\r\n\r\n/// @title  SafeCast\r\n/// @notice Safely cast between uint256 and uint128\r\nlibrary SafeCast {\r\n    /// @notice reverts if x > type(uint128).max\r\n    function toUint128(uint256 x) internal pure returns (uint128 z) {\r\n        require(x <= type(uint128).max);\r\n        z = uint128(x);\r\n    }\r\n}\r\n"
    }
  },
  "settings": {
    "optimizer": {
      "enabled": true,
      "runs": 400
    },
    "outputSelection": {
      "*": {
        "*": [
          "evm.bytecode",
          "evm.deployedBytecode",
          "devdoc",
          "userdoc",
          "metadata",
          "abi"
        ]
      }
    },
    "libraries": {}
  }
}}