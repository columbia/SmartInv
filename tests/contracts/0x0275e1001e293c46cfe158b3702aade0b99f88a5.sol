{{
  "language": "Solidity",
  "sources": {
    "/Users/marcellobardus/Projects/Oiler/vesting-token/contracts/IDistribution.sol": {
      "content": "pragma solidity 0.5.12;\n\ninterface IDistribution {\n    function isInitialized() external view returns (bool);\n    function distributionStartTimestamp() external view returns (uint256);\n    function supply() external view returns(uint256);\n    function poolAddress(uint8) external view returns(address);\n}\n"
    },
    "/Users/marcellobardus/Projects/Oiler/vesting-token/contracts/Token/ERC20.sol": {
      "content": "pragma solidity 0.5.12;\n\nimport \"openzeppelin-solidity/contracts/token/ERC20/IERC20.sol\";\nimport \"openzeppelin-solidity/contracts/math/SafeMath.sol\";\n\n/**\n * @dev Implementation of the `IERC20` interface.\n *\n * This implementation was taken from\n * https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v2.3.0/contracts/token/ERC20/ERC20.sol\n * This differs from the original one only in the definition for the `_balances`\n * mapping: we made it `internal` instead of `private` since we use the `_balances`\n * in the `ERC677BridgeToken` child contract to be able to transfer tokens to address(0)\n * (see its `_superTransfer` function). The original OpenZeppelin implementation\n * doesn't allow transferring to address(0).\n *\n * This implementation is agnostic to the way tokens are created. This means\n * that a supply mechanism has to be added in a derived contract using `_mint`.\n * For a generic mechanism see `ERC20Mintable`.\n *\n * *For a detailed writeup see our guide [How to implement supply\n * mechanisms](https://forum.zeppelin.solutions/t/how-to-implement-erc20-supply-mechanisms/226).*\n *\n * We have followed general OpenZeppelin guidelines: functions revert instead\n * of returning `false` on failure. This behavior is nonetheless conventional\n * and does not conflict with the expectations of ERC20 applications.\n *\n * Additionally, an `Approval` event is emitted on calls to `transferFrom`.\n * This allows applications to reconstruct the allowance for all accounts just\n * by listening to said events. Other implementations of the EIP may not emit\n * these events, as it isn't required by the specification.\n *\n * Finally, the non-standard `decreaseAllowance` and `increaseAllowance`\n * functions have been added to mitigate the well-known issues around setting\n * allowances. See `IERC20.approve`.\n */\ncontract ERC20 is IERC20 {\n    using SafeMath for uint256;\n\n    mapping (address => uint256) internal _balances; // CHANGED: not private to write a custom transfer method\n\n    mapping (address => mapping (address => uint256)) private _allowances;\n\n    uint256 private _totalSupply;\n\n    /**\n     * @dev See `IERC20.totalSupply`.\n     */\n    function totalSupply() public view returns (uint256) {\n        return _totalSupply;\n    }\n\n    /**\n     * @dev See `IERC20.balanceOf`.\n     */\n    function balanceOf(address account) public view returns (uint256) {\n        return _balances[account];\n    }\n\n    /**\n     * @dev See `IERC20.transfer`.\n     *\n     * Requirements:\n     *\n     * - `recipient` cannot be the zero address.\n     * - the caller must have a balance of at least `amount`.\n     */\n    function transfer(address recipient, uint256 amount) public returns (bool) {\n        _transfer(msg.sender, recipient, amount);\n        return true;\n    }\n\n    /**\n     * @dev See `IERC20.allowance`.\n     */\n    function allowance(address owner, address spender) public view returns (uint256) {\n        return _allowances[owner][spender];\n    }\n\n    /**\n     * @dev See `IERC20.approve`.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     */\n    function approve(address spender, uint256 value) public returns (bool) {\n        _approve(msg.sender, spender, value);\n        return true;\n    }\n\n    /**\n     * @dev See `IERC20.transferFrom`.\n     *\n     * Emits an `Approval` event indicating the updated allowance. This is not\n     * required by the EIP. See the note at the beginning of `ERC20`;\n     *\n     * Requirements:\n     * - `sender` and `recipient` cannot be the zero address.\n     * - `sender` must have a balance of at least `value`.\n     * - the caller must have allowance for `sender`'s tokens of at least\n     * `amount`.\n     */\n    function transferFrom(address sender, address recipient, uint256 amount) public returns (bool) {\n        _transfer(sender, recipient, amount);\n        _approve(sender, msg.sender, _allowances[sender][msg.sender].sub(amount));\n        return true;\n    }\n\n    /**\n     * @dev Atomically increases the allowance granted to `spender` by the caller.\n     *\n     * This is an alternative to `approve` that can be used as a mitigation for\n     * problems described in `IERC20.approve`.\n     *\n     * Emits an `Approval` event indicating the updated allowance.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     */\n    function increaseAllowance(address spender, uint256 addedValue) public returns (bool) {\n        _approve(msg.sender, spender, _allowances[msg.sender][spender].add(addedValue));\n        return true;\n    }\n\n    /**\n     * @dev Atomically decreases the allowance granted to `spender` by the caller.\n     *\n     * This is an alternative to `approve` that can be used as a mitigation for\n     * problems described in `IERC20.approve`.\n     *\n     * Emits an `Approval` event indicating the updated allowance.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     * - `spender` must have allowance for the caller of at least\n     * `subtractedValue`.\n     */\n    function decreaseAllowance(address spender, uint256 subtractedValue) public returns (bool) {\n        _approve(msg.sender, spender, _allowances[msg.sender][spender].sub(subtractedValue));\n        return true;\n    }\n\n    /**\n     * @dev Moves tokens `amount` from `sender` to `recipient`.\n     *\n     * This is internal function is equivalent to `transfer`, and can be used to\n     * e.g. implement automatic token fees, slashing mechanisms, etc.\n     *\n     * Emits a `Transfer` event.\n     *\n     * Requirements:\n     *\n     * - `sender` cannot be the zero address.\n     * - `recipient` cannot be the zero address.\n     * - `sender` must have a balance of at least `amount`.\n     */\n    function _transfer(address sender, address recipient, uint256 amount) internal {\n        require(sender != address(0), \"ERC20: transfer from the zero address\");\n        require(recipient != address(0), \"ERC20: transfer to the zero address\");\n\n        _balances[sender] = _balances[sender].sub(amount);\n        _balances[recipient] = _balances[recipient].add(amount);\n        emit Transfer(sender, recipient, amount);\n    }\n\n    /** @dev Creates `amount` tokens and assigns them to `account`, increasing\n     * the total supply.\n     *\n     * Emits a `Transfer` event with `from` set to the zero address.\n     *\n     * Requirements\n     *\n     * - `to` cannot be the zero address.\n     */\n    function _mint(address account, uint256 amount) internal {\n        require(account != address(0), \"ERC20: mint to the zero address\");\n\n        _totalSupply = _totalSupply.add(amount);\n        _balances[account] = _balances[account].add(amount);\n        emit Transfer(address(0), account, amount);\n    }\n\n     /**\n     * @dev Destoys `amount` tokens from `account`, reducing the\n     * total supply.\n     *\n     * Emits a `Transfer` event with `to` set to the zero address.\n     *\n     * Requirements\n     *\n     * - `account` cannot be the zero address.\n     * - `account` must have at least `amount` tokens.\n     */\n    function _burn(address account, uint256 value) internal {\n        require(account != address(0), \"ERC20: burn from the zero address\");\n\n        _totalSupply = _totalSupply.sub(value);\n        _balances[account] = _balances[account].sub(value);\n        emit Transfer(account, address(0), value);\n    }\n\n    /**\n     * @dev Sets `amount` as the allowance of `spender` over the `owner`s tokens.\n     *\n     * This is internal function is equivalent to `approve`, and can be used to\n     * e.g. set automatic allowances for certain subsystems, etc.\n     *\n     * Emits an `Approval` event.\n     *\n     * Requirements:\n     *\n     * - `owner` cannot be the zero address.\n     * - `spender` cannot be the zero address.\n     */\n    function _approve(address owner, address spender, uint256 value) internal {\n        require(owner != address(0), \"ERC20: approve from the zero address\");\n        require(spender != address(0), \"ERC20: approve to the zero address\");\n\n        _allowances[owner][spender] = value;\n        emit Approval(owner, spender, value);\n    }\n\n    /**\n     * @dev Destoys `amount` tokens from `account`.`amount` is then deducted\n     * from the caller's allowance.\n     *\n     * See `_burn` and `_approve`.\n     */\n    function _burnFrom(address account, uint256 amount) internal {\n        _burn(account, amount);\n        _approve(account, msg.sender, _allowances[account][msg.sender].sub(amount));\n    }\n}\n"
    },
    "/Users/marcellobardus/Projects/Oiler/vesting-token/contracts/Token/ERC20Permittable.sol": {
      "content": "pragma solidity 0.5.12;\n\nimport \"./ERC20.sol\";\nimport \"openzeppelin-solidity/contracts/token/ERC20/ERC20Detailed.sol\";\n\n\n/**\n * @title ERC20Permittable\n * @dev This is ERC20 contract extended by the `permit` function (see EIP712).\n */\ncontract ERC20Permittable is ERC20, ERC20Detailed {\n\n    string public constant version = \"1\";\n\n    // EIP712 niceties\n    bytes32 public DOMAIN_SEPARATOR;\n    // bytes32 public constant PERMIT_TYPEHASH = keccak256(\"Permit(address holder,address spender,uint256 nonce,uint256 expiry,bool allowed)\");\n    bytes32 public constant PERMIT_TYPEHASH = 0xea2aa0a1be11a07ed86d755c93467f4f82362b452371d1ba94d1715123511acb;\n\n    mapping(address => uint256) public nonces;\n    mapping(address => mapping(address => uint256)) public expirations;\n\n    constructor(\n        string memory _name,\n        string memory _symbol,\n        uint8 _decimals\n    ) ERC20Detailed(_name, _symbol, _decimals) public {\n        uint256 chainId = 0;\n        assembly {\n            chainId := chainid\n        }\n        DOMAIN_SEPARATOR = keccak256(abi.encode(\n            keccak256(\"EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)\"),\n            keccak256(bytes(_name)),\n            keccak256(bytes(version)),\n            chainId,\n            address(this)\n        ));\n    }\n\n    /// @dev transferFrom in this contract works in a slightly different form than the generic\n    /// transferFrom function. This contract allows for \"unlimited approval\".\n    /// Should the user approve an address for the maximum uint256 value,\n    /// then that address will have unlimited approval until told otherwise.\n    /// @param _sender The address of the sender.\n    /// @param _recipient The address of the recipient.\n    /// @param _amount The value to transfer.\n    /// @return Success status.\n    function transferFrom(address _sender, address _recipient, uint256 _amount) public returns (bool) {\n        _transfer(_sender, _recipient, _amount);\n\n        if (_sender != msg.sender) {\n            uint256 allowedAmount = allowance(_sender, msg.sender);\n\n            if (allowedAmount != uint256(-1)) {\n                // If allowance is limited, adjust it.\n                // In this case `transferFrom` works like the generic\n                _approve(_sender, msg.sender, allowedAmount.sub(_amount));\n            } else {\n                // If allowance is unlimited by `permit`, `approve`, or `increaseAllowance`\n                // function, don't adjust it. But the expiration date must be empty or in the future.\n                // Note that the expiration timestamp can have a 900-second error:\n                // https://github.com/ethereum/wiki/blob/c02254611f218f43cbb07517ca8e5d00fd6d6d75/Block-Protocol-2.0.md\n                require(\n                    // solium-disable-next-line security/no-block-members\n                    expirations[_sender][msg.sender] == 0 || expirations[_sender][msg.sender] >= _now(),\n                    \"expiry is in the past\"\n                );\n            }\n        } else {\n            // If `_sender` is `msg.sender`,\n            // the function works just like `transfer()`\n        }\n\n        return true;\n    }\n\n    /// @dev An alias for `transfer` function.\n    /// @param _to The address of the recipient.\n    /// @param _amount The value to transfer.\n    function push(address _to, uint256 _amount) public {\n        transferFrom(msg.sender, _to, _amount);\n    }\n\n    /// @dev Makes a request to transfer the specified amount\n    /// from the specified address to the caller's address.\n    /// @param _from The address of the holder.\n    /// @param _amount The value to transfer.\n    function pull(address _from, uint256 _amount) public {\n        transferFrom(_from, msg.sender, _amount);\n    }\n\n    /// @dev An alias for `transferFrom` function.\n    /// @param _from The address of the sender.\n    /// @param _to The address of the recipient.\n    /// @param _amount The value to transfer.\n    function move(address _from, address _to, uint256 _amount) public {\n        transferFrom(_from, _to, _amount);\n    }\n\n    /// @dev Allows to spend holder's unlimited amount by the specified spender.\n    /// The function can be called by anyone, but requires having allowance parameters\n    /// signed by the holder according to EIP712.\n    /// @param _holder The holder's address.\n    /// @param _spender The spender's address.\n    /// @param _nonce The nonce taken from `nonces(_holder)` public getter.\n    /// @param _expiry The allowance expiration date (unix timestamp in UTC).\n    /// Can be zero for no expiration. Forced to zero if `_allowed` is `false`.\n    /// @param _allowed True to enable unlimited allowance for the spender by the holder. False to disable.\n    /// @param _v A final byte of signature (ECDSA component).\n    /// @param _r The first 32 bytes of signature (ECDSA component).\n    /// @param _s The second 32 bytes of signature (ECDSA component).\n    function permit(\n        address _holder,\n        address _spender,\n        uint256 _nonce,\n        uint256 _expiry,\n        bool _allowed,\n        uint8 _v,\n        bytes32 _r,\n        bytes32 _s\n    ) external {\n        require(_expiry == 0 || _now() <= _expiry, \"invalid expiry\");\n\n        bytes32 digest = keccak256(abi.encodePacked(\n            \"\\x19\\x01\",\n            DOMAIN_SEPARATOR,\n            keccak256(abi.encode(\n                PERMIT_TYPEHASH,\n                _holder,\n                _spender,\n                _nonce,\n                _expiry,\n                _allowed\n            ))\n        ));\n\n        require(_holder == ecrecover(digest, _v, _r, _s), \"invalid signature or parameters\");\n        require(_nonce == nonces[_holder]++, \"invalid nonce\");\n\n        uint256 amount = _allowed ? uint256(-1) : 0;\n        _approve(_holder, _spender, amount);\n\n        expirations[_holder][_spender] = _allowed ? _expiry : 0;\n    }\n\n    function _now() internal view returns(uint256) {\n        return now;\n    }\n\n}\n"
    },
    "/Users/marcellobardus/Projects/Oiler/vesting-token/contracts/Token/OilerToken.sol": {
      "content": "pragma solidity 0.5.12;\n\nimport \"openzeppelin-solidity/contracts/utils/Address.sol\";\nimport \"openzeppelin-solidity/contracts/ownership/Ownable.sol\";\nimport \"openzeppelin-solidity/contracts/token/ERC20/SafeERC20.sol\";\nimport \"./ERC20Permittable.sol\";\nimport \"./Sacrifice.sol\";\nimport \"../IDistribution.sol\";\n\n// This is Vesting token\n//\n// We used xDAI vesting implementation:\n// https://github.com/xdaichain/stake-token/blob/master/contracts/Token/ERC677BridgeToken.sol\n//\n// Stripping it of any Bridges and other stuff\ncontract OilerToken is Ownable, ERC20Permittable {\n    using SafeERC20 for ERC20;\n    using Address for address;\n\n    ///  @dev Distribution contract address.\n    address public distributionAddress;\n    ///  @dev The PrivateOffering contract address.\n    address public privateOfferingDistributionAddress;\n    ///  @dev The AdvisorsReward contract address.\n    address public advisorsRewardDistributionAddress;\n\n    /// @dev Mint event.\n    /// @param to To address.\n    /// @param amount Minted value.\n    event Mint(address indexed to, uint256 amount);\n\n    /// @dev Modified Transfer event with custom data.\n    /// @param from From address.\n    /// @param to To address.\n    /// @param value Transferred value.\n    /// @param data Custom data to call after transfer.\n    event Transfer(address indexed from, address indexed to, uint256 value, bytes data);\n\n    /// @dev Emits if custom call after transfer fails.\n    /// @param from From address.\n    /// @param to To address.\n    /// @param value Transferred value.\n    event ContractFallbackCallFailed(address from, address to, uint256 value);\n\n    /// @dev Checks that the recipient address is valid.\n    /// @param _recipient Recipient address.\n    modifier validRecipient(address _recipient) {\n        require(_recipient != address(0) && _recipient != address(this), \"not a valid recipient\");\n        _;\n    }\n\n    /// @dev Creates a token and mints the whole supply for the Distribution contract.\n    /// @param _name Token name.\n    /// @param _symbol Token symbol.\n    /// @param _distributionAddress The address of the deployed Distribution contract.\n    /// @param _privateOfferingDistributionAddress The address of the PrivateOffering contract.\n    /// @param _advisorsRewardDistributionAddress The address of the AdvisorsReward contract.\n    constructor(\n        string memory _name,\n        string memory _symbol,\n        address _distributionAddress,\n        address _privateOfferingDistributionAddress,\n        address _advisorsRewardDistributionAddress\n    ) ERC20Permittable(_name, _symbol, 18) public {\n        require(\n            _distributionAddress.isContract() &&\n            _privateOfferingDistributionAddress.isContract() &&\n            _advisorsRewardDistributionAddress.isContract(),\n            \"not a contract address\"\n        );\n        uint256 supply = IDistribution(_distributionAddress).supply();\n        require(supply > 0, \"the supply must be more than 0\");\n        _mint(_distributionAddress, supply);\n        distributionAddress = _distributionAddress;\n        privateOfferingDistributionAddress = _privateOfferingDistributionAddress;\n        advisorsRewardDistributionAddress = _advisorsRewardDistributionAddress;\n        emit Mint(_distributionAddress, supply);\n    }\n\n    /// @dev Extends transfer method with callback.\n    /// @param _to The address of the recipient.\n    /// @param _value The value to transfer.\n    /// @param _data Custom data.\n    /// @return Success status.\n    function transferAndCall(\n        address _to,\n        uint256 _value,\n        bytes calldata _data\n    ) external validRecipient(_to) returns (bool) {\n        _superTransfer(_to, _value);\n        emit Transfer(msg.sender, _to, _value, _data);\n\n        if (_to.isContract()) {\n            require(_contractFallback(msg.sender, _to, _value, _data), \"contract call failed\");\n        }\n        return true;\n    }\n\n    /// @dev Extends transfer method with event when the callback failed.\n    /// @param _to The address of the recipient.\n    /// @param _value The value to transfer.\n    /// @return Success status.\n    function transfer(address _to, uint256 _value) public returns (bool) {\n        _superTransfer(_to, _value);\n        _callAfterTransfer(msg.sender, _to, _value);\n        return true;\n    }\n\n    /// @dev This is a copy of `transfer` function which can only be called by distribution contracts.\n    /// Made to get rid of `onTokenTransfer` calling to save gas when distributing tokens.\n    /// @param _to The address of the recipient.\n    /// @param _value The value to transfer.\n    /// @return Success status.\n    function transferDistribution(address _to, uint256 _value) public returns (bool) {\n        require(\n            msg.sender == distributionAddress ||\n            msg.sender == privateOfferingDistributionAddress ||\n            msg.sender == advisorsRewardDistributionAddress,\n            \"wrong sender\"\n        );\n        _superTransfer(_to, _value);\n        return true;\n    }\n\n    /// @dev Extends transferFrom method with event when the callback failed.\n    /// @param _from The address of the sender.\n    /// @param _to The address of the recipient.\n    /// @param _value The value to transfer.\n    /// @return Success status.\n    function transferFrom(address _from, address _to, uint256 _value) public returns (bool) {\n        _superTransferFrom(_from, _to, _value);\n        _callAfterTransfer(_from, _to, _value);\n        return true;\n    }\n\n    /// @dev If someone sent eth/tokens to the contract mistakenly then the owner can send them back.\n    /// @param _token The token address to transfer.\n    /// @param _to The address of the recipient.\n    function claimTokens(address _token, address payable _to) public onlyOwner validRecipient(_to) {\n        if (_token == address(0)) {\n            uint256 value = address(this).balance;\n            if (!_to.send(value)) { // solium-disable-line security/no-send\n                // We use the `Sacrifice` trick to be sure the coins can be 100% sent to the receiver.\n                // Otherwise, if the receiver is a contract which has a revert in its fallback function,\n                // the sending will fail.\n                (new Sacrifice).value(value)(_to);\n            }\n        } else {\n            ERC20 token = ERC20(_token);\n            uint256 balance = token.balanceOf(address(this));\n            token.safeTransfer(_to, balance);\n        }\n    }\n\n    /// @dev The removed implementation of the ownership renouncing.\n    function renounceOwnership() public onlyOwner {\n        revert(\"not implemented\");\n    }\n\n    /// @dev Calls transfer method and reverts if it fails.\n    /// @param _to The address of the recipient.\n    /// @param _value The value to transfer.\n    function _superTransfer(address _to, uint256 _value) internal {\n        bool success;\n        if (\n            msg.sender == distributionAddress ||\n            msg.sender == privateOfferingDistributionAddress ||\n            msg.sender == advisorsRewardDistributionAddress\n        ) {\n            // Allow sending tokens to `address(0)` by\n            // Distribution, PrivateOffering, or AdvisorsReward contract\n            // since `super.transfer` doesn't allow doing that.\n            // This is needed when the `transferDistribution` function\n            // is called by Distribution, PrivateOffering, AdvisorsReward contract\n            // to send tokens to `address(0)`.\n            // See `Distribution.preInitialize`, `MultipleDistribution.burn` functions.\n            _balances[msg.sender] = _balances[msg.sender].sub(_value);\n            _balances[_to] = _balances[_to].add(_value);\n            emit Transfer(msg.sender, _to, _value);\n            success = true;\n        } else {\n            success = super.transfer(_to, _value);\n        }\n        require(success, \"transfer failed\");\n    }\n\n    /// @dev Calls transferFrom method and reverts if it fails.\n    /// @param _from The address of the sender.\n    /// @param _to The address of the recipient.\n    /// @param _value The value to transfer.\n    function _superTransferFrom(address _from, address _to, uint256 _value) internal {\n        bool success = super.transferFrom(_from, _to, _value);\n        require(success, \"transfer failed\");\n    }\n\n    /// @dev Used by the `transfer` and `transferFrom` functions.\n    /// In case the recipient is a contract, tries to call its `onTokenTransfer` function.\n    /// Reverts if `onTokenTransfer` fails and the recipient is a bridge/distribution contract.\n    /// Emits an event if `onTokenTransfer` fails and the recipient is not a bridge/distribution contract.\n    /// Needed for reverting transfer to a bridge when the bridge doesn't accept tokens due to daily limitations,\n    /// and to revert transfer to a distribution contract (Distribution or MultipleDistribution)\n    /// if the contract doesn't accept tokens due to its limitations defined in `onTokenTransfer` function.\n    /// @param _from The address of the sender.\n    /// @param _to The address of the recipient.\n    /// @param _value The transferred value.\n    function _callAfterTransfer(address _from, address _to, uint256 _value) internal {\n        if (_to.isContract() && !_contractFallback(_from, _to, _value, new bytes(0))) {\n            require(_to != distributionAddress, \"you can't transfer to Distribution contract\");\n            require(_to != privateOfferingDistributionAddress, \"you can't transfer to PrivateOffering contract\");\n            require(_to != advisorsRewardDistributionAddress, \"you can't transfer to AdvisorsReward contract\");\n            emit ContractFallbackCallFailed(_from, _to, _value);\n        }\n    }\n\n    /// @dev Makes a callback after the transfer of tokens.\n    /// @param _from The address of the sender.\n    /// @param _to The address of the recipient.\n    /// @param _value The transferred value.\n    /// @param _data Custom data.\n    /// @return Success status.\n    function _contractFallback(\n        address _from,\n        address _to,\n        uint256 _value,\n        bytes memory _data\n    ) private returns (bool) {\n        string memory signature = \"onTokenTransfer(address,uint256,bytes)\";\n        // solium-disable-next-line security/no-low-level-calls\n        (bool success, ) = _to.call(abi.encodeWithSignature(signature, _from, _value, _data));\n        return success;\n    }\n}\n"
    },
    "/Users/marcellobardus/Projects/Oiler/vesting-token/contracts/Token/Sacrifice.sol": {
      "content": "pragma solidity 0.5.12;\n\n\ncontract Sacrifice {\n    constructor(address payable _recipient) public payable {\n        selfdestruct(_recipient);\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/math/SafeMath.sol": {
      "content": "pragma solidity ^0.5.0;\n\n/**\n * @dev Wrappers over Solidity's arithmetic operations with added overflow\n * checks.\n *\n * Arithmetic operations in Solidity wrap on overflow. This can easily result\n * in bugs, because programmers usually assume that an overflow raises an\n * error, which is the standard behavior in high level programming languages.\n * `SafeMath` restores this intuition by reverting the transaction when an\n * operation overflows.\n *\n * Using this library instead of the unchecked operations eliminates an entire\n * class of bugs, so it's recommended to use it always.\n */\nlibrary SafeMath {\n    /**\n     * @dev Returns the addition of two unsigned integers, reverting on\n     * overflow.\n     *\n     * Counterpart to Solidity's `+` operator.\n     *\n     * Requirements:\n     * - Addition cannot overflow.\n     */\n    function add(uint256 a, uint256 b) internal pure returns (uint256) {\n        uint256 c = a + b;\n        require(c >= a, \"SafeMath: addition overflow\");\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the subtraction of two unsigned integers, reverting on\n     * overflow (when the result is negative).\n     *\n     * Counterpart to Solidity's `-` operator.\n     *\n     * Requirements:\n     * - Subtraction cannot overflow.\n     */\n    function sub(uint256 a, uint256 b) internal pure returns (uint256) {\n        require(b <= a, \"SafeMath: subtraction overflow\");\n        uint256 c = a - b;\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the multiplication of two unsigned integers, reverting on\n     * overflow.\n     *\n     * Counterpart to Solidity's `*` operator.\n     *\n     * Requirements:\n     * - Multiplication cannot overflow.\n     */\n    function mul(uint256 a, uint256 b) internal pure returns (uint256) {\n        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the\n        // benefit is lost if 'b' is also tested.\n        // See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522\n        if (a == 0) {\n            return 0;\n        }\n\n        uint256 c = a * b;\n        require(c / a == b, \"SafeMath: multiplication overflow\");\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the integer division of two unsigned integers. Reverts on\n     * division by zero. The result is rounded towards zero.\n     *\n     * Counterpart to Solidity's `/` operator. Note: this function uses a\n     * `revert` opcode (which leaves remaining gas untouched) while Solidity\n     * uses an invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     * - The divisor cannot be zero.\n     */\n    function div(uint256 a, uint256 b) internal pure returns (uint256) {\n        // Solidity only automatically asserts when dividing by 0\n        require(b > 0, \"SafeMath: division by zero\");\n        uint256 c = a / b;\n        // assert(a == b * c + a % b); // There is no case in which this doesn't hold\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),\n     * Reverts when dividing by zero.\n     *\n     * Counterpart to Solidity's `%` operator. This function uses a `revert`\n     * opcode (which leaves remaining gas untouched) while Solidity uses an\n     * invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     * - The divisor cannot be zero.\n     */\n    function mod(uint256 a, uint256 b) internal pure returns (uint256) {\n        require(b != 0, \"SafeMath: modulo by zero\");\n        return a % b;\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/ownership/Ownable.sol": {
      "content": "pragma solidity ^0.5.0;\n\n/**\n * @dev Contract module which provides a basic access control mechanism, where\n * there is an account (an owner) that can be granted exclusive access to\n * specific functions.\n *\n * This module is used through inheritance. It will make available the modifier\n * `onlyOwner`, which can be aplied to your functions to restrict their use to\n * the owner.\n */\ncontract Ownable {\n    address private _owner;\n\n    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);\n\n    /**\n     * @dev Initializes the contract setting the deployer as the initial owner.\n     */\n    constructor () internal {\n        _owner = msg.sender;\n        emit OwnershipTransferred(address(0), _owner);\n    }\n\n    /**\n     * @dev Returns the address of the current owner.\n     */\n    function owner() public view returns (address) {\n        return _owner;\n    }\n\n    /**\n     * @dev Throws if called by any account other than the owner.\n     */\n    modifier onlyOwner() {\n        require(isOwner(), \"Ownable: caller is not the owner\");\n        _;\n    }\n\n    /**\n     * @dev Returns true if the caller is the current owner.\n     */\n    function isOwner() public view returns (bool) {\n        return msg.sender == _owner;\n    }\n\n    /**\n     * @dev Leaves the contract without owner. It will not be possible to call\n     * `onlyOwner` functions anymore. Can only be called by the current owner.\n     *\n     * > Note: Renouncing ownership will leave the contract without an owner,\n     * thereby removing any functionality that is only available to the owner.\n     */\n    function renounceOwnership() public onlyOwner {\n        emit OwnershipTransferred(_owner, address(0));\n        _owner = address(0);\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Can only be called by the current owner.\n     */\n    function transferOwnership(address newOwner) public onlyOwner {\n        _transferOwnership(newOwner);\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     */\n    function _transferOwnership(address newOwner) internal {\n        require(newOwner != address(0), \"Ownable: new owner is the zero address\");\n        emit OwnershipTransferred(_owner, newOwner);\n        _owner = newOwner;\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/token/ERC20/ERC20Detailed.sol": {
      "content": "pragma solidity ^0.5.0;\n\nimport \"./IERC20.sol\";\n\n/**\n * @dev Optional functions from the ERC20 standard.\n */\ncontract ERC20Detailed is IERC20 {\n    string private _name;\n    string private _symbol;\n    uint8 private _decimals;\n\n    /**\n     * @dev Sets the values for `name`, `symbol`, and `decimals`. All three of\n     * these values are immutable: they can only be set once during\n     * construction.\n     */\n    constructor (string memory name, string memory symbol, uint8 decimals) public {\n        _name = name;\n        _symbol = symbol;\n        _decimals = decimals;\n    }\n\n    /**\n     * @dev Returns the name of the token.\n     */\n    function name() public view returns (string memory) {\n        return _name;\n    }\n\n    /**\n     * @dev Returns the symbol of the token, usually a shorter version of the\n     * name.\n     */\n    function symbol() public view returns (string memory) {\n        return _symbol;\n    }\n\n    /**\n     * @dev Returns the number of decimals used to get its user representation.\n     * For example, if `decimals` equals `2`, a balance of `505` tokens should\n     * be displayed to a user as `5,05` (`505 / 10 ** 2`).\n     *\n     * Tokens usually opt for a value of 18, imitating the relationship between\n     * Ether and Wei.\n     *\n     * > Note that this information is only used for _display_ purposes: it in\n     * no way affects any of the arithmetic of the contract, including\n     * `IERC20.balanceOf` and `IERC20.transfer`.\n     */\n    function decimals() public view returns (uint8) {\n        return _decimals;\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/token/ERC20/IERC20.sol": {
      "content": "pragma solidity ^0.5.0;\n\n/**\n * @dev Interface of the ERC20 standard as defined in the EIP. Does not include\n * the optional functions; to access them see `ERC20Detailed`.\n */\ninterface IERC20 {\n    /**\n     * @dev Returns the amount of tokens in existence.\n     */\n    function totalSupply() external view returns (uint256);\n\n    /**\n     * @dev Returns the amount of tokens owned by `account`.\n     */\n    function balanceOf(address account) external view returns (uint256);\n\n    /**\n     * @dev Moves `amount` tokens from the caller's account to `recipient`.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a `Transfer` event.\n     */\n    function transfer(address recipient, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Returns the remaining number of tokens that `spender` will be\n     * allowed to spend on behalf of `owner` through `transferFrom`. This is\n     * zero by default.\n     *\n     * This value changes when `approve` or `transferFrom` are called.\n     */\n    function allowance(address owner, address spender) external view returns (uint256);\n\n    /**\n     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * > Beware that changing an allowance with this method brings the risk\n     * that someone may use both the old and the new allowance by unfortunate\n     * transaction ordering. One possible solution to mitigate this race\n     * condition is to first reduce the spender's allowance to 0 and set the\n     * desired value afterwards:\n     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729\n     *\n     * Emits an `Approval` event.\n     */\n    function approve(address spender, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Moves `amount` tokens from `sender` to `recipient` using the\n     * allowance mechanism. `amount` is then deducted from the caller's\n     * allowance.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a `Transfer` event.\n     */\n    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Emitted when `value` tokens are moved from one account (`from`) to\n     * another (`to`).\n     *\n     * Note that `value` may be zero.\n     */\n    event Transfer(address indexed from, address indexed to, uint256 value);\n\n    /**\n     * @dev Emitted when the allowance of a `spender` for an `owner` is set by\n     * a call to `approve`. `value` is the new allowance.\n     */\n    event Approval(address indexed owner, address indexed spender, uint256 value);\n}\n"
    },
    "openzeppelin-solidity/contracts/token/ERC20/SafeERC20.sol": {
      "content": "pragma solidity ^0.5.0;\n\nimport \"./IERC20.sol\";\nimport \"../../math/SafeMath.sol\";\nimport \"../../utils/Address.sol\";\n\n/**\n * @title SafeERC20\n * @dev Wrappers around ERC20 operations that throw on failure (when the token\n * contract returns false). Tokens that return no value (and instead revert or\n * throw on failure) are also supported, non-reverting calls are assumed to be\n * successful.\n * To use this library you can add a `using SafeERC20 for ERC20;` statement to your contract,\n * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.\n */\nlibrary SafeERC20 {\n    using SafeMath for uint256;\n    using Address for address;\n\n    function safeTransfer(IERC20 token, address to, uint256 value) internal {\n        callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));\n    }\n\n    function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {\n        callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));\n    }\n\n    function safeApprove(IERC20 token, address spender, uint256 value) internal {\n        // safeApprove should only be called when setting an initial allowance,\n        // or when resetting it to zero. To increase and decrease it, use\n        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'\n        // solhint-disable-next-line max-line-length\n        require((value == 0) || (token.allowance(address(this), spender) == 0),\n            \"SafeERC20: approve from non-zero to non-zero allowance\"\n        );\n        callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));\n    }\n\n    function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {\n        uint256 newAllowance = token.allowance(address(this), spender).add(value);\n        callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));\n    }\n\n    function safeDecreaseAllowance(IERC20 token, address spender, uint256 value) internal {\n        uint256 newAllowance = token.allowance(address(this), spender).sub(value);\n        callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));\n    }\n\n    /**\n     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement\n     * on the return value: the return value is optional (but if data is returned, it must not be false).\n     * @param token The token targeted by the call.\n     * @param data The call data (encoded using abi.encode or one of its variants).\n     */\n    function callOptionalReturn(IERC20 token, bytes memory data) private {\n        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since\n        // we're implementing it ourselves.\n\n        // A Solidity high level call has three parts:\n        //  1. The target address is checked to verify it contains contract code\n        //  2. The call itself is made, and success asserted\n        //  3. The return value is decoded, which in turn checks the size of the returned data.\n        // solhint-disable-next-line max-line-length\n        require(address(token).isContract(), \"SafeERC20: call to non-contract\");\n\n        // solhint-disable-next-line avoid-low-level-calls\n        (bool success, bytes memory returndata) = address(token).call(data);\n        require(success, \"SafeERC20: low-level call failed\");\n\n        if (returndata.length > 0) { // Return data is optional\n            // solhint-disable-next-line max-line-length\n            require(abi.decode(returndata, (bool)), \"SafeERC20: ERC20 operation did not succeed\");\n        }\n    }\n}\n"
    },
    "openzeppelin-solidity/contracts/utils/Address.sol": {
      "content": "pragma solidity ^0.5.0;\n\n/**\n * @dev Collection of functions related to the address type,\n */\nlibrary Address {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * This test is non-exhaustive, and there may be false-negatives: during the\n     * execution of a contract's constructor, its address will be reported as\n     * not containing a contract.\n     *\n     * > It is unsafe to assume that an address for which this function returns\n     * false is an externally-owned account (EOA) and not a contract.\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies in extcodesize, which returns 0 for contracts in\n        // construction, since the code is only stored at the end of the\n        // constructor execution.\n\n        uint256 size;\n        // solhint-disable-next-line no-inline-assembly\n        assembly { size := extcodesize(account) }\n        return size > 0;\n    }\n}\n"
    }
  },
  "settings": {
    "remappings": [],
    "optimizer": {
      "enabled": true,
      "runs": 999999
    },
    "evmVersion": "istanbul",
    "libraries": {},
    "outputSelection": {
      "*": {
        "*": [
          "evm.bytecode",
          "evm.deployedBytecode",
          "abi"
        ]
      }
    }
  }
}}