{{
  "language": "Solidity",
  "sources": {
    "cache/solpp-generated-contracts/HybridERC721.sol": {
      "content": "pragma solidity ^0.8.8;\n\n// SPDX-License-Identifier: MIT\n\n\nimport \"@openzeppelin/contracts/security/Pausable.sol\";\nimport \"@openzeppelin/contracts/utils/Strings.sol\";\nimport \"@openzeppelin/contracts/utils/Context.sol\";\nimport \"./lib/ERC721.sol\";\nimport \"./lib/Hybrid.sol\";\nimport \"./lib/AccessControl.sol\";\n\ncontract OneOfNoneHybrid is ERC721, Hybrid, AccessControl, Pausable {\n  using Strings for uint256;\n\n  string constant METADATA_FROZEN = \"006001\";\n  string constant LIMIT_REACHED = \"006002\";\n\n  mapping(uint256 => string) private _freezeMetadata;\n  string private _baseURI;\n\n  uint256 public constant LIMIT = 300;\n\n  constructor() {\n    _setAdmin(msg.sender);\n  }\n\n  /// @notice according to ERC721Metadata\n  function name() public pure returns (string memory) {\n    return \"SANWEAR Test Units\";\n  }\n\n  /// @notice according to ERC721Metadata\n  function symbol() public pure returns (string memory) {\n    return \"XINSB\";\n  }\n\n  /// @notice allow minter to retrieve a token\n  function mint(address to, TokenStatus status) external virtual whenNotPaused onlyRole(MINTER_ROLE) {\n    require(_maxTokenId + 1 <= LIMIT, LIMIT_REACHED);\n\n    uint256 tokenId = _maxTokenId + 1;\n\n    _mint(to, tokenId);\n    _setStatus(tokenId, status);\n  }\n\n  /// @notice Retrieve metadata URI according to ERC721Metadata standard\n  /// @dev there is an opportunity to freeze metadata URI\n  ///   essentially it means that for the selected tokens we can move metadata to ipfs\n  ///   and keep it there forever\n  function tokenURI(uint256 tokenId) external view returns (string memory) {\n    require(_exists(tokenId), NOT_VALID_NFT);\n\n    if (bytes(_freezeMetadata[tokenId]).length > 0) {\n      return _freezeMetadata[tokenId];\n    }\n\n    return bytes(_baseURI).length > 0 ? string(abi.encodePacked(_baseURI, tokenId.toString())) : '';\n  }\n\n  /// @notice Only owner of the token can freeze metadata.\n  /// @dev this operation is irreversible, use with caution\n  function freezeMetadataURI(uint256 tokenId, string calldata uri) external onlyAdmin {\n    require(_exists(tokenId), NOT_VALID_NFT);\n    require(bytes(_freezeMetadata[tokenId]).length == 0, METADATA_FROZEN);\n\n    _freezeMetadata[tokenId] = uri;\n  }\n\n  /// @notice change base URI for the metadata\n  function setMetadataBaseURI(string calldata uri) external onlyAdmin {\n    _baseURI = uri;\n  }\n\n  /// @notice pause contract\n  function pause() public onlyRole(PAUSER_ROLE) {\n    _pause();\n  }\n\n  /// @notice unpause\n  function unpause() public onlyAdmin {\n    _unpause();\n  }\n\n  /// MARK: Hybrid\n  function setStatus(uint256 tokenId, TokenStatus status) public onlyRole(STATUS_CHANGER_ROLE) {\n    require(_exists(tokenId), NOT_VALID_NFT);\n    _setStatus(tokenId, status);\n  }\n\n  /// @notice beforeTransfer hook\n  /// Disallow transfer if token is redeemed\n  function _beforeTransfer(address from, address to, uint256 tokenId)\n    internal override whenNotPaused notStatus(tokenId, TokenStatus.Redeemed) {}\n\n  /// MARK: AccessControl implementation\n  function setRole(address to, bytes32 role) public onlyAdmin {\n    _grantRole(to, role);\n  }\n\n  function revokeRole(address to, bytes32 role) public onlyAdmin {\n    _revokeRole(to, role);\n  }\n\n  function transferAdmin(address to) public onlyAdmin {\n    _setAdmin(to);\n  }\n}"
    },
    "@openzeppelin/contracts/security/Pausable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.7.0) (security/Pausable.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../utils/Context.sol\";\n\n/**\n * @dev Contract module which allows children to implement an emergency stop\n * mechanism that can be triggered by an authorized account.\n *\n * This module is used through inheritance. It will make available the\n * modifiers `whenNotPaused` and `whenPaused`, which can be applied to\n * the functions of your contract. Note that they will not be pausable by\n * simply including this module, only once the modifiers are put in place.\n */\nabstract contract Pausable is Context {\n    /**\n     * @dev Emitted when the pause is triggered by `account`.\n     */\n    event Paused(address account);\n\n    /**\n     * @dev Emitted when the pause is lifted by `account`.\n     */\n    event Unpaused(address account);\n\n    bool private _paused;\n\n    /**\n     * @dev Initializes the contract in unpaused state.\n     */\n    constructor() {\n        _paused = false;\n    }\n\n    /**\n     * @dev Modifier to make a function callable only when the contract is not paused.\n     *\n     * Requirements:\n     *\n     * - The contract must not be paused.\n     */\n    modifier whenNotPaused() {\n        _requireNotPaused();\n        _;\n    }\n\n    /**\n     * @dev Modifier to make a function callable only when the contract is paused.\n     *\n     * Requirements:\n     *\n     * - The contract must be paused.\n     */\n    modifier whenPaused() {\n        _requirePaused();\n        _;\n    }\n\n    /**\n     * @dev Returns true if the contract is paused, and false otherwise.\n     */\n    function paused() public view virtual returns (bool) {\n        return _paused;\n    }\n\n    /**\n     * @dev Throws if the contract is paused.\n     */\n    function _requireNotPaused() internal view virtual {\n        require(!paused(), \"Pausable: paused\");\n    }\n\n    /**\n     * @dev Throws if the contract is not paused.\n     */\n    function _requirePaused() internal view virtual {\n        require(paused(), \"Pausable: not paused\");\n    }\n\n    /**\n     * @dev Triggers stopped state.\n     *\n     * Requirements:\n     *\n     * - The contract must not be paused.\n     */\n    function _pause() internal virtual whenNotPaused {\n        _paused = true;\n        emit Paused(_msgSender());\n    }\n\n    /**\n     * @dev Returns to normal state.\n     *\n     * Requirements:\n     *\n     * - The contract must be paused.\n     */\n    function _unpause() internal virtual whenPaused {\n        _paused = false;\n        emit Unpaused(_msgSender());\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Strings.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.7.0) (utils/Strings.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev String operations.\n */\nlibrary Strings {\n    bytes16 private constant _HEX_SYMBOLS = \"0123456789abcdef\";\n    uint8 private constant _ADDRESS_LENGTH = 20;\n\n    /**\n     * @dev Converts a `uint256` to its ASCII `string` decimal representation.\n     */\n    function toString(uint256 value) internal pure returns (string memory) {\n        // Inspired by OraclizeAPI's implementation - MIT licence\n        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol\n\n        if (value == 0) {\n            return \"0\";\n        }\n        uint256 temp = value;\n        uint256 digits;\n        while (temp != 0) {\n            digits++;\n            temp /= 10;\n        }\n        bytes memory buffer = new bytes(digits);\n        while (value != 0) {\n            digits -= 1;\n            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));\n            value /= 10;\n        }\n        return string(buffer);\n    }\n\n    /**\n     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.\n     */\n    function toHexString(uint256 value) internal pure returns (string memory) {\n        if (value == 0) {\n            return \"0x00\";\n        }\n        uint256 temp = value;\n        uint256 length = 0;\n        while (temp != 0) {\n            length++;\n            temp >>= 8;\n        }\n        return toHexString(value, length);\n    }\n\n    /**\n     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.\n     */\n    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {\n        bytes memory buffer = new bytes(2 * length + 2);\n        buffer[0] = \"0\";\n        buffer[1] = \"x\";\n        for (uint256 i = 2 * length + 1; i > 1; --i) {\n            buffer[i] = _HEX_SYMBOLS[value & 0xf];\n            value >>= 4;\n        }\n        require(value == 0, \"Strings: hex length insufficient\");\n        return string(buffer);\n    }\n\n    /**\n     * @dev Converts an `address` with fixed length of 20 bytes to its not checksummed ASCII `string` hexadecimal representation.\n     */\n    function toHexString(address addr) internal pure returns (string memory) {\n        return toHexString(uint256(uint160(addr)), _ADDRESS_LENGTH);\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Context.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Provides information about the current execution context, including the\n * sender of the transaction and its data. While these are generally available\n * via msg.sender and msg.data, they should not be accessed in such a direct\n * manner, since when dealing with meta-transactions the account sending and\n * paying for execution may not be the actual sender (as far as an application\n * is concerned).\n *\n * This contract is only required for intermediate, library-like contracts.\n */\nabstract contract Context {\n    function _msgSender() internal view virtual returns (address) {\n        return msg.sender;\n    }\n\n    function _msgData() internal view virtual returns (bytes calldata) {\n        return msg.data;\n    }\n}\n"
    },
    "cache/solpp-generated-contracts/lib/ERC721.sol": {
      "content": "pragma solidity ^0.8.8;\n\n// SPDX-License-Identifier: MIT\n\n\nimport \"@openzeppelin/contracts/token/ERC721/extensions/IERC721Metadata.sol\";\nimport \"@openzeppelin/contracts/token/ERC721/extensions/IERC721Enumerable.sol\";\nimport \"@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol\";\nimport \"@openzeppelin/contracts/utils/introspection/ERC165.sol\";\nimport \"@openzeppelin/contracts/utils/Address.sol\";\nimport \"@openzeppelin/contracts/utils/Context.sol\";\n\n\n// solhint-disable-next-line indent\nabstract contract ERC721 is IERC721Metadata, IERC721Enumerable, Context {\n  using Address for address;\n\n  mapping(uint256 => address) internal _owners;\n  mapping (uint256 => address) internal _idToApproval;\n  mapping (address => mapping (address => bool)) internal _ownerToOperators;\n\n  uint256 internal _maxTokenId;\n\n  /**\n   * @dev List of revert message codes. Implementing dApp should handle showing the correct message.\n   * Based on 0xcert framework error codes.\n   */\n  string constant ZERO_ADDRESS = \"003001\";\n  string constant NOT_VALID_NFT = \"003002\";\n  string constant NOT_OWNER_OR_OPERATOR = \"003003\";\n  string constant NOT_OWNER_APPROVED_OR_OPERATOR = \"003004\";\n  string constant NOT_ABLE_TO_RECEIVE_NFT = \"003005\";\n  string constant NFT_ALREADY_EXISTS = \"003006\";\n  string constant NOT_OWNER = \"003007\";\n  string constant IS_OWNER = \"003008\";\n\n  /**\n * @dev Magic value of a smart contract that can receive NFT.\n   * Equal to: bytes4(keccak256(\"onERC721Received(address,address,uint256,bytes)\")).\n   */\n  bytes4 internal constant MAGIC_ON_ERC721_RECEIVED = 0x150b7a02;\n\n  constructor() {}\n\n  /// @notice MARK: Useful modifiers\n\n  /**\n   * @dev Guarantees that the _msgSender() is an owner or operator of the given NFT.\n   * @param tokenId ID of the NFT to validate.\n   */\n  modifier canOperate(uint256 tokenId) {\n    address tokenOwner = _owners[tokenId];\n    require(\n      tokenOwner == _msgSender() || _ownerToOperators[tokenOwner][_msgSender()],\n      NOT_OWNER_OR_OPERATOR\n    );\n    _;\n  }\n\n  /**\n   * @dev Guarantees that the _msgSender() is allowed to transfer NFT.\n   * @param tokenId ID of the NFT to transfer.\n   */\n  modifier canTransfer(uint256 tokenId) {\n    address tokenOwner = _owners[tokenId];\n\n    require(\n      tokenOwner == _msgSender()\n      || _idToApproval[tokenId] == _msgSender()\n      || _ownerToOperators[tokenOwner][_msgSender()],\n      NOT_OWNER_APPROVED_OR_OPERATOR\n    );\n    _;\n  }\n\n  /**\n   * @dev Guarantees that _tokenId is a valid Token.\n   * @param tokenId ID of the NFT to validate.\n   */\n  modifier validNFToken(uint256 tokenId) {\n    require(_exists(tokenId), NOT_VALID_NFT);\n    _;\n  }\n\n  /// @notice Returns a number of decimal points\n  /// @return Number of decimal points\n  function decimals() public pure virtual returns (uint256) {\n    return 0;\n  }\n\n  /// @notice MARK: ERC721 Implementation\n\n  /// @notice Count all NFTs assigned to an owner\n  /// @dev NFTs assigned to the zero address are considered invalid, and this\n  ///  function throws for queries about the zero address.\n  /// @param owner An address for whom to query the balance\n  /// @return balance The number of NFTs owned by `owner`, possibly zero\n  function balanceOf(address owner) public view virtual returns (uint256 balance) {\n    require(owner != address(0),  ZERO_ADDRESS);\n\n    for (uint256 i; i <= _maxTokenId; i++) {\n      if (_owners[i] == owner) {\n        balance++;\n      }\n    }\n\n    return balance;\n  }\n\n  /// @notice Find the owner of an NFT\n  /// @dev NFTs assigned to zero address are considered invalid, and queries\n  ///  about them do throw.\n  /// @param tokenId The identifier for an NFT\n  /// @return owner The address of the owner of the NFT\n  function ownerOf(uint256 tokenId) external view returns (address owner) {\n    owner = _owners[tokenId];\n    require(owner != address(0), NOT_VALID_NFT);\n  }\n\n  /// @notice Change or reaffirm the approved address for an NFT\n  /// @dev The zero address indicates there is no approved address.\n  ///  Throws unless `_msgSender()` is the current NFT owner, or an authorized\n  ///  operator of the current owner.\n  /// @param approved The new approved NFT controller\n  /// @param tokenId The NFT to approve\n  function approve(address approved, uint256 tokenId) external canOperate(tokenId) validNFToken(tokenId) {\n    address tokenOwner = _owners[tokenId];\n    require(approved != tokenOwner, IS_OWNER);\n\n    _idToApproval[tokenId] = approved;\n    emit Approval(tokenOwner, approved, tokenId);\n  }\n\n  /// @notice Get the approved address for a single NFT\n  /// @dev Throws if `tokenId` is not a valid NFT.\n  /// @param tokenId The NFT to find the approved address for\n  /// @return The approved address for this NFT, or the zero address if there is none\n  function getApproved(uint256 tokenId) external view validNFToken(tokenId) returns (address) {\n    return _idToApproval[tokenId];\n  }\n\n  /// @notice Enable or disable approval for a third party (\"operator\") to manage\n  ///  all of `_msgSender()`'s assets\n  /// @dev Emits the ApprovalForAll event. The contract MUST allow\n  ///  multiple operators per owner.\n  /// @param operator Address to add to the set of authorized operators\n  /// @param approved True if the operator is approved, false to revoke approval\n  function setApprovalForAll(address operator, bool approved) external {\n    _ownerToOperators[_msgSender()][operator] = approved;\n    emit ApprovalForAll(_msgSender(), operator, approved);\n  }\n\n  /// @notice Query if an address is an authorized operator for another address\n  /// @param owner The address that owns the NFTs\n  /// @param operator The address that acts on behalf of the owner\n  /// @return True if `operator` is an approved operator for `owner`, false otherwise\n  function isApprovedForAll(address owner, address operator) public view virtual returns (bool) {\n    return _ownerToOperators[owner][operator];\n  }\n\n  /// @notice Transfers the ownership of an NFT from one address to another address\n  /// @dev This works identically to the other function with an extra data parameter,\n  ///  except this function just sets data to \"\".\n  /// @param from The current owner of the NFT\n  /// @param to The new owner\n  /// @param tokenId The NFT to transfer\n  function safeTransferFrom(address from, address to, uint256 tokenId) external virtual {\n    _safeTransferFrom(from, to, tokenId, '');\n  }\n\n  /// @notice Transfers the ownership of an NFT from one address to another address\n  /// @dev Throws unless `_msgSender()` is the current owner, an authorized\n  ///  operator, or the approved address for this NFT. Throws if `from` is\n  ///  not the current owner. Throws if `to` is the zero address. Throws if\n  ///  `tokenId` is not a valid NFT. When transfer is complete, this function\n  ///  checks if `to` is a smart contract (code size > 0). If so, it calls\n  ///  `onERC721Received` on `to` and throws if the return value is not\n  ///  `bytes4(keccak256(\"onERC721Received(address,address,uint256,bytes)\"))`.\n  /// @param from The current owner of the NFT\n  /// @param to The new owner\n  /// @param tokenId The NFT to transfer\n  /// @param data Additional data with no specified format, sent in call to `to`\n  function safeTransferFrom(address from, address to, uint256 tokenId, bytes calldata data) external canTransfer(tokenId) validNFToken(tokenId) {\n    _safeTransferFrom(from, to, tokenId, data);\n  }\n\n  /// @notice Transfer ownership of an NFT -- THE CALLER IS RESPONSIBLE\n  ///  TO CONFIRM THAT `to` IS CAPABLE OF RECEIVING NFTS OR ELSE\n  ///  THEY MAY BE PERMANENTLY LOST\n  /// @dev Throws unless `_msgSender()` is the current owner, an authorized\n  ///  operator, or the approved address for this NFT. Throws if `from` is\n  ///  not the current owner. Throws if `to` is the zero address. Throws if\n  ///  `tokenId` is not a valid NFT.\n  /// @param from The current owner of the NFT\n  /// @param to The new owner\n  /// @param tokenId The NFT to transfer\n  function transferFrom(address from, address to, uint256 tokenId) external canTransfer(tokenId) validNFToken(tokenId) {\n    address tokenOwner = _owners[tokenId];\n    require(tokenOwner == from, NOT_OWNER);\n    require(to != address(0), ZERO_ADDRESS);\n\n    _transfer(to, tokenId);\n  }\n\n  /// @notice MARK: ERC721Enumerable\n\n  /// @notice Count NFTs tracked by this contract\n  /// @return total A count of valid NFTs tracked by this contract, where each one of\n  ///  them has an assigned and queryable owner not equal to the zero address\n  function totalSupply() public view returns (uint256 total) {\n    for (uint256 i; i <= _maxTokenId; i++) {\n      if (_owners[i] != address(0)) {\n        total++;\n      }\n    }\n\n    return total;\n  }\n\n  /// @notice Enumerate NFTs assigned to an owner\n  /// @dev Throws if `index` >= `balanceOf(owner)` or if\n  ///  `owner` is the zero address, representing invalid NFTs.\n  /// @param owner An address where we are interested in NFTs owned by them\n  /// @param index A counter less than `balanceOf(owner)`\n  /// @return The token identifier for the `index`th NFT assigned to `owner`,\n  ///   (sort order not specified)\n  function tokenOfOwnerByIndex(address owner, uint256 index) external view returns (uint256) {\n    uint256 balance = balanceOf(owner);\n\n    uint256[] memory tokens = new uint256[](balance);\n    uint256 idx;\n\n    for (uint256 i; i <= _maxTokenId; i++) {\n      if (_owners[i] == owner) {\n        tokens[idx] = i;\n        idx++;\n      }\n    }\n\n    return tokens[index];\n  }\n\n  /// @notice Enumerate valid NFTs\n  /// @dev Throws if `index` >= `totalSupply()`.\n  /// @param index A counter less than `totalSupply()`\n  /// @return The token identifier for the `index`th NFT,\n  ///  (sort order not specified)\n  function tokenByIndex(uint256 index) external view returns (uint256) {\n    uint256 supply = totalSupply();\n\n    uint256[] memory tokens = new uint256[](supply);\n    uint256 idx;\n    for (uint256 i; i <= _maxTokenId; i++) {\n      if (_owners[i] != address(0)) {\n        tokens[idx] = i;\n        idx++;\n      }\n    }\n\n    return tokens[index];\n  }\n\n  /// @notice MARK: ERC165 Implementation\n\n  /// @notice Query if a contract implements an interface\n  /// @param interfaceId The interface identifier, as specified in ERC-165\n  /// @dev Interface identification is specified in ERC-165. This function\n  ///  uses less than 30,000 gas.\n  /// @return `true` if the contract implements `interfaceID` and\n  ///  `interfaceId` is not 0xffffffff, `false` otherwise\n  function supportsInterface(bytes4 interfaceId) external pure returns (bool) {\n    if (interfaceId == 0xffffffff) {\n      return false;\n    }\n\n    return\n      interfaceId == type(IERC721).interfaceId ||\n      interfaceId == type(IERC721Receiver).interfaceId ||\n      interfaceId == type(IERC721Metadata).interfaceId ||\n      interfaceId == type(IERC721Enumerable).interfaceId;\n  }\n\n  /// MARK: Private methods\n  function _mint(address to, uint256 tokenId) internal {\n    require(to != address(0), ZERO_ADDRESS);\n    require(!_exists(tokenId), NFT_ALREADY_EXISTS);\n\n    _owners[tokenId] = to;\n\n    if (tokenId > _maxTokenId) {\n      _maxTokenId = tokenId;\n    }\n\n    emit Transfer(address(0), to, tokenId);\n\n    if (to.isContract()) {\n      bytes4 retval = IERC721Receiver(to).onERC721Received(address(this), address(0), tokenId, \"\");\n      require(retval == MAGIC_ON_ERC721_RECEIVED, NOT_ABLE_TO_RECEIVE_NFT);\n    }\n  }\n\n  function _burn(uint256 tokenId) internal virtual validNFToken(tokenId) canTransfer(tokenId) {\n    address tokenOwner = _owners[tokenId];\n\n    _clearApproval(tokenId);\n    delete _owners[tokenId];\n\n    emit Transfer(tokenOwner, address(0), tokenId);\n  }\n\n  function _exists(uint256 tokenId) internal view returns (bool) {\n    return _owners[tokenId] != address(0);\n  }\n\n  function _clearApproval(uint256 tokenId) private {\n    delete _idToApproval[tokenId];\n  }\n\n  /**\n   * @dev Actually perform the safeTransferFrom.\n   * @param _from The current owner of the NFT.\n   * @param _to The new owner.\n   * @param _tokenId The NFT to transfer.\n   * @param _data Additional data with no specified format, sent in call to `_to`.\n   */\n  function _safeTransferFrom(\n    address _from,\n    address _to,\n    uint256 _tokenId,\n    bytes memory _data\n  )\n  private\n  canTransfer(_tokenId)\n  validNFToken(_tokenId)\n  {\n    address tokenOwner = _owners[_tokenId];\n    require(tokenOwner == _from, NOT_OWNER);\n    require(_to != address(0), ZERO_ADDRESS);\n\n    _transfer(_to, _tokenId);\n\n    if (_to.isContract()) {\n      bytes4 retval = IERC721Receiver(_to).onERC721Received(_msgSender(), _from, _tokenId, _data);\n      require(retval == MAGIC_ON_ERC721_RECEIVED, NOT_ABLE_TO_RECEIVE_NFT);\n    }\n  }\n\n  function _transfer(address to, uint256 tokenId) internal virtual {\n    address from = _owners[tokenId];\n\n    _beforeTransfer(from, to, tokenId);\n\n    _clearApproval(tokenId);\n    _owners[tokenId] = to;\n\n    emit Transfer(from, to, tokenId);\n  }\n\n  function _beforeTransfer(address from, address to, uint256 tokenId) internal virtual {}\n}\n"
    },
    "cache/solpp-generated-contracts/lib/Hybrid.sol": {
      "content": "pragma solidity ^0.8.0;\n\n// SPDX-License-Identifier: MIT\n\n\nabstract contract Hybrid {\n  string constant INVALID_STATUS = \"004002\";\n\n  enum  TokenStatus { Vaulted, Redeemed, Lost }\n  mapping(uint256 => TokenStatus) private _tokenStatus;\n\n  function _setStatus(uint256 tokenId, TokenStatus status) internal {\n    _tokenStatus[tokenId] = status;\n  }\n\n  /// Check if token is not in status\n  modifier notStatus(uint256 tokenId, TokenStatus status) {\n    require(_tokenStatus[tokenId] != status, INVALID_STATUS);\n    _;\n  }\n}\n"
    },
    "cache/solpp-generated-contracts/lib/AccessControl.sol": {
      "content": "pragma solidity ^0.8.0;\n\n// SPDX-License-Identifier: MIT\n\n\nimport \"@openzeppelin/contracts/utils/Context.sol\";\n\nabstract contract AccessControl is Context {\n  bytes32 public constant PAUSER_ROLE = keccak256('PAUSER_ROLE');\n  bytes32 public constant MINTER_ROLE = keccak256('MINTER_ROLE');\n  bytes32 public constant STATUS_CHANGER_ROLE = keccak256('STATUS_CHANGER_ROLE');\n\n  string constant INVALID_PERMISSION = \"005001\";\n\n  address private _admin;\n\n  /// Mapping from address to role to boolean\n  mapping(address => mapping(bytes32 => bool)) private _roles;\n\n  modifier onlyRole(bytes32 role) {\n    require(_roles[_msgSender()][role] == true, INVALID_PERMISSION);\n    _;\n  }\n\n  modifier onlyAdmin() {\n    require(_msgSender() == _admin, INVALID_PERMISSION);\n    _;\n  }\n\n  /**\n   * Assign role to the specific address\n   */\n  function _grantRole(address to, bytes32 role) internal {\n    require(to != address(0), INVALID_PERMISSION);\n    _roles[to][role] = true;\n  }\n\n  /**\n   * Revoke role\n   */\n  function _revokeRole(address from, bytes32 role) internal {\n    _roles[from][role] = false;\n  }\n\n  /**\n   * Set admin\n   */\n  function _setAdmin(address to) internal {\n    require(to != address(0), INVALID_PERMISSION);\n    _admin = to;\n  }\n}\n"
    },
    "@openzeppelin/contracts/token/ERC721/extensions/IERC721Metadata.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (token/ERC721/extensions/IERC721Metadata.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../IERC721.sol\";\n\n/**\n * @title ERC-721 Non-Fungible Token Standard, optional metadata extension\n * @dev See https://eips.ethereum.org/EIPS/eip-721\n */\ninterface IERC721Metadata is IERC721 {\n    /**\n     * @dev Returns the token collection name.\n     */\n    function name() external view returns (string memory);\n\n    /**\n     * @dev Returns the token collection symbol.\n     */\n    function symbol() external view returns (string memory);\n\n    /**\n     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.\n     */\n    function tokenURI(uint256 tokenId) external view returns (string memory);\n}\n"
    },
    "@openzeppelin/contracts/token/ERC721/extensions/IERC721Enumerable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC721/extensions/IERC721Enumerable.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../IERC721.sol\";\n\n/**\n * @title ERC-721 Non-Fungible Token Standard, optional enumeration extension\n * @dev See https://eips.ethereum.org/EIPS/eip-721\n */\ninterface IERC721Enumerable is IERC721 {\n    /**\n     * @dev Returns the total amount of tokens stored by the contract.\n     */\n    function totalSupply() external view returns (uint256);\n\n    /**\n     * @dev Returns a token ID owned by `owner` at a given `index` of its token list.\n     * Use along with {balanceOf} to enumerate all of ``owner``'s tokens.\n     */\n    function tokenOfOwnerByIndex(address owner, uint256 index) external view returns (uint256);\n\n    /**\n     * @dev Returns a token ID at a given `index` of all the tokens stored by the contract.\n     * Use along with {totalSupply} to enumerate all tokens.\n     */\n    function tokenByIndex(uint256 index) external view returns (uint256);\n}\n"
    },
    "@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC721/IERC721Receiver.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @title ERC721 token receiver interface\n * @dev Interface for any contract that wants to support safeTransfers\n * from ERC721 asset contracts.\n */\ninterface IERC721Receiver {\n    /**\n     * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}\n     * by `operator` from `from`, this function is called.\n     *\n     * It must return its Solidity selector to confirm the token transfer.\n     * If any other value is returned or the interface is not implemented by the recipient, the transfer will be reverted.\n     *\n     * The selector can be obtained in Solidity with `IERC721Receiver.onERC721Received.selector`.\n     */\n    function onERC721Received(\n        address operator,\n        address from,\n        uint256 tokenId,\n        bytes calldata data\n    ) external returns (bytes4);\n}\n"
    },
    "@openzeppelin/contracts/utils/introspection/ERC165.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)\n\npragma solidity ^0.8.0;\n\nimport \"./IERC165.sol\";\n\n/**\n * @dev Implementation of the {IERC165} interface.\n *\n * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check\n * for the additional interface id that will be supported. For example:\n *\n * ```solidity\n * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {\n *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);\n * }\n * ```\n *\n * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.\n */\nabstract contract ERC165 is IERC165 {\n    /**\n     * @dev See {IERC165-supportsInterface}.\n     */\n    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {\n        return interfaceId == type(IERC165).interfaceId;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Address.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.7.0) (utils/Address.sol)\n\npragma solidity ^0.8.1;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary Address {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * [IMPORTANT]\n     * ====\n     * It is unsafe to assume that an address for which this function returns\n     * false is an externally-owned account (EOA) and not a contract.\n     *\n     * Among others, `isContract` will return false for the following\n     * types of addresses:\n     *\n     *  - an externally-owned account\n     *  - a contract in construction\n     *  - an address where a contract will be created\n     *  - an address where a contract lived, but was destroyed\n     * ====\n     *\n     * [IMPORTANT]\n     * ====\n     * You shouldn't rely on `isContract` to protect against flash loan attacks!\n     *\n     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets\n     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract\n     * constructor.\n     * ====\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies on extcodesize/address.code.length, which returns 0\n        // for contracts in construction, since the code is only stored at the end\n        // of the constructor execution.\n\n        return account.code.length > 0;\n    }\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        require(address(this).balance >= amount, \"Address: insufficient balance\");\n\n        (bool success, ) = recipient.call{value: amount}(\"\");\n        require(success, \"Address: unable to send value, recipient may have reverted\");\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain `call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason, it is bubbled up by this\n     * function (like regular Solidity function calls).\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionCall(target, data, \"Address: low-level call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with\n     * `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(\n        address target,\n        bytes memory data,\n        uint256 value\n    ) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, value, \"Address: low-level call with value failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but\n     * with `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(\n        address target,\n        bytes memory data,\n        uint256 value,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        require(address(this).balance >= value, \"Address: insufficient balance for call\");\n        require(isContract(target), \"Address: call to non-contract\");\n\n        (bool success, bytes memory returndata) = target.call{value: value}(data);\n        return verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {\n        return functionStaticCall(target, data, \"Address: low-level static call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal view returns (bytes memory) {\n        require(isContract(target), \"Address: static call to non-contract\");\n\n        (bool success, bytes memory returndata) = target.staticcall(data);\n        return verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionDelegateCall(target, data, \"Address: low-level delegate call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        require(isContract(target), \"Address: delegate call to non-contract\");\n\n        (bool success, bytes memory returndata) = target.delegatecall(data);\n        return verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the\n     * revert reason using the provided one.\n     *\n     * _Available since v4.3._\n     */\n    function verifyCallResult(\n        bool success,\n        bytes memory returndata,\n        string memory errorMessage\n    ) internal pure returns (bytes memory) {\n        if (success) {\n            return returndata;\n        } else {\n            // Look for revert reason and bubble it up if present\n            if (returndata.length > 0) {\n                // The easiest way to bubble the revert reason is using memory via assembly\n                /// @solidity memory-safe-assembly\n                assembly {\n                    let returndata_size := mload(returndata)\n                    revert(add(32, returndata), returndata_size)\n                }\n            } else {\n                revert(errorMessage);\n            }\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/token/ERC721/IERC721.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.7.0) (token/ERC721/IERC721.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../../utils/introspection/IERC165.sol\";\n\n/**\n * @dev Required interface of an ERC721 compliant contract.\n */\ninterface IERC721 is IERC165 {\n    /**\n     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.\n     */\n    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);\n\n    /**\n     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.\n     */\n    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);\n\n    /**\n     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.\n     */\n    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);\n\n    /**\n     * @dev Returns the number of tokens in ``owner``'s account.\n     */\n    function balanceOf(address owner) external view returns (uint256 balance);\n\n    /**\n     * @dev Returns the owner of the `tokenId` token.\n     *\n     * Requirements:\n     *\n     * - `tokenId` must exist.\n     */\n    function ownerOf(uint256 tokenId) external view returns (address owner);\n\n    /**\n     * @dev Safely transfers `tokenId` token from `from` to `to`.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `tokenId` token must exist and be owned by `from`.\n     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.\n     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.\n     *\n     * Emits a {Transfer} event.\n     */\n    function safeTransferFrom(\n        address from,\n        address to,\n        uint256 tokenId,\n        bytes calldata data\n    ) external;\n\n    /**\n     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients\n     * are aware of the ERC721 protocol to prevent tokens from being forever locked.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `tokenId` token must exist and be owned by `from`.\n     * - If the caller is not `from`, it must have been allowed to move this token by either {approve} or {setApprovalForAll}.\n     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.\n     *\n     * Emits a {Transfer} event.\n     */\n    function safeTransferFrom(\n        address from,\n        address to,\n        uint256 tokenId\n    ) external;\n\n    /**\n     * @dev Transfers `tokenId` token from `from` to `to`.\n     *\n     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `tokenId` token must be owned by `from`.\n     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transferFrom(\n        address from,\n        address to,\n        uint256 tokenId\n    ) external;\n\n    /**\n     * @dev Gives permission to `to` to transfer `tokenId` token to another account.\n     * The approval is cleared when the token is transferred.\n     *\n     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.\n     *\n     * Requirements:\n     *\n     * - The caller must own the token or be an approved operator.\n     * - `tokenId` must exist.\n     *\n     * Emits an {Approval} event.\n     */\n    function approve(address to, uint256 tokenId) external;\n\n    /**\n     * @dev Approve or remove `operator` as an operator for the caller.\n     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.\n     *\n     * Requirements:\n     *\n     * - The `operator` cannot be the caller.\n     *\n     * Emits an {ApprovalForAll} event.\n     */\n    function setApprovalForAll(address operator, bool _approved) external;\n\n    /**\n     * @dev Returns the account approved for `tokenId` token.\n     *\n     * Requirements:\n     *\n     * - `tokenId` must exist.\n     */\n    function getApproved(uint256 tokenId) external view returns (address operator);\n\n    /**\n     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.\n     *\n     * See {setApprovalForAll}\n     */\n    function isApprovedForAll(address owner, address operator) external view returns (bool);\n}\n"
    },
    "@openzeppelin/contracts/utils/introspection/IERC165.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Interface of the ERC165 standard, as defined in the\n * https://eips.ethereum.org/EIPS/eip-165[EIP].\n *\n * Implementers can declare support of contract interfaces, which can then be\n * queried by others ({ERC165Checker}).\n *\n * For an implementation, see {ERC165}.\n */\ninterface IERC165 {\n    /**\n     * @dev Returns true if this contract implements the interface defined by\n     * `interfaceId`. See the corresponding\n     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]\n     * to learn more about how these ids are created.\n     *\n     * This function call must use less than 30 000 gas.\n     */\n    function supportsInterface(bytes4 interfaceId) external view returns (bool);\n}\n"
    }
  },
  "settings": {
    "optimizer": {
      "enabled": true,
      "runs": 1000
    },
    "outputSelection": {
      "*": {
        "*": [
          "evm.bytecode",
          "evm.deployedBytecode",
          "devdoc",
          "userdoc",
          "metadata",
          "abi"
        ]
      }
    },
    "libraries": {}
  }
}}