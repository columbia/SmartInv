{{
  "language": "Solidity",
  "sources": {
    "lib/Bytes2.0/contracts/Staking/BYTES2.sol": {
      "content": "// SPDX-License-Identifier: AGPL-3.0-only\npragma solidity ^0.8.19;\n\nimport \"@openzeppelin/contracts/token/ERC20/ERC20.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\n\nimport \"../access/PermitControl.sol\";\nimport \"../interfaces/IByteContract.sol\";\nimport \"../interfaces/IStaker.sol\";\n\n/**\n\tThis error is thrown when a caller attempts to exchange more BYTES than they \n\thold.\n\n\t@param amount The amount of BYTES that the caller attempted to exchange.\n*/\nerror DoNotHaveEnoughOldBytes (\n\tuint256 amount\n);\n\n/**\n\t@custom:benediction DEVS BENEDICAT ET PROTEGAT CONTRACTVS MEAM\n\t@title A migrated ERC-20 BYTES token contract for the Neo Tokyo ecosystem.\n\t@author Tim Clancy <@_Enoch>\n\n\tThis contract is meant to serve as an upgraded replacement for the original \n\tBYTES contract in order to support tunable emissions from the new Neo Tokyo \n\tstaker. This contract maintains the requisite stubs to function with the rest \n\tof the Neo Tokyo ecosystem. This contract also maintains the original admin \n\tfunctions.\n\n\t@custom:date February 14th, 2023.\n*/\ncontract BYTES2 is PermitControl, ERC20(\"BYTES\", \"BYTES\") {\n\n\t/// The identifier for the right to perform token burns.\n\tbytes32 public constant BURN = keccak256(\"BURN\");\n\n\t/// The identifier for the right to perform some contract changes.\n\tbytes32 public constant ADMIN = keccak256(\"ADMIN\");\n\n\t/// The address of the original BYTES 1.0 contract.\n\taddress immutable public BYTES1;\n\n\t/// The address of the S1 Citizen contract.\n\taddress immutable public S1_CITIZEN;\n\n\t/// The address of the Neo Tokyo staker contract.\n\taddress public STAKER;\n\n\t/// The address of the treasury which will receive minted DAO taxes.\n\taddress public TREASURY;\n\n\t/**\n\t\tThis event is emitted when a caller upgrades their holdings of BYTES 1.0 to \n\t\tthe new BYTES 2.0 token.\n\n\t\t@param caller The address of the caller upgrading their BYTES.\n\t\t@param amount The amount of BYTES upgraded.\n\t*/\n\tevent BytesUpgraded (\n\t\taddress indexed caller,\n\t\tuint256 amount\n\t);\n\n\t/**\n\t\tConstruct a new instance of this BYTES 2.0 contract configured with the \n\t\tgiven immutable contract addresses.\n\n\t\t@param _bytes The address of the BYTES 2.0 ERC-20 token contract.\n\t\t@param _s1Citizen The address of the assembled Neo Tokyo S1 Citizen.\n\t\t@param _staker The address of the new BYTES emitting staker.\n\t\t@param _treasury The address of the DAO treasury.\n\t*/\n\tconstructor (\n\t\taddress _bytes,\n\t\taddress _s1Citizen,\n\t\taddress _staker,\n\t\taddress _treasury\n\t) {\n\t\tBYTES1 = _bytes;\n\t\tS1_CITIZEN = _s1Citizen;\n\t\tSTAKER = _staker;\n\t\tTREASURY = _treasury;\n\t}\n\n\t/**\n\t\tAllow holders of the old BYTES contract to change them for BYTES 2.0; old \n\t\tBYTES tokens will be burnt.\n\n\t\t@param _amount The amount of old BYTES tokens to exchange.\n\t*/\n\tfunction upgradeBytes (\n\t\tuint256 _amount\n\t) external {\n\t\tif (IERC20(BYTES1).balanceOf(msg.sender) < _amount) {\n\t\t\trevert DoNotHaveEnoughOldBytes(_amount);\n\t\t}\n\n\t\t// Burn the original BYTES 1.0 tokens and mint replacement BYTES 2.0.\n\t\tIByteContract(BYTES1).burn(msg.sender, _amount);\n\t\t_mint(msg.sender, _amount);\n\n\t\t// Emit the upgrade event.\n\t\temit BytesUpgraded(msg.sender, _amount);\n\t}\n\n\t/**\n\t\tThis function is called by the S1 Citizen contract to emit BYTES to callers \n\t\tbased on their state from the staker contract.\n\n\t\t@param _to The reward address to mint BYTES to.\n\t*/\n\tfunction getReward (\n\t\taddress _to\n\t) external {\n\t\t(\n\t\t\tuint256 reward,\n\t\t\tuint256 daoCommision\n\t\t) = IStaker(STAKER).claimReward(_to);\n\n\t\t// Mint both reward BYTES and the DAO tax to targeted recipients.\n\t\tif (reward > 0) {\n\t\t\t_mint(_to, reward);\n\t\t}\n\t\tif (daoCommision > 0) {\n\t\t\t_mint(TREASURY, daoCommision);\n\t\t}\n\t}\n\n\t/**\n\t\tPermit authorized callers to burn BYTES from the `_from` address. When \n\t\tBYTES are burnt, 2/3 of the BYTES burnt are minted to the DAO treasury. This \n\t\toperation is never expected to overflow given operational bounds on the \n\t\tamount of BYTES tokens ever allowed to enter circulation.\n\n\t\t@param _from The address to burn tokens from.\n\t\t@param _amount The amount of tokens to burn.\n\t*/\n\tfunction burn (\n\t\taddress _from,\n\t\tuint256 _amount\n\t) hasValidPermit(UNIVERSAL, BURN) external {\n\t\t_burn(_from, _amount);\n\n\t\t/*\n\t\t\tWe are aware that this math does not round perfectly for all values of\n\t\t\t`_amount`. We don't care.\n\t\t*/\n\t\tuint256 treasuryShare;\n\t\tunchecked {\n\t\t\ttreasuryShare = _amount * 2 / 3;\n\t\t}\n\t\t_mint(TREASURY, treasuryShare);\n\t}\n\n\t/**\n\t\tAllow a permitted caller to update the staker contract address.\n\n\t\t@param _staker The address of the new staker contract.\n\t*/\n\tfunction changeStakingContractAddress (\n\t\taddress _staker\n\t) hasValidPermit(UNIVERSAL, ADMIN) external {\n\t\tSTAKER = _staker;\n\t}\n\n\t/**\n\t\tAllow a permitted caller to update the treasury address.\n\n\t\t@param _treasury The address of the new treasury.\n\t*/\n\tfunction changeTreasuryContractAddress (\n\t\taddress _treasury\n\t) hasValidPermit(UNIVERSAL, ADMIN) external {\n\t\tTREASURY = _treasury;\n\t}\n\n\t/**\n\t\tThis function is called by the S1 Citizen contract before an NFT transfer \n\t\tand before a call to `getReward`. For historical reasons it must be left \n\t\there as a stub and cannot be entirely removed, though now it remains as a \n\t\tno-op.\n\n\t\t@custom:param A throw-away parameter to fulfill the Citizen call.\n\t\t@custom:param A throw-away parameter to fulfill the Citizen call.\n\t\t@custom:param A throw-away parameter to fulfill the Citizen call.\n\t*/\n\tfunction updateReward (\n\t\taddress,\n\t\taddress,\n\t\tuint256\n\t) external {\n\t}\n\n\t/**\n\t\tThis function is called by the S1 Citizen contract when a new citizen is \n\t\tminted. For historical reasons it must be left here as a stub and cannot be \n\t\tentirely removed, though now it remains as a no-op.\n\n\t\t@custom:param A throw-away parameter to fulfill the Citizen call.\n\t\t@custom:param A throw-away parameter to fulfill the Citizen call.\n\t*/\n\tfunction updateRewardOnMint (\n\t\taddress,\n\t\tuint256\n\t) external {\n  }\n}\n"
    },
    "lib/openzeppelin-contracts/contracts/token/ERC20/ERC20.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/ERC20.sol)\n\npragma solidity ^0.8.0;\n\nimport \"./IERC20.sol\";\nimport \"./extensions/IERC20Metadata.sol\";\nimport \"../../utils/Context.sol\";\n\n/**\n * @dev Implementation of the {IERC20} interface.\n *\n * This implementation is agnostic to the way tokens are created. This means\n * that a supply mechanism has to be added in a derived contract using {_mint}.\n * For a generic mechanism see {ERC20PresetMinterPauser}.\n *\n * TIP: For a detailed writeup see our guide\n * https://forum.openzeppelin.com/t/how-to-implement-erc20-supply-mechanisms/226[How\n * to implement supply mechanisms].\n *\n * The default value of {decimals} is 18. To change this, you should override\n * this function so it returns a different value.\n *\n * We have followed general OpenZeppelin Contracts guidelines: functions revert\n * instead returning `false` on failure. This behavior is nonetheless\n * conventional and does not conflict with the expectations of ERC20\n * applications.\n *\n * Additionally, an {Approval} event is emitted on calls to {transferFrom}.\n * This allows applications to reconstruct the allowance for all accounts just\n * by listening to said events. Other implementations of the EIP may not emit\n * these events, as it isn't required by the specification.\n *\n * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}\n * functions have been added to mitigate the well-known issues around setting\n * allowances. See {IERC20-approve}.\n */\ncontract ERC20 is Context, IERC20, IERC20Metadata {\n    mapping(address => uint256) private _balances;\n\n    mapping(address => mapping(address => uint256)) private _allowances;\n\n    uint256 private _totalSupply;\n\n    string private _name;\n    string private _symbol;\n\n    /**\n     * @dev Sets the values for {name} and {symbol}.\n     *\n     * All two of these values are immutable: they can only be set once during\n     * construction.\n     */\n    constructor(string memory name_, string memory symbol_) {\n        _name = name_;\n        _symbol = symbol_;\n    }\n\n    /**\n     * @dev Returns the name of the token.\n     */\n    function name() public view virtual override returns (string memory) {\n        return _name;\n    }\n\n    /**\n     * @dev Returns the symbol of the token, usually a shorter version of the\n     * name.\n     */\n    function symbol() public view virtual override returns (string memory) {\n        return _symbol;\n    }\n\n    /**\n     * @dev Returns the number of decimals used to get its user representation.\n     * For example, if `decimals` equals `2`, a balance of `505` tokens should\n     * be displayed to a user as `5.05` (`505 / 10 ** 2`).\n     *\n     * Tokens usually opt for a value of 18, imitating the relationship between\n     * Ether and Wei. This is the default value returned by this function, unless\n     * it's overridden.\n     *\n     * NOTE: This information is only used for _display_ purposes: it in\n     * no way affects any of the arithmetic of the contract, including\n     * {IERC20-balanceOf} and {IERC20-transfer}.\n     */\n    function decimals() public view virtual override returns (uint8) {\n        return 18;\n    }\n\n    /**\n     * @dev See {IERC20-totalSupply}.\n     */\n    function totalSupply() public view virtual override returns (uint256) {\n        return _totalSupply;\n    }\n\n    /**\n     * @dev See {IERC20-balanceOf}.\n     */\n    function balanceOf(address account) public view virtual override returns (uint256) {\n        return _balances[account];\n    }\n\n    /**\n     * @dev See {IERC20-transfer}.\n     *\n     * Requirements:\n     *\n     * - `to` cannot be the zero address.\n     * - the caller must have a balance of at least `amount`.\n     */\n    function transfer(address to, uint256 amount) public virtual override returns (bool) {\n        address owner = _msgSender();\n        _transfer(owner, to, amount);\n        return true;\n    }\n\n    /**\n     * @dev See {IERC20-allowance}.\n     */\n    function allowance(address owner, address spender) public view virtual override returns (uint256) {\n        return _allowances[owner][spender];\n    }\n\n    /**\n     * @dev See {IERC20-approve}.\n     *\n     * NOTE: If `amount` is the maximum `uint256`, the allowance is not updated on\n     * `transferFrom`. This is semantically equivalent to an infinite approval.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     */\n    function approve(address spender, uint256 amount) public virtual override returns (bool) {\n        address owner = _msgSender();\n        _approve(owner, spender, amount);\n        return true;\n    }\n\n    /**\n     * @dev See {IERC20-transferFrom}.\n     *\n     * Emits an {Approval} event indicating the updated allowance. This is not\n     * required by the EIP. See the note at the beginning of {ERC20}.\n     *\n     * NOTE: Does not update the allowance if the current allowance\n     * is the maximum `uint256`.\n     *\n     * Requirements:\n     *\n     * - `from` and `to` cannot be the zero address.\n     * - `from` must have a balance of at least `amount`.\n     * - the caller must have allowance for ``from``'s tokens of at least\n     * `amount`.\n     */\n    function transferFrom(address from, address to, uint256 amount) public virtual override returns (bool) {\n        address spender = _msgSender();\n        _spendAllowance(from, spender, amount);\n        _transfer(from, to, amount);\n        return true;\n    }\n\n    /**\n     * @dev Atomically increases the allowance granted to `spender` by the caller.\n     *\n     * This is an alternative to {approve} that can be used as a mitigation for\n     * problems described in {IERC20-approve}.\n     *\n     * Emits an {Approval} event indicating the updated allowance.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     */\n    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {\n        address owner = _msgSender();\n        _approve(owner, spender, allowance(owner, spender) + addedValue);\n        return true;\n    }\n\n    /**\n     * @dev Atomically decreases the allowance granted to `spender` by the caller.\n     *\n     * This is an alternative to {approve} that can be used as a mitigation for\n     * problems described in {IERC20-approve}.\n     *\n     * Emits an {Approval} event indicating the updated allowance.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     * - `spender` must have allowance for the caller of at least\n     * `subtractedValue`.\n     */\n    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {\n        address owner = _msgSender();\n        uint256 currentAllowance = allowance(owner, spender);\n        require(currentAllowance >= subtractedValue, \"ERC20: decreased allowance below zero\");\n        unchecked {\n            _approve(owner, spender, currentAllowance - subtractedValue);\n        }\n\n        return true;\n    }\n\n    /**\n     * @dev Moves `amount` of tokens from `from` to `to`.\n     *\n     * This internal function is equivalent to {transfer}, and can be used to\n     * e.g. implement automatic token fees, slashing mechanisms, etc.\n     *\n     * Emits a {Transfer} event.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `from` must have a balance of at least `amount`.\n     */\n    function _transfer(address from, address to, uint256 amount) internal virtual {\n        require(from != address(0), \"ERC20: transfer from the zero address\");\n        require(to != address(0), \"ERC20: transfer to the zero address\");\n\n        _beforeTokenTransfer(from, to, amount);\n\n        uint256 fromBalance = _balances[from];\n        require(fromBalance >= amount, \"ERC20: transfer amount exceeds balance\");\n        unchecked {\n            _balances[from] = fromBalance - amount;\n            // Overflow not possible: the sum of all balances is capped by totalSupply, and the sum is preserved by\n            // decrementing then incrementing.\n            _balances[to] += amount;\n        }\n\n        emit Transfer(from, to, amount);\n\n        _afterTokenTransfer(from, to, amount);\n    }\n\n    /** @dev Creates `amount` tokens and assigns them to `account`, increasing\n     * the total supply.\n     *\n     * Emits a {Transfer} event with `from` set to the zero address.\n     *\n     * Requirements:\n     *\n     * - `account` cannot be the zero address.\n     */\n    function _mint(address account, uint256 amount) internal virtual {\n        require(account != address(0), \"ERC20: mint to the zero address\");\n\n        _beforeTokenTransfer(address(0), account, amount);\n\n        _totalSupply += amount;\n        unchecked {\n            // Overflow not possible: balance + amount is at most totalSupply + amount, which is checked above.\n            _balances[account] += amount;\n        }\n        emit Transfer(address(0), account, amount);\n\n        _afterTokenTransfer(address(0), account, amount);\n    }\n\n    /**\n     * @dev Destroys `amount` tokens from `account`, reducing the\n     * total supply.\n     *\n     * Emits a {Transfer} event with `to` set to the zero address.\n     *\n     * Requirements:\n     *\n     * - `account` cannot be the zero address.\n     * - `account` must have at least `amount` tokens.\n     */\n    function _burn(address account, uint256 amount) internal virtual {\n        require(account != address(0), \"ERC20: burn from the zero address\");\n\n        _beforeTokenTransfer(account, address(0), amount);\n\n        uint256 accountBalance = _balances[account];\n        require(accountBalance >= amount, \"ERC20: burn amount exceeds balance\");\n        unchecked {\n            _balances[account] = accountBalance - amount;\n            // Overflow not possible: amount <= accountBalance <= totalSupply.\n            _totalSupply -= amount;\n        }\n\n        emit Transfer(account, address(0), amount);\n\n        _afterTokenTransfer(account, address(0), amount);\n    }\n\n    /**\n     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.\n     *\n     * This internal function is equivalent to `approve`, and can be used to\n     * e.g. set automatic allowances for certain subsystems, etc.\n     *\n     * Emits an {Approval} event.\n     *\n     * Requirements:\n     *\n     * - `owner` cannot be the zero address.\n     * - `spender` cannot be the zero address.\n     */\n    function _approve(address owner, address spender, uint256 amount) internal virtual {\n        require(owner != address(0), \"ERC20: approve from the zero address\");\n        require(spender != address(0), \"ERC20: approve to the zero address\");\n\n        _allowances[owner][spender] = amount;\n        emit Approval(owner, spender, amount);\n    }\n\n    /**\n     * @dev Updates `owner` s allowance for `spender` based on spent `amount`.\n     *\n     * Does not update the allowance amount in case of infinite allowance.\n     * Revert if not enough allowance is available.\n     *\n     * Might emit an {Approval} event.\n     */\n    function _spendAllowance(address owner, address spender, uint256 amount) internal virtual {\n        uint256 currentAllowance = allowance(owner, spender);\n        if (currentAllowance != type(uint256).max) {\n            require(currentAllowance >= amount, \"ERC20: insufficient allowance\");\n            unchecked {\n                _approve(owner, spender, currentAllowance - amount);\n            }\n        }\n    }\n\n    /**\n     * @dev Hook that is called before any transfer of tokens. This includes\n     * minting and burning.\n     *\n     * Calling conditions:\n     *\n     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens\n     * will be transferred to `to`.\n     * - when `from` is zero, `amount` tokens will be minted for `to`.\n     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.\n     * - `from` and `to` are never both zero.\n     *\n     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].\n     */\n    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual {}\n\n    /**\n     * @dev Hook that is called after any transfer of tokens. This includes\n     * minting and burning.\n     *\n     * Calling conditions:\n     *\n     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens\n     * has been transferred to `to`.\n     * - when `from` is zero, `amount` tokens have been minted for `to`.\n     * - when `to` is zero, `amount` of ``from``'s tokens have been burned.\n     * - `from` and `to` are never both zero.\n     *\n     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].\n     */\n    function _afterTokenTransfer(address from, address to, uint256 amount) internal virtual {}\n}\n"
    },
    "lib/openzeppelin-contracts/contracts/token/ERC20/IERC20.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/IERC20.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Interface of the ERC20 standard as defined in the EIP.\n */\ninterface IERC20 {\n    /**\n     * @dev Emitted when `value` tokens are moved from one account (`from`) to\n     * another (`to`).\n     *\n     * Note that `value` may be zero.\n     */\n    event Transfer(address indexed from, address indexed to, uint256 value);\n\n    /**\n     * @dev Emitted when the allowance of a `spender` for an `owner` is set by\n     * a call to {approve}. `value` is the new allowance.\n     */\n    event Approval(address indexed owner, address indexed spender, uint256 value);\n\n    /**\n     * @dev Returns the amount of tokens in existence.\n     */\n    function totalSupply() external view returns (uint256);\n\n    /**\n     * @dev Returns the amount of tokens owned by `account`.\n     */\n    function balanceOf(address account) external view returns (uint256);\n\n    /**\n     * @dev Moves `amount` tokens from the caller's account to `to`.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transfer(address to, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Returns the remaining number of tokens that `spender` will be\n     * allowed to spend on behalf of `owner` through {transferFrom}. This is\n     * zero by default.\n     *\n     * This value changes when {approve} or {transferFrom} are called.\n     */\n    function allowance(address owner, address spender) external view returns (uint256);\n\n    /**\n     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * IMPORTANT: Beware that changing an allowance with this method brings the risk\n     * that someone may use both the old and the new allowance by unfortunate\n     * transaction ordering. One possible solution to mitigate this race\n     * condition is to first reduce the spender's allowance to 0 and set the\n     * desired value afterwards:\n     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729\n     *\n     * Emits an {Approval} event.\n     */\n    function approve(address spender, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Moves `amount` tokens from `from` to `to` using the\n     * allowance mechanism. `amount` is then deducted from the caller's\n     * allowance.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transferFrom(address from, address to, uint256 amount) external returns (bool);\n}\n"
    },
    "lib/Bytes2.0/contracts/access/PermitControl.sol": {
      "content": "// SPDX-License-Identifier: AGPL-3.0-only\npragma solidity ^0.8.19;\n\nimport \"@openzeppelin/contracts/access/Ownable.sol\";\nimport \"@openzeppelin/contracts/utils/Address.sol\";\n\n/**\n\t@custom:benediction DEVS BENEDICAT ET PROTEGAT CONTRACTVS MEAM\n\t@title An advanced permission-management contract.\n\t@author Tim Clancy <@_Enoch>\n\n\tThis contract allows for a contract owner to delegate specific rights to\n\texternal addresses. Additionally, these rights can be gated behind certain\n\tsets of circumstances and granted expiration times. This is useful for some\n\tmore finely-grained access control in contracts.\n\n\tThe owner of this contract is always a fully-permissioned super-administrator.\n\n\t@custom:date August 23rd, 2021.\n*/\nabstract contract PermitControl is Ownable {\n\tusing Address for address;\n\n\t/// A special reserved constant for representing no rights.\n\tbytes32 public constant ZERO_RIGHT = hex\"00000000000000000000000000000000\";\n\n\t/// A special constant specifying the unique, universal-rights circumstance.\n\tbytes32 public constant UNIVERSAL = hex\"FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF\";\n\n\t/**\n\t\tA special constant specifying the unique manager right. This right allows an\n\t\taddress to freely-manipulate the `managedRight` mapping.\n\t*/\n\tbytes32 public constant MANAGER = hex\"FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF\";\n\n\t/**\n\t\tA mapping of per-address permissions to the circumstances, represented as\n\t\tan additional layer of generic bytes32 data, under which the addresses have\n\t\tvarious permits. A permit in this sense is represented by a per-circumstance\n\t\tmapping which couples some right, represented as a generic bytes32, to an\n\t\texpiration time wherein the right may no longer be exercised. An expiration\n\t\ttime of 0 indicates that there is in fact no permit for the specified\n\t\taddress to exercise the specified right under the specified circumstance.\n\n\t\t@dev Universal rights MUST be stored under the 0xFFFFFFFFFFFFFFFFFFFFFFFF...\n\t\tmax-integer circumstance. Perpetual rights may be given an expiry time of\n\t\tmax-integer.\n\t*/\n\tmapping ( address => mapping( bytes32 => mapping( bytes32 => uint256 ))) \n\t\tpublic permissions;\n\n\t/**\n\t\tAn additional mapping of managed rights to manager rights. This mapping\n\t\trepresents the administrator relationship that various rights have with one\n\t\tanother. An address with a manager right may freely set permits for that\n\t\tmanager right's managed rights. Each right may be managed by only one other\n\t\tright.\n\t*/\n\tmapping ( bytes32 => bytes32 ) public managerRight;\n\n\t/**\n\t\tAn event emitted when an address has a permit updated. This event captures,\n\t\tthrough its various parameter combinations, the cases of granting a permit,\n\t\tupdating the expiration time of a permit, or revoking a permit.\n\n\t\t@param updater The address which has updated the permit.\n\t\t@param updatee The address whose permit was updated.\n\t\t@param circumstance The circumstance wherein the permit was updated.\n\t\t@param role The role which was updated.\n\t\t@param expirationTime The time when the permit expires.\n\t*/\n\tevent PermitUpdated (\n\t\taddress indexed updater,\n\t\taddress indexed updatee,\n\t\tbytes32 circumstance,\n\t\tbytes32 indexed role,\n\t\tuint256 expirationTime\n\t);\n\n\t/**\n\t\tAn event emitted when a management relationship in `managerRight` is\n\t\tupdated. This event captures adding and revoking management permissions via\n\t\tobserving the update history of the `managerRight` value.\n\n\t\t@param manager The address of the manager performing this update.\n\t\t@param managedRight The right which had its manager updated.\n\t\t@param managerRight The new manager right which was updated to.\n\t*/\n\tevent ManagementUpdated (\n\t\taddress indexed manager,\n\t\tbytes32 indexed managedRight,\n\t\tbytes32 indexed managerRight\n\t);\n\n\t/**\n\t\tA modifier which allows only the super-administrative owner or addresses\n\t\twith a specified valid right to perform a call.\n\n\t\t@param _circumstance The circumstance under which to check for the validity\n\t\t\tof the specified `right`.\n\t\t@param _right The right to validate for the calling address. It must be\n\t\t\tnon-expired and exist within the specified `_circumstance`.\n\t*/\n\tmodifier hasValidPermit (\n\t\tbytes32 _circumstance,\n\t\tbytes32 _right\n\t) {\n\t\trequire(\n\t\t\t_msgSender() == owner() || hasRight(_msgSender(), _circumstance, _right),\n\t\t\t\"P1\"\n\t\t);\n\t\t_;\n\t}\n\n\t/**\n\t\tSet the `_managerRight` whose `UNIVERSAL` holders may freely manage the\n\t\tspecified `_managedRight`.\n\n\t\t@param _managedRight The right which is to have its manager set to\n\t\t\t`_managerRight`.\n\t\t@param _managerRight The right whose `UNIVERSAL` holders may manage\n\t\t\t`_managedRight`.\n\t*/\n\tfunction setManagerRight (\n\t\tbytes32 _managedRight,\n\t\tbytes32 _managerRight\n\t) external virtual hasValidPermit(UNIVERSAL, MANAGER) {\n\t\trequire(_managedRight != ZERO_RIGHT, \"P3\");\n\t\tmanagerRight[_managedRight] = _managerRight;\n\t\temit ManagementUpdated(_msgSender(), _managedRight, _managerRight);\n\t}\n\n\t/**\n\t\tSet the permit to a specific address under some circumstances. A permit may\n\t\tonly be set by the super-administrative contract owner or an address holding\n\t\tsome delegated management permit.\n\n\t\t@param _address The address to assign the specified `_right` to.\n\t\t@param _circumstance The circumstance in which the `_right` is valid.\n\t\t@param _right The specific right to assign.\n\t\t@param _expirationTime The time when the `_right` expires for the provided\n\t\t\t`_circumstance`.\n\t*/\n\tfunction setPermit (\n\t\taddress _address,\n\t\tbytes32 _circumstance,\n\t\tbytes32 _right,\n\t\tuint256 _expirationTime\n\t) public virtual hasValidPermit(UNIVERSAL, managerRight[_right]) {\n\t\trequire(_right != ZERO_RIGHT, \"P2\");\n\t\tpermissions[_address][_circumstance][_right] = _expirationTime;\n\t\temit PermitUpdated(\n\t\t\t_msgSender(),\n\t\t\t_address,\n\t\t\t_circumstance,\n\t\t\t_right,\n\t\t\t_expirationTime\n\t\t);\n\t}\n\n\t/**\n\t\tDetermine whether or not an address has some rights under the given\n\t\tcircumstance, and if they do have the right, until when.\n\n\t\t@param _address The address to check for the specified `_right`.\n\t\t@param _circumstance The circumstance to check the specified `_right` for.\n\t\t@param _right The right to check for validity.\n\n\t\t@return The timestamp in seconds when the `_right` expires. If the timestamp\n\t\t\tis zero, we can assume that the user never had the right.\n\t*/\n\tfunction hasRightUntil (\n\t\taddress _address,\n\t\tbytes32 _circumstance,\n\t\tbytes32 _right\n\t) public view returns (uint256) {\n\t\treturn permissions[_address][_circumstance][_right];\n\t}\n\n\t/**\n\t\tDetermine whether or not an address has some rights under the given\n\t\tcircumstance,\n\n\t\t@param _address The address to check for the specified `_right`.\n\t\t@param _circumstance The circumstance to check the specified `_right` for.\n\t\t@param _right The right to check for validity.\n\n\t\t@return true or false, whether user has rights and time is valid.\n\t*/\n\tfunction hasRight (\n\t\taddress _address,\n\t\tbytes32 _circumstance,\n\t\tbytes32 _right\n\t) public view returns (bool) {\n\t\treturn permissions[_address][_circumstance][_right] > block.timestamp;\n\t}\n}\n"
    },
    "lib/Bytes2.0/contracts/interfaces/IByteContract.sol": {
      "content": "// SPDX-License-Identifier: AGPL-3.0-only\npragma solidity ^0.8.19;\n\n/**\n\t@custom:benediction DEVS BENEDICAT ET PROTEGAT CONTRACTVS MEAM\n\t@title A migrated ERC-20 BYTES token contract for the Neo Tokyo ecosystem.\n\t@author Tim Clancy <@_Enoch>\n\n\tThis is the interface for the BYTES 2.0 contract.\n\n\t@custom:date February 14th, 2023.\n*/\ninterface IByteContract {\n\n\t/**\n\t\tPermit authorized callers to burn BYTES from the `_from` address. When \n\t\tBYTES are burnt, 2/3 of the BYTES are sent to the DAO treasury. This \n\t\toperation is never expected to overflow given operational bounds on the \n\t\tamount of BYTES tokens ever allowed to enter circulation.\n\n\t\t@param _from The address to burn tokens from.\n\t\t@param _amount The amount of tokens to burn.\n\t*/\n\tfunction burn (\n\t\taddress _from,\n\t\tuint256 _amount\n\t) external;\n\t\n\t/**\n\t\tThis function is called by the S1 Citizen contract to emit BYTES to callers \n\t\tbased on their state from the staker contract.\n\n\t\t@param _to The reward address to mint BYTES to.\n\t*/\n\tfunction getReward (\n\t\taddress _to\n\t) external;\n}\n"
    },
    "lib/Bytes2.0/contracts/interfaces/IStaker.sol": {
      "content": "// SPDX-License-Identifier: AGPL-3.0-only\npragma solidity ^0.8.19;\n\n/**\n\t@custom:benediction DEVS BENEDICAT ET PROTEGAT CONTRACTVS MEAM\n\t@title A pool-based staking contract for the Neo Tokyo ecosystem.\n\t@author Tim Clancy <@_Enoch>\n\t@author Rostislav Khlebnikov <@catpic5buck>\n\n\tThis is the interface for the staker contract.\n\n\t@custom:date February 14th, 2023.\n*/\ninterface IStaker {\n\n\t/**\n\t\tDetermine the reward, based on staking participation at this moment, of a \n\t\tparticular recipient. Due to a historic web of Neo Tokyo dependencies, \n\t\trewards are actually minted through the BYTES contract.\n\n\t\t@param _recipient The recipient to calculate the reward for.\n\n\t\t@return A tuple containing (the number of tokens due to be minted to \n\t\t\t`_recipient` as a reward, and the number of tokens that should be minted \n\t\t\tto the DAO treasury as a DAO tax).\n\t*/\n  function claimReward (\n\t\taddress _recipient\n\t) external returns (uint256, uint256);\n}\n"
    },
    "lib/openzeppelin-contracts/contracts/token/ERC20/extensions/IERC20Metadata.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../IERC20.sol\";\n\n/**\n * @dev Interface for the optional metadata functions from the ERC20 standard.\n *\n * _Available since v4.1._\n */\ninterface IERC20Metadata is IERC20 {\n    /**\n     * @dev Returns the name of the token.\n     */\n    function name() external view returns (string memory);\n\n    /**\n     * @dev Returns the symbol of the token.\n     */\n    function symbol() external view returns (string memory);\n\n    /**\n     * @dev Returns the decimals places of the token.\n     */\n    function decimals() external view returns (uint8);\n}\n"
    },
    "lib/openzeppelin-contracts/contracts/utils/Context.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Provides information about the current execution context, including the\n * sender of the transaction and its data. While these are generally available\n * via msg.sender and msg.data, they should not be accessed in such a direct\n * manner, since when dealing with meta-transactions the account sending and\n * paying for execution may not be the actual sender (as far as an application\n * is concerned).\n *\n * This contract is only required for intermediate, library-like contracts.\n */\nabstract contract Context {\n    function _msgSender() internal view virtual returns (address) {\n        return msg.sender;\n    }\n\n    function _msgData() internal view virtual returns (bytes calldata) {\n        return msg.data;\n    }\n}\n"
    },
    "lib/openzeppelin-contracts/contracts/access/Ownable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (access/Ownable.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../utils/Context.sol\";\n\n/**\n * @dev Contract module which provides a basic access control mechanism, where\n * there is an account (an owner) that can be granted exclusive access to\n * specific functions.\n *\n * By default, the owner account will be the one that deploys the contract. This\n * can later be changed with {transferOwnership}.\n *\n * This module is used through inheritance. It will make available the modifier\n * `onlyOwner`, which can be applied to your functions to restrict their use to\n * the owner.\n */\nabstract contract Ownable is Context {\n    address private _owner;\n\n    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);\n\n    /**\n     * @dev Initializes the contract setting the deployer as the initial owner.\n     */\n    constructor() {\n        _transferOwnership(_msgSender());\n    }\n\n    /**\n     * @dev Throws if called by any account other than the owner.\n     */\n    modifier onlyOwner() {\n        _checkOwner();\n        _;\n    }\n\n    /**\n     * @dev Returns the address of the current owner.\n     */\n    function owner() public view virtual returns (address) {\n        return _owner;\n    }\n\n    /**\n     * @dev Throws if the sender is not the owner.\n     */\n    function _checkOwner() internal view virtual {\n        require(owner() == _msgSender(), \"Ownable: caller is not the owner\");\n    }\n\n    /**\n     * @dev Leaves the contract without owner. It will not be possible to call\n     * `onlyOwner` functions. Can only be called by the current owner.\n     *\n     * NOTE: Renouncing ownership will leave the contract without an owner,\n     * thereby disabling any functionality that is only available to the owner.\n     */\n    function renounceOwnership() public virtual onlyOwner {\n        _transferOwnership(address(0));\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Can only be called by the current owner.\n     */\n    function transferOwnership(address newOwner) public virtual onlyOwner {\n        require(newOwner != address(0), \"Ownable: new owner is the zero address\");\n        _transferOwnership(newOwner);\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Internal function without access restriction.\n     */\n    function _transferOwnership(address newOwner) internal virtual {\n        address oldOwner = _owner;\n        _owner = newOwner;\n        emit OwnershipTransferred(oldOwner, newOwner);\n    }\n}\n"
    },
    "lib/openzeppelin-contracts/contracts/utils/Address.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (utils/Address.sol)\n\npragma solidity ^0.8.1;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary Address {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * [IMPORTANT]\n     * ====\n     * It is unsafe to assume that an address for which this function returns\n     * false is an externally-owned account (EOA) and not a contract.\n     *\n     * Among others, `isContract` will return false for the following\n     * types of addresses:\n     *\n     *  - an externally-owned account\n     *  - a contract in construction\n     *  - an address where a contract will be created\n     *  - an address where a contract lived, but was destroyed\n     *\n     * Furthermore, `isContract` will also return true if the target contract within\n     * the same transaction is already scheduled for destruction by `SELFDESTRUCT`,\n     * which only has an effect at the end of a transaction.\n     * ====\n     *\n     * [IMPORTANT]\n     * ====\n     * You shouldn't rely on `isContract` to protect against flash loan attacks!\n     *\n     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets\n     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract\n     * constructor.\n     * ====\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies on extcodesize/address.code.length, which returns 0\n        // for contracts in construction, since the code is only stored at the end\n        // of the constructor execution.\n\n        return account.code.length > 0;\n    }\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://consensys.net/diligence/blog/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.8.0/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        require(address(this).balance >= amount, \"Address: insufficient balance\");\n\n        (bool success, ) = recipient.call{value: amount}(\"\");\n        require(success, \"Address: unable to send value, recipient may have reverted\");\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain `call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason, it is bubbled up by this\n     * function (like regular Solidity function calls).\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, \"Address: low-level call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with\n     * `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, value, \"Address: low-level call with value failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but\n     * with `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(\n        address target,\n        bytes memory data,\n        uint256 value,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        require(address(this).balance >= value, \"Address: insufficient balance for call\");\n        (bool success, bytes memory returndata) = target.call{value: value}(data);\n        return verifyCallResultFromTarget(target, success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {\n        return functionStaticCall(target, data, \"Address: low-level static call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal view returns (bytes memory) {\n        (bool success, bytes memory returndata) = target.staticcall(data);\n        return verifyCallResultFromTarget(target, success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionDelegateCall(target, data, \"Address: low-level delegate call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        (bool success, bytes memory returndata) = target.delegatecall(data);\n        return verifyCallResultFromTarget(target, success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling\n     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.\n     *\n     * _Available since v4.8._\n     */\n    function verifyCallResultFromTarget(\n        address target,\n        bool success,\n        bytes memory returndata,\n        string memory errorMessage\n    ) internal view returns (bytes memory) {\n        if (success) {\n            if (returndata.length == 0) {\n                // only check isContract if the call was successful and the return data is empty\n                // otherwise we already know that it was a contract\n                require(isContract(target), \"Address: call to non-contract\");\n            }\n            return returndata;\n        } else {\n            _revert(returndata, errorMessage);\n        }\n    }\n\n    /**\n     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the\n     * revert reason or using the provided one.\n     *\n     * _Available since v4.3._\n     */\n    function verifyCallResult(\n        bool success,\n        bytes memory returndata,\n        string memory errorMessage\n    ) internal pure returns (bytes memory) {\n        if (success) {\n            return returndata;\n        } else {\n            _revert(returndata, errorMessage);\n        }\n    }\n\n    function _revert(bytes memory returndata, string memory errorMessage) private pure {\n        // Look for revert reason and bubble it up if present\n        if (returndata.length > 0) {\n            // The easiest way to bubble the revert reason is using memory via assembly\n            /// @solidity memory-safe-assembly\n            assembly {\n                let returndata_size := mload(returndata)\n                revert(add(32, returndata), returndata_size)\n            }\n        } else {\n            revert(errorMessage);\n        }\n    }\n}\n"
    }
  },
  "settings": {
    "remappings": [
      "@openzeppelin/=lib/openzeppelin-contracts/",
      "Bytes2.0/=lib/Bytes2.0/contracts/",
      "bytes/=lib/Bytes2.0/contracts/",
      "ds-test/=lib/forge-std/lib/ds-test/src/",
      "erc4626-tests/=lib/operator-filter-registry/lib/openzeppelin-contracts/lib/erc4626-tests/",
      "forge-std/=lib/forge-std/src/",
      "nt-with-oracle/=lib/nt-with-oracle/contracts/",
      "openzeppelin-contracts-upgradeable/=lib/openzeppelin-contracts-upgradeable/contracts/",
      "openzeppelin-contracts/=lib/openzeppelin-contracts/",
      "openzeppelin-upgradeable/=lib/openzeppelin-contracts-upgradeable/contracts/",
      "openzeppelin/=lib/openzeppelin-contracts/contracts/",
      "operator-filter-registry/=lib/operator-filter-registry/"
    ],
    "optimizer": {
      "enabled": true,
      "runs": 20
    },
    "metadata": {
      "bytecodeHash": "ipfs",
      "appendCBOR": true
    },
    "outputSelection": {
      "*": {
        "*": [
          "evm.bytecode",
          "evm.deployedBytecode",
          "devdoc",
          "userdoc",
          "metadata",
          "abi"
        ]
      }
    },
    "evmVersion": "paris",
    "libraries": {}
  }
}}