{{
  "language": "Solidity",
  "sources": {
    "contracts/BaoToken.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.0;\n\n//                                       -\\**:. `\"^`\n//                                     ;HNXav*,^;}?|:,.`\n//                                    tbKX&]:,:7I}=\\:-_._,`\n//                                  :OKgTv)*7}ri=*;::,^``:;:^\n//                               :&S@OTnccvtl])*\\;:--~.`_;;-,^.`\n//                            :cdXDOhT&Yuctl1=*;::,^..,;+*;:--,\"~^.\n//                         ;LX8dXSDOhasocv}1>+|;:---:;|||;:--,\"_^^^..`\n//                      :3@bd8dXSDPTasocvtl]>+|;::::;;;:::--,_~^^^..'````\n//                   `laP@d88d@DhT0Yo3cjt}i]=+*\\;;:::::::---,_^^^..'``    ``\n//                 `)3&hSd8dbDhT&YoccvIt}l17=)*|;;:::::::--,\"_^^^..'```     ``\n//                ;=rnOb88d@OT&nucvjItr}i17==+**\\;;;:::::--,\"~^^^..''``        `\n//              ^^:lnOXg8XDhaYucvjttr}lir3Luv=**|;;;;;:::--,_~^^....'-XMMh='     `\n//             ``~>YOdKKd@haYucvttrrINMMMMMMMMMMj\\;;;;::::-,\"~^^....-MMMHDr=:`\n//              ,7YDgmK8@haYucItr}}MMMMMMMMmt|1MMM=;;;;::::-,_^^....MMQ8OT-`';`\n//          ` `-}&Smqm8SPTsucjr}}lMMMMMMMMMDl|]MMMM|;;;;:::--\"^^..''MMmSOhV7)1:`\n//        '\"^,+vhdHNNEbOT&ocjtllitMMMMMMMMMMMMMMMMM1||\\;;;::-\"^..''`TMNKSDhaLt\\-\n//        =)=}nOmNQBHgDhaYuvIrli?jMMMMMMMMMMMMMMMMM1|||;;;::-~.'''``.dMBq8Oaor*|\n//       cLY0OgNQQQWHdDh0Yuvt}i?11MMMMMMMMMMMMMMMMM**|;;;:::-_..''```.c#WHSaul7+        `\n//      =OS8mNQMMMMQH8DhasucIrli111MMMMMMMMMMMMMMM+*|;;;::::-,^..''````.1VEHda7`        ``\n//      @mHWQMMMMMMQNEbDkan3vtli1]>=?SMMMMMMMMMh)**|\\;;::::--,~^..-'```````,,``````     ``\n//      NQMMMMMMMMMMQNKbOh0Luv}i7==))))))))+*****||\\;;;1MMMXkncuOKo.'''````````````````````\n//      QMMMMMMMMMMMMQNmdDh&n3I}17==)))))))+****|\\;;;;:::::*71=;,~^^...''''````````````````\n//      QMMMMMMMMMMMMMQBHEbOTVocjtli?11]>>==)+**||;;;;::::::----,,_~^^^.........''''''````\n//      jMMMMMMMMMMMMMMMQ#q8@hTVLuccvjt}}l?1]>=)***|\\;;;::::::-----,,,\"__~^^^^^......''```\n//       qMMMMMMMMMMMMMMMMWNmXDha&noouccjtr}li17==)+**|\\;;;::::::::-----,,,\"_~^^^....'``'\n//        MMMMMMMMMMMMMMMMMQBHgbOhT0Vnoo3cvIt}li117>=)***|\\;;;;::::::::----,,,_~^^^..'''\n//         QMMMMMMMMMMMMMMMMQWNq8bOhTa&snoucvjt}ll?1]>=))**||\\;;;;;:::::::----,\"_^^^..'\n//          -MMMMMMMMMMMMMMMMMQWNq8bDOhka0sLo3cvjIr}li?1]7>==)+***|\\;;;;:::::----,,~^\n//            ;NMMMMMMMMMMMMMMMMQQNqEgdSDOhka0Ynou3ccvjItr}lli117>=))***||;;;:::::-\n//               ]MMMMMMMMMMMMMMMMQQWBNHmKdb@OhkTa&sYLooucccvjItr}}li?17==)+*||:'\n//                 .1qMMMMMMMMMMMMMMMQQBNHqEKdSDOPkaa&snoou3cccvvjItr}li1]>;,`\n//                      :IOMMMMMMMMMMMMQQBNHm8XSOPka0Vnou3cvjtr}llli?>:\"`\n//                            _|jnbQMMMMMMMMQBNqK8XSDPTasLucvI|~.`\n//                                   oMMMMMMMMMMMMQqSTocri7)*|:.\n//                                 IMMMMMMMMMMNdksc}1)\\::-,~^..'`\n//                                lTMMMQMMMQqD0oj}1=*;:-_^.'```````\n//                              -cOQMN8KMMM#gO&uj}1=*;:-^.''`````````\n//                             :hHQ#gDSQMMMMHSTs3v}])|:-~^.'````` '.``\n//                            1KMQEOaLXMMMMMQEDTn3Ii7+;:-_^.''````'^.'`\n//                           -WMMghYuYHMMMMMM#dO0Lcr?=|;:-_^..'```.^^.'\n//                           gMMKhscckWMMMMMMQm@Ts3Il7*\\::-_^..'``^\",~.'\n//                          ?MMNPscvobMMMMMMMMN8Paov}1)*;:-,_^..``,---~.`\n//                         .MMWS&utvV8MMMMMMMMWEDTs3jl>*\\;:--\"^.`.:\\;:-_`\n//                         lMMNOLjrcTgMMMMMMMMMNXOTYujl>*\\:::-,^.`;ll=-`\n//                         'MMNDYccclQMMMMMMMMMWq8@PTsct1)|;;;:\"\n//                            -;\\-`  QMMMMMMMQ#HEEgdSOaLv}i]+:,^\n//                                   ]MMMMMHdhs3t`  ^P8gbhu];-.\n//                                    MMMMW8kci>)    hgKDV}|:^'\n//                                    @MMMHho1*;:    hNHDYl\\:^\n//                                     dMMmav=;:      |t3v7:.\n//                                       '~^'\n//\n//                                         author: phaze\n\nimport \"@openzeppelin/contracts/access/AccessControl.sol\";\nimport \"@openzeppelin/contracts/interfaces/IERC721.sol\";\n\nimport \"./BaoStaking.sol\";\n\ncontract BaoToken is AccessControl, BaoStaking {\n    bytes32 public constant MINT_AUTHORITY = keccak256(\"MINT_AUTHORITY\");\n    bytes32 public constant BURN_AUTHORITY = keccak256(\"BURN_AUTHORITY\");\n\n    address public treasuryAddress = address(0x69D8004d527d72eFe1a4d5eECFf4A7f38f5b2B69);\n\n    constructor() ERC20(\"Bao Token\", \"BAO\", 18) {\n        _setupRole(DEFAULT_ADMIN_ROLE, treasuryAddress);\n        _mint(treasuryAddress, 10_000_000 * 1e18);\n    }\n\n    /* ------------- Restricted ------------- */\n\n    function mint(address user, uint256 amount) external payable onlyRole(MINT_AUTHORITY) {\n        _mint(user, amount);\n    }\n\n    /* ------------- ERC20Burnable ------------- */\n\n    function burn(uint256 amount) external payable {\n        _burn(msg.sender, amount);\n    }\n\n    function burnFrom(address user, uint256 amount) external payable {\n        if (!hasRole(BURN_AUTHORITY, msg.sender)) {\n            uint256 allowed = allowance[user][msg.sender];\n            if (allowed != type(uint256).max) allowance[user][msg.sender] = allowed - amount;\n        }\n        _burn(user, amount);\n    }\n\n    /* ------------- MultiCall ------------- */\n\n    function multiCall(bytes[] calldata data) external {\n        unchecked {\n            for (uint256 i; i < data.length; ++i) address(this).delegatecall(data[i]);\n        }\n    }\n\n    /* ------------- Owner ------------- */\n\n    function withdraw() external onlyOwner {\n        uint256 balance = address(this).balance;\n        payable(msg.sender).transfer(balance);\n    }\n\n    function recoverToken(ERC20 token) external onlyOwner {\n        uint256 balance = token.balanceOf(address(this));\n        token.transfer(msg.sender, balance);\n    }\n\n    function recoverNFT(IERC721 token, uint256 id) external onlyOwner {\n        token.transferFrom(address(this), msg.sender, id);\n    }\n}\n"
    },
    "@openzeppelin/contracts/access/AccessControl.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.6.0) (access/AccessControl.sol)\n\npragma solidity ^0.8.0;\n\nimport \"./IAccessControl.sol\";\nimport \"../utils/Context.sol\";\nimport \"../utils/Strings.sol\";\nimport \"../utils/introspection/ERC165.sol\";\n\n/**\n * @dev Contract module that allows children to implement role-based access\n * control mechanisms. This is a lightweight version that doesn't allow enumerating role\n * members except through off-chain means by accessing the contract event logs. Some\n * applications may benefit from on-chain enumerability, for those cases see\n * {AccessControlEnumerable}.\n *\n * Roles are referred to by their `bytes32` identifier. These should be exposed\n * in the external API and be unique. The best way to achieve this is by\n * using `public constant` hash digests:\n *\n * ```\n * bytes32 public constant MY_ROLE = keccak256(\"MY_ROLE\");\n * ```\n *\n * Roles can be used to represent a set of permissions. To restrict access to a\n * function call, use {hasRole}:\n *\n * ```\n * function foo() public {\n *     require(hasRole(MY_ROLE, msg.sender));\n *     ...\n * }\n * ```\n *\n * Roles can be granted and revoked dynamically via the {grantRole} and\n * {revokeRole} functions. Each role has an associated admin role, and only\n * accounts that have a role's admin role can call {grantRole} and {revokeRole}.\n *\n * By default, the admin role for all roles is `DEFAULT_ADMIN_ROLE`, which means\n * that only accounts with this role will be able to grant or revoke other\n * roles. More complex role relationships can be created by using\n * {_setRoleAdmin}.\n *\n * WARNING: The `DEFAULT_ADMIN_ROLE` is also its own admin: it has permission to\n * grant and revoke this role. Extra precautions should be taken to secure\n * accounts that have been granted it.\n */\nabstract contract AccessControl is Context, IAccessControl, ERC165 {\n    struct RoleData {\n        mapping(address => bool) members;\n        bytes32 adminRole;\n    }\n\n    mapping(bytes32 => RoleData) private _roles;\n\n    bytes32 public constant DEFAULT_ADMIN_ROLE = 0x00;\n\n    /**\n     * @dev Modifier that checks that an account has a specific role. Reverts\n     * with a standardized message including the required role.\n     *\n     * The format of the revert reason is given by the following regular expression:\n     *\n     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/\n     *\n     * _Available since v4.1._\n     */\n    modifier onlyRole(bytes32 role) {\n        _checkRole(role);\n        _;\n    }\n\n    /**\n     * @dev See {IERC165-supportsInterface}.\n     */\n    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {\n        return interfaceId == type(IAccessControl).interfaceId || super.supportsInterface(interfaceId);\n    }\n\n    /**\n     * @dev Returns `true` if `account` has been granted `role`.\n     */\n    function hasRole(bytes32 role, address account) public view virtual override returns (bool) {\n        return _roles[role].members[account];\n    }\n\n    /**\n     * @dev Revert with a standard message if `_msgSender()` is missing `role`.\n     * Overriding this function changes the behavior of the {onlyRole} modifier.\n     *\n     * Format of the revert message is described in {_checkRole}.\n     *\n     * _Available since v4.6._\n     */\n    function _checkRole(bytes32 role) internal view virtual {\n        _checkRole(role, _msgSender());\n    }\n\n    /**\n     * @dev Revert with a standard message if `account` is missing `role`.\n     *\n     * The format of the revert reason is given by the following regular expression:\n     *\n     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/\n     */\n    function _checkRole(bytes32 role, address account) internal view virtual {\n        if (!hasRole(role, account)) {\n            revert(\n                string(\n                    abi.encodePacked(\n                        \"AccessControl: account \",\n                        Strings.toHexString(uint160(account), 20),\n                        \" is missing role \",\n                        Strings.toHexString(uint256(role), 32)\n                    )\n                )\n            );\n        }\n    }\n\n    /**\n     * @dev Returns the admin role that controls `role`. See {grantRole} and\n     * {revokeRole}.\n     *\n     * To change a role's admin, use {_setRoleAdmin}.\n     */\n    function getRoleAdmin(bytes32 role) public view virtual override returns (bytes32) {\n        return _roles[role].adminRole;\n    }\n\n    /**\n     * @dev Grants `role` to `account`.\n     *\n     * If `account` had not been already granted `role`, emits a {RoleGranted}\n     * event.\n     *\n     * Requirements:\n     *\n     * - the caller must have ``role``'s admin role.\n     */\n    function grantRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {\n        _grantRole(role, account);\n    }\n\n    /**\n     * @dev Revokes `role` from `account`.\n     *\n     * If `account` had been granted `role`, emits a {RoleRevoked} event.\n     *\n     * Requirements:\n     *\n     * - the caller must have ``role``'s admin role.\n     */\n    function revokeRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {\n        _revokeRole(role, account);\n    }\n\n    /**\n     * @dev Revokes `role` from the calling account.\n     *\n     * Roles are often managed via {grantRole} and {revokeRole}: this function's\n     * purpose is to provide a mechanism for accounts to lose their privileges\n     * if they are compromised (such as when a trusted device is misplaced).\n     *\n     * If the calling account had been revoked `role`, emits a {RoleRevoked}\n     * event.\n     *\n     * Requirements:\n     *\n     * - the caller must be `account`.\n     */\n    function renounceRole(bytes32 role, address account) public virtual override {\n        require(account == _msgSender(), \"AccessControl: can only renounce roles for self\");\n\n        _revokeRole(role, account);\n    }\n\n    /**\n     * @dev Grants `role` to `account`.\n     *\n     * If `account` had not been already granted `role`, emits a {RoleGranted}\n     * event. Note that unlike {grantRole}, this function doesn't perform any\n     * checks on the calling account.\n     *\n     * [WARNING]\n     * ====\n     * This function should only be called from the constructor when setting\n     * up the initial roles for the system.\n     *\n     * Using this function in any other way is effectively circumventing the admin\n     * system imposed by {AccessControl}.\n     * ====\n     *\n     * NOTE: This function is deprecated in favor of {_grantRole}.\n     */\n    function _setupRole(bytes32 role, address account) internal virtual {\n        _grantRole(role, account);\n    }\n\n    /**\n     * @dev Sets `adminRole` as ``role``'s admin role.\n     *\n     * Emits a {RoleAdminChanged} event.\n     */\n    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {\n        bytes32 previousAdminRole = getRoleAdmin(role);\n        _roles[role].adminRole = adminRole;\n        emit RoleAdminChanged(role, previousAdminRole, adminRole);\n    }\n\n    /**\n     * @dev Grants `role` to `account`.\n     *\n     * Internal function without access restriction.\n     */\n    function _grantRole(bytes32 role, address account) internal virtual {\n        if (!hasRole(role, account)) {\n            _roles[role].members[account] = true;\n            emit RoleGranted(role, account, _msgSender());\n        }\n    }\n\n    /**\n     * @dev Revokes `role` from `account`.\n     *\n     * Internal function without access restriction.\n     */\n    function _revokeRole(bytes32 role, address account) internal virtual {\n        if (hasRole(role, account)) {\n            _roles[role].members[account] = false;\n            emit RoleRevoked(role, account, _msgSender());\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/interfaces/IERC721.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (interfaces/IERC721.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../token/ERC721/IERC721.sol\";\n"
    },
    "contracts/BaoStaking.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.0;\n\n//                                       -\\**:. `\"^`\n//                                     ;HNXav*,^;}?|:,.`\n//                                    tbKX&]:,:7I}=\\:-_._,`\n//                                  :OKgTv)*7}ri=*;::,^``:;:^\n//                               :&S@OTnccvtl])*\\;:--~.`_;;-,^.`\n//                            :cdXDOhT&Yuctl1=*;::,^..,;+*;:--,\"~^.\n//                         ;LX8dXSDOhasocv}1>+|;:---:;|||;:--,\"_^^^..`\n//                      :3@bd8dXSDPTasocvtl]>+|;::::;;;:::--,_~^^^..'````\n//                   `laP@d88d@DhT0Yo3cjt}i]=+*\\;;:::::::---,_^^^..'``    ``\n//                 `)3&hSd8dbDhT&YoccvIt}l17=)*|;;:::::::--,\"_^^^..'```     ``\n//                ;=rnOb88d@OT&nucvjItr}i17==+**\\;;;:::::--,\"~^^^..''``        `\n//              ^^:lnOXg8XDhaYucvjttr}lir3Luv=**|;;;;;:::--,_~^^....'-XMMh='     `\n//             ``~>YOdKKd@haYucvttrrINMMMMMMMMMMj\\;;;;::::-,\"~^^....-MMMHDr=:`\n//              ,7YDgmK8@haYucItr}}MMMMMMMMmt|1MMM=;;;;::::-,_^^....MMQ8OT-`';`\n//          ` `-}&Smqm8SPTsucjr}}lMMMMMMMMMDl|]MMMM|;;;;:::--\"^^..''MMmSOhV7)1:`\n//        '\"^,+vhdHNNEbOT&ocjtllitMMMMMMMMMMMMMMMMM1||\\;;;::-\"^..''`TMNKSDhaLt\\-\n//        =)=}nOmNQBHgDhaYuvIrli?jMMMMMMMMMMMMMMMMM1|||;;;::-~.'''``.dMBq8Oaor*|\n//       cLY0OgNQQQWHdDh0Yuvt}i?11MMMMMMMMMMMMMMMMM**|;;;:::-_..''```.c#WHSaul7+        `\n//      =OS8mNQMMMMQH8DhasucIrli111MMMMMMMMMMMMMMM+*|;;;::::-,^..''````.1VEHda7`        ``\n//      @mHWQMMMMMMQNEbDkan3vtli1]>=?SMMMMMMMMMh)**|\\;;::::--,~^..-'```````,,``````     ``\n//      NQMMMMMMMMMMQNKbOh0Luv}i7==))))))))+*****||\\;;;1MMMXkncuOKo.'''````````````````````\n//      QMMMMMMMMMMMMQNmdDh&n3I}17==)))))))+****|\\;;;;:::::*71=;,~^^...''''````````````````\n//      QMMMMMMMMMMMMMQBHEbOTVocjtli?11]>>==)+**||;;;;::::::----,,_~^^^.........''''''````\n//      jMMMMMMMMMMMMMMMQ#q8@hTVLuccvjt}}l?1]>=)***|\\;;;::::::-----,,,\"__~^^^^^......''```\n//       qMMMMMMMMMMMMMMMMWNmXDha&noouccjtr}li17==)+**|\\;;;::::::::-----,,,\"_~^^^....'``'\n//        MMMMMMMMMMMMMMMMMQBHgbOhT0Vnoo3cvIt}li117>=)***|\\;;;;::::::::----,,,_~^^^..'''\n//         QMMMMMMMMMMMMMMMMQWNq8bOhTa&snoucvjt}ll?1]>=))**||\\;;;;;:::::::----,\"_^^^..'\n//          -MMMMMMMMMMMMMMMMMQWNq8bDOhka0sLo3cvjIr}li?1]7>==)+***|\\;;;;:::::----,,~^\n//            ;NMMMMMMMMMMMMMMMMQQNqEgdSDOhka0Ynou3ccvjItr}lli117>=))***||;;;:::::-\n//               ]MMMMMMMMMMMMMMMMQQWBNHmKdb@OhkTa&sYLooucccvjItr}}li?17==)+*||:'\n//                 .1qMMMMMMMMMMMMMMMQQBNHqEKdSDOPkaa&snoou3cccvvjItr}li1]>;,`\n//                      :IOMMMMMMMMMMMMQQBNHm8XSOPka0Vnou3cvjtr}llli?>:\"`\n//                            _|jnbQMMMMMMMMQBNqK8XSDPTasLucvI|~.`\n//                                   oMMMMMMMMMMMMQqSTocri7)*|:.\n//                                 IMMMMMMMMMMNdksc}1)\\::-,~^..'`\n//                                lTMMMQMMMQqD0oj}1=*;:-_^.'```````\n//                              -cOQMN8KMMM#gO&uj}1=*;:-^.''`````````\n//                             :hHQ#gDSQMMMMHSTs3v}])|:-~^.'````` '.``\n//                            1KMQEOaLXMMMMMQEDTn3Ii7+;:-_^.''````'^.'`\n//                           -WMMghYuYHMMMMMM#dO0Lcr?=|;:-_^..'```.^^.'\n//                           gMMKhscckWMMMMMMQm@Ts3Il7*\\::-_^..'``^\",~.'\n//                          ?MMNPscvobMMMMMMMMN8Paov}1)*;:-,_^..``,---~.`\n//                         .MMWS&utvV8MMMMMMMMWEDTs3jl>*\\;:--\"^.`.:\\;:-_`\n//                         lMMNOLjrcTgMMMMMMMMMNXOTYujl>*\\:::-,^.`;ll=-`\n//                         'MMNDYccclQMMMMMMMMMWq8@PTsct1)|;;;:\"\n//                            -;\\-`  QMMMMMMMQ#HEEgdSOaLv}i]+:,^\n//                                   ]MMMMMHdhs3t`  ^P8gbhu];-.\n//                                    MMMMW8kci>)    hgKDV}|:^'\n//                                    @MMMHho1*;:    hNHDYl\\:^\n//                                     dMMmav=;:      |t3v7:.\n//                                       '~^'\n//\n//                                         author: phaze\n\nimport {IERC721 as IBaoSociety} from \"@openzeppelin/contracts/interfaces/IERC721.sol\";\nimport \"../lib/solmate/src/tokens/ERC20.sol\";\n\nimport \"./Ownable.sol\";\n\nerror IncorrectOwner();\n\nabstract contract BaoStaking is Ownable, ERC20 {\n    struct TokenData {\n        address owner;\n        uint40 lastClaimed;\n        uint40 rarity;\n    }\n\n    struct StakeData {\n        uint40 rarityBonus;\n        uint40 numStaked;\n        uint40 lastClaimed;\n    }\n\n    uint256 constant DAILY_BASE_REWARD = 100;\n    uint256 constant DAILY_STAKED_REWARD = 150;\n\n    uint256 public immutable REWARD_EMISSION_START = 1652662800;\n    uint256 public immutable REWARD_EMISSION_END = 1652662800 + 5 * 365 days;\n\n    mapping(uint256 => TokenData) public tokenData;\n    mapping(address => StakeData) public stakeData;\n\n    IBaoSociety public constant baoSociety = IBaoSociety(0xba033D82c64DD514B184e2d1405cD395dfE6e706);\n\n    /* ------------- External ------------- */\n\n    function stake(uint256[] calldata tokenIds) external payable {\n        unchecked {\n            claimRewardStaked();\n\n            TokenData storage tData;\n            StakeData storage sData = stakeData[msg.sender];\n\n            uint256 tokenId;\n\n            uint256 reward;\n            uint256 rarityBonus = sData.rarityBonus;\n\n            for (uint256 i; i < tokenIds.length; ++i) {\n                tokenId = tokenIds[i];\n\n                baoSociety.transferFrom(msg.sender, address(this), tokenId);\n\n                tData = tokenData[tokenId];\n                tData.owner = msg.sender;\n\n                // accrue non-staked rewards\n                reward += _calculateRewardSingle(tData, tokenId);\n                rarityBonus += getRarityBonus(tData);\n            }\n\n            sData.numStaked += uint40(tokenIds.length);\n            sData.rarityBonus = uint40(rarityBonus);\n\n            _mint(msg.sender, reward);\n        }\n    }\n\n    function unstake(uint256[] calldata tokenIds) external payable {\n        unchecked {\n            claimRewardStaked();\n\n            TokenData storage tData;\n            StakeData storage sData = stakeData[msg.sender];\n\n            uint256 tokenId;\n            uint256 rarityBonus = sData.rarityBonus;\n\n            for (uint256 i; i < tokenIds.length; ++i) {\n                tokenId = tokenIds[i];\n                tData = tokenData[tokenId];\n\n                if (tData.owner != msg.sender) revert IncorrectOwner();\n\n                baoSociety.transferFrom(address(this), msg.sender, tokenId);\n\n                rarityBonus -= getRarityBonus(tData); // underflow not possible if rarity stays constant\n                tData.lastClaimed = uint40(block.timestamp);\n            }\n\n            sData.numStaked -= uint40(tokenIds.length);\n            sData.rarityBonus = uint40(rarityBonus);\n        }\n    }\n\n    function claimRewardStaked() public payable {\n        uint256 reward = pendingRewardStaked(msg.sender);\n\n        _mint(msg.sender, reward);\n\n        stakeData[msg.sender].lastClaimed = uint40(block.timestamp);\n    }\n\n    function claimReward(uint256[] calldata tokenIds) external payable {\n        unchecked {\n            TokenData storage tData;\n\n            uint256 reward;\n            uint256 tokenId;\n\n            for (uint256 i; i < tokenIds.length; ++i) {\n                tokenId = tokenIds[i];\n                tData = tokenData[tokenId];\n\n                if (baoSociety.ownerOf(tokenId) != msg.sender) revert IncorrectOwner();\n\n                reward += _calculateRewardSingle(tData, tokenId);\n                tData.lastClaimed = uint40(block.timestamp);\n            }\n\n            _mint(msg.sender, reward);\n        }\n    }\n\n    /* ------------- View ------------- */\n\n    function pendingRewardStaked(address user) public view returns (uint256) {\n        unchecked {\n            uint256 timestamp = block.timestamp;\n            if (timestamp > REWARD_EMISSION_END) timestamp = REWARD_EMISSION_END;\n\n            uint256 staked = stakeData[user].numStaked;\n            if (staked == 0) return 0;\n\n            uint256 lastClaimed = stakeData[user].lastClaimed;\n\n            if (lastClaimed > timestamp) return 0;\n            if (lastClaimed == 0) lastClaimed = REWARD_EMISSION_START;\n\n            return\n                ((timestamp - lastClaimed) * (staked * DAILY_STAKED_REWARD + stakeData[user].rarityBonus) * 1e18) /\n                1 days;\n        }\n    }\n\n    function dailyRewardStaked(address user) external view returns (uint256) {\n        unchecked {\n            return ((uint256(stakeData[user].numStaked) * DAILY_STAKED_REWARD + stakeData[user].rarityBonus) * 1e18);\n        }\n    }\n\n    function pendingReward(uint256[] calldata tokenIds) external view returns (uint256) {\n        unchecked {\n            TokenData storage tData;\n\n            uint256 reward;\n            uint256 tokenId;\n\n            for (uint256 i; i < tokenIds.length; ++i) {\n                tokenId = tokenIds[i];\n                tData = tokenData[tokenId];\n\n                reward += _calculateRewardSingle(tData, tokenId);\n            }\n\n            return reward;\n        }\n    }\n\n    function dailyReward(uint256[] calldata tokenIds) external view returns (uint256) {\n        unchecked {\n            uint256 reward;\n\n            for (uint256 i; i < tokenIds.length; ++i)\n                reward += (DAILY_BASE_REWARD + getRarityBonus(tokenData[tokenIds[i]])) * 1e18;\n\n            return reward;\n        }\n    }\n\n    function getRarityBonus(uint256 tokenId) external view returns (uint256) {\n        return getRarityBonus(tokenData[tokenId]);\n    }\n\n    function numStaked(address user) external view returns (uint256) {\n        return stakeData[user].numStaked;\n    }\n\n    function numOwned(address user) external view returns (uint256) {\n        return baoSociety.balanceOf(user) + stakeData[user].numStaked;\n    }\n\n    function totalNumStaked() external view returns (uint256) {\n        return baoSociety.balanceOf(address(this));\n    }\n\n    /* ------------- Private ------------- */\n\n    function getRarityBonus(TokenData storage tData) private view returns (uint256) {\n        uint256 rarity = tData.rarity;\n        return rarity == 0 ? 5 : rarity;\n    }\n\n    function _calculateRewardSingle(TokenData storage tData, uint256 tokenId) private view returns (uint256) {\n        uint256 rewardBonus;\n        uint256 lastClaimed = tData.lastClaimed;\n\n        if (lastClaimed > REWARD_EMISSION_END) return 0;\n        if (lastClaimed == 0) {\n            lastClaimed = REWARD_EMISSION_START;\n            if (tokenId % 5 == 0) rewardBonus = 1500 * 1e18;\n        }\n\n        uint256 timestamp = block.timestamp;\n        if (timestamp > REWARD_EMISSION_END) timestamp = REWARD_EMISSION_END;\n\n        return rewardBonus + ((timestamp - lastClaimed) * (DAILY_BASE_REWARD + getRarityBonus(tData)) * 1e18) / 1 days;\n    }\n\n    /* ------------- Owner ------------- */\n\n    // @note should only be called once before any interaction happens\n    // bad things (underflow) can happen if this is changed while someone is staking\n    function setRarities(uint256[] calldata ids, uint256[] calldata rarities) external onlyOwner {\n        unchecked {\n            for (uint256 i; i < ids.length; ++i) tokenData[ids[i]].rarity = uint40(rarities[i]);\n        }\n    }\n\n    /* ------------- O(n) Read-Only ------------- */\n\n    function stakedTokenIdsOf(address user) external view returns (uint256[] memory) {\n        uint256 staked = stakeData[user].numStaked;\n        uint256[] memory stakedIds = new uint256[](staked);\n        if (staked == 0) return stakedIds;\n\n        uint256 count;\n        for (uint256 i = 1; i < 3888 + 1; ++i) {\n            if (tokenData[i].owner == user) {\n                stakedIds[count++] = i;\n                if (staked == count) return stakedIds;\n            }\n        }\n\n        return stakedIds;\n    }\n}\n"
    },
    "@openzeppelin/contracts/access/IAccessControl.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (access/IAccessControl.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev External interface of AccessControl declared to support ERC165 detection.\n */\ninterface IAccessControl {\n    /**\n     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`\n     *\n     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite\n     * {RoleAdminChanged} not being emitted signaling this.\n     *\n     * _Available since v3.1._\n     */\n    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);\n\n    /**\n     * @dev Emitted when `account` is granted `role`.\n     *\n     * `sender` is the account that originated the contract call, an admin role\n     * bearer except when using {AccessControl-_setupRole}.\n     */\n    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);\n\n    /**\n     * @dev Emitted when `account` is revoked `role`.\n     *\n     * `sender` is the account that originated the contract call:\n     *   - if using `revokeRole`, it is the admin role bearer\n     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)\n     */\n    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);\n\n    /**\n     * @dev Returns `true` if `account` has been granted `role`.\n     */\n    function hasRole(bytes32 role, address account) external view returns (bool);\n\n    /**\n     * @dev Returns the admin role that controls `role`. See {grantRole} and\n     * {revokeRole}.\n     *\n     * To change a role's admin, use {AccessControl-_setRoleAdmin}.\n     */\n    function getRoleAdmin(bytes32 role) external view returns (bytes32);\n\n    /**\n     * @dev Grants `role` to `account`.\n     *\n     * If `account` had not been already granted `role`, emits a {RoleGranted}\n     * event.\n     *\n     * Requirements:\n     *\n     * - the caller must have ``role``'s admin role.\n     */\n    function grantRole(bytes32 role, address account) external;\n\n    /**\n     * @dev Revokes `role` from `account`.\n     *\n     * If `account` had been granted `role`, emits a {RoleRevoked} event.\n     *\n     * Requirements:\n     *\n     * - the caller must have ``role``'s admin role.\n     */\n    function revokeRole(bytes32 role, address account) external;\n\n    /**\n     * @dev Revokes `role` from the calling account.\n     *\n     * Roles are often managed via {grantRole} and {revokeRole}: this function's\n     * purpose is to provide a mechanism for accounts to lose their privileges\n     * if they are compromised (such as when a trusted device is misplaced).\n     *\n     * If the calling account had been granted `role`, emits a {RoleRevoked}\n     * event.\n     *\n     * Requirements:\n     *\n     * - the caller must be `account`.\n     */\n    function renounceRole(bytes32 role, address account) external;\n}\n"
    },
    "@openzeppelin/contracts/utils/Context.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Provides information about the current execution context, including the\n * sender of the transaction and its data. While these are generally available\n * via msg.sender and msg.data, they should not be accessed in such a direct\n * manner, since when dealing with meta-transactions the account sending and\n * paying for execution may not be the actual sender (as far as an application\n * is concerned).\n *\n * This contract is only required for intermediate, library-like contracts.\n */\nabstract contract Context {\n    function _msgSender() internal view virtual returns (address) {\n        return msg.sender;\n    }\n\n    function _msgData() internal view virtual returns (bytes calldata) {\n        return msg.data;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Strings.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/Strings.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev String operations.\n */\nlibrary Strings {\n    bytes16 private constant _HEX_SYMBOLS = \"0123456789abcdef\";\n\n    /**\n     * @dev Converts a `uint256` to its ASCII `string` decimal representation.\n     */\n    function toString(uint256 value) internal pure returns (string memory) {\n        // Inspired by OraclizeAPI's implementation - MIT licence\n        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol\n\n        if (value == 0) {\n            return \"0\";\n        }\n        uint256 temp = value;\n        uint256 digits;\n        while (temp != 0) {\n            digits++;\n            temp /= 10;\n        }\n        bytes memory buffer = new bytes(digits);\n        while (value != 0) {\n            digits -= 1;\n            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));\n            value /= 10;\n        }\n        return string(buffer);\n    }\n\n    /**\n     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.\n     */\n    function toHexString(uint256 value) internal pure returns (string memory) {\n        if (value == 0) {\n            return \"0x00\";\n        }\n        uint256 temp = value;\n        uint256 length = 0;\n        while (temp != 0) {\n            length++;\n            temp >>= 8;\n        }\n        return toHexString(value, length);\n    }\n\n    /**\n     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.\n     */\n    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {\n        bytes memory buffer = new bytes(2 * length + 2);\n        buffer[0] = \"0\";\n        buffer[1] = \"x\";\n        for (uint256 i = 2 * length + 1; i > 1; --i) {\n            buffer[i] = _HEX_SYMBOLS[value & 0xf];\n            value >>= 4;\n        }\n        require(value == 0, \"Strings: hex length insufficient\");\n        return string(buffer);\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/introspection/ERC165.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)\n\npragma solidity ^0.8.0;\n\nimport \"./IERC165.sol\";\n\n/**\n * @dev Implementation of the {IERC165} interface.\n *\n * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check\n * for the additional interface id that will be supported. For example:\n *\n * ```solidity\n * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {\n *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);\n * }\n * ```\n *\n * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.\n */\nabstract contract ERC165 is IERC165 {\n    /**\n     * @dev See {IERC165-supportsInterface}.\n     */\n    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {\n        return interfaceId == type(IERC165).interfaceId;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/introspection/IERC165.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Interface of the ERC165 standard, as defined in the\n * https://eips.ethereum.org/EIPS/eip-165[EIP].\n *\n * Implementers can declare support of contract interfaces, which can then be\n * queried by others ({ERC165Checker}).\n *\n * For an implementation, see {ERC165}.\n */\ninterface IERC165 {\n    /**\n     * @dev Returns true if this contract implements the interface defined by\n     * `interfaceId`. See the corresponding\n     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]\n     * to learn more about how these ids are created.\n     *\n     * This function call must use less than 30 000 gas.\n     */\n    function supportsInterface(bytes4 interfaceId) external view returns (bool);\n}\n"
    },
    "@openzeppelin/contracts/token/ERC721/IERC721.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC721/IERC721.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../../utils/introspection/IERC165.sol\";\n\n/**\n * @dev Required interface of an ERC721 compliant contract.\n */\ninterface IERC721 is IERC165 {\n    /**\n     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.\n     */\n    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);\n\n    /**\n     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.\n     */\n    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);\n\n    /**\n     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.\n     */\n    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);\n\n    /**\n     * @dev Returns the number of tokens in ``owner``'s account.\n     */\n    function balanceOf(address owner) external view returns (uint256 balance);\n\n    /**\n     * @dev Returns the owner of the `tokenId` token.\n     *\n     * Requirements:\n     *\n     * - `tokenId` must exist.\n     */\n    function ownerOf(uint256 tokenId) external view returns (address owner);\n\n    /**\n     * @dev Safely transfers `tokenId` token from `from` to `to`.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `tokenId` token must exist and be owned by `from`.\n     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.\n     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.\n     *\n     * Emits a {Transfer} event.\n     */\n    function safeTransferFrom(\n        address from,\n        address to,\n        uint256 tokenId,\n        bytes calldata data\n    ) external;\n\n    /**\n     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients\n     * are aware of the ERC721 protocol to prevent tokens from being forever locked.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `tokenId` token must exist and be owned by `from`.\n     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.\n     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.\n     *\n     * Emits a {Transfer} event.\n     */\n    function safeTransferFrom(\n        address from,\n        address to,\n        uint256 tokenId\n    ) external;\n\n    /**\n     * @dev Transfers `tokenId` token from `from` to `to`.\n     *\n     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `tokenId` token must be owned by `from`.\n     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transferFrom(\n        address from,\n        address to,\n        uint256 tokenId\n    ) external;\n\n    /**\n     * @dev Gives permission to `to` to transfer `tokenId` token to another account.\n     * The approval is cleared when the token is transferred.\n     *\n     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.\n     *\n     * Requirements:\n     *\n     * - The caller must own the token or be an approved operator.\n     * - `tokenId` must exist.\n     *\n     * Emits an {Approval} event.\n     */\n    function approve(address to, uint256 tokenId) external;\n\n    /**\n     * @dev Approve or remove `operator` as an operator for the caller.\n     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.\n     *\n     * Requirements:\n     *\n     * - The `operator` cannot be the caller.\n     *\n     * Emits an {ApprovalForAll} event.\n     */\n    function setApprovalForAll(address operator, bool _approved) external;\n\n    /**\n     * @dev Returns the account approved for `tokenId` token.\n     *\n     * Requirements:\n     *\n     * - `tokenId` must exist.\n     */\n    function getApproved(uint256 tokenId) external view returns (address operator);\n\n    /**\n     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.\n     *\n     * See {setApprovalForAll}\n     */\n    function isApprovedForAll(address owner, address operator) external view returns (bool);\n}\n"
    },
    "lib/solmate/src/tokens/ERC20.sol": {
      "content": "// SPDX-License-Identifier: AGPL-3.0-only\npragma solidity >=0.8.0;\n\n/// @notice Modern and gas efficient ERC20 + EIP-2612 implementation.\n/// @author Solmate (https://github.com/Rari-Capital/solmate/blob/main/src/tokens/ERC20.sol)\n/// @author Modified from Uniswap (https://github.com/Uniswap/uniswap-v2-core/blob/master/contracts/UniswapV2ERC20.sol)\n/// @dev Do not manually set balances without updating totalSupply, as the sum of all user balances must not exceed it.\nabstract contract ERC20 {\n    /*//////////////////////////////////////////////////////////////\n                                 EVENTS\n    //////////////////////////////////////////////////////////////*/\n\n    event Transfer(address indexed from, address indexed to, uint256 amount);\n\n    event Approval(address indexed owner, address indexed spender, uint256 amount);\n\n    /*//////////////////////////////////////////////////////////////\n                            METADATA STORAGE\n    //////////////////////////////////////////////////////////////*/\n\n    string public name;\n\n    string public symbol;\n\n    uint8 public immutable decimals;\n\n    /*//////////////////////////////////////////////////////////////\n                              ERC20 STORAGE\n    //////////////////////////////////////////////////////////////*/\n\n    uint256 public totalSupply;\n\n    mapping(address => uint256) public balanceOf;\n\n    mapping(address => mapping(address => uint256)) public allowance;\n\n    /*//////////////////////////////////////////////////////////////\n                            EIP-2612 STORAGE\n    //////////////////////////////////////////////////////////////*/\n\n    uint256 internal immutable INITIAL_CHAIN_ID;\n\n    bytes32 internal immutable INITIAL_DOMAIN_SEPARATOR;\n\n    mapping(address => uint256) public nonces;\n\n    /*//////////////////////////////////////////////////////////////\n                               CONSTRUCTOR\n    //////////////////////////////////////////////////////////////*/\n\n    constructor(\n        string memory _name,\n        string memory _symbol,\n        uint8 _decimals\n    ) {\n        name = _name;\n        symbol = _symbol;\n        decimals = _decimals;\n\n        INITIAL_CHAIN_ID = block.chainid;\n        INITIAL_DOMAIN_SEPARATOR = computeDomainSeparator();\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                               ERC20 LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    function approve(address spender, uint256 amount) public virtual returns (bool) {\n        allowance[msg.sender][spender] = amount;\n\n        emit Approval(msg.sender, spender, amount);\n\n        return true;\n    }\n\n    function transfer(address to, uint256 amount) public virtual returns (bool) {\n        balanceOf[msg.sender] -= amount;\n\n        // Cannot overflow because the sum of all user\n        // balances can't exceed the max uint256 value.\n        unchecked {\n            balanceOf[to] += amount;\n        }\n\n        emit Transfer(msg.sender, to, amount);\n\n        return true;\n    }\n\n    function transferFrom(\n        address from,\n        address to,\n        uint256 amount\n    ) public virtual returns (bool) {\n        uint256 allowed = allowance[from][msg.sender]; // Saves gas for limited approvals.\n\n        if (allowed != type(uint256).max) allowance[from][msg.sender] = allowed - amount;\n\n        balanceOf[from] -= amount;\n\n        // Cannot overflow because the sum of all user\n        // balances can't exceed the max uint256 value.\n        unchecked {\n            balanceOf[to] += amount;\n        }\n\n        emit Transfer(from, to, amount);\n\n        return true;\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                             EIP-2612 LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    function permit(\n        address owner,\n        address spender,\n        uint256 value,\n        uint256 deadline,\n        uint8 v,\n        bytes32 r,\n        bytes32 s\n    ) public virtual {\n        require(deadline >= block.timestamp, \"PERMIT_DEADLINE_EXPIRED\");\n\n        // Unchecked because the only math done is incrementing\n        // the owner's nonce which cannot realistically overflow.\n        unchecked {\n            address recoveredAddress = ecrecover(\n                keccak256(\n                    abi.encodePacked(\n                        \"\\x19\\x01\",\n                        DOMAIN_SEPARATOR(),\n                        keccak256(\n                            abi.encode(\n                                keccak256(\n                                    \"Permit(address owner,address spender,uint256 value,uint256 nonce,uint256 deadline)\"\n                                ),\n                                owner,\n                                spender,\n                                value,\n                                nonces[owner]++,\n                                deadline\n                            )\n                        )\n                    )\n                ),\n                v,\n                r,\n                s\n            );\n\n            require(recoveredAddress != address(0) && recoveredAddress == owner, \"INVALID_SIGNER\");\n\n            allowance[recoveredAddress][spender] = value;\n        }\n\n        emit Approval(owner, spender, value);\n    }\n\n    function DOMAIN_SEPARATOR() public view virtual returns (bytes32) {\n        return block.chainid == INITIAL_CHAIN_ID ? INITIAL_DOMAIN_SEPARATOR : computeDomainSeparator();\n    }\n\n    function computeDomainSeparator() internal view virtual returns (bytes32) {\n        return\n            keccak256(\n                abi.encode(\n                    keccak256(\"EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)\"),\n                    keccak256(bytes(name)),\n                    keccak256(\"1\"),\n                    block.chainid,\n                    address(this)\n                )\n            );\n    }\n\n    /*//////////////////////////////////////////////////////////////\n                        INTERNAL MINT/BURN LOGIC\n    //////////////////////////////////////////////////////////////*/\n\n    function _mint(address to, uint256 amount) internal virtual {\n        totalSupply += amount;\n\n        // Cannot overflow because the sum of all user\n        // balances can't exceed the max uint256 value.\n        unchecked {\n            balanceOf[to] += amount;\n        }\n\n        emit Transfer(address(0), to, amount);\n    }\n\n    function _burn(address from, uint256 amount) internal virtual {\n        balanceOf[from] -= amount;\n\n        // Cannot underflow because a user's balance\n        // will never be larger than the total supply.\n        unchecked {\n            totalSupply -= amount;\n        }\n\n        emit Transfer(from, address(0), amount);\n    }\n}\n"
    },
    "contracts/Ownable.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.0;\n\nerror CallerNotOwner();\n\nabstract contract Ownable {\n    address _owner = msg.sender;\n\n    function owner() public view returns (address) {\n        return _owner;\n    }\n\n    function transferOwnership(address newOwner) external onlyOwner {\n        _owner = newOwner;\n    }\n\n    modifier onlyOwner() {\n        if (msg.sender != _owner) revert CallerNotOwner();\n        _;\n    }\n}\n"
    }
  },
  "settings": {
    "optimizer": {
      "enabled": true,
      "runs": 10000
    },
    "outputSelection": {
      "*": {
        "*": [
          "evm.bytecode",
          "evm.deployedBytecode",
          "devdoc",
          "userdoc",
          "metadata",
          "abi"
        ]
      }
    },
    "libraries": {}
  }
}}