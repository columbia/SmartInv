{{
  "language": "Solidity",
  "settings": {
    "evmVersion": "berlin",
    "libraries": {},
    "metadata": {
      "bytecodeHash": "ipfs",
      "useLiteralContent": true
    },
    "optimizer": {
      "enabled": true,
      "runs": 10000
    },
    "remappings": [],
    "outputSelection": {
      "*": {
        "*": [
          "evm.bytecode",
          "evm.deployedBytecode",
          "devdoc",
          "userdoc",
          "metadata",
          "abi"
        ]
      }
    }
  },
  "sources": {
    "@openzeppelin/contracts/token/ERC721/IERC721.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (token/ERC721/IERC721.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../../utils/introspection/IERC165.sol\";\n\n/**\n * @dev Required interface of an ERC721 compliant contract.\n */\ninterface IERC721 is IERC165 {\n    /**\n     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.\n     */\n    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);\n\n    /**\n     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.\n     */\n    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);\n\n    /**\n     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.\n     */\n    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);\n\n    /**\n     * @dev Returns the number of tokens in ``owner``'s account.\n     */\n    function balanceOf(address owner) external view returns (uint256 balance);\n\n    /**\n     * @dev Returns the owner of the `tokenId` token.\n     *\n     * Requirements:\n     *\n     * - `tokenId` must exist.\n     */\n    function ownerOf(uint256 tokenId) external view returns (address owner);\n\n    /**\n     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients\n     * are aware of the ERC721 protocol to prevent tokens from being forever locked.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `tokenId` token must exist and be owned by `from`.\n     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.\n     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.\n     *\n     * Emits a {Transfer} event.\n     */\n    function safeTransferFrom(\n        address from,\n        address to,\n        uint256 tokenId\n    ) external;\n\n    /**\n     * @dev Transfers `tokenId` token from `from` to `to`.\n     *\n     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `tokenId` token must be owned by `from`.\n     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transferFrom(\n        address from,\n        address to,\n        uint256 tokenId\n    ) external;\n\n    /**\n     * @dev Gives permission to `to` to transfer `tokenId` token to another account.\n     * The approval is cleared when the token is transferred.\n     *\n     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.\n     *\n     * Requirements:\n     *\n     * - The caller must own the token or be an approved operator.\n     * - `tokenId` must exist.\n     *\n     * Emits an {Approval} event.\n     */\n    function approve(address to, uint256 tokenId) external;\n\n    /**\n     * @dev Returns the account approved for `tokenId` token.\n     *\n     * Requirements:\n     *\n     * - `tokenId` must exist.\n     */\n    function getApproved(uint256 tokenId) external view returns (address operator);\n\n    /**\n     * @dev Approve or remove `operator` as an operator for the caller.\n     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.\n     *\n     * Requirements:\n     *\n     * - The `operator` cannot be the caller.\n     *\n     * Emits an {ApprovalForAll} event.\n     */\n    function setApprovalForAll(address operator, bool _approved) external;\n\n    /**\n     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.\n     *\n     * See {setApprovalForAll}\n     */\n    function isApprovedForAll(address owner, address operator) external view returns (bool);\n\n    /**\n     * @dev Safely transfers `tokenId` token from `from` to `to`.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `tokenId` token must exist and be owned by `from`.\n     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.\n     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.\n     *\n     * Emits a {Transfer} event.\n     */\n    function safeTransferFrom(\n        address from,\n        address to,\n        uint256 tokenId,\n        bytes calldata data\n    ) external;\n}\n"
    },
    "@openzeppelin/contracts/utils/introspection/ERC165.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)\n\npragma solidity ^0.8.0;\n\nimport \"./IERC165.sol\";\n\n/**\n * @dev Implementation of the {IERC165} interface.\n *\n * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check\n * for the additional interface id that will be supported. For example:\n *\n * ```solidity\n * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {\n *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);\n * }\n * ```\n *\n * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.\n */\nabstract contract ERC165 is IERC165 {\n    /**\n     * @dev See {IERC165-supportsInterface}.\n     */\n    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {\n        return interfaceId == type(IERC165).interfaceId;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/introspection/IERC165.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Interface of the ERC165 standard, as defined in the\n * https://eips.ethereum.org/EIPS/eip-165[EIP].\n *\n * Implementers can declare support of contract interfaces, which can then be\n * queried by others ({ERC165Checker}).\n *\n * For an implementation, see {ERC165}.\n */\ninterface IERC165 {\n    /**\n     * @dev Returns true if this contract implements the interface defined by\n     * `interfaceId`. See the corresponding\n     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]\n     * to learn more about how these ids are created.\n     *\n     * This function call must use less than 30 000 gas.\n     */\n    function supportsInterface(bytes4 interfaceId) external view returns (bool);\n}\n"
    },
    "@paulrberg/contracts/math/PRBMath.sol": {
      "content": "// SPDX-License-Identifier: Unlicense\npragma solidity >=0.8.4;\n\nimport \"prb-math/contracts/PRBMath.sol\";\n"
    },
    "contracts/JBController.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/ERC165.sol';\nimport '@paulrberg/contracts/math/PRBMath.sol';\nimport './abstract/JBOperatable.sol';\nimport './interfaces/IJBController.sol';\nimport './interfaces/IJBMigratable.sol';\nimport './interfaces/IJBOperatorStore.sol';\nimport './interfaces/IJBPaymentTerminal.sol';\nimport './interfaces/IJBProjects.sol';\nimport './libraries/JBConstants.sol';\nimport './libraries/JBFundingCycleMetadataResolver.sol';\nimport './libraries/JBOperations.sol';\nimport './libraries/JBSplitsGroups.sol';\n\n/**\n  @notice\n  Stitches together funding cycles and community tokens, making sure all activity is accounted for and correct.\n\n  @dev\n  Adheres to -\n  IJBController: General interface for the generic controller methods in this contract that interacts with funding cycles and tokens according to the protocol's rules.\n  IJBMigratable: Allows migrating to this contract, with a hook called to prepare for the migration.\n\n  @dev\n  Inherits from -\n  JBOperatable: Several functions in this contract can only be accessed by a project owner, or an address that has been preconfifigured to be an operator of the project.\n  ERC165: Introspection on interface adherance. \n*/\ncontract JBController is IJBController, IJBMigratable, JBOperatable, ERC165 {\n  // A library that parses the packed funding cycle metadata into a more friendly format.\n  using JBFundingCycleMetadataResolver for JBFundingCycle;\n\n  //*********************************************************************//\n  // --------------------------- custom errors ------------------------- //\n  //*********************************************************************//\n  error BURN_PAUSED_AND_SENDER_NOT_VALID_TERMINAL_DELEGATE();\n  error CANT_MIGRATE_TO_CURRENT_CONTROLLER();\n  error CHANGE_TOKEN_NOT_ALLOWED();\n  error FUNDING_CYCLE_ALREADY_LAUNCHED();\n  error INVALID_BALLOT_REDEMPTION_RATE();\n  error INVALID_DISTRIBUTION_LIMIT();\n  error INVALID_DISTRIBUTION_LIMIT_CURRENCY();\n  error INVALID_OVERFLOW_ALLOWANCE();\n  error INVALID_OVERFLOW_ALLOWANCE_CURRENCY();\n  error INVALID_REDEMPTION_RATE();\n  error INVALID_RESERVED_RATE();\n  error MIGRATION_NOT_ALLOWED();\n  error MINT_NOT_ALLOWED_AND_NOT_TERMINAL_DELEGATE();\n  error NO_BURNABLE_TOKENS();\n  error NOT_CURRENT_CONTROLLER();\n  error ZERO_TOKENS_TO_MINT();\n\n  //*********************************************************************//\n  // --------------------- internal stored properties ------------------ //\n  //*********************************************************************//\n\n  /**\n    @notice\n    The difference between the processed token tracker of a project and the project's token's total supply is the amount of tokens that still need to have reserves minted against them.\n\n    _projectId The ID of the project to get the tracker of.\n  */\n  mapping(uint256 => int256) internal _processedTokenTrackerOf;\n\n  /**\n    @notice\n    Data regarding the distribution limit of a project during a configuration.\n\n    @dev\n    bits 0-231: The amount of token that a project can distribute per funding cycle.\n\n    @dev\n    bits 232-255: The currency of amount that a project can distribute.\n\n    _projectId The ID of the project to get the packed distribution limit data of.\n    _configuration The configuration during which the packed distribution limit data applies.\n    _terminal The terminal from which distributions are being limited.\n    _token The token for which distributions are being limited.\n  */\n  mapping(uint256 => mapping(uint256 => mapping(IJBPaymentTerminal => mapping(address => uint256))))\n    internal _packedDistributionLimitDataOf;\n\n  /**\n    @notice\n    Data regarding the overflow allowance of a project during a configuration.\n\n    @dev\n    bits 0-231: The amount of overflow that a project is allowed to tap into on-demand throughout the configuration.\n\n    @dev\n    bits 232-255: The currency of the amount of overflow that a project is allowed to tap.\n\n    _projectId The ID of the project to get the packed overflow allowance data of.\n    _configuration The configuration during which the packed overflow allowance data applies.\n    _terminal The terminal managing the overflow.\n    _token The token for which overflow is being allowed.\n  */\n  mapping(uint256 => mapping(uint256 => mapping(IJBPaymentTerminal => mapping(address => uint256))))\n    internal _packedOverflowAllowanceDataOf;\n\n  //*********************************************************************//\n  // --------------- public immutable stored properties ---------------- //\n  //*********************************************************************//\n\n  /**\n    @notice\n    Mints ERC-721's that represent project ownership.\n  */\n  IJBProjects public immutable override projects;\n\n  /**\n    @notice\n    The contract storing all funding cycle configurations.\n  */\n  IJBFundingCycleStore public immutable override fundingCycleStore;\n\n  /**\n    @notice\n    The contract that manages token minting and burning.\n  */\n  IJBTokenStore public immutable override tokenStore;\n\n  /**\n    @notice\n    The contract that stores splits for each project.\n  */\n  IJBSplitsStore public immutable override splitsStore;\n\n  /**\n    @notice\n    The directory of terminals and controllers for projects.\n  */\n  IJBDirectory public immutable override directory;\n\n  //*********************************************************************//\n  // ------------------------- external views -------------------------- //\n  //*********************************************************************//\n\n  /**\n    @notice\n    The amount of token that a project can distribute per funding cycle, and the currency it's in terms of.\n\n    @dev\n    The number of decimals in the returned fixed point amount is the same as that of the specified terminal. \n\n    @param _projectId The ID of the project to get the distribution limit of.\n    @param _configuration The configuration during which the distribution limit applies.\n    @param _terminal The terminal from which distributions are being limited.\n    @param _token The token for which the distribution limit applies.\n\n    @return The distribution limit, as a fixed point number with the same number of decimals as the provided terminal.\n    @return The currency of the distribution limit.\n  */\n  function distributionLimitOf(\n    uint256 _projectId,\n    uint256 _configuration,\n    IJBPaymentTerminal _terminal,\n    address _token\n  ) external view override returns (uint256, uint256) {\n    // Get a reference to the packed data.\n    uint256 _data = _packedDistributionLimitDataOf[_projectId][_configuration][_terminal][_token];\n\n    // The limit is in bits 0-231. The currency is in bits 232-255.\n    return (uint256(uint232(_data)), _data >> 232);\n  }\n\n  /**\n    @notice\n    The amount of overflow that a project is allowed to tap into on-demand throughout a configuration, and the currency it's in terms of.\n\n    @dev\n    The number of decimals in the returned fixed point amount is the same as that of the specified terminal. \n\n    @param _projectId The ID of the project to get the overflow allowance of.\n    @param _configuration The configuration of the during which the allowance applies.\n    @param _terminal The terminal managing the overflow.\n    @param _token The token for which the overflow allowance applies.\n\n    @return The overflow allowance, as a fixed point number with the same number of decimals as the provided terminal.\n    @return The currency of the overflow allowance.\n  */\n  function overflowAllowanceOf(\n    uint256 _projectId,\n    uint256 _configuration,\n    IJBPaymentTerminal _terminal,\n    address _token\n  ) external view override returns (uint256, uint256) {\n    // Get a reference to the packed data.\n    uint256 _data = _packedOverflowAllowanceDataOf[_projectId][_configuration][_terminal][_token];\n\n    // The allowance is in bits 0-231. The currency is in bits 232-255.\n    return (uint256(uint232(_data)), _data >> 232);\n  }\n\n  /**\n    @notice\n    Gets the amount of reserved tokens that a project has available to distribute.\n\n    @param _projectId The ID of the project to get a reserved token balance of.\n    @param _reservedRate The reserved rate to use when making the calculation.\n\n    @return The current amount of reserved tokens.\n  */\n  function reservedTokenBalanceOf(uint256 _projectId, uint256 _reservedRate)\n    external\n    view\n    override\n    returns (uint256)\n  {\n    return\n      _reservedTokenAmountFrom(\n        _processedTokenTrackerOf[_projectId],\n        _reservedRate,\n        tokenStore.totalSupplyOf(_projectId)\n      );\n  }\n\n  /**\n    @notice\n    Gets the current total amount of outstanding tokens for a project, given a reserved rate.\n\n    @param _projectId The ID of the project to get total outstanding tokens of.\n    @param _reservedRate The reserved rate to use when making the calculation.\n\n    @return The current total amount of outstanding tokens for the project.\n  */\n  function totalOutstandingTokensOf(uint256 _projectId, uint256 _reservedRate)\n    external\n    view\n    override\n    returns (uint256)\n  {\n    // Get the total number of tokens in circulation.\n    uint256 _totalSupply = tokenStore.totalSupplyOf(_projectId);\n\n    // Get the number of reserved tokens the project has.\n    uint256 _reservedTokenAmount = _reservedTokenAmountFrom(\n      _processedTokenTrackerOf[_projectId],\n      _reservedRate,\n      _totalSupply\n    );\n\n    // Add the reserved tokens to the total supply.\n    return _totalSupply + _reservedTokenAmount;\n  }\n\n  /** \n    @notice\n    A project's funding cycle for the specified configuration along with its metadata.\n\n    @param _projectId The ID of the project to which the funding cycle belongs.\n  \n    @return fundingCycle The funding cycle.\n    @return metadata The funding cycle's metadata.\n  */\n  function getFundingCycleOf(uint256 _projectId, uint256 _configuration)\n    external\n    view\n    override\n    returns (JBFundingCycle memory fundingCycle, JBFundingCycleMetadata memory metadata)\n  {\n    fundingCycle = fundingCycleStore.get(_projectId, _configuration);\n    metadata = fundingCycle.expandMetadata();\n  }\n\n  /** \n    @notice\n    A project's latest configured funding cycle along with its metadata and the ballot state of the configuration.\n\n    @param _projectId The ID of the project to which the funding cycle belongs.\n  \n    @return fundingCycle The latest configured funding cycle.\n    @return metadata The latest configured funding cycle's metadata.\n    @return ballotState The state of the configuration.\n  */\n  function latestConfiguredFundingCycleOf(uint256 _projectId)\n    external\n    view\n    override\n    returns (\n      JBFundingCycle memory fundingCycle,\n      JBFundingCycleMetadata memory metadata,\n      JBBallotState ballotState\n    )\n  {\n    (fundingCycle, ballotState) = fundingCycleStore.latestConfiguredOf(_projectId);\n    metadata = fundingCycle.expandMetadata();\n  }\n\n  /** \n    @notice\n    A project's current funding cycle along with its metadata.\n\n    @param _projectId The ID of the project to which the funding cycle belongs.\n  \n    @return fundingCycle The current funding cycle.\n    @return metadata The current funding cycle's metadata.\n  */\n  function currentFundingCycleOf(uint256 _projectId)\n    external\n    view\n    override\n    returns (JBFundingCycle memory fundingCycle, JBFundingCycleMetadata memory metadata)\n  {\n    fundingCycle = fundingCycleStore.currentOf(_projectId);\n    metadata = fundingCycle.expandMetadata();\n  }\n\n  /** \n    @notice\n    A project's queued funding cycle along with its metadata.\n\n    @param _projectId The ID of the project to which the funding cycle belongs.\n  \n    @return fundingCycle The queued funding cycle.\n    @return metadata The queued funding cycle's metadata.\n  */\n  function queuedFundingCycleOf(uint256 _projectId)\n    external\n    view\n    override\n    returns (JBFundingCycle memory fundingCycle, JBFundingCycleMetadata memory metadata)\n  {\n    fundingCycle = fundingCycleStore.queuedOf(_projectId);\n    metadata = fundingCycle.expandMetadata();\n  }\n\n  //*********************************************************************//\n  // -------------------------- public views --------------------------- //\n  //*********************************************************************//\n\n  /**\n    @notice\n    Indicates if this contract adheres to the specified interface.\n\n    @dev \n    See {IERC165-supportsInterface}.\n\n    @param _interfaceId The ID of the interface to check for adherance to.\n  */\n  function supportsInterface(bytes4 _interfaceId)\n    public\n    view\n    virtual\n    override(ERC165, IERC165)\n    returns (bool)\n  {\n    return\n      _interfaceId == type(IJBController).interfaceId ||\n      _interfaceId == type(IJBMigratable).interfaceId ||\n      _interfaceId == type(IJBOperatable).interfaceId ||\n      super.supportsInterface(_interfaceId);\n  }\n\n  //*********************************************************************//\n  // ---------------------------- constructor -------------------------- //\n  //*********************************************************************//\n\n  /**\n    @param _operatorStore A contract storing operator assignments.\n    @param _projects A contract which mints ERC-721's that represent project ownership and transfers.\n    @param _directory A contract storing directories of terminals and controllers for each project.\n    @param _fundingCycleStore A contract storing all funding cycle configurations.\n    @param _tokenStore A contract that manages token minting and burning.\n    @param _splitsStore A contract that stores splits for each project.\n  */\n  constructor(\n    IJBOperatorStore _operatorStore,\n    IJBProjects _projects,\n    IJBDirectory _directory,\n    IJBFundingCycleStore _fundingCycleStore,\n    IJBTokenStore _tokenStore,\n    IJBSplitsStore _splitsStore\n  ) JBOperatable(_operatorStore) {\n    projects = _projects;\n    directory = _directory;\n    fundingCycleStore = _fundingCycleStore;\n    tokenStore = _tokenStore;\n    splitsStore = _splitsStore;\n  }\n\n  //*********************************************************************//\n  // --------------------- external transactions ----------------------- //\n  //*********************************************************************//\n\n  /**\n    @notice\n    Creates a project. This will mint an ERC-721 into the specified owner's account, configure a first funding cycle, and set up any splits.\n\n    @dev\n    Each operation within this transaction can be done in sequence separately.\n\n    @dev\n    Anyone can deploy a project on an owner's behalf.\n\n    @param _owner The address to set as the owner of the project. The project ERC-721 will be owned by this address.\n    @param _projectMetadata Metadata to associate with the project within a particular domain. This can be updated any time by the owner of the project.\n    @param _data Data that defines the project's first funding cycle. These properties will remain fixed for the duration of the funding cycle.\n    @param _metadata Metadata specifying the controller specific params that a funding cycle can have. These properties will remain fixed for the duration of the funding cycle.\n    @param _mustStartAtOrAfter The time before which the configured funding cycle cannot start.\n    @param _groupedSplits An array of splits to set for any number of groups. \n    @param _fundAccessConstraints An array containing amounts that a project can use from its treasury for each payment terminal. Amounts are fixed point numbers using the same number of decimals as the accompanying terminal. The `_distributionLimit` and `_overflowAllowance` parameters must fit in a `uint232`.\n    @param _terminals Payment terminals to add for the project.\n    @param _memo A memo to pass along to the emitted event.\n\n    @return projectId The ID of the project.\n  */\n  function launchProjectFor(\n    address _owner,\n    JBProjectMetadata calldata _projectMetadata,\n    JBFundingCycleData calldata _data,\n    JBFundingCycleMetadata calldata _metadata,\n    uint256 _mustStartAtOrAfter,\n    JBGroupedSplits[] calldata _groupedSplits,\n    JBFundAccessConstraints[] calldata _fundAccessConstraints,\n    IJBPaymentTerminal[] memory _terminals,\n    string memory _memo\n  ) external virtual override returns (uint256 projectId) {\n    // Mint the project into the wallet of the message sender.\n    projectId = projects.createFor(_owner, _projectMetadata);\n\n    // Set this contract as the project's controller in the directory.\n    directory.setControllerOf(projectId, address(this));\n\n    // Configure the first funding cycle.\n    uint256 _configuration = _configure(\n      projectId,\n      _data,\n      _metadata,\n      _mustStartAtOrAfter,\n      _groupedSplits,\n      _fundAccessConstraints\n    );\n\n    // Add the provided terminals to the list of terminals.\n    if (_terminals.length > 0) directory.setTerminalsOf(projectId, _terminals);\n\n    emit LaunchProject(_configuration, projectId, _memo, msg.sender);\n  }\n\n  /**\n    @notice\n    Creates a funding cycle for an already existing project ERC-721.\n\n    @dev\n    Each operation within this transaction can be done in sequence separately.\n\n    @dev\n    Only a project owner or operator can launch its funding cycles.\n\n    @param _projectId The ID of the project to launch funding cycles for.\n    @param _data Data that defines the project's first funding cycle. These properties will remain fixed for the duration of the funding cycle.\n    @param _metadata Metadata specifying the controller specific params that a funding cycle can have. These properties will remain fixed for the duration of the funding cycle.\n    @param _mustStartAtOrAfter The time before which the configured funding cycle cannot start.\n    @param _groupedSplits An array of splits to set for any number of groups. \n    @param _fundAccessConstraints An array containing amounts that a project can use from its treasury for each payment terminal. Amounts are fixed point numbers using the same number of decimals as the accompanying terminal. The `_distributionLimit` and `_overflowAllowance` parameters must fit in a `uint232`.\n    @param _terminals Payment terminals to add for the project.\n    @param _memo A memo to pass along to the emitted event.\n\n    @return configuration The configuration of the funding cycle that was successfully created.\n  */\n  function launchFundingCyclesFor(\n    uint256 _projectId,\n    JBFundingCycleData calldata _data,\n    JBFundingCycleMetadata calldata _metadata,\n    uint256 _mustStartAtOrAfter,\n    JBGroupedSplits[] calldata _groupedSplits,\n    JBFundAccessConstraints[] memory _fundAccessConstraints,\n    IJBPaymentTerminal[] memory _terminals,\n    string memory _memo\n  )\n    external\n    virtual\n    override\n    requirePermission(projects.ownerOf(_projectId), _projectId, JBOperations.RECONFIGURE)\n    returns (uint256 configuration)\n  {\n    // If there is a previous configuration, reconfigureFundingCyclesOf should be called instead\n    if (fundingCycleStore.latestConfigurationOf(_projectId) > 0)\n      revert FUNDING_CYCLE_ALREADY_LAUNCHED();\n\n    // Set this contract as the project's controller in the directory.\n    directory.setControllerOf(_projectId, address(this));\n\n    // Configure the first funding cycle.\n    configuration = _configure(\n      _projectId,\n      _data,\n      _metadata,\n      _mustStartAtOrAfter,\n      _groupedSplits,\n      _fundAccessConstraints\n    );\n\n    // Add the provided terminals to the list of terminals.\n    if (_terminals.length > 0) directory.setTerminalsOf(_projectId, _terminals);\n\n    emit LaunchFundingCycles(configuration, _projectId, _memo, msg.sender);\n  }\n\n  /**\n    @notice\n    Proposes a configuration of a subsequent funding cycle that will take effect once the current one expires if it is approved by the current funding cycle's ballot.\n\n    @dev\n    Only a project's owner or a designated operator can configure its funding cycles.\n\n    @param _projectId The ID of the project whose funding cycles are being reconfigured.\n    @param _data Data that defines the funding cycle. These properties will remain fixed for the duration of the funding cycle.\n    @param _metadata Metadata specifying the controller specific params that a funding cycle can have. These properties will remain fixed for the duration of the funding cycle.\n    @param _mustStartAtOrAfter The time before which the configured funding cycle cannot start.\n    @param _groupedSplits An array of splits to set for any number of groups. \n    @param _fundAccessConstraints An array containing amounts that a project can use from its treasury for each payment terminal. Amounts are fixed point numbers using the same number of decimals as the accompanying terminal. The `_distributionLimit` and `_overflowAllowance` parameters must fit in a `uint232`.\n    @param _memo A memo to pass along to the emitted event.\n\n    @return configuration The configuration of the funding cycle that was successfully reconfigured.\n  */\n  function reconfigureFundingCyclesOf(\n    uint256 _projectId,\n    JBFundingCycleData calldata _data,\n    JBFundingCycleMetadata calldata _metadata,\n    uint256 _mustStartAtOrAfter,\n    JBGroupedSplits[] calldata _groupedSplits,\n    JBFundAccessConstraints[] calldata _fundAccessConstraints,\n    string calldata _memo\n  )\n    external\n    virtual\n    override\n    requirePermission(projects.ownerOf(_projectId), _projectId, JBOperations.RECONFIGURE)\n    returns (uint256 configuration)\n  {\n    // Configure the next funding cycle.\n    configuration = _configure(\n      _projectId,\n      _data,\n      _metadata,\n      _mustStartAtOrAfter,\n      _groupedSplits,\n      _fundAccessConstraints\n    );\n\n    emit ReconfigureFundingCycles(configuration, _projectId, _memo, msg.sender);\n  }\n\n  /**\n    @notice\n    Issues an owner's ERC20 JBTokens that'll be used when claiming tokens.\n\n    @dev\n    Deploys a project's ERC20 JBToken contract.\n\n    @dev\n    Only a project's owner or operator can issue its token.\n\n    @param _projectId The ID of the project being issued tokens.\n    @param _name The ERC20's name.\n    @param _symbol The ERC20's symbol.\n  */\n  function issueTokenFor(\n    uint256 _projectId,\n    string calldata _name,\n    string calldata _symbol\n  )\n    external\n    virtual\n    override\n    requirePermission(projects.ownerOf(_projectId), _projectId, JBOperations.ISSUE)\n    returns (IJBToken token)\n  {\n    // Issue the token in the store.\n    return tokenStore.issueFor(_projectId, _name, _symbol);\n  }\n\n  /**\n    @notice\n    Swap the current project's token that is minted and burned for another, and transfer ownership of the current token to another address if needed.\n\n    @dev\n    Only a project's owner or operator can change its token.\n\n    @param _projectId The ID of the project to which the changed token belongs.\n    @param _token The new token.\n    @param _newOwner An address to transfer the current token's ownership to. This is optional, but it cannot be done later.\n  */\n  function changeTokenOf(\n    uint256 _projectId,\n    IJBToken _token,\n    address _newOwner\n  )\n    external\n    virtual\n    override\n    requirePermission(projects.ownerOf(_projectId), _projectId, JBOperations.CHANGE_TOKEN)\n  {\n    // Get a reference to the project's current funding cycle.\n    JBFundingCycle memory _fundingCycle = fundingCycleStore.currentOf(_projectId);\n\n    // The current funding cycle must not be paused.\n    if (!_fundingCycle.changeTokenAllowed()) revert CHANGE_TOKEN_NOT_ALLOWED();\n\n    // Change the token in the store.\n    tokenStore.changeFor(_projectId, _token, _newOwner);\n  }\n\n  /**\n    @notice\n    Mint new token supply into an account, and optionally reserve a supply to be distributed according to the project's current funding cycle configuration.\n\n    @dev\n    Only a project's owner, a designated operator, one of its terminals, or the current data source can mint its tokens.\n\n    @param _projectId The ID of the project to which the tokens being minted belong.\n    @param _tokenCount The amount of tokens to mint in total, counting however many should be reserved.\n    @param _beneficiary The account that the tokens are being minted for.\n    @param _memo A memo to pass along to the emitted event.\n    @param _preferClaimedTokens A flag indicating whether a project's attached token contract should be minted if they have been issued.\n    @param _useReservedRate Whether to use the current funding cycle's reserved rate in the mint calculation.\n\n    @return beneficiaryTokenCount The amount of tokens minted for the beneficiary.\n  */\n  function mintTokensOf(\n    uint256 _projectId,\n    uint256 _tokenCount,\n    address _beneficiary,\n    string calldata _memo,\n    bool _preferClaimedTokens,\n    bool _useReservedRate\n  ) external virtual override returns (uint256 beneficiaryTokenCount) {\n    // There should be tokens to mint.\n    if (_tokenCount == 0) revert ZERO_TOKENS_TO_MINT();\n\n    // Define variables that will be needed outside scoped section below.\n    // Keep a reference to the reserved rate to use\n    uint256 _reservedRate;\n\n    // Scoped section prevents stack too deep. `_fundingCycle` only used within scope.\n    {\n      // Get a reference to the project's current funding cycle.\n      JBFundingCycle memory _fundingCycle = fundingCycleStore.currentOf(_projectId);\n\n      // Minting limited to: project owner, authorized callers, project terminal and current funding cycle data source\n      _requirePermissionAllowingOverride(\n        projects.ownerOf(_projectId),\n        _projectId,\n        JBOperations.MINT,\n        directory.isTerminalOf(_projectId, IJBPaymentTerminal(msg.sender)) ||\n          msg.sender == address(_fundingCycle.dataSource())\n      );\n\n      // If the message sender is not a terminal or a datasource, the current funding cycle must allow minting.\n      if (\n        !_fundingCycle.mintingAllowed() &&\n        !directory.isTerminalOf(_projectId, IJBPaymentTerminal(msg.sender)) &&\n        msg.sender != address(_fundingCycle.dataSource())\n      ) revert MINT_NOT_ALLOWED_AND_NOT_TERMINAL_DELEGATE();\n\n      // Determine the reserved rate to use.\n      _reservedRate = _useReservedRate ? _fundingCycle.reservedRate() : 0;\n    }\n\n    if (_reservedRate == JBConstants.MAX_RESERVED_RATE)\n      // Subtract the total weighted amount from the tracker so the full reserved token amount can be printed later.\n      _processedTokenTrackerOf[_projectId] =\n        _processedTokenTrackerOf[_projectId] -\n        int256(_tokenCount);\n    else {\n      // The unreserved token count that will be minted for the beneficiary.\n      beneficiaryTokenCount = PRBMath.mulDiv(\n        _tokenCount,\n        JBConstants.MAX_RESERVED_RATE - _reservedRate,\n        JBConstants.MAX_RESERVED_RATE\n      );\n\n      if (_reservedRate == 0)\n        // If there's no reserved rate, increment the tracker with the newly minted tokens.\n        _processedTokenTrackerOf[_projectId] =\n          _processedTokenTrackerOf[_projectId] +\n          int256(beneficiaryTokenCount);\n\n      // Mint the tokens.\n      tokenStore.mintFor(_beneficiary, _projectId, beneficiaryTokenCount, _preferClaimedTokens);\n    }\n\n    emit MintTokens(\n      _beneficiary,\n      _projectId,\n      _tokenCount,\n      beneficiaryTokenCount,\n      _memo,\n      _reservedRate,\n      msg.sender\n    );\n  }\n\n  /**\n    @notice\n    Burns a token holder's supply.\n\n    @dev\n    Only a token's holder, a designated operator, or a project's terminal can burn it.\n\n    @param _holder The account that is having its tokens burned.\n    @param _projectId The ID of the project to which the tokens being burned belong.\n    @param _tokenCount The number of tokens to burn.\n    @param _memo A memo to pass along to the emitted event.\n    @param _preferClaimedTokens A flag indicating whether a project's attached token contract should be burned first if they have been issued.\n  */\n  function burnTokensOf(\n    address _holder,\n    uint256 _projectId,\n    uint256 _tokenCount,\n    string calldata _memo,\n    bool _preferClaimedTokens\n  )\n    external\n    virtual\n    override\n    requirePermissionAllowingOverride(\n      _holder,\n      _projectId,\n      JBOperations.BURN,\n      directory.isTerminalOf(_projectId, IJBPaymentTerminal(msg.sender))\n    )\n  {\n    // There should be tokens to burn\n    if (_tokenCount == 0) revert NO_BURNABLE_TOKENS();\n\n    // Get a reference to the project's current funding cycle.\n    JBFundingCycle memory _fundingCycle = fundingCycleStore.currentOf(_projectId);\n\n    // If the message sender is not a terminal, the current funding cycle must not be paused.\n    if (\n      _fundingCycle.burnPaused() &&\n      !directory.isTerminalOf(_projectId, IJBPaymentTerminal(msg.sender))\n    ) revert BURN_PAUSED_AND_SENDER_NOT_VALID_TERMINAL_DELEGATE();\n\n    // Update the token tracker so that reserved tokens will still be correctly mintable.\n    _processedTokenTrackerOf[_projectId] =\n      _processedTokenTrackerOf[_projectId] -\n      int256(_tokenCount);\n\n    // Burn the tokens.\n    tokenStore.burnFrom(_holder, _projectId, _tokenCount, _preferClaimedTokens);\n\n    emit BurnTokens(_holder, _projectId, _tokenCount, _memo, msg.sender);\n  }\n\n  /**\n    @notice\n    Distributes all outstanding reserved tokens for a project.\n\n    @param _projectId The ID of the project to which the reserved tokens belong.\n    @param _memo A memo to pass along to the emitted event.\n\n    @return The amount of minted reserved tokens.\n  */\n  function distributeReservedTokensOf(uint256 _projectId, string calldata _memo)\n    external\n    virtual\n    override\n    returns (uint256)\n  {\n    return _distributeReservedTokensOf(_projectId, _memo);\n  }\n\n  /**\n    @notice\n    Allows other controllers to signal to this one that a migration is expected for the specified project.\n\n    @dev\n    This controller should not yet be the project's controller.\n\n    @param _projectId The ID of the project that will be migrated to this controller.\n    @param _from The controller being migrated from.\n  */\n  function prepForMigrationOf(uint256 _projectId, address _from) external virtual override {\n    // This controller must not be the project's current controller.\n    if (directory.controllerOf(_projectId) == address(this))\n      revert CANT_MIGRATE_TO_CURRENT_CONTROLLER();\n\n    // Set the tracker as the total supply.\n    _processedTokenTrackerOf[_projectId] = int256(tokenStore.totalSupplyOf(_projectId));\n\n    emit PrepMigration(_projectId, _from, msg.sender);\n  }\n\n  /**\n    @notice\n    Allows a project to migrate from this controller to another.\n\n    @dev\n    Only a project's owner or a designated operator can migrate it.\n\n    @param _projectId The ID of the project that will be migrated from this controller.\n    @param _to The controller to which the project is migrating.\n  */\n  function migrate(uint256 _projectId, IJBMigratable _to)\n    external\n    virtual\n    override\n    requirePermission(projects.ownerOf(_projectId), _projectId, JBOperations.MIGRATE_CONTROLLER)\n  {\n    // This controller must be the project's current controller.\n    if (directory.controllerOf(_projectId) != address(this)) revert NOT_CURRENT_CONTROLLER();\n\n    // Get a reference to the project's current funding cycle.\n    JBFundingCycle memory _fundingCycle = fundingCycleStore.currentOf(_projectId);\n\n    // Migration must be allowed.\n    if (!_fundingCycle.controllerMigrationAllowed()) revert MIGRATION_NOT_ALLOWED();\n\n    // All reserved tokens must be minted before migrating.\n    if (uint256(_processedTokenTrackerOf[_projectId]) != tokenStore.totalSupplyOf(_projectId))\n      _distributeReservedTokensOf(_projectId, '');\n\n    // Make sure the new controller is prepped for the migration.\n    _to.prepForMigrationOf(_projectId, address(this));\n\n    // Set the new controller.\n    directory.setControllerOf(_projectId, address(_to));\n\n    emit Migrate(_projectId, _to, msg.sender);\n  }\n\n  //*********************************************************************//\n  // ------------------------ internal functions ----------------------- //\n  //*********************************************************************//\n\n  /**\n    @notice\n    Distributes all outstanding reserved tokens for a project.\n\n    @param _projectId The ID of the project to which the reserved tokens belong.\n    @param _memo A memo to pass along to the emitted event.\n\n    @return tokenCount The amount of minted reserved tokens.\n  */\n  function _distributeReservedTokensOf(uint256 _projectId, string memory _memo)\n    internal\n    returns (uint256 tokenCount)\n  {\n    // Get the current funding cycle to read the reserved rate from.\n    JBFundingCycle memory _fundingCycle = fundingCycleStore.currentOf(_projectId);\n\n    // Get a reference to new total supply of tokens before minting reserved tokens.\n    uint256 _totalTokens = tokenStore.totalSupplyOf(_projectId);\n\n    // Get a reference to the number of tokens that need to be minted.\n    tokenCount = _reservedTokenAmountFrom(\n      _processedTokenTrackerOf[_projectId],\n      _fundingCycle.reservedRate(),\n      _totalTokens\n    );\n\n    // Set the tracker to be the new total supply.\n    _processedTokenTrackerOf[_projectId] = int256(_totalTokens + tokenCount);\n\n    // Get a reference to the project owner.\n    address _owner = projects.ownerOf(_projectId);\n\n    // Distribute tokens to splits and get a reference to the leftover amount to mint after all splits have gotten their share.\n    uint256 _leftoverTokenCount = tokenCount == 0\n      ? 0\n      : _distributeToReservedTokenSplitsOf(\n        _projectId,\n        _fundingCycle.configuration,\n        JBSplitsGroups.RESERVED_TOKENS,\n        tokenCount\n      );\n\n    // Mint any leftover tokens to the project owner.\n    if (_leftoverTokenCount > 0) tokenStore.mintFor(_owner, _projectId, _leftoverTokenCount, false);\n\n    emit DistributeReservedTokens(\n      _fundingCycle.configuration,\n      _fundingCycle.number,\n      _projectId,\n      _owner,\n      tokenCount,\n      _leftoverTokenCount,\n      _memo,\n      msg.sender\n    );\n  }\n\n  /**\n    @notice\n    Distribute tokens to the splits according to the specified funding cycle configuration.\n\n    @param _projectId The ID of the project for which reserved token splits are being distributed.\n    @param _domain The domain of the splits to distribute the reserved tokens between.\n    @param _group The group of the splits to distribute the reserved tokens between.\n    @param _amount The total amount of tokens to mint.\n\n    @return leftoverAmount If the splits percents dont add up to 100%, the leftover amount is returned.\n  */\n  function _distributeToReservedTokenSplitsOf(\n    uint256 _projectId,\n    uint256 _domain,\n    uint256 _group,\n    uint256 _amount\n  ) internal returns (uint256 leftoverAmount) {\n    // Set the leftover amount to the initial amount.\n    leftoverAmount = _amount;\n\n    // Get a reference to the project's reserved token splits.\n    JBSplit[] memory _splits = splitsStore.splitsOf(_projectId, _domain, _group);\n\n    //Transfer between all splits.\n    for (uint256 _i = 0; _i < _splits.length; _i++) {\n      // Get a reference to the split being iterated on.\n      JBSplit memory _split = _splits[_i];\n\n      // The amount to send towards the split.\n      uint256 _tokenCount = PRBMath.mulDiv(\n        _amount,\n        _split.percent,\n        JBConstants.SPLITS_TOTAL_PERCENT\n      );\n\n      // Mints tokens for the split if needed.\n      if (_tokenCount > 0) {\n        tokenStore.mintFor(\n          // If an allocator is set in the splits, set it as the beneficiary.\n          // Otherwise if a projectId is set in the split, set the project's owner as the beneficiary.\n          // If the split has a beneficiary send to the split's beneficiary. Otherwise send to the msg.sender.\n          _split.allocator != IJBSplitAllocator(address(0))\n            ? address(_split.allocator)\n            : _split.projectId != 0\n            ? projects.ownerOf(_split.projectId)\n            : _split.beneficiary != address(0)\n            ? _split.beneficiary\n            : msg.sender,\n          _projectId,\n          _tokenCount,\n          _split.preferClaimed\n        );\n\n        // If there's an allocator set, trigger its `allocate` function.\n        if (_split.allocator != IJBSplitAllocator(address(0)))\n          _split.allocator.allocate(\n            JBSplitAllocationData(\n              address(tokenStore.tokenOf(_projectId)),\n              _tokenCount,\n              18,\n              _projectId,\n              _group,\n              _split\n            )\n          );\n\n        // Subtract from the amount to be sent to the beneficiary.\n        leftoverAmount = leftoverAmount - _tokenCount;\n      }\n\n      emit DistributeToReservedTokenSplit(\n        _projectId,\n        _domain,\n        _group,\n        _split,\n        _tokenCount,\n        msg.sender\n      );\n    }\n  }\n\n  /**\n    @notice\n    Configures a funding cycle and stores information pertinent to the configuration.\n\n    @param _projectId The ID of the project whose funding cycles are being reconfigured.\n    @param _data Data that defines the funding cycle. These properties will remain fixed for the duration of the funding cycle.\n    @param _metadata Metadata specifying the controller specific params that a funding cycle can have. These properties will remain fixed for the duration of the funding cycle.\n    @param _mustStartAtOrAfter The time before which the configured funding cycle cannot start.\n    @param _groupedSplits An array of splits to set for any number of groups. \n    @param _fundAccessConstraints An array containing amounts that a project can use from its treasury for each payment terminal. Amounts are fixed point numbers using the same number of decimals as the accompanying terminal.\n\n    @return configuration The configuration of the funding cycle that was successfully reconfigured.\n  */\n  function _configure(\n    uint256 _projectId,\n    JBFundingCycleData calldata _data,\n    JBFundingCycleMetadata calldata _metadata,\n    uint256 _mustStartAtOrAfter,\n    JBGroupedSplits[] memory _groupedSplits,\n    JBFundAccessConstraints[] memory _fundAccessConstraints\n  ) internal returns (uint256) {\n    // Make sure the provided reserved rate is valid.\n    if (_metadata.reservedRate > JBConstants.MAX_RESERVED_RATE) revert INVALID_RESERVED_RATE();\n\n    // Make sure the provided redemption rate is valid.\n    if (_metadata.redemptionRate > JBConstants.MAX_REDEMPTION_RATE)\n      revert INVALID_REDEMPTION_RATE();\n\n    // Make sure the provided ballot redemption rate is valid.\n    if (_metadata.ballotRedemptionRate > JBConstants.MAX_REDEMPTION_RATE)\n      revert INVALID_BALLOT_REDEMPTION_RATE();\n\n    // Configure the funding cycle's properties.\n    JBFundingCycle memory _fundingCycle = fundingCycleStore.configureFor(\n      _projectId,\n      _data,\n      JBFundingCycleMetadataResolver.packFundingCycleMetadata(_metadata),\n      _mustStartAtOrAfter\n    );\n\n    // Set splits for the group.\n    splitsStore.set(_projectId, _fundingCycle.configuration, _groupedSplits);\n\n    // Set distribution limits if there are any.\n    for (uint256 _i; _i < _fundAccessConstraints.length; _i++) {\n      JBFundAccessConstraints memory _constraints = _fundAccessConstraints[_i];\n\n      // If distribution limit value is larger than 232 bits, revert.\n      if (_constraints.distributionLimit > type(uint232).max) revert INVALID_DISTRIBUTION_LIMIT();\n\n      // If distribution limit currency value is larger than 24 bits, revert.\n      if (_constraints.distributionLimitCurrency > type(uint24).max)\n        revert INVALID_DISTRIBUTION_LIMIT_CURRENCY();\n\n      // If overflow allowance value is larger than 232 bits, revert.\n      if (_constraints.overflowAllowance > type(uint232).max) revert INVALID_OVERFLOW_ALLOWANCE();\n\n      // If overflow allowance currency value is larger than 24 bits, revert.\n      if (_constraints.overflowAllowanceCurrency > type(uint24).max)\n        revert INVALID_OVERFLOW_ALLOWANCE_CURRENCY();\n\n      // Set the distribution limit if there is one.\n      if (_constraints.distributionLimit > 0)\n        _packedDistributionLimitDataOf[_projectId][_fundingCycle.configuration][\n          _constraints.terminal\n        ][_constraints.token] =\n          _constraints.distributionLimit |\n          (_constraints.distributionLimitCurrency << 232);\n\n      // Set the overflow allowance if there is one.\n      if (_constraints.overflowAllowance > 0)\n        _packedOverflowAllowanceDataOf[_projectId][_fundingCycle.configuration][\n          _constraints.terminal\n        ][_constraints.token] =\n          _constraints.overflowAllowance |\n          (_constraints.overflowAllowanceCurrency << 232);\n\n      emit SetFundAccessConstraints(\n        _fundingCycle.configuration,\n        _fundingCycle.number,\n        _projectId,\n        _constraints,\n        msg.sender\n      );\n    }\n\n    return _fundingCycle.configuration;\n  }\n\n  /**\n    @notice\n    Gets the amount of reserved tokens currently tracked for a project given a reserved rate.\n\n    @param _processedTokenTracker The tracker to make the calculation with.\n    @param _reservedRate The reserved rate to use to make the calculation.\n    @param _totalEligibleTokens The total amount to make the calculation with.\n\n    @return amount reserved token amount.\n  */\n  function _reservedTokenAmountFrom(\n    int256 _processedTokenTracker,\n    uint256 _reservedRate,\n    uint256 _totalEligibleTokens\n  ) internal pure returns (uint256) {\n    // Get a reference to the amount of tokens that are unprocessed.\n    uint256 _unprocessedTokenBalanceOf = _processedTokenTracker >= 0\n      ? _totalEligibleTokens - uint256(_processedTokenTracker)\n      : _totalEligibleTokens + uint256(-_processedTokenTracker);\n\n    // If there are no unprocessed tokens, return.\n    if (_unprocessedTokenBalanceOf == 0) return 0;\n\n    // If all tokens are reserved, return the full unprocessed amount.\n    if (_reservedRate == JBConstants.MAX_RESERVED_RATE) return _unprocessedTokenBalanceOf;\n\n    return\n      PRBMath.mulDiv(\n        _unprocessedTokenBalanceOf,\n        JBConstants.MAX_RESERVED_RATE,\n        JBConstants.MAX_RESERVED_RATE - _reservedRate\n      ) - _unprocessedTokenBalanceOf;\n  }\n}\n"
    },
    "contracts/abstract/JBOperatable.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBOperatable.sol';\n\n/** \n  @notice\n  Modifiers to allow access to functions based on the message sender's operator status.\n\n  @dev\n  Adheres to -\n  IJBOperatable: General interface for the methods in this contract that interact with the blockchain's state according to the protocol's rules.\n*/\nabstract contract JBOperatable is IJBOperatable {\n  //*********************************************************************//\n  // --------------------------- custom errors -------------------------- //\n  //*********************************************************************//\n  error UNAUTHORIZED();\n\n  //*********************************************************************//\n  // ---------------------------- modifiers ---------------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice\n    Only allows the speficied account or an operator of the account to proceed. \n\n    @param _account The account to check for.\n    @param _domain The domain namespace to look for an operator within. \n    @param _permissionIndex The index of the permission to check for. \n  */\n  modifier requirePermission(\n    address _account,\n    uint256 _domain,\n    uint256 _permissionIndex\n  ) {\n    _requirePermission(_account, _domain, _permissionIndex);\n    _;\n  }\n\n  /** \n    @notice\n    Only allows the speficied account, an operator of the account to proceed, or a truthy override flag. \n\n    @param _account The account to check for.\n    @param _domain The domain namespace to look for an operator within. \n    @param _permissionIndex The index of the permission to check for. \n    @param _override A condition to force allowance for.\n  */\n  modifier requirePermissionAllowingOverride(\n    address _account,\n    uint256 _domain,\n    uint256 _permissionIndex,\n    bool _override\n  ) {\n    _requirePermissionAllowingOverride(_account, _domain, _permissionIndex, _override);\n    _;\n  }\n\n  //*********************************************************************//\n  // ---------------- public immutable stored properties --------------- //\n  //*********************************************************************//\n\n  /** \n    @notice \n    A contract storing operator assignments.\n  */\n  IJBOperatorStore public immutable override operatorStore;\n\n  //*********************************************************************//\n  // -------------------------- constructor ---------------------------- //\n  //*********************************************************************//\n\n  /** \n    @param _operatorStore A contract storing operator assignments.\n  */\n  constructor(IJBOperatorStore _operatorStore) {\n    operatorStore = _operatorStore;\n  }\n\n  //*********************************************************************//\n  // -------------------------- internal views ------------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice\n    Require the message sender is either the account or has the specified permission.\n\n    @param _account The account to allow.\n    @param _domain The domain namespace within which the permission index will be checked.\n    @param _permissionIndex The permission index that an operator must have within the specified domain to be allowed.\n  */\n  function _requirePermission(\n    address _account,\n    uint256 _domain,\n    uint256 _permissionIndex\n  ) internal view {\n    if (\n      msg.sender != _account &&\n      !operatorStore.hasPermission(msg.sender, _account, _domain, _permissionIndex) &&\n      !operatorStore.hasPermission(msg.sender, _account, 0, _permissionIndex)\n    ) revert UNAUTHORIZED();\n  }\n\n  /** \n    @notice\n    Require the message sender is either the account, has the specified permission, or the override condition is true.\n\n    @param _account The account to allow.\n    @param _domain The domain namespace within which the permission index will be checked.\n    @param _domain The permission index that an operator must have within the specified domain to be allowed.\n    @param _override The override condition to allow.\n  */\n  function _requirePermissionAllowingOverride(\n    address _account,\n    uint256 _domain,\n    uint256 _permissionIndex,\n    bool _override\n  ) internal view {\n    if (\n      !_override &&\n      msg.sender != _account &&\n      !operatorStore.hasPermission(msg.sender, _account, _domain, _permissionIndex) &&\n      !operatorStore.hasPermission(msg.sender, _account, 0, _permissionIndex)\n    ) revert UNAUTHORIZED();\n  }\n}\n"
    },
    "contracts/enums/JBBallotState.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nenum JBBallotState {\n  Active,\n  Approved,\n  Failed\n}\n"
    },
    "contracts/interfaces/IJBController.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/IERC165.sol';\nimport './../structs/JBFundAccessConstraints.sol';\nimport './../structs/JBFundingCycleData.sol';\nimport './../structs/JBFundingCycleMetadata.sol';\nimport './../structs/JBGroupedSplits.sol';\nimport './../structs/JBProjectMetadata.sol';\nimport './IJBDirectory.sol';\nimport './IJBFundingCycleStore.sol';\nimport './IJBMigratable.sol';\nimport './IJBPaymentTerminal.sol';\nimport './IJBSplitsStore.sol';\nimport './IJBToken.sol';\nimport './IJBTokenStore.sol';\n\ninterface IJBController is IERC165 {\n  event LaunchProject(uint256 configuration, uint256 projectId, string memo, address caller);\n\n  event LaunchFundingCycles(uint256 configuration, uint256 projectId, string memo, address caller);\n\n  event ReconfigureFundingCycles(\n    uint256 configuration,\n    uint256 projectId,\n    string memo,\n    address caller\n  );\n\n  event SetFundAccessConstraints(\n    uint256 indexed fundingCycleConfiguration,\n    uint256 indexed fundingCycleNumber,\n    uint256 indexed projectId,\n    JBFundAccessConstraints constraints,\n    address caller\n  );\n\n  event DistributeReservedTokens(\n    uint256 indexed fundingCycleConfiguration,\n    uint256 indexed fundingCycleNumber,\n    uint256 indexed projectId,\n    address beneficiary,\n    uint256 tokenCount,\n    uint256 beneficiaryTokenCount,\n    string memo,\n    address caller\n  );\n\n  event DistributeToReservedTokenSplit(\n    uint256 indexed projectId,\n    uint256 indexed domain,\n    uint256 indexed group,\n    JBSplit split,\n    uint256 tokenCount,\n    address caller\n  );\n\n  event MintTokens(\n    address indexed beneficiary,\n    uint256 indexed projectId,\n    uint256 tokenCount,\n    uint256 beneficiaryTokenCount,\n    string memo,\n    uint256 reservedRate,\n    address caller\n  );\n\n  event BurnTokens(\n    address indexed holder,\n    uint256 indexed projectId,\n    uint256 tokenCount,\n    string memo,\n    address caller\n  );\n\n  event Migrate(uint256 indexed projectId, IJBMigratable to, address caller);\n\n  event PrepMigration(uint256 indexed projectId, address from, address caller);\n\n  function projects() external view returns (IJBProjects);\n\n  function fundingCycleStore() external view returns (IJBFundingCycleStore);\n\n  function tokenStore() external view returns (IJBTokenStore);\n\n  function splitsStore() external view returns (IJBSplitsStore);\n\n  function directory() external view returns (IJBDirectory);\n\n  function reservedTokenBalanceOf(uint256 _projectId, uint256 _reservedRate)\n    external\n    view\n    returns (uint256);\n\n  function distributionLimitOf(\n    uint256 _projectId,\n    uint256 _configuration,\n    IJBPaymentTerminal _terminal,\n    address _token\n  ) external view returns (uint256 distributionLimit, uint256 distributionLimitCurrency);\n\n  function overflowAllowanceOf(\n    uint256 _projectId,\n    uint256 _configuration,\n    IJBPaymentTerminal _terminal,\n    address _token\n  ) external view returns (uint256 overflowAllowance, uint256 overflowAllowanceCurrency);\n\n  function totalOutstandingTokensOf(uint256 _projectId, uint256 _reservedRate)\n    external\n    view\n    returns (uint256);\n\n  function getFundingCycleOf(uint256 _projectId, uint256 _configuration)\n    external\n    view\n    returns (JBFundingCycle memory fundingCycle, JBFundingCycleMetadata memory metadata);\n\n  function latestConfiguredFundingCycleOf(uint256 _projectId)\n    external\n    view\n    returns (\n      JBFundingCycle memory,\n      JBFundingCycleMetadata memory metadata,\n      JBBallotState\n    );\n\n  function currentFundingCycleOf(uint256 _projectId)\n    external\n    view\n    returns (JBFundingCycle memory fundingCycle, JBFundingCycleMetadata memory metadata);\n\n  function queuedFundingCycleOf(uint256 _projectId)\n    external\n    view\n    returns (JBFundingCycle memory fundingCycle, JBFundingCycleMetadata memory metadata);\n\n  function launchProjectFor(\n    address _owner,\n    JBProjectMetadata calldata _projectMetadata,\n    JBFundingCycleData calldata _data,\n    JBFundingCycleMetadata calldata _metadata,\n    uint256 _mustStartAtOrAfter,\n    JBGroupedSplits[] memory _groupedSplits,\n    JBFundAccessConstraints[] memory _fundAccessConstraints,\n    IJBPaymentTerminal[] memory _terminals,\n    string calldata _memo\n  ) external returns (uint256 projectId);\n\n  function launchFundingCyclesFor(\n    uint256 _projectId,\n    JBFundingCycleData calldata _data,\n    JBFundingCycleMetadata calldata _metadata,\n    uint256 _mustStartAtOrAfter,\n    JBGroupedSplits[] memory _groupedSplits,\n    JBFundAccessConstraints[] memory _fundAccessConstraints,\n    IJBPaymentTerminal[] memory _terminals,\n    string calldata _memo\n  ) external returns (uint256 configuration);\n\n  function reconfigureFundingCyclesOf(\n    uint256 _projectId,\n    JBFundingCycleData calldata _data,\n    JBFundingCycleMetadata calldata _metadata,\n    uint256 _mustStartAtOrAfter,\n    JBGroupedSplits[] memory _groupedSplits,\n    JBFundAccessConstraints[] memory _fundAccessConstraints,\n    string calldata _memo\n  ) external returns (uint256);\n\n  function issueTokenFor(\n    uint256 _projectId,\n    string calldata _name,\n    string calldata _symbol\n  ) external returns (IJBToken token);\n\n  function changeTokenOf(\n    uint256 _projectId,\n    IJBToken _token,\n    address _newOwner\n  ) external;\n\n  function mintTokensOf(\n    uint256 _projectId,\n    uint256 _tokenCount,\n    address _beneficiary,\n    string calldata _memo,\n    bool _preferClaimedTokens,\n    bool _useReservedRate\n  ) external returns (uint256 beneficiaryTokenCount);\n\n  function burnTokensOf(\n    address _holder,\n    uint256 _projectId,\n    uint256 _tokenCount,\n    string calldata _memo,\n    bool _preferClaimedTokens\n  ) external;\n\n  function distributeReservedTokensOf(uint256 _projectId, string memory _memo)\n    external\n    returns (uint256);\n\n  function migrate(uint256 _projectId, IJBMigratable _to) external;\n}\n"
    },
    "contracts/interfaces/IJBDirectory.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './IJBFundingCycleStore.sol';\nimport './IJBPaymentTerminal.sol';\nimport './IJBProjects.sol';\n\ninterface IJBDirectory {\n  event SetController(uint256 indexed projectId, address indexed controller, address caller);\n\n  event AddTerminal(uint256 indexed projectId, IJBPaymentTerminal indexed terminal, address caller);\n\n  event SetTerminals(uint256 indexed projectId, IJBPaymentTerminal[] terminals, address caller);\n\n  event SetPrimaryTerminal(\n    uint256 indexed projectId,\n    address indexed token,\n    IJBPaymentTerminal indexed terminal,\n    address caller\n  );\n\n  event SetIsAllowedToSetFirstController(address indexed addr, bool indexed flag, address caller);\n\n  function projects() external view returns (IJBProjects);\n\n  function fundingCycleStore() external view returns (IJBFundingCycleStore);\n\n  function controllerOf(uint256 _projectId) external view returns (address);\n\n  function isAllowedToSetFirstController(address _address) external view returns (bool);\n\n  function terminalsOf(uint256 _projectId) external view returns (IJBPaymentTerminal[] memory);\n\n  function isTerminalOf(uint256 _projectId, IJBPaymentTerminal _terminal)\n    external\n    view\n    returns (bool);\n\n  function primaryTerminalOf(uint256 _projectId, address _token)\n    external\n    view\n    returns (IJBPaymentTerminal);\n\n  function setControllerOf(uint256 _projectId, address _controller) external;\n\n  function setTerminalsOf(uint256 _projectId, IJBPaymentTerminal[] calldata _terminals) external;\n\n  function setPrimaryTerminalOf(\n    uint256 _projectId,\n    address _token,\n    IJBPaymentTerminal _terminal\n  ) external;\n\n  function setIsAllowedToSetFirstController(address _address, bool _flag) external;\n}\n"
    },
    "contracts/interfaces/IJBFundingCycleBallot.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/IERC165.sol';\nimport './../enums/JBBallotState.sol';\nimport './IJBFundingCycleStore.sol';\n\ninterface IJBFundingCycleBallot is IERC165 {\n  function duration() external view returns (uint256);\n\n  function stateOf(\n    uint256 _projectId,\n    uint256 _configuration,\n    uint256 _start\n  ) external view returns (JBBallotState);\n}\n"
    },
    "contracts/interfaces/IJBFundingCycleDataSource.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/IERC165.sol';\nimport './../structs/JBPayParamsData.sol';\nimport './../structs/JBRedeemParamsData.sol';\nimport './IJBFundingCycleStore.sol';\nimport './IJBPayDelegate.sol';\nimport './IJBRedemptionDelegate.sol';\n\ninterface IJBFundingCycleDataSource is IERC165 {\n  function payParams(JBPayParamsData calldata _data)\n    external\n    returns (\n      uint256 weight,\n      string memory memo,\n      IJBPayDelegate delegate\n    );\n\n  function redeemParams(JBRedeemParamsData calldata _data)\n    external\n    returns (\n      uint256 reclaimAmount,\n      string memory memo,\n      IJBRedemptionDelegate delegate\n    );\n}\n"
    },
    "contracts/interfaces/IJBFundingCycleStore.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../enums/JBBallotState.sol';\nimport './../structs/JBFundingCycle.sol';\nimport './../structs/JBFundingCycleData.sol';\n\ninterface IJBFundingCycleStore {\n  event Configure(\n    uint256 indexed configuration,\n    uint256 indexed projectId,\n    JBFundingCycleData data,\n    uint256 metadata,\n    uint256 mustStartAtOrAfter,\n    address caller\n  );\n\n  event Init(uint256 indexed configuration, uint256 indexed projectId, uint256 indexed basedOn);\n\n  function latestConfigurationOf(uint256 _projectId) external view returns (uint256);\n\n  function get(uint256 _projectId, uint256 _configuration)\n    external\n    view\n    returns (JBFundingCycle memory);\n\n  function latestConfiguredOf(uint256 _projectId)\n    external\n    view\n    returns (JBFundingCycle memory fundingCycle, JBBallotState ballotState);\n\n  function queuedOf(uint256 _projectId) external view returns (JBFundingCycle memory fundingCycle);\n\n  function currentOf(uint256 _projectId) external view returns (JBFundingCycle memory fundingCycle);\n\n  function currentBallotStateOf(uint256 _projectId) external view returns (JBBallotState);\n\n  function configureFor(\n    uint256 _projectId,\n    JBFundingCycleData calldata _data,\n    uint256 _metadata,\n    uint256 _mustStartAtOrAfter\n  ) external returns (JBFundingCycle memory fundingCycle);\n}\n"
    },
    "contracts/interfaces/IJBMigratable.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\ninterface IJBMigratable {\n  function prepForMigrationOf(uint256 _projectId, address _from) external;\n}\n"
    },
    "contracts/interfaces/IJBOperatable.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './IJBOperatorStore.sol';\n\ninterface IJBOperatable {\n  function operatorStore() external view returns (IJBOperatorStore);\n}\n"
    },
    "contracts/interfaces/IJBOperatorStore.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../structs/JBOperatorData.sol';\n\ninterface IJBOperatorStore {\n  event SetOperator(\n    address indexed operator,\n    address indexed account,\n    uint256 indexed domain,\n    uint256[] permissionIndexes,\n    uint256 packed\n  );\n\n  function permissionsOf(\n    address _operator,\n    address _account,\n    uint256 _domain\n  ) external view returns (uint256);\n\n  function hasPermission(\n    address _operator,\n    address _account,\n    uint256 _domain,\n    uint256 _permissionIndex\n  ) external view returns (bool);\n\n  function hasPermissions(\n    address _operator,\n    address _account,\n    uint256 _domain,\n    uint256[] calldata _permissionIndexes\n  ) external view returns (bool);\n\n  function setOperator(JBOperatorData calldata _operatorData) external;\n\n  function setOperators(JBOperatorData[] calldata _operatorData) external;\n}\n"
    },
    "contracts/interfaces/IJBPayDelegate.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/IERC165.sol';\nimport './../structs/JBDidPayData.sol';\n\ninterface IJBPayDelegate is IERC165 {\n  function didPay(JBDidPayData calldata _data) external;\n}\n"
    },
    "contracts/interfaces/IJBPaymentTerminal.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/IERC165.sol';\n\ninterface IJBPaymentTerminal is IERC165 {\n  function acceptsToken(address _token, uint256 _projectId) external view returns (bool);\n\n  function currencyForToken(address _token) external view returns (uint256);\n\n  function decimalsForToken(address _token) external view returns (uint256);\n\n  // Return value must be a fixed point number with 18 decimals.\n  function currentEthOverflowOf(uint256 _projectId) external view returns (uint256);\n\n  function pay(\n    uint256 _projectId,\n    uint256 _amount,\n    address _token,\n    address _beneficiary,\n    uint256 _minReturnedTokens,\n    bool _preferClaimedTokens,\n    string calldata _memo,\n    bytes calldata _metadata\n  ) external payable returns (uint256 beneficiaryTokenCount);\n\n  function addToBalanceOf(\n    uint256 _projectId,\n    uint256 _amount,\n    address _token,\n    string calldata _memo,\n    bytes calldata _metadata\n  ) external payable;\n}\n"
    },
    "contracts/interfaces/IJBProjects.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/token/ERC721/IERC721.sol';\nimport './../structs/JBProjectMetadata.sol';\nimport './IJBTokenUriResolver.sol';\n\ninterface IJBProjects is IERC721 {\n  event Create(\n    uint256 indexed projectId,\n    address indexed owner,\n    JBProjectMetadata metadata,\n    address caller\n  );\n\n  event SetMetadata(uint256 indexed projectId, JBProjectMetadata metadata, address caller);\n\n  event SetTokenUriResolver(IJBTokenUriResolver indexed resolver, address caller);\n\n  function count() external view returns (uint256);\n\n  function metadataContentOf(uint256 _projectId, uint256 _domain)\n    external\n    view\n    returns (string memory);\n\n  function tokenUriResolver() external view returns (IJBTokenUriResolver);\n\n  function createFor(address _owner, JBProjectMetadata calldata _metadata)\n    external\n    returns (uint256 projectId);\n\n  function setMetadataOf(uint256 _projectId, JBProjectMetadata calldata _metadata) external;\n\n  function setTokenUriResolver(IJBTokenUriResolver _newResolver) external;\n}\n"
    },
    "contracts/interfaces/IJBRedemptionDelegate.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/IERC165.sol';\nimport './../structs/JBDidRedeemData.sol';\n\ninterface IJBRedemptionDelegate is IERC165 {\n  function didRedeem(JBDidRedeemData calldata _data) external;\n}\n"
    },
    "contracts/interfaces/IJBSplitAllocator.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/IERC165.sol';\nimport '../structs/JBSplitAllocationData.sol';\n\ninterface IJBSplitAllocator is IERC165 {\n  function allocate(JBSplitAllocationData calldata _data) external payable;\n}\n"
    },
    "contracts/interfaces/IJBSplitsStore.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../structs/JBGroupedSplits.sol';\nimport './../structs/JBSplit.sol';\nimport './IJBDirectory.sol';\nimport './IJBProjects.sol';\n\ninterface IJBSplitsStore {\n  event SetSplit(\n    uint256 indexed projectId,\n    uint256 indexed domain,\n    uint256 indexed group,\n    JBSplit split,\n    address caller\n  );\n\n  function projects() external view returns (IJBProjects);\n\n  function directory() external view returns (IJBDirectory);\n\n  function splitsOf(\n    uint256 _projectId,\n    uint256 _domain,\n    uint256 _group\n  ) external view returns (JBSplit[] memory);\n\n  function set(\n    uint256 _projectId,\n    uint256 _domain,\n    JBGroupedSplits[] memory _groupedSplits\n  ) external;\n}\n"
    },
    "contracts/interfaces/IJBToken.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\ninterface IJBToken {\n  function decimals() external view returns (uint8);\n\n  function totalSupply(uint256 _projectId) external view returns (uint256);\n\n  function balanceOf(address _account, uint256 _projectId) external view returns (uint256);\n\n  function mint(\n    uint256 _projectId,\n    address _account,\n    uint256 _amount\n  ) external;\n\n  function burn(\n    uint256 _projectId,\n    address _account,\n    uint256 _amount\n  ) external;\n\n  function approve(\n    uint256,\n    address _spender,\n    uint256 _amount\n  ) external;\n\n  function transfer(\n    uint256 _projectId,\n    address _to,\n    uint256 _amount\n  ) external;\n\n  function transferFrom(\n    uint256 _projectId,\n    address _from,\n    address _to,\n    uint256 _amount\n  ) external;\n\n  function transferOwnership(uint256 _projectId, address _newOwner) external;\n}\n"
    },
    "contracts/interfaces/IJBTokenStore.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './IJBProjects.sol';\nimport './IJBToken.sol';\n\ninterface IJBTokenStore {\n  event Issue(\n    uint256 indexed projectId,\n    IJBToken indexed token,\n    string name,\n    string symbol,\n    address caller\n  );\n\n  event Mint(\n    address indexed holder,\n    uint256 indexed projectId,\n    uint256 amount,\n    bool tokensWereClaimed,\n    bool preferClaimedTokens,\n    address caller\n  );\n\n  event Burn(\n    address indexed holder,\n    uint256 indexed projectId,\n    uint256 amount,\n    uint256 initialUnclaimedBalance,\n    uint256 initialClaimedBalance,\n    bool preferClaimedTokens,\n    address caller\n  );\n\n  event Claim(\n    address indexed holder,\n    uint256 indexed projectId,\n    uint256 initialUnclaimedBalance,\n    uint256 amount,\n    address caller\n  );\n\n  event ShouldRequireClaim(uint256 indexed projectId, bool indexed flag, address caller);\n\n  event Change(\n    uint256 indexed projectId,\n    IJBToken indexed newToken,\n    IJBToken indexed oldToken,\n    address owner,\n    address caller\n  );\n\n  event Transfer(\n    address indexed holder,\n    uint256 indexed projectId,\n    address indexed recipient,\n    uint256 amount,\n    address caller\n  );\n\n  function tokenOf(uint256 _projectId) external view returns (IJBToken);\n\n  function projectOf(IJBToken _token) external view returns (uint256);\n\n  function projects() external view returns (IJBProjects);\n\n  function unclaimedBalanceOf(address _holder, uint256 _projectId) external view returns (uint256);\n\n  function unclaimedTotalSupplyOf(uint256 _projectId) external view returns (uint256);\n\n  function totalSupplyOf(uint256 _projectId) external view returns (uint256);\n\n  function balanceOf(address _holder, uint256 _projectId) external view returns (uint256 _result);\n\n  function requireClaimFor(uint256 _projectId) external view returns (bool);\n\n  function issueFor(\n    uint256 _projectId,\n    string calldata _name,\n    string calldata _symbol\n  ) external returns (IJBToken token);\n\n  function changeFor(\n    uint256 _projectId,\n    IJBToken _token,\n    address _newOwner\n  ) external returns (IJBToken oldToken);\n\n  function burnFrom(\n    address _holder,\n    uint256 _projectId,\n    uint256 _amount,\n    bool _preferClaimedTokens\n  ) external;\n\n  function mintFor(\n    address _holder,\n    uint256 _projectId,\n    uint256 _amount,\n    bool _preferClaimedTokens\n  ) external;\n\n  function shouldRequireClaimingFor(uint256 _projectId, bool _flag) external;\n\n  function claimFor(\n    address _holder,\n    uint256 _projectId,\n    uint256 _amount\n  ) external;\n\n  function transferFrom(\n    address _holder,\n    uint256 _projectId,\n    address _recipient,\n    uint256 _amount\n  ) external;\n}\n"
    },
    "contracts/interfaces/IJBTokenUriResolver.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\ninterface IJBTokenUriResolver {\n  function getUri(uint256 _projectId) external view returns (string memory tokenUri);\n}\n"
    },
    "contracts/libraries/JBConstants.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\n/**\n  @notice\n  Global constants used across Juicebox contracts.\n*/\nlibrary JBConstants {\n  uint256 public constant MAX_RESERVED_RATE = 10000;\n  uint256 public constant MAX_REDEMPTION_RATE = 10000;\n  uint256 public constant MAX_DISCOUNT_RATE = 1000000000;\n  uint256 public constant SPLITS_TOTAL_PERCENT = 1000000000;\n  uint256 public constant MAX_FEE = 1000000000;\n  uint256 public constant MAX_FEE_DISCOUNT = 1000000000;\n}\n"
    },
    "contracts/libraries/JBFundingCycleMetadataResolver.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBFundingCycleDataSource.sol';\nimport './../structs/JBFundingCycleMetadata.sol';\nimport './../structs/JBGlobalFundingCycleMetadata.sol';\nimport './JBConstants.sol';\nimport './JBGlobalFundingCycleMetadataResolver.sol';\n\nlibrary JBFundingCycleMetadataResolver {\n  function global(JBFundingCycle memory _fundingCycle)\n    internal\n    pure\n    returns (JBGlobalFundingCycleMetadata memory metadata)\n  {\n    return JBGlobalFundingCycleMetadataResolver.expandMetadata(uint8(_fundingCycle.metadata >> 8));\n  }\n\n  function reservedRate(JBFundingCycle memory _fundingCycle) internal pure returns (uint256) {\n    return uint256(uint16(_fundingCycle.metadata >> 24));\n  }\n\n  function redemptionRate(JBFundingCycle memory _fundingCycle) internal pure returns (uint256) {\n    // Redemption rate is a number 0-10000. It's inverse was stored so the most common case of 100% results in no storage needs.\n    return JBConstants.MAX_REDEMPTION_RATE - uint256(uint16(_fundingCycle.metadata >> 40));\n  }\n\n  function ballotRedemptionRate(JBFundingCycle memory _fundingCycle)\n    internal\n    pure\n    returns (uint256)\n  {\n    // Redemption rate is a number 0-10000. It's inverse was stored so the most common case of 100% results in no storage needs.\n    return JBConstants.MAX_REDEMPTION_RATE - uint256(uint16(_fundingCycle.metadata >> 56));\n  }\n\n  function payPaused(JBFundingCycle memory _fundingCycle) internal pure returns (bool) {\n    return ((_fundingCycle.metadata >> 72) & 1) == 1;\n  }\n\n  function distributionsPaused(JBFundingCycle memory _fundingCycle) internal pure returns (bool) {\n    return ((_fundingCycle.metadata >> 73) & 1) == 1;\n  }\n\n  function redeemPaused(JBFundingCycle memory _fundingCycle) internal pure returns (bool) {\n    return ((_fundingCycle.metadata >> 74) & 1) == 1;\n  }\n\n  function burnPaused(JBFundingCycle memory _fundingCycle) internal pure returns (bool) {\n    return ((_fundingCycle.metadata >> 75) & 1) == 1;\n  }\n\n  function mintingAllowed(JBFundingCycle memory _fundingCycle) internal pure returns (bool) {\n    return ((_fundingCycle.metadata >> 76) & 1) == 1;\n  }\n\n  function changeTokenAllowed(JBFundingCycle memory _fundingCycle) internal pure returns (bool) {\n    return ((_fundingCycle.metadata >> 77) & 1) == 1;\n  }\n\n  function terminalMigrationAllowed(JBFundingCycle memory _fundingCycle)\n    internal\n    pure\n    returns (bool)\n  {\n    return ((_fundingCycle.metadata >> 78) & 1) == 1;\n  }\n\n  function controllerMigrationAllowed(JBFundingCycle memory _fundingCycle)\n    internal\n    pure\n    returns (bool)\n  {\n    return ((_fundingCycle.metadata >> 79) & 1) == 1;\n  }\n\n  function shouldHoldFees(JBFundingCycle memory _fundingCycle) internal pure returns (bool) {\n    return ((_fundingCycle.metadata >> 80) & 1) == 1;\n  }\n\n  function useTotalOverflowForRedemptions(JBFundingCycle memory _fundingCycle)\n    internal\n    pure\n    returns (bool)\n  {\n    return ((_fundingCycle.metadata >> 81) & 1) == 1;\n  }\n\n  function useDataSourceForPay(JBFundingCycle memory _fundingCycle) internal pure returns (bool) {\n    return (_fundingCycle.metadata >> 82) & 1 == 1;\n  }\n\n  function useDataSourceForRedeem(JBFundingCycle memory _fundingCycle)\n    internal\n    pure\n    returns (bool)\n  {\n    return (_fundingCycle.metadata >> 83) & 1 == 1;\n  }\n\n  function dataSource(JBFundingCycle memory _fundingCycle) internal pure returns (address) {\n    return address(uint160(_fundingCycle.metadata >> 84));\n  }\n\n  /**\n    @notice\n    Pack the funding cycle metadata.\n\n    @param _metadata The metadata to validate and pack.\n\n    @return packed The packed uint256 of all metadata params. The first 8 bits specify the version.\n  */\n  function packFundingCycleMetadata(JBFundingCycleMetadata memory _metadata)\n    internal\n    pure\n    returns (uint256 packed)\n  {\n    // version 1 in the bits 0-7 (8 bits).\n    packed = 1;\n    // global metadta in bits 8-23 (16 bits).\n    packed |=\n      JBGlobalFundingCycleMetadataResolver.packFundingCycleGlobalMetadata(_metadata.global) <<\n      8;\n    // reserved rate in bits 24-39 (16 bits).\n    packed |= _metadata.reservedRate << 24;\n    // redemption rate in bits 40-55 (16 bits).\n    // redemption rate is a number 0-10000. Store the reverse so the most common case of 100% results in no storage needs.\n    packed |= (JBConstants.MAX_REDEMPTION_RATE - _metadata.redemptionRate) << 40;\n    // ballot redemption rate rate in bits 56-71 (16 bits).\n    // ballot redemption rate is a number 0-10000. Store the reverse so the most common case of 100% results in no storage needs.\n    packed |= (JBConstants.MAX_REDEMPTION_RATE - _metadata.ballotRedemptionRate) << 56;\n    // pause pay in bit 72.\n    if (_metadata.pausePay) packed |= 1 << 72;\n    // pause tap in bit 73.\n    if (_metadata.pauseDistributions) packed |= 1 << 73;\n    // pause redeem in bit 74.\n    if (_metadata.pauseRedeem) packed |= 1 << 74;\n    // pause burn in bit 75.\n    if (_metadata.pauseBurn) packed |= 1 << 75;\n    // allow minting in bit 76.\n    if (_metadata.allowMinting) packed |= 1 << 76;\n    // allow change token in bit 77.\n    if (_metadata.allowChangeToken) packed |= 1 << 77;\n    // allow terminal migration in bit 78.\n    if (_metadata.allowTerminalMigration) packed |= 1 << 78;\n    // allow controller migration in bit 79.\n    if (_metadata.allowControllerMigration) packed |= 1 << 79;\n    // hold fees in bit 80.\n    if (_metadata.holdFees) packed |= 1 << 80;\n    // useTotalOverflowForRedemptions in bit 81.\n    if (_metadata.useTotalOverflowForRedemptions) packed |= 1 << 81;\n    // use pay data source in bit 82.\n    if (_metadata.useDataSourceForPay) packed |= 1 << 82;\n    // use redeem data source in bit 83.\n    if (_metadata.useDataSourceForRedeem) packed |= 1 << 83;\n    // data source address in bits 84-243.\n    packed |= uint256(uint160(address(_metadata.dataSource))) << 84;\n  }\n\n  /**\n    @notice\n    Expand the funding cycle metadata.\n\n    @param _fundingCycle The funding cycle having its metadata expanded.\n\n    @return metadata The metadata object.\n  */\n  function expandMetadata(JBFundingCycle memory _fundingCycle)\n    internal\n    pure\n    returns (JBFundingCycleMetadata memory metadata)\n  {\n    return\n      JBFundingCycleMetadata(\n        global(_fundingCycle),\n        reservedRate(_fundingCycle),\n        redemptionRate(_fundingCycle),\n        ballotRedemptionRate(_fundingCycle),\n        payPaused(_fundingCycle),\n        distributionsPaused(_fundingCycle),\n        redeemPaused(_fundingCycle),\n        burnPaused(_fundingCycle),\n        mintingAllowed(_fundingCycle),\n        changeTokenAllowed(_fundingCycle),\n        terminalMigrationAllowed(_fundingCycle),\n        controllerMigrationAllowed(_fundingCycle),\n        shouldHoldFees(_fundingCycle),\n        useTotalOverflowForRedemptions(_fundingCycle),\n        useDataSourceForPay(_fundingCycle),\n        useDataSourceForRedeem(_fundingCycle),\n        dataSource(_fundingCycle)\n      );\n  }\n}\n"
    },
    "contracts/libraries/JBGlobalFundingCycleMetadataResolver.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBFundingCycleDataSource.sol';\nimport './../structs/JBFundingCycleMetadata.sol';\nimport './JBConstants.sol';\n\nlibrary JBGlobalFundingCycleMetadataResolver {\n  function setTerminalsAllowed(uint8 _data) internal pure returns (bool) {\n    return (_data & 1) == 1;\n  }\n\n  function setControllerAllowed(uint8 _data) internal pure returns (bool) {\n    return ((_data >> 1) & 1) == 1;\n  }\n\n  /**\n    @notice\n    Pack the global funding cycle metadata.\n\n    @param _metadata The metadata to validate and pack.\n\n    @return packed The packed uint256 of all global metadata params. The first 8 bits specify the version.\n  */\n  function packFundingCycleGlobalMetadata(JBGlobalFundingCycleMetadata memory _metadata)\n    internal\n    pure\n    returns (uint256 packed)\n  {\n    // allow set terminals in bit 0.\n    if (_metadata.allowSetTerminals) packed |= 1;\n    // allow set controller in bit 1.\n    if (_metadata.allowSetController) packed |= 1 << 1;\n  }\n\n  /**\n    @notice\n    Expand the global funding cycle metadata.\n\n    @param _packedMetadata The packed metadata to expand.\n\n    @return metadata The global metadata object.\n  */\n  function expandMetadata(uint8 _packedMetadata)\n    internal\n    pure\n    returns (JBGlobalFundingCycleMetadata memory metadata)\n  {\n    return\n      JBGlobalFundingCycleMetadata(\n        setTerminalsAllowed(_packedMetadata),\n        setControllerAllowed(_packedMetadata)\n      );\n  }\n}\n"
    },
    "contracts/libraries/JBOperations.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nlibrary JBOperations {\n  uint256 public constant RECONFIGURE = 1;\n  uint256 public constant REDEEM = 2;\n  uint256 public constant MIGRATE_CONTROLLER = 3;\n  uint256 public constant MIGRATE_TERMINAL = 4;\n  uint256 public constant PROCESS_FEES = 5;\n  uint256 public constant SET_METADATA = 6;\n  uint256 public constant ISSUE = 7;\n  uint256 public constant CHANGE_TOKEN = 8;\n  uint256 public constant MINT = 9;\n  uint256 public constant BURN = 10;\n  uint256 public constant CLAIM = 11;\n  uint256 public constant TRANSFER = 12;\n  uint256 public constant REQUIRE_CLAIM = 13;\n  uint256 public constant SET_CONTROLLER = 14;\n  uint256 public constant SET_TERMINALS = 15;\n  uint256 public constant SET_PRIMARY_TERMINAL = 16;\n  uint256 public constant USE_ALLOWANCE = 17;\n  uint256 public constant SET_SPLITS = 18;\n}\n"
    },
    "contracts/libraries/JBSplitsGroups.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nlibrary JBSplitsGroups {\n  uint256 public constant ETH_PAYOUT = 1;\n  uint256 public constant RESERVED_TOKENS = 2;\n}\n"
    },
    "contracts/structs/JBDidPayData.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './JBTokenAmount.sol';\n\n/** \n  @member payer The address from which the payment originated.\n  @member projectId The ID of the project for which the payment was made.\n  @member currentFundingCycleConfiguration The configuration of the funding cycle during which the payment is being made.\n  @member amount The amount of the payment. Includes the token being paid, the value, the number of decimals included, and the currency of the amount.\n  @member projectTokenCount The number of project tokens minted for the beneficiary.\n  @member beneficiary The address to which the tokens were minted.\n  @member preferClaimedTokens A flag indicating whether the request prefered to mint project tokens into the beneficiaries wallet rather than leaving them unclaimed. This is only possible if the project has an attached token contract.\n  @member memo The memo that is being emitted alongside the payment.\n  @member metadata Extra data to send to the delegate.\n*/\nstruct JBDidPayData {\n  address payer;\n  uint256 projectId;\n  uint256 currentFundingCycleConfiguration;\n  JBTokenAmount amount;\n  uint256 projectTokenCount;\n  address beneficiary;\n  bool preferClaimedTokens;\n  string memo;\n  bytes metadata;\n}\n"
    },
    "contracts/structs/JBDidRedeemData.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './JBTokenAmount.sol';\n\n/** \n  @member holder The holder of the tokens being redeemed.\n  @member projectId The ID of the project with which the redeemed tokens are associated.\n  @member currentFundingCycleConfiguration The configuration of the funding cycle during which the redemption is being made.\n  @member projectTokenCount The number of project tokens being redeemed.\n  @member reclaimedAmount The amount reclaimed from the treasury. Includes the token being reclaimed, the value, the number of decimals included, and the currency of the amount.\n  @member beneficiary The address to which the reclaimed amount will be sent.\n  @member memo The memo that is being emitted alongside the redemption.\n  @member metadata Extra data to send to the delegate.\n*/\nstruct JBDidRedeemData {\n  address holder;\n  uint256 projectId;\n  uint256 currentFundingCycleConfiguration;\n  uint256 projectTokenCount;\n  JBTokenAmount reclaimedAmount;\n  address payable beneficiary;\n  string memo;\n  bytes metadata;\n}\n"
    },
    "contracts/structs/JBFundAccessConstraints.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBPaymentTerminal.sol';\n\n/** \n  @member terminal The terminal within which the distribution limit and the overflow allowance applies.\n  @member token The token for which the fund access constraints apply.\n  @member distributionLimit The amount of the distribution limit, as a fixed point number with the same number of decimals as the terminal within which the limit applies.\n  @member distributionLimitCurrency The currency of the distribution limit.\n  @member overflowAllowance The amount of the allowance, as a fixed point number with the same number of decimals as the terminal within which the allowance applies.\n  @member overflowAllowanceCurrency The currency of the overflow allowance.\n*/\nstruct JBFundAccessConstraints {\n  IJBPaymentTerminal terminal;\n  address token;\n  uint256 distributionLimit;\n  uint256 distributionLimitCurrency;\n  uint256 overflowAllowance;\n  uint256 overflowAllowanceCurrency;\n}\n"
    },
    "contracts/structs/JBFundingCycle.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBFundingCycleBallot.sol';\n\n/** \n  @member number The funding cycle number for the cycle's project. Each funding cycle has a number that is an increment of the cycle that directly preceded it. Each project's first funding cycle has a number of 1.\n  @member configuration The timestamp when the parameters for this funding cycle were configured. This value will stay the same for subsequent funding cycles that roll over from an originally configured cycle.\n  @member basedOn The `configuration` of the funding cycle that was active when this cycle was created.\n  @member start The timestamp marking the moment from which the funding cycle is considered active. It is a unix timestamp measured in seconds.\n  @member duration The number of seconds the funding cycle lasts for, after which a new funding cycle will start. A duration of 0 means that the funding cycle will stay active until the project owner explicitly issues a reconfiguration, at which point a new funding cycle will immediately start with the updated properties. If the duration is greater than 0, a project owner cannot make changes to a funding cycle's parameters while it is active  any proposed changes will apply to the subsequent cycle. If no changes are proposed, a funding cycle rolls over to another one with the same properties but new `start` timestamp and a discounted `weight`.\n  @member weight A fixed point number with 18 decimals that contracts can use to base arbitrary calculations on. For example, payment terminals can use this to determine how many tokens should be minted when a payment is received.\n  @member discountRate A percent by how much the `weight` of the subsequent funding cycle should be reduced, if the project owner hasn't configured the subsequent funding cycle with an explicit `weight`. If it's 0, each funding cycle will have equal weight. If the number is 90%, the next funding cycle will have a 10% smaller weight. This weight is out of `JBConstants.MAX_DISCOUNT_RATE`.\n  @member ballot An address of a contract that says whether a proposed reconfiguration should be accepted or rejected. It can be used to create rules around how a project owner can change funding cycle parameters over time.\n  @member metadata Extra data that can be associated with a funding cycle.\n*/\nstruct JBFundingCycle {\n  uint256 number;\n  uint256 configuration;\n  uint256 basedOn;\n  uint256 start;\n  uint256 duration;\n  uint256 weight;\n  uint256 discountRate;\n  IJBFundingCycleBallot ballot;\n  uint256 metadata;\n}\n"
    },
    "contracts/structs/JBFundingCycleData.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBFundingCycleBallot.sol';\n\n/** \n  @member duration The number of seconds the funding cycle lasts for, after which a new funding cycle will start. A duration of 0 means that the funding cycle will stay active until the project owner explicitly issues a reconfiguration, at which point a new funding cycle will immediately start with the updated properties. If the duration is greater than 0, a project owner cannot make changes to a funding cycle's parameters while it is active  any proposed changes will apply to the subsequent cycle. If no changes are proposed, a funding cycle rolls over to another one with the same properties but new `start` timestamp and a discounted `weight`.\n  @member weight A fixed point number with 18 decimals that contracts can use to base arbitrary calculations on. For example, payment terminals can use this to determine how many tokens should be minted when a payment is received.\n  @member discountRate A percent by how much the `weight` of the subsequent funding cycle should be reduced, if the project owner hasn't configured the subsequent funding cycle with an explicit `weight`. If it's 0, each funding cycle will have equal weight. If the number is 90%, the next funding cycle will have a 10% smaller weight. This weight is out of `JBConstants.MAX_DISCOUNT_RATE`.\n  @member ballot An address of a contract that says whether a proposed reconfiguration should be accepted or rejected. It can be used to create rules around how a project owner can change funding cycle parameters over time.\n*/\nstruct JBFundingCycleData {\n  uint256 duration;\n  uint256 weight;\n  uint256 discountRate;\n  IJBFundingCycleBallot ballot;\n}\n"
    },
    "contracts/structs/JBFundingCycleMetadata.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './JBGlobalFundingCycleMetadata.sol';\nimport './../interfaces/IJBFundingCycleDataSource.sol';\n\n/** \n  @member global Data used globally in non-migratable ecosystem contracts.\n  @member reservedRate The reserved rate of the funding cycle. This number is a percentage calculated out of `JBConstants.MAX_RESERVED_RATE`.\n  @member redemptionRate The redemption rate of the funding cycle. This number is a percentage calculated out of `JBConstants.MAX_REDEMPTION_RATE`.\n  @member ballotRedemptionRate The redemption rate to use during an active ballot of the funding cycle. This number is a percentage calculated out of `JBConstants.MAX_REDEMPTION_RATE`.\n  @member pausePay A flag indicating if the pay functionality should be paused during the funding cycle.\n  @member pauseDistributions A flag indicating if the distribute functionality should be paused during the funding cycle.\n  @member pauseRedeem A flag indicating if the redeem functionality should be paused during the funding cycle.\n  @member pauseBurn A flag indicating if the burn functionality should be paused during the funding cycle.\n  @member allowMinting A flag indicating if the mint functionality should be allowed during the funding cycle.\n  @member allowChangeToken A flag indicating if changing tokens should be allowed during this funding cycle.\n  @member allowTerminalMigration A flag indicating if migrating terminals should be allowed during this funding cycle.\n  @member allowControllerMigration A flag indicating if migrating controllers should be allowed during this funding cycle.\n  @member holdFees A flag indicating if fees should be held during this funding cycle.\n  @member useTotalOverflowForRedemptions A flag indicating if redemptions should use the project's balance held in all terminals instead of the project's local terminal balance from which the redemption is being fulfilled.\n  @member useDataSourceForPay A flag indicating if the data source should be used for pay transactions during this funding cycle.\n  @member useDataSourceForRedeem A flag indicating if the data source should be used for redeem transactions during this funding cycle.\n  @member dataSource The data source to use during this funding cycle.\n*/\nstruct JBFundingCycleMetadata {\n  JBGlobalFundingCycleMetadata global;\n  uint256 reservedRate;\n  uint256 redemptionRate;\n  uint256 ballotRedemptionRate;\n  bool pausePay;\n  bool pauseDistributions;\n  bool pauseRedeem;\n  bool pauseBurn;\n  bool allowMinting;\n  bool allowChangeToken;\n  bool allowTerminalMigration;\n  bool allowControllerMigration;\n  bool holdFees;\n  bool useTotalOverflowForRedemptions;\n  bool useDataSourceForPay;\n  bool useDataSourceForRedeem;\n  address dataSource;\n}\n"
    },
    "contracts/structs/JBGlobalFundingCycleMetadata.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBFundingCycleDataSource.sol';\n\n/** \n  @member allowSetTerminals A flag indicating if setting terminals should be allowed during this funding cycle.\n  @member allowSetController A flag indicating if setting a new controller should be allowed during this funding cycle.\n*/\nstruct JBGlobalFundingCycleMetadata {\n  bool allowSetTerminals;\n  bool allowSetController;\n}\n"
    },
    "contracts/structs/JBGroupedSplits.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './JBSplit.sol';\n\n/** \n  @member group The group indentifier.\n  @member splits The splits to associate with the group.\n*/\nstruct JBGroupedSplits {\n  uint256 group;\n  JBSplit[] splits;\n}\n"
    },
    "contracts/structs/JBOperatorData.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\n/** \n  @member operator The address of the operator.\n  @member domain The domain within which the operator is being given permissions. A domain of 0 is a wildcard domain, which gives an operator access to all domains.\n  @member permissionIndexes The indexes of the permissions the operator is being given.\n*/\nstruct JBOperatorData {\n  address operator;\n  uint256 domain;\n  uint256[] permissionIndexes;\n}\n"
    },
    "contracts/structs/JBPayParamsData.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBPaymentTerminal.sol';\nimport './JBTokenAmount.sol';\n\n/** \n  @member terminal The terminal that is facilitating the payment.\n  @member payer The address from which the payment originated.\n  @member amount The amount of the payment. Includes the token being paid, the value, the number of decimals included, and the currency of the amount.\n  @member projectId The ID of the project being paid.\n  @member currentFundingCycleConfiguration The configuration of the funding cycle during which the payment is being made.\n  @member beneficiary The specified address that should be the beneficiary of anything that results from the payment.\n  @member weight The weight of the funding cycle during which the payment is being made.\n  @member reservedRate The reserved rate of the funding cycle during which the payment is being made.\n  @member memo The memo that was sent alongside the payment.\n  @member metadata Extra data provided by the payer.\n*/\nstruct JBPayParamsData {\n  IJBPaymentTerminal terminal;\n  address payer;\n  JBTokenAmount amount;\n  uint256 projectId;\n  uint256 currentFundingCycleConfiguration;\n  address beneficiary;\n  uint256 weight;\n  uint256 reservedRate;\n  string memo;\n  bytes metadata;\n}\n"
    },
    "contracts/structs/JBProjectMetadata.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\n/** \n  @member content The metadata content.\n  @member domain The domain within which the metadata applies.\n*/\nstruct JBProjectMetadata {\n  string content;\n  uint256 domain;\n}\n"
    },
    "contracts/structs/JBRedeemParamsData.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBPaymentTerminal.sol';\nimport './JBTokenAmount.sol';\n\n/** \n  @member terminal The terminal that is facilitating the redemption.\n  @member holder The holder of the tokens being redeemed.\n  @member projectId The ID of the project whos tokens are being redeemed.\n  @member currentFundingCycleConfiguration The configuration of the funding cycle during which the redemption is being made.\n  @member tokenCount The proposed number of tokens being redeemed, as a fixed point number with 18 decimals.\n  @member totalSupply The total supply of tokens used in the calculation, as a fixed point number with 18 decimals.\n  @member overflow The amount of overflow used in the reclaim amount calculation.\n  @member reclaimAmount The amount that should be reclaimed by the redeemer using the protocol's standard bonding curve redemption formula. Includes the token being reclaimed, the reclaim value, the number of decimals included, and the currency of the reclaim amount.\n  @member useTotalOverflow If overflow across all of a project's terminals is being used when making redemptions.\n  @member redemptionRate The redemption rate of the funding cycle during which the redemption is being made.\n  @member ballotRedemptionRate The ballot redemption rate of the funding cycle during which the redemption is being made.\n  @member memo The proposed memo that is being emitted alongside the redemption.\n  @member metadata Extra data provided by the redeemer.\n*/\nstruct JBRedeemParamsData {\n  IJBPaymentTerminal terminal;\n  address holder;\n  uint256 projectId;\n  uint256 currentFundingCycleConfiguration;\n  uint256 tokenCount;\n  uint256 totalSupply;\n  uint256 overflow;\n  JBTokenAmount reclaimAmount;\n  bool useTotalOverflow;\n  uint256 redemptionRate;\n  uint256 ballotRedemptionRate;\n  string memo;\n  bytes metadata;\n}\n"
    },
    "contracts/structs/JBSplit.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBSplitAllocator.sol';\n\n/** \n  @member preferClaimed A flag that only has effect if a projectId is also specified, and the project has a token contract attached. If so, this flag indicates if the tokens that result from making a payment to the project should be delivered claimed into the beneficiary's wallet, or unclaimed to save gas.\n  @member preferAddToBalance A flag indicating if a distribution to a project should prefer triggering it's addToBalance function instead of its pay function.\n  @member percent The percent of the whole group that this split occupies. This number is out of `JBConstants.SPLITS_TOTAL_PERCENT`.\n  @member projectId The ID of a project. If an allocator is not set but a projectId is set, funds will be sent to the protocol treasury belonging to the project who's ID is specified. Resulting tokens will be routed to the beneficiary with the claimed token preference respected.\n  @member beneficiary An address. The role the of the beneficary depends on whether or not projectId is specified, and whether or not an allocator is specified. If allocator is set, the beneficiary will be forwarded to the allocator for it to use. If allocator is not set but projectId is set, the beneficiary is the address to which the project's tokens will be sent that result from a payment to it. If neither allocator or projectId are set, the beneficiary is where the funds from the split will be sent.\n  @member lockedUntil Specifies if the split should be unchangeable until the specified time, with the exception of extending the locked period.\n  @member allocator If an allocator is specified, funds will be sent to the allocator contract along with all properties of this split.\n*/\nstruct JBSplit {\n  bool preferClaimed;\n  bool preferAddToBalance;\n  uint256 percent;\n  uint256 projectId;\n  address payable beneficiary;\n  uint256 lockedUntil;\n  IJBSplitAllocator allocator;\n}\n"
    },
    "contracts/structs/JBSplitAllocationData.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './JBSplit.sol';\n\n/** \n  @member token The token being sent to the split allocator.\n  @member amount The amount being sent to the split allocator, as a fixed point number.\n  @member decimals The number of decimals in the amount.\n  @member projectId The project to which the split belongs.\n  @member group The group to which the split belongs.\n  @member split The split that caused the allocation.\n*/\nstruct JBSplitAllocationData {\n  address token;\n  uint256 amount;\n  uint256 decimals;\n  uint256 projectId;\n  uint256 group;\n  JBSplit split;\n}\n"
    },
    "contracts/structs/JBTokenAmount.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\n/* \n  @member token The token the payment was made in.\n  @member value The amount of tokens that was paid, as a fixed point number.\n  @member decimals The number of decimals included in the value fixed point number.\n  @member currency The expected currency of the value.\n**/\nstruct JBTokenAmount {\n  address token;\n  uint256 value;\n  uint256 decimals;\n  uint256 currency;\n}\n"
    },
    "prb-math/contracts/PRBMath.sol": {
      "content": "// SPDX-License-Identifier: Unlicense\npragma solidity >=0.8.4;\n\n/// @notice Emitted when the result overflows uint256.\nerror PRBMath__MulDivFixedPointOverflow(uint256 prod1);\n\n/// @notice Emitted when the result overflows uint256.\nerror PRBMath__MulDivOverflow(uint256 prod1, uint256 denominator);\n\n/// @notice Emitted when one of the inputs is type(int256).min.\nerror PRBMath__MulDivSignedInputTooSmall();\n\n/// @notice Emitted when the intermediary absolute result overflows int256.\nerror PRBMath__MulDivSignedOverflow(uint256 rAbs);\n\n/// @notice Emitted when the input is MIN_SD59x18.\nerror PRBMathSD59x18__AbsInputTooSmall();\n\n/// @notice Emitted when ceiling a number overflows SD59x18.\nerror PRBMathSD59x18__CeilOverflow(int256 x);\n\n/// @notice Emitted when one of the inputs is MIN_SD59x18.\nerror PRBMathSD59x18__DivInputTooSmall();\n\n/// @notice Emitted when one of the intermediary unsigned results overflows SD59x18.\nerror PRBMathSD59x18__DivOverflow(uint256 rAbs);\n\n/// @notice Emitted when the input is greater than 133.084258667509499441.\nerror PRBMathSD59x18__ExpInputTooBig(int256 x);\n\n/// @notice Emitted when the input is greater than 192.\nerror PRBMathSD59x18__Exp2InputTooBig(int256 x);\n\n/// @notice Emitted when flooring a number underflows SD59x18.\nerror PRBMathSD59x18__FloorUnderflow(int256 x);\n\n/// @notice Emitted when converting a basic integer to the fixed-point format overflows SD59x18.\nerror PRBMathSD59x18__FromIntOverflow(int256 x);\n\n/// @notice Emitted when converting a basic integer to the fixed-point format underflows SD59x18.\nerror PRBMathSD59x18__FromIntUnderflow(int256 x);\n\n/// @notice Emitted when the product of the inputs is negative.\nerror PRBMathSD59x18__GmNegativeProduct(int256 x, int256 y);\n\n/// @notice Emitted when multiplying the inputs overflows SD59x18.\nerror PRBMathSD59x18__GmOverflow(int256 x, int256 y);\n\n/// @notice Emitted when the input is less than or equal to zero.\nerror PRBMathSD59x18__LogInputTooSmall(int256 x);\n\n/// @notice Emitted when one of the inputs is MIN_SD59x18.\nerror PRBMathSD59x18__MulInputTooSmall();\n\n/// @notice Emitted when the intermediary absolute result overflows SD59x18.\nerror PRBMathSD59x18__MulOverflow(uint256 rAbs);\n\n/// @notice Emitted when the intermediary absolute result overflows SD59x18.\nerror PRBMathSD59x18__PowuOverflow(uint256 rAbs);\n\n/// @notice Emitted when the input is negative.\nerror PRBMathSD59x18__SqrtNegativeInput(int256 x);\n\n/// @notice Emitted when the calculating the square root overflows SD59x18.\nerror PRBMathSD59x18__SqrtOverflow(int256 x);\n\n/// @notice Emitted when addition overflows UD60x18.\nerror PRBMathUD60x18__AddOverflow(uint256 x, uint256 y);\n\n/// @notice Emitted when ceiling a number overflows UD60x18.\nerror PRBMathUD60x18__CeilOverflow(uint256 x);\n\n/// @notice Emitted when the input is greater than 133.084258667509499441.\nerror PRBMathUD60x18__ExpInputTooBig(uint256 x);\n\n/// @notice Emitted when the input is greater than 192.\nerror PRBMathUD60x18__Exp2InputTooBig(uint256 x);\n\n/// @notice Emitted when converting a basic integer to the fixed-point format format overflows UD60x18.\nerror PRBMathUD60x18__FromUintOverflow(uint256 x);\n\n/// @notice Emitted when multiplying the inputs overflows UD60x18.\nerror PRBMathUD60x18__GmOverflow(uint256 x, uint256 y);\n\n/// @notice Emitted when the input is less than 1.\nerror PRBMathUD60x18__LogInputTooSmall(uint256 x);\n\n/// @notice Emitted when the calculating the square root overflows UD60x18.\nerror PRBMathUD60x18__SqrtOverflow(uint256 x);\n\n/// @notice Emitted when subtraction underflows UD60x18.\nerror PRBMathUD60x18__SubUnderflow(uint256 x, uint256 y);\n\n/// @dev Common mathematical functions used in both PRBMathSD59x18 and PRBMathUD60x18. Note that this shared library\n/// does not always assume the signed 59.18-decimal fixed-point or the unsigned 60.18-decimal fixed-point\n/// representation. When it does not, it is explicitly mentioned in the NatSpec documentation.\nlibrary PRBMath {\n    /// STRUCTS ///\n\n    struct SD59x18 {\n        int256 value;\n    }\n\n    struct UD60x18 {\n        uint256 value;\n    }\n\n    /// STORAGE ///\n\n    /// @dev How many trailing decimals can be represented.\n    uint256 internal constant SCALE = 1e18;\n\n    /// @dev Largest power of two divisor of SCALE.\n    uint256 internal constant SCALE_LPOTD = 262144;\n\n    /// @dev SCALE inverted mod 2^256.\n    uint256 internal constant SCALE_INVERSE =\n        78156646155174841979727994598816262306175212592076161876661_508869554232690281;\n\n    /// FUNCTIONS ///\n\n    /// @notice Calculates the binary exponent of x using the binary fraction method.\n    /// @dev Has to use 192.64-bit fixed-point numbers.\n    /// See https://ethereum.stackexchange.com/a/96594/24693.\n    /// @param x The exponent as an unsigned 192.64-bit fixed-point number.\n    /// @return result The result as an unsigned 60.18-decimal fixed-point number.\n    function exp2(uint256 x) internal pure returns (uint256 result) {\n        unchecked {\n            // Start from 0.5 in the 192.64-bit fixed-point format.\n            result = 0x800000000000000000000000000000000000000000000000;\n\n            // Multiply the result by root(2, 2^-i) when the bit at position i is 1. None of the intermediary results overflows\n            // because the initial result is 2^191 and all magic factors are less than 2^65.\n            if (x & 0x8000000000000000 > 0) {\n                result = (result * 0x16A09E667F3BCC909) >> 64;\n            }\n            if (x & 0x4000000000000000 > 0) {\n                result = (result * 0x1306FE0A31B7152DF) >> 64;\n            }\n            if (x & 0x2000000000000000 > 0) {\n                result = (result * 0x1172B83C7D517ADCE) >> 64;\n            }\n            if (x & 0x1000000000000000 > 0) {\n                result = (result * 0x10B5586CF9890F62A) >> 64;\n            }\n            if (x & 0x800000000000000 > 0) {\n                result = (result * 0x1059B0D31585743AE) >> 64;\n            }\n            if (x & 0x400000000000000 > 0) {\n                result = (result * 0x102C9A3E778060EE7) >> 64;\n            }\n            if (x & 0x200000000000000 > 0) {\n                result = (result * 0x10163DA9FB33356D8) >> 64;\n            }\n            if (x & 0x100000000000000 > 0) {\n                result = (result * 0x100B1AFA5ABCBED61) >> 64;\n            }\n            if (x & 0x80000000000000 > 0) {\n                result = (result * 0x10058C86DA1C09EA2) >> 64;\n            }\n            if (x & 0x40000000000000 > 0) {\n                result = (result * 0x1002C605E2E8CEC50) >> 64;\n            }\n            if (x & 0x20000000000000 > 0) {\n                result = (result * 0x100162F3904051FA1) >> 64;\n            }\n            if (x & 0x10000000000000 > 0) {\n                result = (result * 0x1000B175EFFDC76BA) >> 64;\n            }\n            if (x & 0x8000000000000 > 0) {\n                result = (result * 0x100058BA01FB9F96D) >> 64;\n            }\n            if (x & 0x4000000000000 > 0) {\n                result = (result * 0x10002C5CC37DA9492) >> 64;\n            }\n            if (x & 0x2000000000000 > 0) {\n                result = (result * 0x1000162E525EE0547) >> 64;\n            }\n            if (x & 0x1000000000000 > 0) {\n                result = (result * 0x10000B17255775C04) >> 64;\n            }\n            if (x & 0x800000000000 > 0) {\n                result = (result * 0x1000058B91B5BC9AE) >> 64;\n            }\n            if (x & 0x400000000000 > 0) {\n                result = (result * 0x100002C5C89D5EC6D) >> 64;\n            }\n            if (x & 0x200000000000 > 0) {\n                result = (result * 0x10000162E43F4F831) >> 64;\n            }\n            if (x & 0x100000000000 > 0) {\n                result = (result * 0x100000B1721BCFC9A) >> 64;\n            }\n            if (x & 0x80000000000 > 0) {\n                result = (result * 0x10000058B90CF1E6E) >> 64;\n            }\n            if (x & 0x40000000000 > 0) {\n                result = (result * 0x1000002C5C863B73F) >> 64;\n            }\n            if (x & 0x20000000000 > 0) {\n                result = (result * 0x100000162E430E5A2) >> 64;\n            }\n            if (x & 0x10000000000 > 0) {\n                result = (result * 0x1000000B172183551) >> 64;\n            }\n            if (x & 0x8000000000 > 0) {\n                result = (result * 0x100000058B90C0B49) >> 64;\n            }\n            if (x & 0x4000000000 > 0) {\n                result = (result * 0x10000002C5C8601CC) >> 64;\n            }\n            if (x & 0x2000000000 > 0) {\n                result = (result * 0x1000000162E42FFF0) >> 64;\n            }\n            if (x & 0x1000000000 > 0) {\n                result = (result * 0x10000000B17217FBB) >> 64;\n            }\n            if (x & 0x800000000 > 0) {\n                result = (result * 0x1000000058B90BFCE) >> 64;\n            }\n            if (x & 0x400000000 > 0) {\n                result = (result * 0x100000002C5C85FE3) >> 64;\n            }\n            if (x & 0x200000000 > 0) {\n                result = (result * 0x10000000162E42FF1) >> 64;\n            }\n            if (x & 0x100000000 > 0) {\n                result = (result * 0x100000000B17217F8) >> 64;\n            }\n            if (x & 0x80000000 > 0) {\n                result = (result * 0x10000000058B90BFC) >> 64;\n            }\n            if (x & 0x40000000 > 0) {\n                result = (result * 0x1000000002C5C85FE) >> 64;\n            }\n            if (x & 0x20000000 > 0) {\n                result = (result * 0x100000000162E42FF) >> 64;\n            }\n            if (x & 0x10000000 > 0) {\n                result = (result * 0x1000000000B17217F) >> 64;\n            }\n            if (x & 0x8000000 > 0) {\n                result = (result * 0x100000000058B90C0) >> 64;\n            }\n            if (x & 0x4000000 > 0) {\n                result = (result * 0x10000000002C5C860) >> 64;\n            }\n            if (x & 0x2000000 > 0) {\n                result = (result * 0x1000000000162E430) >> 64;\n            }\n            if (x & 0x1000000 > 0) {\n                result = (result * 0x10000000000B17218) >> 64;\n            }\n            if (x & 0x800000 > 0) {\n                result = (result * 0x1000000000058B90C) >> 64;\n            }\n            if (x & 0x400000 > 0) {\n                result = (result * 0x100000000002C5C86) >> 64;\n            }\n            if (x & 0x200000 > 0) {\n                result = (result * 0x10000000000162E43) >> 64;\n            }\n            if (x & 0x100000 > 0) {\n                result = (result * 0x100000000000B1721) >> 64;\n            }\n            if (x & 0x80000 > 0) {\n                result = (result * 0x10000000000058B91) >> 64;\n            }\n            if (x & 0x40000 > 0) {\n                result = (result * 0x1000000000002C5C8) >> 64;\n            }\n            if (x & 0x20000 > 0) {\n                result = (result * 0x100000000000162E4) >> 64;\n            }\n            if (x & 0x10000 > 0) {\n                result = (result * 0x1000000000000B172) >> 64;\n            }\n            if (x & 0x8000 > 0) {\n                result = (result * 0x100000000000058B9) >> 64;\n            }\n            if (x & 0x4000 > 0) {\n                result = (result * 0x10000000000002C5D) >> 64;\n            }\n            if (x & 0x2000 > 0) {\n                result = (result * 0x1000000000000162E) >> 64;\n            }\n            if (x & 0x1000 > 0) {\n                result = (result * 0x10000000000000B17) >> 64;\n            }\n            if (x & 0x800 > 0) {\n                result = (result * 0x1000000000000058C) >> 64;\n            }\n            if (x & 0x400 > 0) {\n                result = (result * 0x100000000000002C6) >> 64;\n            }\n            if (x & 0x200 > 0) {\n                result = (result * 0x10000000000000163) >> 64;\n            }\n            if (x & 0x100 > 0) {\n                result = (result * 0x100000000000000B1) >> 64;\n            }\n            if (x & 0x80 > 0) {\n                result = (result * 0x10000000000000059) >> 64;\n            }\n            if (x & 0x40 > 0) {\n                result = (result * 0x1000000000000002C) >> 64;\n            }\n            if (x & 0x20 > 0) {\n                result = (result * 0x10000000000000016) >> 64;\n            }\n            if (x & 0x10 > 0) {\n                result = (result * 0x1000000000000000B) >> 64;\n            }\n            if (x & 0x8 > 0) {\n                result = (result * 0x10000000000000006) >> 64;\n            }\n            if (x & 0x4 > 0) {\n                result = (result * 0x10000000000000003) >> 64;\n            }\n            if (x & 0x2 > 0) {\n                result = (result * 0x10000000000000001) >> 64;\n            }\n            if (x & 0x1 > 0) {\n                result = (result * 0x10000000000000001) >> 64;\n            }\n\n            // We're doing two things at the same time:\n            //\n            //   1. Multiply the result by 2^n + 1, where \"2^n\" is the integer part and the one is added to account for\n            //      the fact that we initially set the result to 0.5. This is accomplished by subtracting from 191\n            //      rather than 192.\n            //   2. Convert the result to the unsigned 60.18-decimal fixed-point format.\n            //\n            // This works because 2^(191-ip) = 2^ip / 2^191, where \"ip\" is the integer part \"2^n\".\n            result *= SCALE;\n            result >>= (191 - (x >> 64));\n        }\n    }\n\n    /// @notice Finds the zero-based index of the first one in the binary representation of x.\n    /// @dev See the note on msb in the \"Find First Set\" Wikipedia article https://en.wikipedia.org/wiki/Find_first_set\n    /// @param x The uint256 number for which to find the index of the most significant bit.\n    /// @return msb The index of the most significant bit as an uint256.\n    function mostSignificantBit(uint256 x) internal pure returns (uint256 msb) {\n        if (x >= 2**128) {\n            x >>= 128;\n            msb += 128;\n        }\n        if (x >= 2**64) {\n            x >>= 64;\n            msb += 64;\n        }\n        if (x >= 2**32) {\n            x >>= 32;\n            msb += 32;\n        }\n        if (x >= 2**16) {\n            x >>= 16;\n            msb += 16;\n        }\n        if (x >= 2**8) {\n            x >>= 8;\n            msb += 8;\n        }\n        if (x >= 2**4) {\n            x >>= 4;\n            msb += 4;\n        }\n        if (x >= 2**2) {\n            x >>= 2;\n            msb += 2;\n        }\n        if (x >= 2**1) {\n            // No need to shift x any more.\n            msb += 1;\n        }\n    }\n\n    /// @notice Calculates floor(x*ydenominator) with full precision.\n    ///\n    /// @dev Credit to Remco Bloemen under MIT license https://xn--2-umb.com/21/muldiv.\n    ///\n    /// Requirements:\n    /// - The denominator cannot be zero.\n    /// - The result must fit within uint256.\n    ///\n    /// Caveats:\n    /// - This function does not work with fixed-point numbers.\n    ///\n    /// @param x The multiplicand as an uint256.\n    /// @param y The multiplier as an uint256.\n    /// @param denominator The divisor as an uint256.\n    /// @return result The result as an uint256.\n    function mulDiv(\n        uint256 x,\n        uint256 y,\n        uint256 denominator\n    ) internal pure returns (uint256 result) {\n        // 512-bit multiply [prod1 prod0] = x * y. Compute the product mod 2^256 and mod 2^256 - 1, then use\n        // use the Chinese Remainder Theorem to reconstruct the 512 bit result. The result is stored in two 256\n        // variables such that product = prod1 * 2^256 + prod0.\n        uint256 prod0; // Least significant 256 bits of the product\n        uint256 prod1; // Most significant 256 bits of the product\n        assembly {\n            let mm := mulmod(x, y, not(0))\n            prod0 := mul(x, y)\n            prod1 := sub(sub(mm, prod0), lt(mm, prod0))\n        }\n\n        // Handle non-overflow cases, 256 by 256 division.\n        if (prod1 == 0) {\n            unchecked {\n                result = prod0 / denominator;\n            }\n            return result;\n        }\n\n        // Make sure the result is less than 2^256. Also prevents denominator == 0.\n        if (prod1 >= denominator) {\n            revert PRBMath__MulDivOverflow(prod1, denominator);\n        }\n\n        ///////////////////////////////////////////////\n        // 512 by 256 division.\n        ///////////////////////////////////////////////\n\n        // Make division exact by subtracting the remainder from [prod1 prod0].\n        uint256 remainder;\n        assembly {\n            // Compute remainder using mulmod.\n            remainder := mulmod(x, y, denominator)\n\n            // Subtract 256 bit number from 512 bit number.\n            prod1 := sub(prod1, gt(remainder, prod0))\n            prod0 := sub(prod0, remainder)\n        }\n\n        // Factor powers of two out of denominator and compute largest power of two divisor of denominator. Always >= 1.\n        // See https://cs.stackexchange.com/q/138556/92363.\n        unchecked {\n            // Does not overflow because the denominator cannot be zero at this stage in the function.\n            uint256 lpotdod = denominator & (~denominator + 1);\n            assembly {\n                // Divide denominator by lpotdod.\n                denominator := div(denominator, lpotdod)\n\n                // Divide [prod1 prod0] by lpotdod.\n                prod0 := div(prod0, lpotdod)\n\n                // Flip lpotdod such that it is 2^256 / lpotdod. If lpotdod is zero, then it becomes one.\n                lpotdod := add(div(sub(0, lpotdod), lpotdod), 1)\n            }\n\n            // Shift in bits from prod1 into prod0.\n            prod0 |= prod1 * lpotdod;\n\n            // Invert denominator mod 2^256. Now that denominator is an odd number, it has an inverse modulo 2^256 such\n            // that denominator * inv = 1 mod 2^256. Compute the inverse by starting with a seed that is correct for\n            // four bits. That is, denominator * inv = 1 mod 2^4.\n            uint256 inverse = (3 * denominator) ^ 2;\n\n            // Use the Newton-Raphson iteration to improve the precision. Thanks to Hensel's lifting lemma, this also works\n            // in modular arithmetic, doubling the correct bits in each step.\n            inverse *= 2 - denominator * inverse; // inverse mod 2^8\n            inverse *= 2 - denominator * inverse; // inverse mod 2^16\n            inverse *= 2 - denominator * inverse; // inverse mod 2^32\n            inverse *= 2 - denominator * inverse; // inverse mod 2^64\n            inverse *= 2 - denominator * inverse; // inverse mod 2^128\n            inverse *= 2 - denominator * inverse; // inverse mod 2^256\n\n            // Because the division is now exact we can divide by multiplying with the modular inverse of denominator.\n            // This will give us the correct result modulo 2^256. Since the preconditions guarantee that the outcome is\n            // less than 2^256, this is the final result. We don't need to compute the high bits of the result and prod1\n            // is no longer required.\n            result = prod0 * inverse;\n            return result;\n        }\n    }\n\n    /// @notice Calculates floor(x*y1e18) with full precision.\n    ///\n    /// @dev Variant of \"mulDiv\" with constant folding, i.e. in which the denominator is always 1e18. Before returning the\n    /// final result, we add 1 if (x * y) % SCALE >= HALF_SCALE. Without this, 6.6e-19 would be truncated to 0 instead of\n    /// being rounded to 1e-18.  See \"Listing 6\" and text above it at https://accu.org/index.php/journals/1717.\n    ///\n    /// Requirements:\n    /// - The result must fit within uint256.\n    ///\n    /// Caveats:\n    /// - The body is purposely left uncommented; see the NatSpec comments in \"PRBMath.mulDiv\" to understand how this works.\n    /// - It is assumed that the result can never be type(uint256).max when x and y solve the following two equations:\n    ///     1. x * y = type(uint256).max * SCALE\n    ///     2. (x * y) % SCALE >= SCALE / 2\n    ///\n    /// @param x The multiplicand as an unsigned 60.18-decimal fixed-point number.\n    /// @param y The multiplier as an unsigned 60.18-decimal fixed-point number.\n    /// @return result The result as an unsigned 60.18-decimal fixed-point number.\n    function mulDivFixedPoint(uint256 x, uint256 y) internal pure returns (uint256 result) {\n        uint256 prod0;\n        uint256 prod1;\n        assembly {\n            let mm := mulmod(x, y, not(0))\n            prod0 := mul(x, y)\n            prod1 := sub(sub(mm, prod0), lt(mm, prod0))\n        }\n\n        if (prod1 >= SCALE) {\n            revert PRBMath__MulDivFixedPointOverflow(prod1);\n        }\n\n        uint256 remainder;\n        uint256 roundUpUnit;\n        assembly {\n            remainder := mulmod(x, y, SCALE)\n            roundUpUnit := gt(remainder, 499999999999999999)\n        }\n\n        if (prod1 == 0) {\n            unchecked {\n                result = (prod0 / SCALE) + roundUpUnit;\n                return result;\n            }\n        }\n\n        assembly {\n            result := add(\n                mul(\n                    or(\n                        div(sub(prod0, remainder), SCALE_LPOTD),\n                        mul(sub(prod1, gt(remainder, prod0)), add(div(sub(0, SCALE_LPOTD), SCALE_LPOTD), 1))\n                    ),\n                    SCALE_INVERSE\n                ),\n                roundUpUnit\n            )\n        }\n    }\n\n    /// @notice Calculates floor(x*ydenominator) with full precision.\n    ///\n    /// @dev An extension of \"mulDiv\" for signed numbers. Works by computing the signs and the absolute values separately.\n    ///\n    /// Requirements:\n    /// - None of the inputs can be type(int256).min.\n    /// - The result must fit within int256.\n    ///\n    /// @param x The multiplicand as an int256.\n    /// @param y The multiplier as an int256.\n    /// @param denominator The divisor as an int256.\n    /// @return result The result as an int256.\n    function mulDivSigned(\n        int256 x,\n        int256 y,\n        int256 denominator\n    ) internal pure returns (int256 result) {\n        if (x == type(int256).min || y == type(int256).min || denominator == type(int256).min) {\n            revert PRBMath__MulDivSignedInputTooSmall();\n        }\n\n        // Get hold of the absolute values of x, y and the denominator.\n        uint256 ax;\n        uint256 ay;\n        uint256 ad;\n        unchecked {\n            ax = x < 0 ? uint256(-x) : uint256(x);\n            ay = y < 0 ? uint256(-y) : uint256(y);\n            ad = denominator < 0 ? uint256(-denominator) : uint256(denominator);\n        }\n\n        // Compute the absolute value of (x*y)denominator. The result must fit within int256.\n        uint256 rAbs = mulDiv(ax, ay, ad);\n        if (rAbs > uint256(type(int256).max)) {\n            revert PRBMath__MulDivSignedOverflow(rAbs);\n        }\n\n        // Get the signs of x, y and the denominator.\n        uint256 sx;\n        uint256 sy;\n        uint256 sd;\n        assembly {\n            sx := sgt(x, sub(0, 1))\n            sy := sgt(y, sub(0, 1))\n            sd := sgt(denominator, sub(0, 1))\n        }\n\n        // XOR over sx, sy and sd. This is checking whether there are one or three negative signs in the inputs.\n        // If yes, the result should be negative.\n        result = sx ^ sy ^ sd == 0 ? -int256(rAbs) : int256(rAbs);\n    }\n\n    /// @notice Calculates the square root of x, rounding down.\n    /// @dev Uses the Babylonian method https://en.wikipedia.org/wiki/Methods_of_computing_square_roots#Babylonian_method.\n    ///\n    /// Caveats:\n    /// - This function does not work with fixed-point numbers.\n    ///\n    /// @param x The uint256 number for which to calculate the square root.\n    /// @return result The result as an uint256.\n    function sqrt(uint256 x) internal pure returns (uint256 result) {\n        if (x == 0) {\n            return 0;\n        }\n\n        // Set the initial guess to the closest power of two that is higher than x.\n        uint256 xAux = uint256(x);\n        result = 1;\n        if (xAux >= 0x100000000000000000000000000000000) {\n            xAux >>= 128;\n            result <<= 64;\n        }\n        if (xAux >= 0x10000000000000000) {\n            xAux >>= 64;\n            result <<= 32;\n        }\n        if (xAux >= 0x100000000) {\n            xAux >>= 32;\n            result <<= 16;\n        }\n        if (xAux >= 0x10000) {\n            xAux >>= 16;\n            result <<= 8;\n        }\n        if (xAux >= 0x100) {\n            xAux >>= 8;\n            result <<= 4;\n        }\n        if (xAux >= 0x10) {\n            xAux >>= 4;\n            result <<= 2;\n        }\n        if (xAux >= 0x8) {\n            result <<= 1;\n        }\n\n        // The operations can never overflow because the result is max 2^127 when it enters this block.\n        unchecked {\n            result = (result + x / result) >> 1;\n            result = (result + x / result) >> 1;\n            result = (result + x / result) >> 1;\n            result = (result + x / result) >> 1;\n            result = (result + x / result) >> 1;\n            result = (result + x / result) >> 1;\n            result = (result + x / result) >> 1; // Seven iterations should be enough\n            uint256 roundedDownResult = x / result;\n            return result >= roundedDownResult ? roundedDownResult : result;\n        }\n    }\n}\n"
    }
  }
}}