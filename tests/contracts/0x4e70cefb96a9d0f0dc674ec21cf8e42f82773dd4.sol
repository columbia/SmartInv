{{
  "language": "Solidity",
  "sources": {
    "contracts/ForeignChain.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.6.8;\npragma experimental ABIEncoderV2;\n\nimport \"./Registry.sol\";\nimport \"./BaseChain.sol\";\n\ncontract ForeignChain is BaseChain {\n  // we will not be changing replicator that often, so we can make it immutable, it saves 200gas\n  address public immutable replicator;\n  uint32 public lastBlockId;\n  bool public deprecated;\n\n  // ========== EVENTS ========== //\n\n  event LogBlockReplication(address indexed minter, uint32 blockId);\n  event LogDeprecation(address indexed deprecator);\n\n  // ========== CONSTRUCTOR ========== //\n\n  constructor(\n    address _contractRegistry,\n    uint16 _padding,\n    uint16 _requiredSignatures,\n    address _replicator\n  ) public BaseChain(_contractRegistry, _padding, _requiredSignatures) {\n    replicator = _replicator;\n  }\n\n  modifier onlyReplicator() {\n    require(msg.sender == replicator, \"onlyReplicator\");\n    _;\n  }\n\n  // ========== MUTATIVE FUNCTIONS ========== //\n\n  function register() override external {\n    require(msg.sender == address(contractRegistry), \"only contractRegistry can register\");\n\n    ForeignChain oldChain = ForeignChain(contractRegistry.getAddress(\"Chain\"));\n    require(address(oldChain) != address(this), \"registration must be done before address in registry is replaced\");\n\n    if (address(oldChain) != address(0x0)) {\n      lastBlockId = oldChain.lastBlockId();\n      // we cloning last block time, because we will need reference point for next submissions\n      blocks[lastBlockId].dataTimestamp = oldChain.getBlockTimestamp(lastBlockId);\n    }\n  }\n\n  function unregister() override external {\n    require(msg.sender == address(contractRegistry), \"only contractRegistry can unregister\");\n    require(!deprecated, \"contract is already deprecated\");\n\n    ForeignChain newChain = ForeignChain(contractRegistry.getAddress(\"Chain\"));\n    require(address(newChain) != address(this), \"unregistering must be done after address in registry is replaced\");\n    require(newChain.isForeign(), \"can not be replaced with chain of different type\");\n\n    deprecated = true;\n    emit LogDeprecation(msg.sender);\n  }\n\n  function submit(\n    uint32 _dataTimestamp,\n    bytes32 _root,\n    bytes32[] calldata _keys,\n    uint256[] calldata _values,\n    uint32 _blockId\n  ) external onlyReplicator {\n    require(!deprecated, \"contract is deprecated\");\n    uint lastDataTimestamp = blocks[lastBlockId].dataTimestamp;\n\n    require(blocks[_blockId].dataTimestamp == 0, \"blockId already taken\");\n    require(lastDataTimestamp < _dataTimestamp, \"can NOT submit older data\");\n    require(lastDataTimestamp + padding < block.timestamp, \"do not spam\");\n    require(_keys.length == _values.length, \"numbers of keys and values not the same\");\n\n    for (uint256 i = 0; i < _keys.length; i++) {\n      require(uint224(_values[i]) == _values[i], \"FCD overflow\");\n      fcds[_keys[i]] = FirstClassData(uint224(_values[i]), _dataTimestamp);\n    }\n\n    blocks[_blockId] = Block(_root, _dataTimestamp);\n    lastBlockId = _blockId;\n\n    emit LogBlockReplication(msg.sender, _blockId);\n  }\n\n  // ========== VIEWS ========== //\n\n  function isForeign() override external pure returns (bool) {\n    return true;\n  }\n\n  function getName() override external pure returns (bytes32) {\n    return \"Chain\";\n  }\n\n  function getStatus() external view returns(\n    uint256 blockNumber,\n    uint16 timePadding,\n    uint32 lastDataTimestamp,\n    uint32 lastId,\n    uint32 nextBlockId\n  ) {\n    blockNumber = block.number;\n    timePadding = padding;\n    lastId = lastBlockId;\n    lastDataTimestamp = blocks[lastId].dataTimestamp;\n    nextBlockId = getBlockIdAtTimestamp(block.timestamp + 1);\n  }\n\n  // this function does not works for past timestamps\n  function getBlockIdAtTimestamp(uint256 _timestamp) override public view  returns (uint32) {\n    uint32 _lastId = lastBlockId;\n\n    if (blocks[_lastId].dataTimestamp == 0) {\n      return 0;\n    }\n\n    if (blocks[_lastId].dataTimestamp + padding < _timestamp) {\n      return _lastId + 1;\n    }\n\n    return _lastId;\n  }\n\n  function getLatestBlockId() override public view returns (uint32) {\n    return lastBlockId;\n  }\n}\n"
    },
    "contracts/Registry.sol": {
      "content": "//SPDX-License-Identifier: MIT\npragma solidity 0.6.8;\n\n// Inheritance\nimport \"@openzeppelin/contracts/access/Ownable.sol\";\n\nimport \"./extensions/Registrable.sol\";\n\ncontract Registry is Ownable {\n  mapping(bytes32 => address) public registry;\n\n  // ========== EVENTS ========== //\n\n  event LogRegistered(address indexed destination, bytes32 name);\n\n  // ========== MUTATIVE FUNCTIONS ========== //\n\n  function importAddresses(bytes32[] calldata _names, address[] calldata _destinations) external onlyOwner {\n    require(_names.length == _destinations.length, \"Input lengths must match\");\n\n    for (uint i = 0; i < _names.length; i++) {\n      registry[_names[i]] = _destinations[i];\n      emit LogRegistered(_destinations[i], _names[i]);\n    }\n  }\n\n  function importContracts(address[] calldata _destinations) external onlyOwner {\n    for (uint i = 0; i < _destinations.length; i++) {\n      bytes32 name = Registrable(_destinations[i]).getName();\n      registry[name] = _destinations[i];\n      emit LogRegistered(_destinations[i], name);\n    }\n  }\n\n  function atomicUpdate(address _newContract) external onlyOwner {\n    Registrable(_newContract).register();\n\n    bytes32 name = Registrable(_newContract).getName();\n    address oldContract = registry[name];\n    registry[name] = _newContract;\n\n    Registrable(oldContract).unregister();\n\n    emit LogRegistered(_newContract, name);\n  }\n\n  // ========== VIEWS ========== //\n\n  function requireAndGetAddress(bytes32 name) external view returns (address) {\n    address _foundAddress = registry[name];\n    require(_foundAddress != address(0), string(abi.encodePacked(\"Name not registered: \", name)));\n    return _foundAddress;\n  }\n\n  function getAddress(bytes32 _bytes) external view returns (address) {\n    return registry[_bytes];\n  }\n\n  function getAddressByString(string memory _name) public view returns (address) {\n    return registry[stringToBytes32(_name)];\n  }\n\n  function stringToBytes32(string memory _string) public pure returns (bytes32 result) {\n    bytes memory tempEmptyStringTest = bytes(_string);\n\n    if (tempEmptyStringTest.length == 0) {\n      return 0x0;\n    }\n\n    assembly {\n      result := mload(add(_string, 32))\n    }\n  }\n}\n"
    },
    "contracts/BaseChain.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.6.8;\npragma experimental ABIEncoderV2;\n\nimport \"@openzeppelin/contracts/cryptography/MerkleProof.sol\";\nimport \"@openzeppelin/contracts/access/Ownable.sol\";\nimport \"@umb-network/toolbox/dist/contracts/lib/ValueDecoder.sol\";\n\nimport \"./interfaces/IStakingBank.sol\";\n\nimport \"./extensions/Registrable.sol\";\nimport \"./Registry.sol\";\n\nabstract contract BaseChain is Registrable, Ownable {\n  using ValueDecoder for bytes;\n\n  // ========== STATE VARIABLES ========== //\n\n  bytes constant public ETH_PREFIX = \"\\x19Ethereum Signed Message:\\n32\";\n\n  struct Block {\n    bytes32 root;\n    uint32 dataTimestamp;\n  }\n\n  struct FirstClassData {\n    uint224 value;\n    uint32 dataTimestamp;\n  }\n\n  mapping(uint256 => Block) public blocks;\n  mapping(bytes32 => FirstClassData) public fcds;\n\n  uint32 public blocksCount;\n  uint32 public immutable blocksCountOffset;\n  uint16 public padding;\n  uint16 public immutable requiredSignatures;\n\n  // ========== CONSTRUCTOR ========== //\n\n  constructor(\n    address _contractRegistry,\n    uint16 _padding,\n    uint16 _requiredSignatures // we have a plan to use signatures also in foreign Chains so lets keep it in base\n  ) public Registrable(_contractRegistry) {\n    padding = _padding;\n    requiredSignatures = _requiredSignatures;\n    BaseChain oldChain = BaseChain(Registry(_contractRegistry).getAddress(\"Chain\"));\n\n    blocksCountOffset = address(oldChain) != address(0x0)\n      // +1 because it might be situation when tx is already in progress in old contract\n      ? oldChain.blocksCount() + oldChain.blocksCountOffset() + 1\n      : 0;\n  }\n\n  // ========== MUTATIVE FUNCTIONS ========== //\n\n  function setPadding(uint16 _padding) external onlyOwner {\n    padding = _padding;\n    emit LogPadding(msg.sender, _padding);\n  }\n\n  // ========== VIEWS ========== //\n\n  function isForeign() virtual external pure returns (bool);\n\n  function recoverSigner(bytes32 _affidavit, uint8 _v, bytes32 _r, bytes32 _s) public pure returns (address) {\n    bytes32 hash = keccak256(abi.encodePacked(ETH_PREFIX, _affidavit));\n    return ecrecover(hash, _v, _r, _s);\n  }\n\n  function getBlockId() public view returns (uint32) {\n    return getBlockIdAtTimestamp(block.timestamp);\n  }\n\n  // this function does not works for past timestamps\n  function getBlockIdAtTimestamp(uint256 _timestamp) virtual public view returns (uint32) {\n    uint32 _blocksCount = blocksCount + blocksCountOffset;\n\n    if (_blocksCount == 0) {\n      return 0;\n    }\n\n    if (blocks[_blocksCount - 1].dataTimestamp + padding < _timestamp) {\n      return _blocksCount;\n    }\n\n    return _blocksCount - 1;\n  }\n\n  function getLatestBlockId() virtual public view returns (uint32) {\n    return blocksCount + blocksCountOffset - 1;\n  }\n\n  function verifyProof(bytes32[] memory _proof, bytes32 _root, bytes32 _leaf) public pure returns (bool) {\n    if (_root == bytes32(0)) {\n      return false;\n    }\n\n    return MerkleProof.verify(_proof, _root, _leaf);\n  }\n\n  function hashLeaf(bytes memory _key, bytes memory _value) public pure returns (bytes32) {\n    return keccak256(abi.encodePacked(_key, _value));\n  }\n\n  function verifyProofForBlock(\n    uint256 _blockId,\n    bytes32[] memory _proof,\n    bytes memory _key,\n    bytes memory _value\n  ) public view returns (bool) {\n    return verifyProof(_proof, blocks[_blockId].root, keccak256(abi.encodePacked(_key, _value)));\n  }\n\n  function bytesToBytes32Array(\n    bytes memory _data,\n    uint256 _offset,\n    uint256 _items\n  ) public pure returns (bytes32[] memory) {\n    bytes32[] memory dataList = new bytes32[](_items);\n\n    for (uint256 i = 0; i < _items; i++) {\n      bytes32 temp;\n      uint256 idx = (i + 1 + _offset) * 32;\n\n      assembly {\n        temp := mload(add(_data, idx))\n      }\n\n      dataList[i] = temp;\n    }\n\n    return (dataList);\n  }\n\n  function verifyProofs(\n    uint32[] memory _blockIds,\n    bytes memory _proofs,\n    uint256[] memory _proofItemsCounter,\n    bytes32[] memory _leaves\n  ) public view returns (bool[] memory results) {\n    results = new bool[](_leaves.length);\n    uint256 offset = 0;\n\n    for (uint256 i = 0; i < _leaves.length; i++) {\n      results[i] = verifyProof(\n        bytesToBytes32Array(_proofs, offset, _proofItemsCounter[i]),\n        blocks[_blockIds[i]].root,\n        _leaves[i]\n      );\n\n      offset += _proofItemsCounter[i];\n    }\n  }\n\n  function getBlockRoot(uint32 _blockId) external view returns (bytes32) {\n    return blocks[_blockId].root;\n  }\n\n  function getBlockTimestamp(uint32 _blockId) external view returns (uint32) {\n    return blocks[_blockId].dataTimestamp;\n  }\n\n  function getCurrentValues(bytes32[] calldata _keys)\n  external view returns (uint256[] memory values, uint32[] memory timestamps) {\n    timestamps = new uint32[](_keys.length);\n    values = new uint256[](_keys.length);\n\n    for (uint i=0; i<_keys.length; i++) {\n      FirstClassData storage numericFCD = fcds[_keys[i]];\n      values[i] = uint256(numericFCD.value);\n      timestamps[i] = numericFCD.dataTimestamp;\n    }\n  }\n\n  function getCurrentValue(bytes32 _key) external view returns (uint256 value, uint256 timestamp) {\n    FirstClassData storage numericFCD = fcds[_key];\n    return (uint256(numericFCD.value), numericFCD.dataTimestamp);\n  }\n\n  // ========== EVENTS ========== //\n\n  event LogPadding(address indexed executor, uint16 timePadding);\n}\n"
    },
    "@openzeppelin/contracts/access/Ownable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.6.0;\n\nimport \"../GSN/Context.sol\";\n/**\n * @dev Contract module which provides a basic access control mechanism, where\n * there is an account (an owner) that can be granted exclusive access to\n * specific functions.\n *\n * By default, the owner account will be the one that deploys the contract. This\n * can later be changed with {transferOwnership}.\n *\n * This module is used through inheritance. It will make available the modifier\n * `onlyOwner`, which can be applied to your functions to restrict their use to\n * the owner.\n */\ncontract Ownable is Context {\n    address private _owner;\n\n    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);\n\n    /**\n     * @dev Initializes the contract setting the deployer as the initial owner.\n     */\n    constructor () internal {\n        address msgSender = _msgSender();\n        _owner = msgSender;\n        emit OwnershipTransferred(address(0), msgSender);\n    }\n\n    /**\n     * @dev Returns the address of the current owner.\n     */\n    function owner() public view returns (address) {\n        return _owner;\n    }\n\n    /**\n     * @dev Throws if called by any account other than the owner.\n     */\n    modifier onlyOwner() {\n        require(_owner == _msgSender(), \"Ownable: caller is not the owner\");\n        _;\n    }\n\n    /**\n     * @dev Leaves the contract without owner. It will not be possible to call\n     * `onlyOwner` functions anymore. Can only be called by the current owner.\n     *\n     * NOTE: Renouncing ownership will leave the contract without an owner,\n     * thereby removing any functionality that is only available to the owner.\n     */\n    function renounceOwnership() public virtual onlyOwner {\n        emit OwnershipTransferred(_owner, address(0));\n        _owner = address(0);\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Can only be called by the current owner.\n     */\n    function transferOwnership(address newOwner) public virtual onlyOwner {\n        require(newOwner != address(0), \"Ownable: new owner is the zero address\");\n        emit OwnershipTransferred(_owner, newOwner);\n        _owner = newOwner;\n    }\n}\n"
    },
    "contracts/extensions/Registrable.sol": {
      "content": "//SPDX-License-Identifier: MIT\npragma solidity 0.6.8;\n\nimport \"@openzeppelin/contracts/token/ERC20/ERC20.sol\";\nimport \"../interfaces/IRegistry.sol\";\nimport \"../interfaces/IStakingBank.sol\";\n\nabstract contract Registrable {\n  IRegistry public contractRegistry;\n\n  // ========== CONSTRUCTOR ========== //\n\n  constructor(address _contractRegistry) internal {\n    require(_contractRegistry != address(0x0), \"_registry is empty\");\n    contractRegistry = IRegistry(_contractRegistry);\n  }\n\n  // ========== MODIFIERS ========== //\n\n  modifier onlyFromContract(address _msgSender, bytes32 _contractName) {\n    require(\n      contractRegistry.getAddress(_contractName) == _msgSender,\n        string(abi.encodePacked(\"caller is not \", _contractName))\n    );\n    _;\n  }\n\n  modifier withRegistrySetUp() {\n    require(address(contractRegistry) != address(0x0), \"_registry is empty\");\n    _;\n  }\n\n  // ========== MUTATIVE ========== //\n\n  function register() virtual external {\n    // this is required only for ForeignChain\n    // but for backward compatibility the body is implemented as empty\n    // also note, that in order to use this method, we need new registry\n  }\n\n  function unregister() virtual external {\n    // this is required only for ForeignChain\n    // but for backward compatibility the body is implemented as empty\n    // also note, that in order to use this method, we need new registry\n  }\n\n  // ========== VIEWS ========== //\n\n  function getName() virtual external pure returns (bytes32);\n\n  function stakingBankContract() public view returns (IStakingBank) {\n    return IStakingBank(contractRegistry.requireAndGetAddress(\"StakingBank\"));\n  }\n\n  function tokenContract() public view withRegistrySetUp returns (ERC20) {\n    return ERC20(contractRegistry.requireAndGetAddress(\"UMB\"));\n  }\n}\n"
    },
    "@openzeppelin/contracts/GSN/Context.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.6.0;\n\n/*\n * @dev Provides information about the current execution context, including the\n * sender of the transaction and its data. While these are generally available\n * via msg.sender and msg.data, they should not be accessed in such a direct\n * manner, since when dealing with GSN meta-transactions the account sending and\n * paying for execution may not be the actual sender (as far as an application\n * is concerned).\n *\n * This contract is only required for intermediate, library-like contracts.\n */\nabstract contract Context {\n    function _msgSender() internal view virtual returns (address payable) {\n        return msg.sender;\n    }\n\n    function _msgData() internal view virtual returns (bytes memory) {\n        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691\n        return msg.data;\n    }\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/ERC20.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.6.0;\n\nimport \"../../GSN/Context.sol\";\nimport \"./IERC20.sol\";\nimport \"../../math/SafeMath.sol\";\nimport \"../../utils/Address.sol\";\n\n/**\n * @dev Implementation of the {IERC20} interface.\n *\n * This implementation is agnostic to the way tokens are created. This means\n * that a supply mechanism has to be added in a derived contract using {_mint}.\n * For a generic mechanism see {ERC20PresetMinterPauser}.\n *\n * TIP: For a detailed writeup see our guide\n * https://forum.zeppelin.solutions/t/how-to-implement-erc20-supply-mechanisms/226[How\n * to implement supply mechanisms].\n *\n * We have followed general OpenZeppelin guidelines: functions revert instead\n * of returning `false` on failure. This behavior is nonetheless conventional\n * and does not conflict with the expectations of ERC20 applications.\n *\n * Additionally, an {Approval} event is emitted on calls to {transferFrom}.\n * This allows applications to reconstruct the allowance for all accounts just\n * by listening to said events. Other implementations of the EIP may not emit\n * these events, as it isn't required by the specification.\n *\n * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}\n * functions have been added to mitigate the well-known issues around setting\n * allowances. See {IERC20-approve}.\n */\ncontract ERC20 is Context, IERC20 {\n    using SafeMath for uint256;\n    using Address for address;\n\n    mapping (address => uint256) private _balances;\n\n    mapping (address => mapping (address => uint256)) private _allowances;\n\n    uint256 private _totalSupply;\n\n    string private _name;\n    string private _symbol;\n    uint8 private _decimals;\n\n    /**\n     * @dev Sets the values for {name} and {symbol}, initializes {decimals} with\n     * a default value of 18.\n     *\n     * To select a different value for {decimals}, use {_setupDecimals}.\n     *\n     * All three of these values are immutable: they can only be set once during\n     * construction.\n     */\n    constructor (string memory name, string memory symbol) public {\n        _name = name;\n        _symbol = symbol;\n        _decimals = 18;\n    }\n\n    /**\n     * @dev Returns the name of the token.\n     */\n    function name() public view returns (string memory) {\n        return _name;\n    }\n\n    /**\n     * @dev Returns the symbol of the token, usually a shorter version of the\n     * name.\n     */\n    function symbol() public view returns (string memory) {\n        return _symbol;\n    }\n\n    /**\n     * @dev Returns the number of decimals used to get its user representation.\n     * For example, if `decimals` equals `2`, a balance of `505` tokens should\n     * be displayed to a user as `5,05` (`505 / 10 ** 2`).\n     *\n     * Tokens usually opt for a value of 18, imitating the relationship between\n     * Ether and Wei. This is the value {ERC20} uses, unless {_setupDecimals} is\n     * called.\n     *\n     * NOTE: This information is only used for _display_ purposes: it in\n     * no way affects any of the arithmetic of the contract, including\n     * {IERC20-balanceOf} and {IERC20-transfer}.\n     */\n    function decimals() public view returns (uint8) {\n        return _decimals;\n    }\n\n    /**\n     * @dev See {IERC20-totalSupply}.\n     */\n    function totalSupply() public view override returns (uint256) {\n        return _totalSupply;\n    }\n\n    /**\n     * @dev See {IERC20-balanceOf}.\n     */\n    function balanceOf(address account) public view override returns (uint256) {\n        return _balances[account];\n    }\n\n    /**\n     * @dev See {IERC20-transfer}.\n     *\n     * Requirements:\n     *\n     * - `recipient` cannot be the zero address.\n     * - the caller must have a balance of at least `amount`.\n     */\n    function transfer(address recipient, uint256 amount) public virtual override returns (bool) {\n        _transfer(_msgSender(), recipient, amount);\n        return true;\n    }\n\n    /**\n     * @dev See {IERC20-allowance}.\n     */\n    function allowance(address owner, address spender) public view virtual override returns (uint256) {\n        return _allowances[owner][spender];\n    }\n\n    /**\n     * @dev See {IERC20-approve}.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     */\n    function approve(address spender, uint256 amount) public virtual override returns (bool) {\n        _approve(_msgSender(), spender, amount);\n        return true;\n    }\n\n    /**\n     * @dev See {IERC20-transferFrom}.\n     *\n     * Emits an {Approval} event indicating the updated allowance. This is not\n     * required by the EIP. See the note at the beginning of {ERC20};\n     *\n     * Requirements:\n     * - `sender` and `recipient` cannot be the zero address.\n     * - `sender` must have a balance of at least `amount`.\n     * - the caller must have allowance for ``sender``'s tokens of at least\n     * `amount`.\n     */\n    function transferFrom(address sender, address recipient, uint256 amount) public virtual override returns (bool) {\n        _transfer(sender, recipient, amount);\n        _approve(sender, _msgSender(), _allowances[sender][_msgSender()].sub(amount, \"ERC20: transfer amount exceeds allowance\"));\n        return true;\n    }\n\n    /**\n     * @dev Atomically increases the allowance granted to `spender` by the caller.\n     *\n     * This is an alternative to {approve} that can be used as a mitigation for\n     * problems described in {IERC20-approve}.\n     *\n     * Emits an {Approval} event indicating the updated allowance.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     */\n    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {\n        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].add(addedValue));\n        return true;\n    }\n\n    /**\n     * @dev Atomically decreases the allowance granted to `spender` by the caller.\n     *\n     * This is an alternative to {approve} that can be used as a mitigation for\n     * problems described in {IERC20-approve}.\n     *\n     * Emits an {Approval} event indicating the updated allowance.\n     *\n     * Requirements:\n     *\n     * - `spender` cannot be the zero address.\n     * - `spender` must have allowance for the caller of at least\n     * `subtractedValue`.\n     */\n    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {\n        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].sub(subtractedValue, \"ERC20: decreased allowance below zero\"));\n        return true;\n    }\n\n    /**\n     * @dev Moves tokens `amount` from `sender` to `recipient`.\n     *\n     * This is internal function is equivalent to {transfer}, and can be used to\n     * e.g. implement automatic token fees, slashing mechanisms, etc.\n     *\n     * Emits a {Transfer} event.\n     *\n     * Requirements:\n     *\n     * - `sender` cannot be the zero address.\n     * - `recipient` cannot be the zero address.\n     * - `sender` must have a balance of at least `amount`.\n     */\n    function _transfer(address sender, address recipient, uint256 amount) internal virtual {\n        require(sender != address(0), \"ERC20: transfer from the zero address\");\n        require(recipient != address(0), \"ERC20: transfer to the zero address\");\n\n        _beforeTokenTransfer(sender, recipient, amount);\n\n        _balances[sender] = _balances[sender].sub(amount, \"ERC20: transfer amount exceeds balance\");\n        _balances[recipient] = _balances[recipient].add(amount);\n        emit Transfer(sender, recipient, amount);\n    }\n\n    /** @dev Creates `amount` tokens and assigns them to `account`, increasing\n     * the total supply.\n     *\n     * Emits a {Transfer} event with `from` set to the zero address.\n     *\n     * Requirements\n     *\n     * - `to` cannot be the zero address.\n     */\n    function _mint(address account, uint256 amount) internal virtual {\n        require(account != address(0), \"ERC20: mint to the zero address\");\n\n        _beforeTokenTransfer(address(0), account, amount);\n\n        _totalSupply = _totalSupply.add(amount);\n        _balances[account] = _balances[account].add(amount);\n        emit Transfer(address(0), account, amount);\n    }\n\n    /**\n     * @dev Destroys `amount` tokens from `account`, reducing the\n     * total supply.\n     *\n     * Emits a {Transfer} event with `to` set to the zero address.\n     *\n     * Requirements\n     *\n     * - `account` cannot be the zero address.\n     * - `account` must have at least `amount` tokens.\n     */\n    function _burn(address account, uint256 amount) internal virtual {\n        require(account != address(0), \"ERC20: burn from the zero address\");\n\n        _beforeTokenTransfer(account, address(0), amount);\n\n        _balances[account] = _balances[account].sub(amount, \"ERC20: burn amount exceeds balance\");\n        _totalSupply = _totalSupply.sub(amount);\n        emit Transfer(account, address(0), amount);\n    }\n\n    /**\n     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.\n     *\n     * This internal function is equivalent to `approve`, and can be used to\n     * e.g. set automatic allowances for certain subsystems, etc.\n     *\n     * Emits an {Approval} event.\n     *\n     * Requirements:\n     *\n     * - `owner` cannot be the zero address.\n     * - `spender` cannot be the zero address.\n     */\n    function _approve(address owner, address spender, uint256 amount) internal virtual {\n        require(owner != address(0), \"ERC20: approve from the zero address\");\n        require(spender != address(0), \"ERC20: approve to the zero address\");\n\n        _allowances[owner][spender] = amount;\n        emit Approval(owner, spender, amount);\n    }\n\n    /**\n     * @dev Sets {decimals} to a value other than the default one of 18.\n     *\n     * WARNING: This function should only be called from the constructor. Most\n     * applications that interact with token contracts will not expect\n     * {decimals} to ever change, and may work incorrectly if it does.\n     */\n    function _setupDecimals(uint8 decimals_) internal {\n        _decimals = decimals_;\n    }\n\n    /**\n     * @dev Hook that is called before any transfer of tokens. This includes\n     * minting and burning.\n     *\n     * Calling conditions:\n     *\n     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens\n     * will be to transferred to `to`.\n     * - when `from` is zero, `amount` tokens will be minted for `to`.\n     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.\n     * - `from` and `to` are never both zero.\n     *\n     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].\n     */\n    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual { }\n}\n"
    },
    "contracts/interfaces/IRegistry.sol": {
      "content": "//SPDX-License-Identifier: MIT\npragma solidity 0.6.8;\n\n\ninterface IRegistry {\n    function getAddress(bytes32 name) external view returns (address);\n\n    function requireAndGetAddress(bytes32 name) external view returns (address);\n}\n"
    },
    "contracts/interfaces/IStakingBank.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.6.8;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\n\ninterface IStakingBank is IERC20 {\n  function receiveApproval(address _from) external returns (bool success);\n\n  function withdraw(uint256 _value) external returns (bool success);\n\n  function create(address _id, string calldata _location) external;\n\n  function update(address _id, string calldata _location) external;\n\n  function addresses(uint256 _ix) external view returns (address);\n\n  function validators(address _id) external view returns (address id, string memory location);\n\n  function getNumberOfValidators() external view returns (uint256);\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/IERC20.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.6.0;\n\n/**\n * @dev Interface of the ERC20 standard as defined in the EIP.\n */\ninterface IERC20 {\n    /**\n     * @dev Returns the amount of tokens in existence.\n     */\n    function totalSupply() external view returns (uint256);\n\n    /**\n     * @dev Returns the amount of tokens owned by `account`.\n     */\n    function balanceOf(address account) external view returns (uint256);\n\n    /**\n     * @dev Moves `amount` tokens from the caller's account to `recipient`.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transfer(address recipient, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Returns the remaining number of tokens that `spender` will be\n     * allowed to spend on behalf of `owner` through {transferFrom}. This is\n     * zero by default.\n     *\n     * This value changes when {approve} or {transferFrom} are called.\n     */\n    function allowance(address owner, address spender) external view returns (uint256);\n\n    /**\n     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * IMPORTANT: Beware that changing an allowance with this method brings the risk\n     * that someone may use both the old and the new allowance by unfortunate\n     * transaction ordering. One possible solution to mitigate this race\n     * condition is to first reduce the spender's allowance to 0 and set the\n     * desired value afterwards:\n     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729\n     *\n     * Emits an {Approval} event.\n     */\n    function approve(address spender, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Moves `amount` tokens from `sender` to `recipient` using the\n     * allowance mechanism. `amount` is then deducted from the caller's\n     * allowance.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Emitted when `value` tokens are moved from one account (`from`) to\n     * another (`to`).\n     *\n     * Note that `value` may be zero.\n     */\n    event Transfer(address indexed from, address indexed to, uint256 value);\n\n    /**\n     * @dev Emitted when the allowance of a `spender` for an `owner` is set by\n     * a call to {approve}. `value` is the new allowance.\n     */\n    event Approval(address indexed owner, address indexed spender, uint256 value);\n}\n"
    },
    "@openzeppelin/contracts/math/SafeMath.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.6.0;\n\n/**\n * @dev Wrappers over Solidity's arithmetic operations with added overflow\n * checks.\n *\n * Arithmetic operations in Solidity wrap on overflow. This can easily result\n * in bugs, because programmers usually assume that an overflow raises an\n * error, which is the standard behavior in high level programming languages.\n * `SafeMath` restores this intuition by reverting the transaction when an\n * operation overflows.\n *\n * Using this library instead of the unchecked operations eliminates an entire\n * class of bugs, so it's recommended to use it always.\n */\nlibrary SafeMath {\n    /**\n     * @dev Returns the addition of two unsigned integers, reverting on\n     * overflow.\n     *\n     * Counterpart to Solidity's `+` operator.\n     *\n     * Requirements:\n     *\n     * - Addition cannot overflow.\n     */\n    function add(uint256 a, uint256 b) internal pure returns (uint256) {\n        uint256 c = a + b;\n        require(c >= a, \"SafeMath: addition overflow\");\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the subtraction of two unsigned integers, reverting on\n     * overflow (when the result is negative).\n     *\n     * Counterpart to Solidity's `-` operator.\n     *\n     * Requirements:\n     *\n     * - Subtraction cannot overflow.\n     */\n    function sub(uint256 a, uint256 b) internal pure returns (uint256) {\n        return sub(a, b, \"SafeMath: subtraction overflow\");\n    }\n\n    /**\n     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on\n     * overflow (when the result is negative).\n     *\n     * Counterpart to Solidity's `-` operator.\n     *\n     * Requirements:\n     *\n     * - Subtraction cannot overflow.\n     */\n    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {\n        require(b <= a, errorMessage);\n        uint256 c = a - b;\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the multiplication of two unsigned integers, reverting on\n     * overflow.\n     *\n     * Counterpart to Solidity's `*` operator.\n     *\n     * Requirements:\n     *\n     * - Multiplication cannot overflow.\n     */\n    function mul(uint256 a, uint256 b) internal pure returns (uint256) {\n        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the\n        // benefit is lost if 'b' is also tested.\n        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522\n        if (a == 0) {\n            return 0;\n        }\n\n        uint256 c = a * b;\n        require(c / a == b, \"SafeMath: multiplication overflow\");\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the integer division of two unsigned integers. Reverts on\n     * division by zero. The result is rounded towards zero.\n     *\n     * Counterpart to Solidity's `/` operator. Note: this function uses a\n     * `revert` opcode (which leaves remaining gas untouched) while Solidity\n     * uses an invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     *\n     * - The divisor cannot be zero.\n     */\n    function div(uint256 a, uint256 b) internal pure returns (uint256) {\n        return div(a, b, \"SafeMath: division by zero\");\n    }\n\n    /**\n     * @dev Returns the integer division of two unsigned integers. Reverts with custom message on\n     * division by zero. The result is rounded towards zero.\n     *\n     * Counterpart to Solidity's `/` operator. Note: this function uses a\n     * `revert` opcode (which leaves remaining gas untouched) while Solidity\n     * uses an invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     *\n     * - The divisor cannot be zero.\n     */\n    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {\n        require(b > 0, errorMessage);\n        uint256 c = a / b;\n        // assert(a == b * c + a % b); // There is no case in which this doesn't hold\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),\n     * Reverts when dividing by zero.\n     *\n     * Counterpart to Solidity's `%` operator. This function uses a `revert`\n     * opcode (which leaves remaining gas untouched) while Solidity uses an\n     * invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     *\n     * - The divisor cannot be zero.\n     */\n    function mod(uint256 a, uint256 b) internal pure returns (uint256) {\n        return mod(a, b, \"SafeMath: modulo by zero\");\n    }\n\n    /**\n     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),\n     * Reverts with custom message when dividing by zero.\n     *\n     * Counterpart to Solidity's `%` operator. This function uses a `revert`\n     * opcode (which leaves remaining gas untouched) while Solidity uses an\n     * invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     *\n     * - The divisor cannot be zero.\n     */\n    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {\n        require(b != 0, errorMessage);\n        return a % b;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Address.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.6.2;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary Address {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * [IMPORTANT]\n     * ====\n     * It is unsafe to assume that an address for which this function returns\n     * false is an externally-owned account (EOA) and not a contract.\n     *\n     * Among others, `isContract` will return false for the following\n     * types of addresses:\n     *\n     *  - an externally-owned account\n     *  - a contract in construction\n     *  - an address where a contract will be created\n     *  - an address where a contract lived, but was destroyed\n     * ====\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies in extcodesize, which returns 0 for contracts in\n        // construction, since the code is only stored at the end of the\n        // constructor execution.\n\n        uint256 size;\n        // solhint-disable-next-line no-inline-assembly\n        assembly { size := extcodesize(account) }\n        return size > 0;\n    }\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        require(address(this).balance >= amount, \"Address: insufficient balance\");\n\n        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value\n        (bool success, ) = recipient.call{ value: amount }(\"\");\n        require(success, \"Address: unable to send value, recipient may have reverted\");\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain`call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason, it is bubbled up by this\n     * function (like regular Solidity function calls).\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n      return functionCall(target, data, \"Address: low-level call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with\n     * `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {\n        return _functionCallWithValue(target, data, 0, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, value, \"Address: low-level call with value failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but\n     * with `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value, string memory errorMessage) internal returns (bytes memory) {\n        require(address(this).balance >= value, \"Address: insufficient balance for call\");\n        return _functionCallWithValue(target, data, value, errorMessage);\n    }\n\n    function _functionCallWithValue(address target, bytes memory data, uint256 weiValue, string memory errorMessage) private returns (bytes memory) {\n        require(isContract(target), \"Address: call to non-contract\");\n\n        // solhint-disable-next-line avoid-low-level-calls\n        (bool success, bytes memory returndata) = target.call{ value: weiValue }(data);\n        if (success) {\n            return returndata;\n        } else {\n            // Look for revert reason and bubble it up if present\n            if (returndata.length > 0) {\n                // The easiest way to bubble the revert reason is using memory via assembly\n\n                // solhint-disable-next-line no-inline-assembly\n                assembly {\n                    let returndata_size := mload(returndata)\n                    revert(add(32, returndata), returndata_size)\n                }\n            } else {\n                revert(errorMessage);\n            }\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/cryptography/MerkleProof.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.6.0;\n\n/**\n * @dev These functions deal with verification of Merkle trees (hash trees),\n */\nlibrary MerkleProof {\n    /**\n     * @dev Returns true if a `leaf` can be proved to be a part of a Merkle tree\n     * defined by `root`. For this, a `proof` must be provided, containing\n     * sibling hashes on the branch from the leaf to the root of the tree. Each\n     * pair of leaves and each pair of pre-images are assumed to be sorted.\n     */\n    function verify(bytes32[] memory proof, bytes32 root, bytes32 leaf) internal pure returns (bool) {\n        bytes32 computedHash = leaf;\n\n        for (uint256 i = 0; i < proof.length; i++) {\n            bytes32 proofElement = proof[i];\n\n            if (computedHash <= proofElement) {\n                // Hash(current computed hash + current element of the proof)\n                computedHash = keccak256(abi.encodePacked(computedHash, proofElement));\n            } else {\n                // Hash(current element of the proof + current computed hash)\n                computedHash = keccak256(abi.encodePacked(proofElement, computedHash));\n            }\n        }\n\n        // Check if the computed hash (root) is equal to the provided root\n        return computedHash == root;\n    }\n}\n"
    },
    "@umb-network/toolbox/dist/contracts/lib/ValueDecoder.sol": {
      "content": "//SPDX-License-Identifier: Unlicensed\npragma solidity >=0.6.8;\n\nlibrary ValueDecoder {\n  function toUint(bytes memory _bytes) internal pure returns (uint256 value) {\n    assembly {\n      value := mload(add(_bytes, 32))\n    }\n  }\n\n  function toUint(bytes32 _bytes) internal pure returns (uint256 value) {\n    assembly {\n      value := _bytes\n    }\n  }\n}\n"
    }
  },
  "settings": {
    "optimizer": {
      "enabled": false,
      "runs": 200
    },
    "outputSelection": {
      "*": {
        "*": [
          "evm.bytecode",
          "evm.deployedBytecode",
          "abi"
        ]
      }
    },
    "metadata": {
      "useLiteralContent": true
    },
    "libraries": {}
  }
}}