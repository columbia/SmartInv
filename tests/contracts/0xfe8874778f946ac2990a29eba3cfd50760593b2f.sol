{{
  "language": "Solidity",
  "sources": {
    "contracts/XAppConnectionManager.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.6.11;\n\n// ============ Internal Imports ============\nimport {Home} from \"./Home.sol\";\nimport {Replica} from \"./Replica.sol\";\nimport {TypeCasts} from \"../libs/TypeCasts.sol\";\n// ============ External Imports ============\nimport {ECDSA} from \"@openzeppelin/contracts/cryptography/ECDSA.sol\";\nimport {Ownable} from \"@openzeppelin/contracts/access/Ownable.sol\";\n\n/**\n * @title XAppConnectionManager\n * @author Illusory Systems Inc.\n * @notice Manages a registry of local Replica contracts\n * for remote Home domains. Accepts Watcher signatures\n * to un-enroll Replicas attached to fraudulent remote Homes\n */\ncontract XAppConnectionManager is Ownable {\n    // ============ Public Storage ============\n\n    // Home contract\n    Home public home;\n    // local Replica address => remote Home domain\n    mapping(address => uint32) public replicaToDomain;\n    // remote Home domain => local Replica address\n    mapping(uint32 => address) public domainToReplica;\n    // watcher address => replica remote domain => has/doesn't have permission\n    mapping(address => mapping(uint32 => bool)) private watcherPermissions;\n\n    // ============ Events ============\n\n    /**\n     * @notice Emitted when a new Replica is enrolled / added\n     * @param domain the remote domain of the Home contract for the Replica\n     * @param replica the address of the Replica\n     */\n    event ReplicaEnrolled(uint32 indexed domain, address replica);\n\n    /**\n     * @notice Emitted when a new Replica is un-enrolled / removed\n     * @param domain the remote domain of the Home contract for the Replica\n     * @param replica the address of the Replica\n     */\n    event ReplicaUnenrolled(uint32 indexed domain, address replica);\n\n    /**\n     * @notice Emitted when Watcher permissions are changed\n     * @param domain the remote domain of the Home contract for the Replica\n     * @param watcher the address of the Watcher\n     * @param access TRUE if the Watcher was given permissions, FALSE if permissions were removed\n     */\n    event WatcherPermissionSet(\n        uint32 indexed domain,\n        address watcher,\n        bool access\n    );\n\n    // ============ Modifiers ============\n\n    modifier onlyReplica() {\n        require(isReplica(msg.sender), \"!replica\");\n        _;\n    }\n\n    // ============ Constructor ============\n\n    // solhint-disable-next-line no-empty-blocks\n    constructor() Ownable() {}\n\n    // ============ External Functions ============\n\n    /**\n     * @notice Un-Enroll a replica contract\n     * in the case that fraud was detected on the Home\n     * @dev in the future, if fraud occurs on the Home contract,\n     * the Watcher will submit their signature directly to the Home\n     * and it can be relayed to all remote chains to un-enroll the Replicas\n     * @param _domain the remote domain of the Home contract for the Replica\n     * @param _updater the address of the Updater for the Home contract (also stored on Replica)\n     * @param _signature signature of watcher on (domain, replica address, updater address)\n     */\n    function unenrollReplica(\n        uint32 _domain,\n        bytes32 _updater,\n        bytes memory _signature\n    ) external {\n        // ensure that the replica is currently set\n        address _replica = domainToReplica[_domain];\n        require(_replica != address(0), \"!replica exists\");\n        // ensure that the signature is on the proper updater\n        require(\n            Replica(_replica).updater() == TypeCasts.bytes32ToAddress(_updater),\n            \"!current updater\"\n        );\n        // get the watcher address from the signature\n        // and ensure that the watcher has permission to un-enroll this replica\n        address _watcher = _recoverWatcherFromSig(\n            _domain,\n            TypeCasts.addressToBytes32(_replica),\n            _updater,\n            _signature\n        );\n        require(watcherPermissions[_watcher][_domain], \"!valid watcher\");\n        // remove the replica from mappings\n        _unenrollReplica(_replica);\n    }\n\n    /**\n     * @notice Set the address of the local Home contract\n     * @param _home the address of the local Home contract\n     */\n    function setHome(address _home) external onlyOwner {\n        home = Home(_home);\n    }\n\n    /**\n     * @notice Allow Owner to enroll Replica contract\n     * @param _replica the address of the Replica\n     * @param _domain the remote domain of the Home contract for the Replica\n     */\n    function ownerEnrollReplica(address _replica, uint32 _domain)\n        external\n        onlyOwner\n    {\n        // un-enroll any existing replica\n        _unenrollReplica(_replica);\n        // add replica and domain to two-way mapping\n        replicaToDomain[_replica] = _domain;\n        domainToReplica[_domain] = _replica;\n        emit ReplicaEnrolled(_domain, _replica);\n    }\n\n    /**\n     * @notice Allow Owner to un-enroll Replica contract\n     * @param _replica the address of the Replica\n     */\n    function ownerUnenrollReplica(address _replica) external onlyOwner {\n        _unenrollReplica(_replica);\n    }\n\n    /**\n     * @notice Allow Owner to set Watcher permissions for a Replica\n     * @param _watcher the address of the Watcher\n     * @param _domain the remote domain of the Home contract for the Replica\n     * @param _access TRUE to give the Watcher permissions, FALSE to remove permissions\n     */\n    function setWatcherPermission(\n        address _watcher,\n        uint32 _domain,\n        bool _access\n    ) external onlyOwner {\n        watcherPermissions[_watcher][_domain] = _access;\n        emit WatcherPermissionSet(_domain, _watcher, _access);\n    }\n\n    /**\n     * @notice Query local domain from Home\n     * @return local domain\n     */\n    function localDomain() external view returns (uint32) {\n        return home.localDomain();\n    }\n\n    /**\n     * @notice Get access permissions for the watcher on the domain\n     * @param _watcher the address of the watcher\n     * @param _domain the domain to check for watcher permissions\n     * @return TRUE iff _watcher has permission to un-enroll replicas on _domain\n     */\n    function watcherPermission(address _watcher, uint32 _domain)\n        external\n        view\n        returns (bool)\n    {\n        return watcherPermissions[_watcher][_domain];\n    }\n\n    // ============ Public Functions ============\n\n    /**\n     * @notice Check whether _replica is enrolled\n     * @param _replica the replica to check for enrollment\n     * @return TRUE iff _replica is enrolled\n     */\n    function isReplica(address _replica) public view returns (bool) {\n        return replicaToDomain[_replica] != 0;\n    }\n\n    // ============ Internal Functions ============\n\n    /**\n     * @notice Remove the replica from the two-way mappings\n     * @param _replica replica to un-enroll\n     */\n    function _unenrollReplica(address _replica) internal {\n        uint32 _currentDomain = replicaToDomain[_replica];\n        domainToReplica[_currentDomain] = address(0);\n        replicaToDomain[_replica] = 0;\n        emit ReplicaUnenrolled(_currentDomain, _replica);\n    }\n\n    /**\n     * @notice Get the Watcher address from the provided signature\n     * @return address of watcher that signed\n     */\n    function _recoverWatcherFromSig(\n        uint32 _domain,\n        bytes32 _replica,\n        bytes32 _updater,\n        bytes memory _signature\n    ) internal view returns (address) {\n        bytes32 _homeDomainHash = Replica(TypeCasts.bytes32ToAddress(_replica))\n            .homeDomainHash();\n        bytes32 _digest = keccak256(\n            abi.encodePacked(_homeDomainHash, _domain, _updater)\n        );\n        _digest = ECDSA.toEthSignedMessageHash(_digest);\n        return ECDSA.recover(_digest, _signature);\n    }\n}\n"
    },
    "contracts/Home.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.6.11;\n\n// ============ Internal Imports ============\nimport {Version0} from \"./Version0.sol\";\nimport {NomadBase} from \"./NomadBase.sol\";\nimport {QueueLib} from \"../libs/Queue.sol\";\nimport {MerkleLib} from \"../libs/Merkle.sol\";\nimport {Message} from \"../libs/Message.sol\";\nimport {MerkleTreeManager} from \"./Merkle.sol\";\nimport {QueueManager} from \"./Queue.sol\";\nimport {IUpdaterManager} from \"../interfaces/IUpdaterManager.sol\";\n// ============ External Imports ============\nimport {Address} from \"@openzeppelin/contracts/utils/Address.sol\";\n\n/**\n * @title Home\n * @author Illusory Systems Inc.\n * @notice Accepts messages to be dispatched to remote chains,\n * constructs a Merkle tree of the messages,\n * and accepts signatures from a bonded Updater\n * which notarize the Merkle tree roots.\n * Accepts submissions of fraudulent signatures\n * by the Updater and slashes the Updater in this case.\n */\ncontract Home is Version0, QueueManager, MerkleTreeManager, NomadBase {\n    // ============ Libraries ============\n\n    using QueueLib for QueueLib.Queue;\n    using MerkleLib for MerkleLib.Tree;\n\n    // ============ Constants ============\n\n    // Maximum bytes per message = 2 KiB\n    // (somewhat arbitrarily set to begin)\n    uint256 public constant MAX_MESSAGE_BODY_BYTES = 2 * 2**10;\n\n    // ============ Public Storage Variables ============\n\n    // domain => next available nonce for the domain\n    mapping(uint32 => uint32) public nonces;\n    // contract responsible for Updater bonding, slashing and rotation\n    IUpdaterManager public updaterManager;\n\n    // ============ Upgrade Gap ============\n\n    // gap for upgrade safety\n    uint256[48] private __GAP;\n\n    // ============ Events ============\n\n    /**\n     * @notice Emitted when a new message is dispatched via Nomad\n     * @param leafIndex Index of message's leaf in merkle tree\n     * @param destinationAndNonce Destination and destination-specific\n     * nonce combined in single field ((destination << 32) & nonce)\n     * @param messageHash Hash of message; the leaf inserted to the Merkle tree for the message\n     * @param committedRoot the latest notarized root submitted in the last signed Update\n     * @param message Raw bytes of message\n     */\n    event Dispatch(\n        bytes32 indexed messageHash,\n        uint256 indexed leafIndex,\n        uint64 indexed destinationAndNonce,\n        bytes32 committedRoot,\n        bytes message\n    );\n\n    /**\n     * @notice Emitted when proof of an improper update is submitted,\n     * which sets the contract to FAILED state\n     * @param oldRoot Old root of the improper update\n     * @param newRoot New root of the improper update\n     * @param signature Signature on `oldRoot` and `newRoot\n     */\n    event ImproperUpdate(bytes32 oldRoot, bytes32 newRoot, bytes signature);\n\n    /**\n     * @notice Emitted when the Updater is slashed\n     * (should be paired with ImproperUpdater or DoubleUpdate event)\n     * @param updater The address of the updater\n     * @param reporter The address of the entity that reported the updater misbehavior\n     */\n    event UpdaterSlashed(address indexed updater, address indexed reporter);\n\n    /**\n     * @notice Emitted when the UpdaterManager contract is changed\n     * @param updaterManager The address of the new updaterManager\n     */\n    event NewUpdaterManager(address updaterManager);\n\n    // ============ Constructor ============\n\n    constructor(uint32 _localDomain) NomadBase(_localDomain) {} // solhint-disable-line no-empty-blocks\n\n    // ============ Initializer ============\n\n    function initialize(IUpdaterManager _updaterManager) public initializer {\n        // initialize queue, set Updater Manager, and initialize\n        __QueueManager_initialize();\n        _setUpdaterManager(_updaterManager);\n        __NomadBase_initialize(updaterManager.updater());\n    }\n\n    // ============ Modifiers ============\n\n    /**\n     * @notice Ensures that function is called by the UpdaterManager contract\n     */\n    modifier onlyUpdaterManager() {\n        require(msg.sender == address(updaterManager), \"!updaterManager\");\n        _;\n    }\n\n    // ============ External: Updater & UpdaterManager Configuration  ============\n\n    /**\n     * @notice Set a new Updater\n     * @param _updater the new Updater\n     */\n    function setUpdater(address _updater) external onlyUpdaterManager {\n        _setUpdater(_updater);\n    }\n\n    /**\n     * @notice Set a new UpdaterManager contract\n     * @dev Home(s) will initially be initialized using a trusted UpdaterManager contract;\n     * we will progressively decentralize by swapping the trusted contract with a new implementation\n     * that implements Updater bonding & slashing, and rules for Updater selection & rotation\n     * @param _updaterManager the new UpdaterManager contract\n     */\n    function setUpdaterManager(address _updaterManager) external onlyOwner {\n        _setUpdaterManager(IUpdaterManager(_updaterManager));\n    }\n\n    // ============ External Functions  ============\n\n    /**\n     * @notice Dispatch the message it to the destination domain & recipient\n     * @dev Format the message, insert its hash into Merkle tree,\n     * enqueue the new Merkle root, and emit `Dispatch` event with message information.\n     * @param _destinationDomain Domain of destination chain\n     * @param _recipientAddress Address of recipient on destination chain as bytes32\n     * @param _messageBody Raw bytes content of message\n     */\n    function dispatch(\n        uint32 _destinationDomain,\n        bytes32 _recipientAddress,\n        bytes memory _messageBody\n    ) external notFailed {\n        require(_messageBody.length <= MAX_MESSAGE_BODY_BYTES, \"msg too long\");\n        // get the next nonce for the destination domain, then increment it\n        uint32 _nonce = nonces[_destinationDomain];\n        nonces[_destinationDomain] = _nonce + 1;\n        // format the message into packed bytes\n        bytes memory _message = Message.formatMessage(\n            localDomain,\n            bytes32(uint256(uint160(msg.sender))),\n            _nonce,\n            _destinationDomain,\n            _recipientAddress,\n            _messageBody\n        );\n        // insert the hashed message into the Merkle tree\n        bytes32 _messageHash = keccak256(_message);\n        tree.insert(_messageHash);\n        // enqueue the new Merkle root after inserting the message\n        queue.enqueue(root());\n        // Emit Dispatch event with message information\n        // note: leafIndex is count() - 1 since new leaf has already been inserted\n        emit Dispatch(\n            _messageHash,\n            count() - 1,\n            _destinationAndNonce(_destinationDomain, _nonce),\n            committedRoot,\n            _message\n        );\n    }\n\n    /**\n     * @notice Submit a signature from the Updater \"notarizing\" a root,\n     * which updates the Home contract's `committedRoot`,\n     * and publishes the signature which will be relayed to Replica contracts\n     * @dev emits Update event\n     * @dev If _newRoot is not contained in the queue,\n     * the Update is a fraudulent Improper Update, so\n     * the Updater is slashed & Home is set to FAILED state\n     * @param _committedRoot Current updated merkle root which the update is building off of\n     * @param _newRoot New merkle root to update the contract state to\n     * @param _signature Updater signature on `_committedRoot` and `_newRoot`\n     */\n    function update(\n        bytes32 _committedRoot,\n        bytes32 _newRoot,\n        bytes memory _signature\n    ) external notFailed {\n        // check that the update is not fraudulent;\n        // if fraud is detected, Updater is slashed & Home is set to FAILED state\n        if (improperUpdate(_committedRoot, _newRoot, _signature)) return;\n        // clear all of the intermediate roots contained in this update from the queue\n        while (true) {\n            bytes32 _next = queue.dequeue();\n            if (_next == _newRoot) break;\n        }\n        // update the Home state with the latest signed root & emit event\n        committedRoot = _newRoot;\n        emit Update(localDomain, _committedRoot, _newRoot, _signature);\n    }\n\n    /**\n     * @notice Suggest an update for the Updater to sign and submit.\n     * @dev If queue is empty, null bytes returned for both\n     * (No update is necessary because no messages have been dispatched since the last update)\n     * @return _committedRoot Latest root signed by the Updater\n     * @return _new Latest enqueued Merkle root\n     */\n    function suggestUpdate()\n        external\n        view\n        returns (bytes32 _committedRoot, bytes32 _new)\n    {\n        if (queue.length() != 0) {\n            _committedRoot = committedRoot;\n            _new = queue.lastItem();\n        }\n    }\n\n    // ============ Public Functions  ============\n\n    /**\n     * @notice Hash of Home domain concatenated with \"NOMAD\"\n     */\n    function homeDomainHash() public view override returns (bytes32) {\n        return _homeDomainHash(localDomain);\n    }\n\n    /**\n     * @notice Check if an Update is an Improper Update;\n     * if so, slash the Updater and set the contract to FAILED state.\n     *\n     * An Improper Update is an update building off of the Home's `committedRoot`\n     * for which the `_newRoot` does not currently exist in the Home's queue.\n     * This would mean that message(s) that were not truly\n     * dispatched on Home were falsely included in the signed root.\n     *\n     * An Improper Update will only be accepted as valid by the Replica\n     * If an Improper Update is attempted on Home,\n     * the Updater will be slashed immediately.\n     * If an Improper Update is submitted to the Replica,\n     * it should be relayed to the Home contract using this function\n     * in order to slash the Updater with an Improper Update.\n     *\n     * An Improper Update submitted to the Replica is only valid\n     * while the `_oldRoot` is still equal to the `committedRoot` on Home;\n     * if the `committedRoot` on Home has already been updated with a valid Update,\n     * then the Updater should be slashed with a Double Update.\n     * @dev Reverts (and doesn't slash updater) if signature is invalid or\n     * update not current\n     * @param _oldRoot Old merkle tree root (should equal home's committedRoot)\n     * @param _newRoot New merkle tree root\n     * @param _signature Updater signature on `_oldRoot` and `_newRoot`\n     * @return TRUE if update was an Improper Update (implying Updater was slashed)\n     */\n    function improperUpdate(\n        bytes32 _oldRoot,\n        bytes32 _newRoot,\n        bytes memory _signature\n    ) public notFailed returns (bool) {\n        require(\n            _isUpdaterSignature(_oldRoot, _newRoot, _signature),\n            \"!updater sig\"\n        );\n        require(_oldRoot == committedRoot, \"not a current update\");\n        // if the _newRoot is not currently contained in the queue,\n        // slash the Updater and set the contract to FAILED state\n        if (!queue.contains(_newRoot)) {\n            _fail();\n            emit ImproperUpdate(_oldRoot, _newRoot, _signature);\n            return true;\n        }\n        // if the _newRoot is contained in the queue,\n        // this is not an improper update\n        return false;\n    }\n\n    // ============ Internal Functions  ============\n\n    /**\n     * @notice Set the UpdaterManager\n     * @param _updaterManager Address of the UpdaterManager\n     */\n    function _setUpdaterManager(IUpdaterManager _updaterManager) internal {\n        require(\n            Address.isContract(address(_updaterManager)),\n            \"!contract updaterManager\"\n        );\n        updaterManager = IUpdaterManager(_updaterManager);\n        emit NewUpdaterManager(address(_updaterManager));\n    }\n\n    /**\n     * @notice Slash the Updater and set contract state to FAILED\n     * @dev Called when fraud is proven (Improper Update or Double Update)\n     */\n    function _fail() internal override {\n        // set contract to FAILED\n        _setFailed();\n        // slash Updater\n        updaterManager.slashUpdater(msg.sender);\n        emit UpdaterSlashed(updater, msg.sender);\n    }\n\n    /**\n     * @notice Internal utility function that combines\n     * `_destination` and `_nonce`.\n     * @dev Both destination and nonce should be less than 2^32 - 1\n     * @param _destination Domain of destination chain\n     * @param _nonce Current nonce for given destination chain\n     * @return Returns (`_destination` << 32) & `_nonce`\n     */\n    function _destinationAndNonce(uint32 _destination, uint32 _nonce)\n        internal\n        pure\n        returns (uint64)\n    {\n        return (uint64(_destination) << 32) | _nonce;\n    }\n}\n"
    },
    "contracts/Replica.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.6.11;\n\n// ============ Internal Imports ============\nimport {Version0} from \"./Version0.sol\";\nimport {NomadBase} from \"./NomadBase.sol\";\nimport {MerkleLib} from \"../libs/Merkle.sol\";\nimport {Message} from \"../libs/Message.sol\";\n// ============ External Imports ============\nimport {TypedMemView} from \"@summa-tx/memview-sol/contracts/TypedMemView.sol\";\n\n/**\n * @title Replica\n * @author Illusory Systems Inc.\n * @notice Track root updates on Home,\n * prove and dispatch messages to end recipients.\n */\ncontract Replica is Version0, NomadBase {\n    // ============ Libraries ============\n\n    using MerkleLib for MerkleLib.Tree;\n    using TypedMemView for bytes;\n    using TypedMemView for bytes29;\n    using Message for bytes29;\n\n    // ============ Enums ============\n\n    // Status of Message:\n    //   0 - None - message has not been proven or processed\n    //   1 - Proven - message inclusion proof has been validated\n    //   2 - Processed - message has been dispatched to recipient\n    enum MessageStatus {\n        None,\n        Proven,\n        Processed\n    }\n\n    // ============ Immutables ============\n\n    // Minimum gas for message processing\n    uint256 public immutable PROCESS_GAS;\n    // Reserved gas (to ensure tx completes in case message processing runs out)\n    uint256 public immutable RESERVE_GAS;\n\n    // ============ Public Storage ============\n\n    // Domain of home chain\n    uint32 public remoteDomain;\n    // Number of seconds to wait before root becomes confirmable\n    uint256 public optimisticSeconds;\n    // re-entrancy guard\n    uint8 private entered;\n    // Mapping of roots to allowable confirmation times\n    mapping(bytes32 => uint256) public confirmAt;\n    // Mapping of message leaves to MessageStatus\n    mapping(bytes32 => MessageStatus) public messages;\n\n    // ============ Upgrade Gap ============\n\n    // gap for upgrade safety\n    uint256[45] private __GAP;\n\n    // ============ Events ============\n\n    /**\n     * @notice Emitted when message is processed\n     * @param messageHash Hash of message that failed to process\n     * @param success TRUE if the call was executed successfully, FALSE if the call reverted\n     * @param returnData the return data from the external call\n     */\n    event Process(\n        bytes32 indexed messageHash,\n        bool indexed success,\n        bytes indexed returnData\n    );\n\n    /**\n     * @notice Emitted when the value for optimisticTimeout is set\n     * @param timeout The new value for optimistic timeout\n     */\n    event SetOptimisticTimeout(uint256 timeout);\n\n    /**\n     * @notice Emitted when a root's confirmation is modified by governance\n     * @param root The root for which confirmAt has been set\n     * @param previousConfirmAt The previous value of confirmAt\n     * @param newConfirmAt The new value of confirmAt\n     */\n    event SetConfirmation(\n        bytes32 indexed root,\n        uint256 previousConfirmAt,\n        uint256 newConfirmAt\n    );\n\n    // ============ Constructor ============\n\n    // solhint-disable-next-line no-empty-blocks\n    constructor(\n        uint32 _localDomain,\n        uint256 _processGas,\n        uint256 _reserveGas\n    ) NomadBase(_localDomain) {\n        require(_processGas >= 850_000, \"!process gas\");\n        require(_reserveGas >= 15_000, \"!reserve gas\");\n        PROCESS_GAS = _processGas;\n        RESERVE_GAS = _reserveGas;\n    }\n\n    // ============ Initializer ============\n\n    function initialize(\n        uint32 _remoteDomain,\n        address _updater,\n        bytes32 _committedRoot,\n        uint256 _optimisticSeconds\n    ) public initializer {\n        __NomadBase_initialize(_updater);\n        // set storage variables\n        entered = 1;\n        remoteDomain = _remoteDomain;\n        committedRoot = _committedRoot;\n        confirmAt[_committedRoot] = 1;\n        optimisticSeconds = _optimisticSeconds;\n        emit SetOptimisticTimeout(_optimisticSeconds);\n    }\n\n    // ============ External Functions ============\n\n    /**\n     * @notice Called by external agent. Submits the signed update's new root,\n     * marks root's allowable confirmation time, and emits an `Update` event.\n     * @dev Reverts if update doesn't build off latest committedRoot\n     * or if signature is invalid.\n     * @param _oldRoot Old merkle root\n     * @param _newRoot New merkle root\n     * @param _signature Updater's signature on `_oldRoot` and `_newRoot`\n     */\n    function update(\n        bytes32 _oldRoot,\n        bytes32 _newRoot,\n        bytes memory _signature\n    ) external notFailed {\n        // ensure that update is building off the last submitted root\n        require(_oldRoot == committedRoot, \"not current update\");\n        // validate updater signature\n        require(\n            _isUpdaterSignature(_oldRoot, _newRoot, _signature),\n            \"!updater sig\"\n        );\n        // Hook for future use\n        _beforeUpdate();\n        // set the new root's confirmation timer\n        confirmAt[_newRoot] = block.timestamp + optimisticSeconds;\n        // update committedRoot\n        committedRoot = _newRoot;\n        emit Update(remoteDomain, _oldRoot, _newRoot, _signature);\n    }\n\n    /**\n     * @notice First attempts to prove the validity of provided formatted\n     * `message`. If the message is successfully proven, then tries to process\n     * message.\n     * @dev Reverts if `prove` call returns false\n     * @param _message Formatted message (refer to NomadBase.sol Message library)\n     * @param _proof Merkle proof of inclusion for message's leaf\n     * @param _index Index of leaf in home's merkle tree\n     */\n    function proveAndProcess(\n        bytes memory _message,\n        bytes32[32] calldata _proof,\n        uint256 _index\n    ) external {\n        require(prove(keccak256(_message), _proof, _index), \"!prove\");\n        process(_message);\n    }\n\n    /**\n     * @notice Given formatted message, attempts to dispatch\n     * message payload to end recipient.\n     * @dev Recipient must implement a `handle` method (refer to IMessageRecipient.sol)\n     * Reverts if formatted message's destination domain is not the Replica's domain,\n     * if message has not been proven,\n     * or if not enough gas is provided for the dispatch transaction.\n     * @param _message Formatted message\n     * @return _success TRUE iff dispatch transaction succeeded\n     */\n    function process(bytes memory _message) public returns (bool _success) {\n        bytes29 _m = _message.ref(0);\n        // ensure message was meant for this domain\n        require(_m.destination() == localDomain, \"!destination\");\n        // ensure message has been proven\n        bytes32 _messageHash = _m.keccak();\n        require(messages[_messageHash] == MessageStatus.Proven, \"!proven\");\n        // check re-entrancy guard\n        require(entered == 1, \"!reentrant\");\n        entered = 0;\n        // update message status as processed\n        messages[_messageHash] = MessageStatus.Processed;\n        // A call running out of gas TYPICALLY errors the whole tx. We want to\n        // a) ensure the call has a sufficient amount of gas to make a\n        //    meaningful state change.\n        // b) ensure that if the subcall runs out of gas, that the tx as a whole\n        //    does not revert (i.e. we still mark the message processed)\n        // To do this, we require that we have enough gas to process\n        // and still return. We then delegate only the minimum processing gas.\n        require(gasleft() >= PROCESS_GAS + RESERVE_GAS, \"!gas\");\n        // get the message recipient\n        address _recipient = _m.recipientAddress();\n        // set up for assembly call\n        uint256 _toCopy;\n        uint256 _maxCopy = 256;\n        uint256 _gas = PROCESS_GAS;\n        // allocate memory for returndata\n        bytes memory _returnData = new bytes(_maxCopy);\n        bytes memory _calldata = abi.encodeWithSignature(\n            \"handle(uint32,uint32,bytes32,bytes)\",\n            _m.origin(),\n            _m.nonce(),\n            _m.sender(),\n            _m.body().clone()\n        );\n        // dispatch message to recipient\n        // by assembly calling \"handle\" function\n        // we call via assembly to avoid memcopying a very large returndata\n        // returned by a malicious contract\n        assembly {\n            _success := call(\n                _gas, // gas\n                _recipient, // recipient\n                0, // ether value\n                add(_calldata, 0x20), // inloc\n                mload(_calldata), // inlen\n                0, // outloc\n                0 // outlen\n            )\n            // limit our copy to 256 bytes\n            _toCopy := returndatasize()\n            if gt(_toCopy, _maxCopy) {\n                _toCopy := _maxCopy\n            }\n            // Store the length of the copied bytes\n            mstore(_returnData, _toCopy)\n            // copy the bytes from returndata[0:_toCopy]\n            returndatacopy(add(_returnData, 0x20), 0, _toCopy)\n        }\n        // emit process results\n        emit Process(_messageHash, _success, _returnData);\n        // reset re-entrancy guard\n        entered = 1;\n    }\n\n    // ============ External Owner Functions ============\n\n    /**\n     * @notice Set optimistic timeout period for new roots\n     * @dev Only callable by owner (Governance)\n     * @param _optimisticSeconds New optimistic timeout period\n     */\n    function setOptimisticTimeout(uint256 _optimisticSeconds)\n        external\n        onlyOwner\n    {\n        optimisticSeconds = _optimisticSeconds;\n        emit SetOptimisticTimeout(_optimisticSeconds);\n    }\n\n    /**\n     * @notice Set Updater role\n     * @dev MUST ensure that all roots signed by previous Updater have\n     * been relayed before calling. Only callable by owner (Governance)\n     * @param _updater New Updater\n     */\n    function setUpdater(address _updater) external onlyOwner {\n        _setUpdater(_updater);\n    }\n\n    /**\n     * @notice Set confirmAt for a given root\n     * @dev To be used if in the case that fraud is proven\n     * and roots need to be deleted / added. Only callable by owner (Governance)\n     * @param _root The root for which to modify confirm time\n     * @param _confirmAt The new confirmation time. Set to 0 to \"delete\" a root.\n     */\n    function setConfirmation(bytes32 _root, uint256 _confirmAt)\n        external\n        onlyOwner\n    {\n        uint256 _previousConfirmAt = confirmAt[_root];\n        confirmAt[_root] = _confirmAt;\n        emit SetConfirmation(_root, _previousConfirmAt, _confirmAt);\n    }\n\n    // ============ Public Functions ============\n\n    /**\n     * @notice Check that the root has been submitted\n     * and that the optimistic timeout period has expired,\n     * meaning the root can be processed\n     * @param _root the Merkle root, submitted in an update, to check\n     * @return TRUE iff root has been submitted & timeout has expired\n     */\n    function acceptableRoot(bytes32 _root) public view returns (bool) {\n        uint256 _time = confirmAt[_root];\n        if (_time == 0) {\n            return false;\n        }\n        return block.timestamp >= _time;\n    }\n\n    /**\n     * @notice Attempts to prove the validity of message given its leaf, the\n     * merkle proof of inclusion for the leaf, and the index of the leaf.\n     * @dev Reverts if message's MessageStatus != None (i.e. if message was\n     * already proven or processed)\n     * @dev For convenience, we allow proving against any previous root.\n     * This means that witnesses never need to be updated for the new root\n     * @param _leaf Leaf of message to prove\n     * @param _proof Merkle proof of inclusion for leaf\n     * @param _index Index of leaf in home's merkle tree\n     * @return Returns true if proof was valid and `prove` call succeeded\n     **/\n    function prove(\n        bytes32 _leaf,\n        bytes32[32] calldata _proof,\n        uint256 _index\n    ) public returns (bool) {\n        // ensure that message has not been proven or processed\n        require(messages[_leaf] == MessageStatus.None, \"!MessageStatus.None\");\n        // calculate the expected root based on the proof\n        bytes32 _calculatedRoot = MerkleLib.branchRoot(_leaf, _proof, _index);\n        // if the root is valid, change status to Proven\n        if (acceptableRoot(_calculatedRoot)) {\n            messages[_leaf] = MessageStatus.Proven;\n            return true;\n        }\n        return false;\n    }\n\n    /**\n     * @notice Hash of Home domain concatenated with \"NOMAD\"\n     */\n    function homeDomainHash() public view override returns (bytes32) {\n        return _homeDomainHash(remoteDomain);\n    }\n\n    // ============ Internal Functions ============\n\n    /**\n     * @notice Moves the contract into failed state\n     * @dev Called when a Double Update is submitted\n     */\n    function _fail() internal override {\n        _setFailed();\n    }\n\n    /// @notice Hook for potential future use\n    // solhint-disable-next-line no-empty-blocks\n    function _beforeUpdate() internal {}\n}\n"
    },
    "libs/TypeCasts.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.6.11;\n\nimport \"@summa-tx/memview-sol/contracts/TypedMemView.sol\";\n\nlibrary TypeCasts {\n    using TypedMemView for bytes;\n    using TypedMemView for bytes29;\n\n    function coerceBytes32(string memory _s)\n        internal\n        pure\n        returns (bytes32 _b)\n    {\n        _b = bytes(_s).ref(0).index(0, uint8(bytes(_s).length));\n    }\n\n    // treat it as a null-terminated string of max 32 bytes\n    function coerceString(bytes32 _buf)\n        internal\n        pure\n        returns (string memory _newStr)\n    {\n        uint8 _slen = 0;\n        while (_slen < 32 && _buf[_slen] != 0) {\n            _slen++;\n        }\n\n        // solhint-disable-next-line no-inline-assembly\n        assembly {\n            _newStr := mload(0x40)\n            mstore(0x40, add(_newStr, 0x40)) // may end up with extra\n            mstore(_newStr, _slen)\n            mstore(add(_newStr, 0x20), _buf)\n        }\n    }\n\n    // alignment preserving cast\n    function addressToBytes32(address _addr) internal pure returns (bytes32) {\n        return bytes32(uint256(uint160(_addr)));\n    }\n\n    // alignment preserving cast\n    function bytes32ToAddress(bytes32 _buf) internal pure returns (address) {\n        return address(uint160(uint256(_buf)));\n    }\n}\n"
    },
    "@openzeppelin/contracts/cryptography/ECDSA.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity >=0.6.0 <0.8.0;\n\n/**\n * @dev Elliptic Curve Digital Signature Algorithm (ECDSA) operations.\n *\n * These functions can be used to verify that a message was signed by the holder\n * of the private keys of a given address.\n */\nlibrary ECDSA {\n    /**\n     * @dev Returns the address that signed a hashed message (`hash`) with\n     * `signature`. This address can then be used for verification purposes.\n     *\n     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:\n     * this function rejects them by requiring the `s` value to be in the lower\n     * half order, and the `v` value to be either 27 or 28.\n     *\n     * IMPORTANT: `hash` _must_ be the result of a hash operation for the\n     * verification to be secure: it is possible to craft signatures that\n     * recover to arbitrary addresses for non-hashed data. A safe way to ensure\n     * this is by receiving a hash of the original message (which may otherwise\n     * be too long), and then calling {toEthSignedMessageHash} on it.\n     */\n    function recover(bytes32 hash, bytes memory signature) internal pure returns (address) {\n        // Check the signature length\n        if (signature.length != 65) {\n            revert(\"ECDSA: invalid signature length\");\n        }\n\n        // Divide the signature in r, s and v variables\n        bytes32 r;\n        bytes32 s;\n        uint8 v;\n\n        // ecrecover takes the signature parameters, and the only way to get them\n        // currently is to use assembly.\n        // solhint-disable-next-line no-inline-assembly\n        assembly {\n            r := mload(add(signature, 0x20))\n            s := mload(add(signature, 0x40))\n            v := byte(0, mload(add(signature, 0x60)))\n        }\n\n        return recover(hash, v, r, s);\n    }\n\n    /**\n     * @dev Overload of {ECDSA-recover-bytes32-bytes-} that receives the `v`,\n     * `r` and `s` signature fields separately.\n     */\n    function recover(bytes32 hash, uint8 v, bytes32 r, bytes32 s) internal pure returns (address) {\n        // EIP-2 still allows signature malleability for ecrecover(). Remove this possibility and make the signature\n        // unique. Appendix F in the Ethereum Yellow paper (https://ethereum.github.io/yellowpaper/paper.pdf), defines\n        // the valid range for s in (281): 0 < s < secp256k1n ÷ 2 + 1, and for v in (282): v ∈ {27, 28}. Most\n        // signatures from current libraries generate a unique signature with an s-value in the lower half order.\n        //\n        // If your library generates malleable signatures, such as s-values in the upper range, calculate a new s-value\n        // with 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1 and flip v from 27 to 28 or\n        // vice versa. If your library also generates signatures with 0/1 for v instead 27/28, add 27 to v to accept\n        // these malleable signatures as well.\n        require(uint256(s) <= 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0, \"ECDSA: invalid signature 's' value\");\n        require(v == 27 || v == 28, \"ECDSA: invalid signature 'v' value\");\n\n        // If the signature is valid (and not malleable), return the signer address\n        address signer = ecrecover(hash, v, r, s);\n        require(signer != address(0), \"ECDSA: invalid signature\");\n\n        return signer;\n    }\n\n    /**\n     * @dev Returns an Ethereum Signed Message, created from a `hash`. This\n     * replicates the behavior of the\n     * https://github.com/ethereum/wiki/wiki/JSON-RPC#eth_sign[`eth_sign`]\n     * JSON-RPC method.\n     *\n     * See {recover}.\n     */\n    function toEthSignedMessageHash(bytes32 hash) internal pure returns (bytes32) {\n        // 32 is the length in bytes of hash,\n        // enforced by the type signature above\n        return keccak256(abi.encodePacked(\"\\x19Ethereum Signed Message:\\n32\", hash));\n    }\n}\n"
    },
    "@openzeppelin/contracts/access/Ownable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity >=0.6.0 <0.8.0;\n\nimport \"../utils/Context.sol\";\n/**\n * @dev Contract module which provides a basic access control mechanism, where\n * there is an account (an owner) that can be granted exclusive access to\n * specific functions.\n *\n * By default, the owner account will be the one that deploys the contract. This\n * can later be changed with {transferOwnership}.\n *\n * This module is used through inheritance. It will make available the modifier\n * `onlyOwner`, which can be applied to your functions to restrict their use to\n * the owner.\n */\nabstract contract Ownable is Context {\n    address private _owner;\n\n    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);\n\n    /**\n     * @dev Initializes the contract setting the deployer as the initial owner.\n     */\n    constructor () internal {\n        address msgSender = _msgSender();\n        _owner = msgSender;\n        emit OwnershipTransferred(address(0), msgSender);\n    }\n\n    /**\n     * @dev Returns the address of the current owner.\n     */\n    function owner() public view virtual returns (address) {\n        return _owner;\n    }\n\n    /**\n     * @dev Throws if called by any account other than the owner.\n     */\n    modifier onlyOwner() {\n        require(owner() == _msgSender(), \"Ownable: caller is not the owner\");\n        _;\n    }\n\n    /**\n     * @dev Leaves the contract without owner. It will not be possible to call\n     * `onlyOwner` functions anymore. Can only be called by the current owner.\n     *\n     * NOTE: Renouncing ownership will leave the contract without an owner,\n     * thereby removing any functionality that is only available to the owner.\n     */\n    function renounceOwnership() public virtual onlyOwner {\n        emit OwnershipTransferred(_owner, address(0));\n        _owner = address(0);\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Can only be called by the current owner.\n     */\n    function transferOwnership(address newOwner) public virtual onlyOwner {\n        require(newOwner != address(0), \"Ownable: new owner is the zero address\");\n        emit OwnershipTransferred(_owner, newOwner);\n        _owner = newOwner;\n    }\n}\n"
    },
    "contracts/Version0.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.6.11;\n\n/**\n * @title Version0\n * @notice Version getter for contracts\n **/\ncontract Version0 {\n    uint8 public constant VERSION = 0;\n}\n"
    },
    "contracts/NomadBase.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.6.11;\n\n// ============ Internal Imports ============\nimport {Message} from \"../libs/Message.sol\";\n// ============ External Imports ============\nimport {ECDSA} from \"@openzeppelin/contracts/cryptography/ECDSA.sol\";\nimport {Initializable} from \"@openzeppelin/contracts-upgradeable/proxy/Initializable.sol\";\nimport {OwnableUpgradeable} from \"@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol\";\n\n/**\n * @title NomadBase\n * @author Illusory Systems Inc.\n * @notice Shared utilities between Home and Replica.\n */\nabstract contract NomadBase is Initializable, OwnableUpgradeable {\n    // ============ Enums ============\n\n    // States:\n    //   0 - UnInitialized - before initialize function is called\n    //   note: the contract is initialized at deploy time, so it should never be in this state\n    //   1 - Active - as long as the contract has not become fraudulent\n    //   2 - Failed - after a valid fraud proof has been submitted;\n    //   contract will no longer accept updates or new messages\n    enum States {\n        UnInitialized,\n        Active,\n        Failed\n    }\n\n    // ============ Immutable Variables ============\n\n    // Domain of chain on which the contract is deployed\n    uint32 public immutable localDomain;\n\n    // ============ Public Variables ============\n\n    // Address of bonded Updater\n    address public updater;\n    // Current state of contract\n    States public state;\n    // The latest root that has been signed by the Updater\n    bytes32 public committedRoot;\n\n    // ============ Upgrade Gap ============\n\n    // gap for upgrade safety\n    uint256[47] private __GAP;\n\n    // ============ Events ============\n\n    /**\n     * @notice Emitted when update is made on Home\n     * or unconfirmed update root is submitted on Replica\n     * @param homeDomain Domain of home contract\n     * @param oldRoot Old merkle root\n     * @param newRoot New merkle root\n     * @param signature Updater's signature on `oldRoot` and `newRoot`\n     */\n    event Update(\n        uint32 indexed homeDomain,\n        bytes32 indexed oldRoot,\n        bytes32 indexed newRoot,\n        bytes signature\n    );\n\n    /**\n     * @notice Emitted when proof of a double update is submitted,\n     * which sets the contract to FAILED state\n     * @param oldRoot Old root shared between two conflicting updates\n     * @param newRoot Array containing two conflicting new roots\n     * @param signature Signature on `oldRoot` and `newRoot`[0]\n     * @param signature2 Signature on `oldRoot` and `newRoot`[1]\n     */\n    event DoubleUpdate(\n        bytes32 oldRoot,\n        bytes32[2] newRoot,\n        bytes signature,\n        bytes signature2\n    );\n\n    /**\n     * @notice Emitted when Updater is rotated\n     * @param oldUpdater The address of the old updater\n     * @param newUpdater The address of the new updater\n     */\n    event NewUpdater(address oldUpdater, address newUpdater);\n\n    // ============ Modifiers ============\n\n    /**\n     * @notice Ensures that contract state != FAILED when the function is called\n     */\n    modifier notFailed() {\n        require(state != States.Failed, \"failed state\");\n        _;\n    }\n\n    // ============ Constructor ============\n\n    constructor(uint32 _localDomain) {\n        localDomain = _localDomain;\n    }\n\n    // ============ Initializer ============\n\n    function __NomadBase_initialize(address _updater) internal initializer {\n        __Ownable_init();\n        _setUpdater(_updater);\n        state = States.Active;\n    }\n\n    // ============ External Functions ============\n\n    /**\n     * @notice Called by external agent. Checks that signatures on two sets of\n     * roots are valid and that the new roots conflict with each other. If both\n     * cases hold true, the contract is failed and a `DoubleUpdate` event is\n     * emitted.\n     * @dev When `fail()` is called on Home, updater is slashed.\n     * @param _oldRoot Old root shared between two conflicting updates\n     * @param _newRoot Array containing two conflicting new roots\n     * @param _signature Signature on `_oldRoot` and `_newRoot`[0]\n     * @param _signature2 Signature on `_oldRoot` and `_newRoot`[1]\n     */\n    function doubleUpdate(\n        bytes32 _oldRoot,\n        bytes32[2] calldata _newRoot,\n        bytes calldata _signature,\n        bytes calldata _signature2\n    ) external notFailed {\n        if (\n            NomadBase._isUpdaterSignature(_oldRoot, _newRoot[0], _signature) &&\n            NomadBase._isUpdaterSignature(_oldRoot, _newRoot[1], _signature2) &&\n            _newRoot[0] != _newRoot[1]\n        ) {\n            _fail();\n            emit DoubleUpdate(_oldRoot, _newRoot, _signature, _signature2);\n        }\n    }\n\n    // ============ Public Functions ============\n\n    /**\n     * @notice Hash of Home domain concatenated with \"NOMAD\"\n     */\n    function homeDomainHash() public view virtual returns (bytes32);\n\n    // ============ Internal Functions ============\n\n    /**\n     * @notice Hash of Home domain concatenated with \"NOMAD\"\n     * @param _homeDomain the Home domain to hash\n     */\n    function _homeDomainHash(uint32 _homeDomain)\n        internal\n        pure\n        returns (bytes32)\n    {\n        return keccak256(abi.encodePacked(_homeDomain, \"NOMAD\"));\n    }\n\n    /**\n     * @notice Set contract state to FAILED\n     * @dev Called when a valid fraud proof is submitted\n     */\n    function _setFailed() internal {\n        state = States.Failed;\n    }\n\n    /**\n     * @notice Moves the contract into failed state\n     * @dev Called when fraud is proven\n     * (Double Update is submitted on Home or Replica,\n     * or Improper Update is submitted on Home)\n     */\n    function _fail() internal virtual;\n\n    /**\n     * @notice Set the Updater\n     * @param _newUpdater Address of the new Updater\n     */\n    function _setUpdater(address _newUpdater) internal {\n        address _oldUpdater = updater;\n        updater = _newUpdater;\n        emit NewUpdater(_oldUpdater, _newUpdater);\n    }\n\n    /**\n     * @notice Checks that signature was signed by Updater\n     * @param _oldRoot Old merkle root\n     * @param _newRoot New merkle root\n     * @param _signature Signature on `_oldRoot` and `_newRoot`\n     * @return TRUE iff signature is valid signed by updater\n     **/\n    function _isUpdaterSignature(\n        bytes32 _oldRoot,\n        bytes32 _newRoot,\n        bytes memory _signature\n    ) internal view returns (bool) {\n        bytes32 _digest = keccak256(\n            abi.encodePacked(homeDomainHash(), _oldRoot, _newRoot)\n        );\n        _digest = ECDSA.toEthSignedMessageHash(_digest);\n        return (ECDSA.recover(_digest, _signature) == updater);\n    }\n}\n"
    },
    "libs/Queue.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.6.11;\n\n/**\n * @title QueueLib\n * @author Illusory Systems Inc.\n * @notice Library containing queue struct and operations for queue used by\n * Home and Replica.\n **/\nlibrary QueueLib {\n    /**\n     * @notice Queue struct\n     * @dev Internally keeps track of the `first` and `last` elements through\n     * indices and a mapping of indices to enqueued elements.\n     **/\n    struct Queue {\n        uint128 first;\n        uint128 last;\n        mapping(uint256 => bytes32) queue;\n    }\n\n    /**\n     * @notice Initializes the queue\n     * @dev Empty state denoted by _q.first > q._last. Queue initialized\n     * with _q.first = 1 and _q.last = 0.\n     **/\n    function initialize(Queue storage _q) internal {\n        if (_q.first == 0) {\n            _q.first = 1;\n        }\n    }\n\n    /**\n     * @notice Enqueues a single new element\n     * @param _item New element to be enqueued\n     * @return _last Index of newly enqueued element\n     **/\n    function enqueue(Queue storage _q, bytes32 _item)\n        internal\n        returns (uint128 _last)\n    {\n        _last = _q.last + 1;\n        _q.last = _last;\n        if (_item != bytes32(0)) {\n            // saves gas if we're queueing 0\n            _q.queue[_last] = _item;\n        }\n    }\n\n    /**\n     * @notice Dequeues element at front of queue\n     * @dev Removes dequeued element from storage\n     * @return _item Dequeued element\n     **/\n    function dequeue(Queue storage _q) internal returns (bytes32 _item) {\n        uint128 _last = _q.last;\n        uint128 _first = _q.first;\n        require(_length(_last, _first) != 0, \"Empty\");\n        _item = _q.queue[_first];\n        if (_item != bytes32(0)) {\n            // saves gas if we're dequeuing 0\n            delete _q.queue[_first];\n        }\n        _q.first = _first + 1;\n    }\n\n    /**\n     * @notice Batch enqueues several elements\n     * @param _items Array of elements to be enqueued\n     * @return _last Index of last enqueued element\n     **/\n    function enqueue(Queue storage _q, bytes32[] memory _items)\n        internal\n        returns (uint128 _last)\n    {\n        _last = _q.last;\n        for (uint256 i = 0; i < _items.length; i += 1) {\n            _last += 1;\n            bytes32 _item = _items[i];\n            if (_item != bytes32(0)) {\n                _q.queue[_last] = _item;\n            }\n        }\n        _q.last = _last;\n    }\n\n    /**\n     * @notice Batch dequeues `_number` elements\n     * @dev Reverts if `_number` > queue length\n     * @param _number Number of elements to dequeue\n     * @return Array of dequeued elements\n     **/\n    function dequeue(Queue storage _q, uint256 _number)\n        internal\n        returns (bytes32[] memory)\n    {\n        uint128 _last = _q.last;\n        uint128 _first = _q.first;\n        // Cannot underflow unless state is corrupted\n        require(_length(_last, _first) >= _number, \"Insufficient\");\n\n        bytes32[] memory _items = new bytes32[](_number);\n\n        for (uint256 i = 0; i < _number; i++) {\n            _items[i] = _q.queue[_first];\n            delete _q.queue[_first];\n            _first++;\n        }\n        _q.first = _first;\n        return _items;\n    }\n\n    /**\n     * @notice Returns true if `_item` is in the queue and false if otherwise\n     * @dev Linearly scans from _q.first to _q.last looking for `_item`\n     * @param _item Item being searched for in queue\n     * @return True if `_item` currently exists in queue, false if otherwise\n     **/\n    function contains(Queue storage _q, bytes32 _item)\n        internal\n        view\n        returns (bool)\n    {\n        for (uint256 i = _q.first; i <= _q.last; i++) {\n            if (_q.queue[i] == _item) {\n                return true;\n            }\n        }\n        return false;\n    }\n\n    /// @notice Returns last item in queue\n    /// @dev Returns bytes32(0) if queue empty\n    function lastItem(Queue storage _q) internal view returns (bytes32) {\n        return _q.queue[_q.last];\n    }\n\n    /// @notice Returns element at front of queue without removing element\n    /// @dev Reverts if queue is empty\n    function peek(Queue storage _q) internal view returns (bytes32 _item) {\n        require(!isEmpty(_q), \"Empty\");\n        _item = _q.queue[_q.first];\n    }\n\n    /// @notice Returns true if queue is empty and false if otherwise\n    function isEmpty(Queue storage _q) internal view returns (bool) {\n        return _q.last < _q.first;\n    }\n\n    /// @notice Returns number of elements in queue\n    function length(Queue storage _q) internal view returns (uint256) {\n        uint128 _last = _q.last;\n        uint128 _first = _q.first;\n        // Cannot underflow unless state is corrupted\n        return _length(_last, _first);\n    }\n\n    /// @notice Returns number of elements between `_last` and `_first` (used internally)\n    function _length(uint128 _last, uint128 _first)\n        internal\n        pure\n        returns (uint256)\n    {\n        return uint256(_last + 1 - _first);\n    }\n}\n"
    },
    "libs/Merkle.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.6.11;\n\n// work based on eth2 deposit contract, which is used under CC0-1.0\n\n/**\n * @title MerkleLib\n * @author Illusory Systems Inc.\n * @notice An incremental merkle tree modeled on the eth2 deposit contract.\n **/\nlibrary MerkleLib {\n    uint256 internal constant TREE_DEPTH = 32;\n    uint256 internal constant MAX_LEAVES = 2**TREE_DEPTH - 1;\n\n    /**\n     * @notice Struct representing incremental merkle tree. Contains current\n     * branch and the number of inserted leaves in the tree.\n     **/\n    struct Tree {\n        bytes32[TREE_DEPTH] branch;\n        uint256 count;\n    }\n\n    /**\n     * @notice Inserts `_node` into merkle tree\n     * @dev Reverts if tree is full\n     * @param _node Element to insert into tree\n     **/\n    function insert(Tree storage _tree, bytes32 _node) internal {\n        require(_tree.count < MAX_LEAVES, \"merkle tree full\");\n\n        _tree.count += 1;\n        uint256 size = _tree.count;\n        for (uint256 i = 0; i < TREE_DEPTH; i++) {\n            if ((size & 1) == 1) {\n                _tree.branch[i] = _node;\n                return;\n            }\n            _node = keccak256(abi.encodePacked(_tree.branch[i], _node));\n            size /= 2;\n        }\n        // As the loop should always end prematurely with the `return` statement,\n        // this code should be unreachable. We assert `false` just to be safe.\n        assert(false);\n    }\n\n    /**\n     * @notice Calculates and returns`_tree`'s current root given array of zero\n     * hashes\n     * @param _zeroes Array of zero hashes\n     * @return _current Calculated root of `_tree`\n     **/\n    function rootWithCtx(Tree storage _tree, bytes32[TREE_DEPTH] memory _zeroes)\n        internal\n        view\n        returns (bytes32 _current)\n    {\n        uint256 _index = _tree.count;\n\n        for (uint256 i = 0; i < TREE_DEPTH; i++) {\n            uint256 _ithBit = (_index >> i) & 0x01;\n            bytes32 _next = _tree.branch[i];\n            if (_ithBit == 1) {\n                _current = keccak256(abi.encodePacked(_next, _current));\n            } else {\n                _current = keccak256(abi.encodePacked(_current, _zeroes[i]));\n            }\n        }\n    }\n\n    /// @notice Calculates and returns`_tree`'s current root\n    function root(Tree storage _tree) internal view returns (bytes32) {\n        return rootWithCtx(_tree, zeroHashes());\n    }\n\n    /// @notice Returns array of TREE_DEPTH zero hashes\n    /// @return _zeroes Array of TREE_DEPTH zero hashes\n    function zeroHashes()\n        internal\n        pure\n        returns (bytes32[TREE_DEPTH] memory _zeroes)\n    {\n        _zeroes[0] = Z_0;\n        _zeroes[1] = Z_1;\n        _zeroes[2] = Z_2;\n        _zeroes[3] = Z_3;\n        _zeroes[4] = Z_4;\n        _zeroes[5] = Z_5;\n        _zeroes[6] = Z_6;\n        _zeroes[7] = Z_7;\n        _zeroes[8] = Z_8;\n        _zeroes[9] = Z_9;\n        _zeroes[10] = Z_10;\n        _zeroes[11] = Z_11;\n        _zeroes[12] = Z_12;\n        _zeroes[13] = Z_13;\n        _zeroes[14] = Z_14;\n        _zeroes[15] = Z_15;\n        _zeroes[16] = Z_16;\n        _zeroes[17] = Z_17;\n        _zeroes[18] = Z_18;\n        _zeroes[19] = Z_19;\n        _zeroes[20] = Z_20;\n        _zeroes[21] = Z_21;\n        _zeroes[22] = Z_22;\n        _zeroes[23] = Z_23;\n        _zeroes[24] = Z_24;\n        _zeroes[25] = Z_25;\n        _zeroes[26] = Z_26;\n        _zeroes[27] = Z_27;\n        _zeroes[28] = Z_28;\n        _zeroes[29] = Z_29;\n        _zeroes[30] = Z_30;\n        _zeroes[31] = Z_31;\n    }\n\n    /**\n     * @notice Calculates and returns the merkle root for the given leaf\n     * `_item`, a merkle branch, and the index of `_item` in the tree.\n     * @param _item Merkle leaf\n     * @param _branch Merkle proof\n     * @param _index Index of `_item` in tree\n     * @return _current Calculated merkle root\n     **/\n    function branchRoot(\n        bytes32 _item,\n        bytes32[TREE_DEPTH] memory _branch,\n        uint256 _index\n    ) internal pure returns (bytes32 _current) {\n        _current = _item;\n\n        for (uint256 i = 0; i < TREE_DEPTH; i++) {\n            uint256 _ithBit = (_index >> i) & 0x01;\n            bytes32 _next = _branch[i];\n            if (_ithBit == 1) {\n                _current = keccak256(abi.encodePacked(_next, _current));\n            } else {\n                _current = keccak256(abi.encodePacked(_current, _next));\n            }\n        }\n    }\n\n    // keccak256 zero hashes\n    bytes32 internal constant Z_0 =\n        hex\"0000000000000000000000000000000000000000000000000000000000000000\";\n    bytes32 internal constant Z_1 =\n        hex\"ad3228b676f7d3cd4284a5443f17f1962b36e491b30a40b2405849e597ba5fb5\";\n    bytes32 internal constant Z_2 =\n        hex\"b4c11951957c6f8f642c4af61cd6b24640fec6dc7fc607ee8206a99e92410d30\";\n    bytes32 internal constant Z_3 =\n        hex\"21ddb9a356815c3fac1026b6dec5df3124afbadb485c9ba5a3e3398a04b7ba85\";\n    bytes32 internal constant Z_4 =\n        hex\"e58769b32a1beaf1ea27375a44095a0d1fb664ce2dd358e7fcbfb78c26a19344\";\n    bytes32 internal constant Z_5 =\n        hex\"0eb01ebfc9ed27500cd4dfc979272d1f0913cc9f66540d7e8005811109e1cf2d\";\n    bytes32 internal constant Z_6 =\n        hex\"887c22bd8750d34016ac3c66b5ff102dacdd73f6b014e710b51e8022af9a1968\";\n    bytes32 internal constant Z_7 =\n        hex\"ffd70157e48063fc33c97a050f7f640233bf646cc98d9524c6b92bcf3ab56f83\";\n    bytes32 internal constant Z_8 =\n        hex\"9867cc5f7f196b93bae1e27e6320742445d290f2263827498b54fec539f756af\";\n    bytes32 internal constant Z_9 =\n        hex\"cefad4e508c098b9a7e1d8feb19955fb02ba9675585078710969d3440f5054e0\";\n    bytes32 internal constant Z_10 =\n        hex\"f9dc3e7fe016e050eff260334f18a5d4fe391d82092319f5964f2e2eb7c1c3a5\";\n    bytes32 internal constant Z_11 =\n        hex\"f8b13a49e282f609c317a833fb8d976d11517c571d1221a265d25af778ecf892\";\n    bytes32 internal constant Z_12 =\n        hex\"3490c6ceeb450aecdc82e28293031d10c7d73bf85e57bf041a97360aa2c5d99c\";\n    bytes32 internal constant Z_13 =\n        hex\"c1df82d9c4b87413eae2ef048f94b4d3554cea73d92b0f7af96e0271c691e2bb\";\n    bytes32 internal constant Z_14 =\n        hex\"5c67add7c6caf302256adedf7ab114da0acfe870d449a3a489f781d659e8becc\";\n    bytes32 internal constant Z_15 =\n        hex\"da7bce9f4e8618b6bd2f4132ce798cdc7a60e7e1460a7299e3c6342a579626d2\";\n    bytes32 internal constant Z_16 =\n        hex\"2733e50f526ec2fa19a22b31e8ed50f23cd1fdf94c9154ed3a7609a2f1ff981f\";\n    bytes32 internal constant Z_17 =\n        hex\"e1d3b5c807b281e4683cc6d6315cf95b9ade8641defcb32372f1c126e398ef7a\";\n    bytes32 internal constant Z_18 =\n        hex\"5a2dce0a8a7f68bb74560f8f71837c2c2ebbcbf7fffb42ae1896f13f7c7479a0\";\n    bytes32 internal constant Z_19 =\n        hex\"b46a28b6f55540f89444f63de0378e3d121be09e06cc9ded1c20e65876d36aa0\";\n    bytes32 internal constant Z_20 =\n        hex\"c65e9645644786b620e2dd2ad648ddfcbf4a7e5b1a3a4ecfe7f64667a3f0b7e2\";\n    bytes32 internal constant Z_21 =\n        hex\"f4418588ed35a2458cffeb39b93d26f18d2ab13bdce6aee58e7b99359ec2dfd9\";\n    bytes32 internal constant Z_22 =\n        hex\"5a9c16dc00d6ef18b7933a6f8dc65ccb55667138776f7dea101070dc8796e377\";\n    bytes32 internal constant Z_23 =\n        hex\"4df84f40ae0c8229d0d6069e5c8f39a7c299677a09d367fc7b05e3bc380ee652\";\n    bytes32 internal constant Z_24 =\n        hex\"cdc72595f74c7b1043d0e1ffbab734648c838dfb0527d971b602bc216c9619ef\";\n    bytes32 internal constant Z_25 =\n        hex\"0abf5ac974a1ed57f4050aa510dd9c74f508277b39d7973bb2dfccc5eeb0618d\";\n    bytes32 internal constant Z_26 =\n        hex\"b8cd74046ff337f0a7bf2c8e03e10f642c1886798d71806ab1e888d9e5ee87d0\";\n    bytes32 internal constant Z_27 =\n        hex\"838c5655cb21c6cb83313b5a631175dff4963772cce9108188b34ac87c81c41e\";\n    bytes32 internal constant Z_28 =\n        hex\"662ee4dd2dd7b2bc707961b1e646c4047669dcb6584f0d8d770daf5d7e7deb2e\";\n    bytes32 internal constant Z_29 =\n        hex\"388ab20e2573d171a88108e79d820e98f26c0b84aa8b2f4aa4968dbb818ea322\";\n    bytes32 internal constant Z_30 =\n        hex\"93237c50ba75ee485f4c22adf2f741400bdf8d6a9cc7df7ecae576221665d735\";\n    bytes32 internal constant Z_31 =\n        hex\"8448818bb4ae4562849e949e17ac16e0be16688e156b5cf15e098c627c0056a9\";\n}\n"
    },
    "libs/Message.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.6.11;\n\nimport \"@summa-tx/memview-sol/contracts/TypedMemView.sol\";\n\nimport {\n    TypeCasts\n} from \"./TypeCasts.sol\";\n\n/**\n * @title Message Library\n * @author Illusory Systems Inc.\n * @notice Library for formatted messages used by Home and Replica.\n **/\nlibrary Message {\n    using TypedMemView for bytes;\n    using TypedMemView for bytes29;\n\n    // Number of bytes in formatted message before `body` field\n    uint256 internal constant PREFIX_LENGTH = 76;\n\n    /**\n     * @notice Returns formatted (packed) message with provided fields\n     * @param _originDomain Domain of home chain\n     * @param _sender Address of sender as bytes32\n     * @param _nonce Destination-specific nonce\n     * @param _destinationDomain Domain of destination chain\n     * @param _recipient Address of recipient on destination chain as bytes32\n     * @param _messageBody Raw bytes of message body\n     * @return Formatted message\n     **/\n    function formatMessage(\n        uint32 _originDomain,\n        bytes32 _sender,\n        uint32 _nonce,\n        uint32 _destinationDomain,\n        bytes32 _recipient,\n        bytes memory _messageBody\n    ) internal pure returns (bytes memory) {\n        return\n            abi.encodePacked(\n                _originDomain,\n                _sender,\n                _nonce,\n                _destinationDomain,\n                _recipient,\n                _messageBody\n            );\n    }\n\n    /**\n     * @notice Returns leaf of formatted message with provided fields.\n     * @param _origin Domain of home chain\n     * @param _sender Address of sender as bytes32\n     * @param _nonce Destination-specific nonce number\n     * @param _destination Domain of destination chain\n     * @param _recipient Address of recipient on destination chain as bytes32\n     * @param _body Raw bytes of message body\n     * @return Leaf (hash) of formatted message\n     **/\n    function messageHash(\n        uint32 _origin,\n        bytes32 _sender,\n        uint32 _nonce,\n        uint32 _destination,\n        bytes32 _recipient,\n        bytes memory _body\n    ) internal pure returns (bytes32) {\n        return\n            keccak256(\n                formatMessage(\n                    _origin,\n                    _sender,\n                    _nonce,\n                    _destination,\n                    _recipient,\n                    _body\n                )\n            );\n    }\n\n    /// @notice Returns message's origin field\n    function origin(bytes29 _message) internal pure returns (uint32) {\n        return uint32(_message.indexUint(0, 4));\n    }\n\n    /// @notice Returns message's sender field\n    function sender(bytes29 _message) internal pure returns (bytes32) {\n        return _message.index(4, 32);\n    }\n\n    /// @notice Returns message's nonce field\n    function nonce(bytes29 _message) internal pure returns (uint32) {\n        return uint32(_message.indexUint(36, 4));\n    }\n\n    /// @notice Returns message's destination field\n    function destination(bytes29 _message) internal pure returns (uint32) {\n        return uint32(_message.indexUint(40, 4));\n    }\n\n    /// @notice Returns message's recipient field as bytes32\n    function recipient(bytes29 _message) internal pure returns (bytes32) {\n        return _message.index(44, 32);\n    }\n\n    /// @notice Returns message's recipient field as an address\n    function recipientAddress(bytes29 _message)\n        internal\n        pure\n        returns (address)\n    {\n        return TypeCasts.bytes32ToAddress(recipient(_message));\n    }\n\n    /// @notice Returns message's body field as bytes29 (refer to TypedMemView library for details on bytes29 type)\n    function body(bytes29 _message) internal pure returns (bytes29) {\n        return _message.slice(PREFIX_LENGTH, _message.len() - PREFIX_LENGTH, 0);\n    }\n\n    function leaf(bytes29 _message) internal view returns (bytes32) {\n        return messageHash(origin(_message), sender(_message), nonce(_message), destination(_message), recipient(_message), TypedMemView.clone(body(_message)));\n    }\n}\n"
    },
    "contracts/Merkle.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.6.11;\n\n// ============ Internal Imports ============\nimport {MerkleLib} from \"../libs/Merkle.sol\";\n\n/**\n * @title MerkleTreeManager\n * @author Illusory Systems Inc.\n * @notice Contains a Merkle tree instance and\n * exposes view functions for the tree.\n */\ncontract MerkleTreeManager {\n    // ============ Libraries ============\n\n    using MerkleLib for MerkleLib.Tree;\n    MerkleLib.Tree public tree;\n\n    // ============ Upgrade Gap ============\n\n    // gap for upgrade safety\n    uint256[49] private __GAP;\n\n    // ============ Public Functions ============\n\n    /**\n     * @notice Calculates and returns tree's current root\n     */\n    function root() public view returns (bytes32) {\n        return tree.root();\n    }\n\n    /**\n     * @notice Returns the number of inserted leaves in the tree (current index)\n     */\n    function count() public view returns (uint256) {\n        return tree.count;\n    }\n}\n"
    },
    "contracts/Queue.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.6.11;\n\n// ============ Internal Imports ============\nimport {QueueLib} from \"../libs/Queue.sol\";\n// ============ External Imports ============\nimport {Initializable} from \"@openzeppelin/contracts-upgradeable/proxy/Initializable.sol\";\n\n/**\n * @title QueueManager\n * @author Illusory Systems Inc.\n * @notice Contains a queue instance and\n * exposes view functions for the queue.\n **/\ncontract QueueManager is Initializable {\n    // ============ Libraries ============\n\n    using QueueLib for QueueLib.Queue;\n    QueueLib.Queue internal queue;\n\n    // ============ Upgrade Gap ============\n\n    // gap for upgrade safety\n    uint256[49] private __GAP;\n\n    // ============ Initializer ============\n\n    function __QueueManager_initialize() internal initializer {\n        queue.initialize();\n    }\n\n    // ============ Public Functions ============\n\n    /**\n     * @notice Returns number of elements in queue\n     */\n    function queueLength() external view returns (uint256) {\n        return queue.length();\n    }\n\n    /**\n     * @notice Returns TRUE iff `_item` is in the queue\n     */\n    function queueContains(bytes32 _item) external view returns (bool) {\n        return queue.contains(_item);\n    }\n\n    /**\n     * @notice Returns last item enqueued to the queue\n     */\n    function queueEnd() external view returns (bytes32) {\n        return queue.lastItem();\n    }\n}\n"
    },
    "interfaces/IUpdaterManager.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.6.11;\n\ninterface IUpdaterManager {\n    function slashUpdater(address payable _reporter) external;\n\n    function updater() external view returns (address);\n}\n"
    },
    "@openzeppelin/contracts/utils/Address.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity >=0.6.2 <0.8.0;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary Address {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * [IMPORTANT]\n     * ====\n     * It is unsafe to assume that an address for which this function returns\n     * false is an externally-owned account (EOA) and not a contract.\n     *\n     * Among others, `isContract` will return false for the following\n     * types of addresses:\n     *\n     *  - an externally-owned account\n     *  - a contract in construction\n     *  - an address where a contract will be created\n     *  - an address where a contract lived, but was destroyed\n     * ====\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies on extcodesize, which returns 0 for contracts in\n        // construction, since the code is only stored at the end of the\n        // constructor execution.\n\n        uint256 size;\n        // solhint-disable-next-line no-inline-assembly\n        assembly { size := extcodesize(account) }\n        return size > 0;\n    }\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        require(address(this).balance >= amount, \"Address: insufficient balance\");\n\n        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value\n        (bool success, ) = recipient.call{ value: amount }(\"\");\n        require(success, \"Address: unable to send value, recipient may have reverted\");\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain`call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason, it is bubbled up by this\n     * function (like regular Solidity function calls).\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n      return functionCall(target, data, \"Address: low-level call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with\n     * `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, value, \"Address: low-level call with value failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but\n     * with `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value, string memory errorMessage) internal returns (bytes memory) {\n        require(address(this).balance >= value, \"Address: insufficient balance for call\");\n        require(isContract(target), \"Address: call to non-contract\");\n\n        // solhint-disable-next-line avoid-low-level-calls\n        (bool success, bytes memory returndata) = target.call{ value: value }(data);\n        return _verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {\n        return functionStaticCall(target, data, \"Address: low-level static call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data, string memory errorMessage) internal view returns (bytes memory) {\n        require(isContract(target), \"Address: static call to non-contract\");\n\n        // solhint-disable-next-line avoid-low-level-calls\n        (bool success, bytes memory returndata) = target.staticcall(data);\n        return _verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionDelegateCall(target, data, \"Address: low-level delegate call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {\n        require(isContract(target), \"Address: delegate call to non-contract\");\n\n        // solhint-disable-next-line avoid-low-level-calls\n        (bool success, bytes memory returndata) = target.delegatecall(data);\n        return _verifyCallResult(success, returndata, errorMessage);\n    }\n\n    function _verifyCallResult(bool success, bytes memory returndata, string memory errorMessage) private pure returns(bytes memory) {\n        if (success) {\n            return returndata;\n        } else {\n            // Look for revert reason and bubble it up if present\n            if (returndata.length > 0) {\n                // The easiest way to bubble the revert reason is using memory via assembly\n\n                // solhint-disable-next-line no-inline-assembly\n                assembly {\n                    let returndata_size := mload(returndata)\n                    revert(add(32, returndata), returndata_size)\n                }\n            } else {\n                revert(errorMessage);\n            }\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts-upgradeable/proxy/Initializable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\n// solhint-disable-next-line compiler-version\npragma solidity >=0.4.24 <0.8.0;\n\nimport \"../utils/AddressUpgradeable.sol\";\n\n/**\n * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed\n * behind a proxy. Since a proxied contract can't have a constructor, it's common to move constructor logic to an\n * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer\n * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.\n *\n * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as\n * possible by providing the encoded function call as the `_data` argument to {UpgradeableProxy-constructor}.\n *\n * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure\n * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.\n */\nabstract contract Initializable {\n\n    /**\n     * @dev Indicates that the contract has been initialized.\n     */\n    bool private _initialized;\n\n    /**\n     * @dev Indicates that the contract is in the process of being initialized.\n     */\n    bool private _initializing;\n\n    /**\n     * @dev Modifier to protect an initializer function from being invoked twice.\n     */\n    modifier initializer() {\n        require(_initializing || _isConstructor() || !_initialized, \"Initializable: contract is already initialized\");\n\n        bool isTopLevelCall = !_initializing;\n        if (isTopLevelCall) {\n            _initializing = true;\n            _initialized = true;\n        }\n\n        _;\n\n        if (isTopLevelCall) {\n            _initializing = false;\n        }\n    }\n\n    /// @dev Returns true if and only if the function is running in the constructor\n    function _isConstructor() private view returns (bool) {\n        return !AddressUpgradeable.isContract(address(this));\n    }\n}\n"
    },
    "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity >=0.6.0 <0.8.0;\n\nimport \"../utils/ContextUpgradeable.sol\";\nimport \"../proxy/Initializable.sol\";\n/**\n * @dev Contract module which provides a basic access control mechanism, where\n * there is an account (an owner) that can be granted exclusive access to\n * specific functions.\n *\n * By default, the owner account will be the one that deploys the contract. This\n * can later be changed with {transferOwnership}.\n *\n * This module is used through inheritance. It will make available the modifier\n * `onlyOwner`, which can be applied to your functions to restrict their use to\n * the owner.\n */\nabstract contract OwnableUpgradeable is Initializable, ContextUpgradeable {\n    address private _owner;\n\n    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);\n\n    /**\n     * @dev Initializes the contract setting the deployer as the initial owner.\n     */\n    function __Ownable_init() internal initializer {\n        __Context_init_unchained();\n        __Ownable_init_unchained();\n    }\n\n    function __Ownable_init_unchained() internal initializer {\n        address msgSender = _msgSender();\n        _owner = msgSender;\n        emit OwnershipTransferred(address(0), msgSender);\n    }\n\n    /**\n     * @dev Returns the address of the current owner.\n     */\n    function owner() public view virtual returns (address) {\n        return _owner;\n    }\n\n    /**\n     * @dev Throws if called by any account other than the owner.\n     */\n    modifier onlyOwner() {\n        require(owner() == _msgSender(), \"Ownable: caller is not the owner\");\n        _;\n    }\n\n    /**\n     * @dev Leaves the contract without owner. It will not be possible to call\n     * `onlyOwner` functions anymore. Can only be called by the current owner.\n     *\n     * NOTE: Renouncing ownership will leave the contract without an owner,\n     * thereby removing any functionality that is only available to the owner.\n     */\n    function renounceOwnership() public virtual onlyOwner {\n        emit OwnershipTransferred(_owner, address(0));\n        _owner = address(0);\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Can only be called by the current owner.\n     */\n    function transferOwnership(address newOwner) public virtual onlyOwner {\n        require(newOwner != address(0), \"Ownable: new owner is the zero address\");\n        emit OwnershipTransferred(_owner, newOwner);\n        _owner = newOwner;\n    }\n    uint256[49] private __gap;\n}\n"
    },
    "@summa-tx/memview-sol/contracts/TypedMemView.sol": {
      "content": "// SPDX-License-Identifier: MIT OR Apache-2.0\npragma solidity >=0.5.10;\n\nimport {SafeMath} from \"./SafeMath.sol\";\n\nlibrary TypedMemView {\n    using SafeMath for uint256;\n\n    // Why does this exist?\n    // the solidity `bytes memory` type has a few weaknesses.\n    // 1. You can't index ranges effectively\n    // 2. You can't slice without copying\n    // 3. The underlying data may represent any type\n    // 4. Solidity never deallocates memory, and memory costs grow\n    //    superlinearly\n\n    // By using a memory view instead of a `bytes memory` we get the following\n    // advantages:\n    // 1. Slices are done on the stack, by manipulating the pointer\n    // 2. We can index arbitrary ranges and quickly convert them to stack types\n    // 3. We can insert type info into the pointer, and typecheck at runtime\n\n    // This makes `TypedMemView` a useful tool for efficient zero-copy\n    // algorithms.\n\n    // Why bytes29?\n    // We want to avoid confusion between views, digests, and other common\n    // types so we chose a large and uncommonly used odd number of bytes\n    //\n    // Note that while bytes are left-aligned in a word, integers and addresses\n    // are right-aligned. This means when working in assembly we have to\n    // account for the 3 unused bytes on the righthand side\n    //\n    // First 5 bytes are a type flag.\n    // - ff_ffff_fffe is reserved for unknown type.\n    // - ff_ffff_ffff is reserved for invalid types/errors.\n    // next 12 are memory address\n    // next 12 are len\n    // bottom 3 bytes are empty\n\n    // Assumptions:\n    // - non-modification of memory.\n    // - No Solidity updates\n    // - - wrt free mem point\n    // - - wrt bytes representation in memory\n    // - - wrt memory addressing in general\n\n    // Usage:\n    // - create type constants\n    // - use `assertType` for runtime type assertions\n    // - - unfortunately we can't do this at compile time yet :(\n    // - recommended: implement modifiers that perform type checking\n    // - - e.g.\n    // - - `uint40 constant MY_TYPE = 3;`\n    // - - ` modifer onlyMyType(bytes29 myView) { myView.assertType(MY_TYPE); }`\n    // - instantiate a typed view from a bytearray using `ref`\n    // - use `index` to inspect the contents of the view\n    // - use `slice` to create smaller views into the same memory\n    // - - `slice` can increase the offset\n    // - - `slice can decrease the length`\n    // - - must specify the output type of `slice`\n    // - - `slice` will return a null view if you try to overrun\n    // - - make sure to explicitly check for this with `notNull` or `assertType`\n    // - use `equal` for typed comparisons.\n\n\n    // The null view\n    bytes29 public constant NULL = hex\"ffffffffffffffffffffffffffffffffffffffffffffffffffffffffff\";\n    uint256 constant LOW_12_MASK = 0xffffffffffffffffffffffff;\n    uint8 constant TWELVE_BYTES = 96;\n\n    /**\n     * @notice      Returns the encoded hex character that represents the lower 4 bits of the argument.\n     * @param _b    The byte\n     * @return      char - The encoded hex character\n     */\n    function nibbleHex(uint8 _b) internal pure returns (uint8 char) {\n        // This can probably be done more efficiently, but it's only in error\n        // paths, so we don't really care :)\n        uint8 _nibble = _b | 0xf0; // set top 4, keep bottom 4\n        if (_nibble == 0xf0) {return 0x30;} // 0\n        if (_nibble == 0xf1) {return 0x31;} // 1\n        if (_nibble == 0xf2) {return 0x32;} // 2\n        if (_nibble == 0xf3) {return 0x33;} // 3\n        if (_nibble == 0xf4) {return 0x34;} // 4\n        if (_nibble == 0xf5) {return 0x35;} // 5\n        if (_nibble == 0xf6) {return 0x36;} // 6\n        if (_nibble == 0xf7) {return 0x37;} // 7\n        if (_nibble == 0xf8) {return 0x38;} // 8\n        if (_nibble == 0xf9) {return 0x39;} // 9\n        if (_nibble == 0xfa) {return 0x61;} // a\n        if (_nibble == 0xfb) {return 0x62;} // b\n        if (_nibble == 0xfc) {return 0x63;} // c\n        if (_nibble == 0xfd) {return 0x64;} // d\n        if (_nibble == 0xfe) {return 0x65;} // e\n        if (_nibble == 0xff) {return 0x66;} // f\n    }\n\n    /**\n     * @notice      Returns a uint16 containing the hex-encoded byte.\n     * @param _b    The byte\n     * @return      encoded - The hex-encoded byte\n     */\n    function byteHex(uint8 _b) internal pure returns (uint16 encoded) {\n        encoded |= nibbleHex(_b >> 4); // top 4 bits\n        encoded <<= 8;\n        encoded |= nibbleHex(_b); // lower 4 bits\n    }\n\n    /**\n     * @notice      Encodes the uint256 to hex. `first` contains the encoded top 16 bytes.\n     *              `second` contains the encoded lower 16 bytes.\n     *\n     * @param _b    The 32 bytes as uint256\n     * @return      first - The top 16 bytes\n     * @return      second - The bottom 16 bytes\n     */\n    function encodeHex(uint256 _b) internal pure returns (uint256 first, uint256 second) {\n        for (uint8 i = 31; i > 15; i -= 1) {\n            uint8 _byte = uint8(_b >> (i * 8));\n            first |= byteHex(_byte);\n            if (i != 16) {\n                first <<= 16;\n            }\n        }\n\n        // abusing underflow here =_=\n        for (uint8 i = 15; i < 255 ; i -= 1) {\n            uint8 _byte = uint8(_b >> (i * 8));\n            second |= byteHex(_byte);\n            if (i != 0) {\n                second <<= 16;\n            }\n        }\n    }\n\n    /**\n     * @notice          Changes the endianness of a uint256.\n     * @dev             https://graphics.stanford.edu/~seander/bithacks.html#ReverseParallel\n     * @param _b        The unsigned integer to reverse\n     * @return          v - The reversed value\n     */\n    function reverseUint256(uint256 _b) internal pure returns (uint256 v) {\n        v = _b;\n\n        // swap bytes\n        v = ((v >> 8) & 0x00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF) |\n            ((v & 0x00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF00FF) << 8);\n        // swap 2-byte long pairs\n        v = ((v >> 16) & 0x0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF) |\n            ((v & 0x0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF0000FFFF) << 16);\n        // swap 4-byte long pairs\n        v = ((v >> 32) & 0x00000000FFFFFFFF00000000FFFFFFFF00000000FFFFFFFF00000000FFFFFFFF) |\n            ((v & 0x00000000FFFFFFFF00000000FFFFFFFF00000000FFFFFFFF00000000FFFFFFFF) << 32);\n        // swap 8-byte long pairs\n        v = ((v >> 64) & 0x0000000000000000FFFFFFFFFFFFFFFF0000000000000000FFFFFFFFFFFFFFFF) |\n            ((v & 0x0000000000000000FFFFFFFFFFFFFFFF0000000000000000FFFFFFFFFFFFFFFF) << 64);\n        // swap 16-byte long pairs\n        v = (v >> 128) | (v << 128);\n    }\n\n    /**\n     * @notice      Create a mask with the highest `_len` bits set.\n     * @param _len  The length\n     * @return      mask - The mask\n     */\n    function leftMask(uint8 _len) private pure returns (uint256 mask) {\n        // ugly. redo without assembly?\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            mask := sar(\n                sub(_len, 1),\n                0x8000000000000000000000000000000000000000000000000000000000000000\n            )\n        }\n    }\n\n    /**\n     * @notice      Return the null view.\n     * @return      bytes29 - The null view\n     */\n    function nullView() internal pure returns (bytes29) {\n        return NULL;\n    }\n\n    /**\n     * @notice      Check if the view is null.\n     * @return      bool - True if the view is null\n     */\n    function isNull(bytes29 memView) internal pure returns (bool) {\n        return memView == NULL;\n    }\n\n    /**\n     * @notice      Check if the view is not null.\n     * @return      bool - True if the view is not null\n     */\n    function notNull(bytes29 memView) internal pure returns (bool) {\n        return !isNull(memView);\n    }\n\n    /**\n     * @notice          Check if the view is of a valid type and points to a valid location\n     *                  in memory.\n     * @dev             We perform this check by examining solidity's unallocated memory\n     *                  pointer and ensuring that the view's upper bound is less than that.\n     * @param memView   The view\n     * @return          ret - True if the view is valid\n     */\n    function isValid(bytes29 memView) internal pure returns (bool ret) {\n        if (typeOf(memView) == 0xffffffffff) {return false;}\n        uint256 _end = end(memView);\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            ret := not(gt(_end, mload(0x40)))\n        }\n    }\n\n    /**\n     * @notice          Require that a typed memory view be valid.\n     * @dev             Returns the view for easy chaining.\n     * @param memView   The view\n     * @return          bytes29 - The validated view\n     */\n    function assertValid(bytes29 memView) internal pure returns (bytes29) {\n        require(isValid(memView), \"Validity assertion failed\");\n        return memView;\n    }\n\n    /**\n     * @notice          Return true if the memview is of the expected type. Otherwise false.\n     * @param memView   The view\n     * @param _expected The expected type\n     * @return          bool - True if the memview is of the expected type\n     */\n    function isType(bytes29 memView, uint40 _expected) internal pure returns (bool) {\n        return typeOf(memView) == _expected;\n    }\n\n    /**\n     * @notice          Require that a typed memory view has a specific type.\n     * @dev             Returns the view for easy chaining.\n     * @param memView   The view\n     * @param _expected The expected type\n     * @return          bytes29 - The view with validated type\n     */\n    function assertType(bytes29 memView, uint40 _expected) internal pure returns (bytes29) {\n        if (!isType(memView, _expected)) {\n            (, uint256 g) = encodeHex(uint256(typeOf(memView)));\n            (, uint256 e) = encodeHex(uint256(_expected));\n            string memory err = string(\n                abi.encodePacked(\n                    \"Type assertion failed. Got 0x\",\n                    uint80(g),\n                    \". Expected 0x\",\n                    uint80(e)\n                )\n            );\n            revert(err);\n        }\n        return memView;\n    }\n\n    /**\n     * @notice          Return an identical view with a different type.\n     * @param memView   The view\n     * @param _newType  The new type\n     * @return          newView - The new view with the specified type\n     */\n    function castTo(bytes29 memView, uint40 _newType) internal pure returns (bytes29 newView) {\n        // then | in the new type\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            // shift off the top 5 bytes\n            newView := or(newView, shr(40, shl(40, memView)))\n            newView := or(newView, shl(216, _newType))\n        }\n    }\n\n    /**\n     * @notice          Unsafe raw pointer construction. This should generally not be called\n     *                  directly. Prefer `ref` wherever possible.\n     * @dev             Unsafe raw pointer construction. This should generally not be called\n     *                  directly. Prefer `ref` wherever possible.\n     * @param _type     The type\n     * @param _loc      The memory address\n     * @param _len      The length\n     * @return          newView - The new view with the specified type, location and length\n     */\n    function unsafeBuildUnchecked(uint256 _type, uint256 _loc, uint256 _len) private pure returns (bytes29 newView) {\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            newView := shl(96, or(newView, _type)) // insert type\n            newView := shl(96, or(newView, _loc))  // insert loc\n            newView := shl(24, or(newView, _len))  // empty bottom 3 bytes\n        }\n    }\n\n    /**\n     * @notice          Instantiate a new memory view. This should generally not be called\n     *                  directly. Prefer `ref` wherever possible.\n     * @dev             Instantiate a new memory view. This should generally not be called\n     *                  directly. Prefer `ref` wherever possible.\n     * @param _type     The type\n     * @param _loc      The memory address\n     * @param _len      The length\n     * @return          newView - The new view with the specified type, location and length\n     */\n    function build(uint256 _type, uint256 _loc, uint256 _len) internal pure returns (bytes29 newView) {\n        uint256 _end = _loc.add(_len);\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            if gt(_end, mload(0x40)) {\n                _end := 0\n            }\n        }\n        if (_end == 0) {\n            return NULL;\n        }\n        newView = unsafeBuildUnchecked(_type, _loc, _len);\n    }\n\n    /**\n     * @notice          Instantiate a memory view from a byte array.\n     * @dev             Note that due to Solidity memory representation, it is not possible to\n     *                  implement a deref, as the `bytes` type stores its len in memory.\n     * @param arr       The byte array\n     * @param newType   The type\n     * @return          bytes29 - The memory view\n     */\n    function ref(bytes memory arr, uint40 newType) internal pure returns (bytes29) {\n        uint256 _len = arr.length;\n\n        uint256 _loc;\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            _loc := add(arr, 0x20)  // our view is of the data, not the struct\n        }\n\n        return build(newType, _loc, _len);\n    }\n\n    /**\n     * @notice          Return the associated type information.\n     * @param memView   The memory view\n     * @return          _type - The type associated with the view\n     */\n    function typeOf(bytes29 memView) internal pure returns (uint40 _type) {\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            // 216 == 256 - 40\n            _type := shr(216, memView) // shift out lower 24 bytes\n        }\n    }\n\n    /**\n     * @notice          Optimized type comparison. Checks that the 5-byte type flag is equal.\n     * @param left      The first view\n     * @param right     The second view\n     * @return          bool - True if the 5-byte type flag is equal\n     */\n    function sameType(bytes29 left, bytes29 right) internal pure returns (bool) {\n        return (left ^ right) >> (2 * TWELVE_BYTES) == 0;\n    }\n\n    /**\n     * @notice          Return the memory address of the underlying bytes.\n     * @param memView   The view\n     * @return          _loc - The memory address\n     */\n    function loc(bytes29 memView) internal pure returns (uint96 _loc) {\n        uint256 _mask = LOW_12_MASK;  // assembly can't use globals\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            // 120 bits = 12 bytes (the encoded loc) + 3 bytes (empty low space)\n            _loc := and(shr(120, memView), _mask)\n        }\n    }\n\n    /**\n     * @notice          The number of memory words this memory view occupies, rounded up.\n     * @param memView   The view\n     * @return          uint256 - The number of memory words\n     */\n    function words(bytes29 memView) internal pure returns (uint256) {\n        return uint256(len(memView)).add(32) / 32;\n    }\n\n    /**\n     * @notice          The in-memory footprint of a fresh copy of the view.\n     * @param memView   The view\n     * @return          uint256 - The in-memory footprint of a fresh copy of the view.\n     */\n    function footprint(bytes29 memView) internal pure returns (uint256) {\n        return words(memView) * 32;\n    }\n\n    /**\n     * @notice          The number of bytes of the view.\n     * @param memView   The view\n     * @return          _len - The length of the view\n     */\n    function len(bytes29 memView) internal pure returns (uint96 _len) {\n        uint256 _mask = LOW_12_MASK;  // assembly can't use globals\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            _len := and(shr(24, memView), _mask)\n        }\n    }\n\n    /**\n     * @notice          Returns the endpoint of `memView`.\n     * @param memView   The view\n     * @return          uint256 - The endpoint of `memView`\n     */\n    function end(bytes29 memView) internal pure returns (uint256) {\n        return loc(memView) + len(memView);\n    }\n\n    /**\n     * @notice          Safe slicing without memory modification.\n     * @param memView   The view\n     * @param _index    The start index\n     * @param _len      The length\n     * @param newType   The new type\n     * @return          bytes29 - The new view\n     */\n    function slice(bytes29 memView, uint256 _index, uint256 _len, uint40 newType) internal pure returns (bytes29) {\n        uint256 _loc = loc(memView);\n\n        // Ensure it doesn't overrun the view\n        if (_loc.add(_index).add(_len) > end(memView)) {\n            return NULL;\n        }\n\n        _loc = _loc.add(_index);\n        return build(newType, _loc, _len);\n    }\n\n    /**\n     * @notice          Shortcut to `slice`. Gets a view representing the first `_len` bytes.\n     * @param memView   The view\n     * @param _len      The length\n     * @param newType   The new type\n     * @return          bytes29 - The new view\n     */\n    function prefix(bytes29 memView, uint256 _len, uint40 newType) internal pure returns (bytes29) {\n        return slice(memView, 0, _len, newType);\n    }\n\n    /**\n     * @notice          Shortcut to `slice`. Gets a view representing the last `_len` byte.\n     * @param memView   The view\n     * @param _len      The length\n     * @param newType   The new type\n     * @return          bytes29 - The new view\n     */\n    function postfix(bytes29 memView, uint256 _len, uint40 newType) internal pure returns (bytes29) {\n        return slice(memView, uint256(len(memView)).sub(_len), _len, newType);\n    }\n\n    /**\n     * @notice          Construct an error message for an indexing overrun.\n     * @param _loc      The memory address\n     * @param _len      The length\n     * @param _index    The index\n     * @param _slice    The slice where the overrun occurred\n     * @return          err - The err\n     */\n    function indexErrOverrun(\n        uint256 _loc,\n        uint256 _len,\n        uint256 _index,\n        uint256 _slice\n    ) internal pure returns (string memory err) {\n        (, uint256 a) = encodeHex(_loc);\n        (, uint256 b) = encodeHex(_len);\n        (, uint256 c) = encodeHex(_index);\n        (, uint256 d) = encodeHex(_slice);\n        err = string(\n            abi.encodePacked(\n                \"TypedMemView/index - Overran the view. Slice is at 0x\",\n                uint48(a),\n                \" with length 0x\",\n                uint48(b),\n                \". Attempted to index at offset 0x\",\n                uint48(c),\n                \" with length 0x\",\n                uint48(d),\n                \".\"\n            )\n        );\n    }\n\n    /**\n     * @notice          Load up to 32 bytes from the view onto the stack.\n     * @dev             Returns a bytes32 with only the `_bytes` highest bytes set.\n     *                  This can be immediately cast to a smaller fixed-length byte array.\n     *                  To automatically cast to an integer, use `indexUint`.\n     * @param memView   The view\n     * @param _index    The index\n     * @param _bytes    The bytes\n     * @return          result - The 32 byte result\n     */\n    function index(bytes29 memView, uint256 _index, uint8 _bytes) internal pure returns (bytes32 result) {\n        if (_bytes == 0) {return bytes32(0);}\n        if (_index.add(_bytes) > len(memView)) {\n            revert(indexErrOverrun(loc(memView), len(memView), _index, uint256(_bytes)));\n        }\n        require(_bytes <= 32, \"TypedMemView/index - Attempted to index more than 32 bytes\");\n\n        uint8 bitLength = _bytes * 8;\n        uint256 _loc = loc(memView);\n        uint256 _mask = leftMask(bitLength);\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            result := and(mload(add(_loc, _index)), _mask)\n        }\n    }\n\n    /**\n     * @notice          Parse an unsigned integer from the view at `_index`.\n     * @dev             Requires that the view have >= `_bytes` bytes following that index.\n     * @param memView   The view\n     * @param _index    The index\n     * @param _bytes    The bytes\n     * @return          result - The unsigned integer\n     */\n    function indexUint(bytes29 memView, uint256 _index, uint8 _bytes) internal pure returns (uint256 result) {\n        return uint256(index(memView, _index, _bytes)) >> ((32 - _bytes) * 8);\n    }\n\n    /**\n     * @notice          Parse an unsigned integer from LE bytes.\n     * @param memView   The view\n     * @param _index    The index\n     * @param _bytes    The bytes\n     * @return          result - The unsigned integer\n     */\n    function indexLEUint(bytes29 memView, uint256 _index, uint8 _bytes) internal pure returns (uint256 result) {\n        return reverseUint256(uint256(index(memView, _index, _bytes)));\n    }\n\n    /**\n     * @notice          Parse an address from the view at `_index`. Requires that the view have >= 20 bytes\n     *                  following that index.\n     * @param memView   The view\n     * @param _index    The index\n     * @return          address - The address\n     */\n    function indexAddress(bytes29 memView, uint256 _index) internal pure returns (address) {\n        return address(uint160(indexUint(memView, _index, 20)));\n    }\n\n    /**\n     * @notice          Return the keccak256 hash of the underlying memory\n     * @param memView   The view\n     * @return          digest - The keccak256 hash of the underlying memory\n     */\n    function keccak(bytes29 memView) internal pure returns (bytes32 digest) {\n        uint256 _loc = loc(memView);\n        uint256 _len = len(memView);\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            digest := keccak256(_loc, _len)\n        }\n    }\n\n    /**\n     * @notice          Return the sha2 digest of the underlying memory.\n     * @dev             We explicitly deallocate memory afterwards.\n     * @param memView   The view\n     * @return          digest - The sha2 hash of the underlying memory\n     */\n    function sha2(bytes29 memView) internal view returns (bytes32 digest) {\n        uint256 _loc = loc(memView);\n        uint256 _len = len(memView);\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            let ptr := mload(0x40)\n            pop(staticcall(gas(), 2, _loc, _len, ptr, 0x20)) // sha2 #1\n            digest := mload(ptr)\n        }\n    }\n\n    /**\n     * @notice          Implements bitcoin's hash160 (rmd160(sha2()))\n     * @param memView   The pre-image\n     * @return          digest - the Digest\n     */\n    function hash160(bytes29 memView) internal view returns (bytes20 digest) {\n        uint256 _loc = loc(memView);\n        uint256 _len = len(memView);\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            let ptr := mload(0x40)\n            pop(staticcall(gas(), 2, _loc, _len, ptr, 0x20)) // sha2\n            pop(staticcall(gas(), 3, ptr, 0x20, ptr, 0x20)) // rmd160\n            digest := mload(add(ptr, 0xc)) // return value is 0-prefixed.\n        }\n    }\n\n    /**\n     * @notice          Implements bitcoin's hash256 (double sha2)\n     * @param memView   A view of the preimage\n     * @return          digest - the Digest\n     */\n    function hash256(bytes29 memView) internal view returns (bytes32 digest) {\n        uint256 _loc = loc(memView);\n        uint256 _len = len(memView);\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            let ptr := mload(0x40)\n            pop(staticcall(gas(), 2, _loc, _len, ptr, 0x20)) // sha2 #1\n            pop(staticcall(gas(), 2, ptr, 0x20, ptr, 0x20)) // sha2 #2\n            digest := mload(ptr)\n        }\n    }\n\n    /**\n     * @notice          Return true if the underlying memory is equal. Else false.\n     * @param left      The first view\n     * @param right     The second view\n     * @return          bool - True if the underlying memory is equal\n     */\n    function untypedEqual(bytes29 left, bytes29 right) internal pure returns (bool) {\n        return (loc(left) == loc(right) && len(left) == len(right)) || keccak(left) == keccak(right);\n    }\n\n    /**\n     * @notice          Return false if the underlying memory is equal. Else true.\n     * @param left      The first view\n     * @param right     The second view\n     * @return          bool - False if the underlying memory is equal\n     */\n    function untypedNotEqual(bytes29 left, bytes29 right) internal pure returns (bool) {\n        return !untypedEqual(left, right);\n    }\n\n    /**\n     * @notice          Compares type equality.\n     * @dev             Shortcuts if the pointers are identical, otherwise compares type and digest.\n     * @param left      The first view\n     * @param right     The second view\n     * @return          bool - True if the types are the same\n     */\n    function equal(bytes29 left, bytes29 right) internal pure returns (bool) {\n        return left == right || (typeOf(left) == typeOf(right) && keccak(left) == keccak(right));\n    }\n\n    /**\n     * @notice          Compares type inequality.\n     * @dev             Shortcuts if the pointers are identical, otherwise compares type and digest.\n     * @param left      The first view\n     * @param right     The second view\n     * @return          bool - True if the types are not the same\n     */\n    function notEqual(bytes29 left, bytes29 right) internal pure returns (bool) {\n        return !equal(left, right);\n    }\n\n    /**\n     * @notice          Copy the view to a location, return an unsafe memory reference\n     * @dev             Super Dangerous direct memory access.\n     *\n     *                  This reference can be overwritten if anything else modifies memory (!!!).\n     *                  As such it MUST be consumed IMMEDIATELY.\n     *                  This function is private to prevent unsafe usage by callers.\n     * @param memView   The view\n     * @param _newLoc   The new location\n     * @return          written - the unsafe memory reference\n     */\n    function unsafeCopyTo(bytes29 memView, uint256 _newLoc) private view returns (bytes29 written) {\n        require(notNull(memView), \"TypedMemView/copyTo - Null pointer deref\");\n        require(isValid(memView), \"TypedMemView/copyTo - Invalid pointer deref\");\n        uint256 _len = len(memView);\n        uint256 _oldLoc = loc(memView);\n\n        uint256 ptr;\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            ptr := mload(0x40)\n            // revert if we're writing in occupied memory\n            if gt(ptr, _newLoc) {\n                revert(0x60, 0x20) // empty revert message\n            }\n\n            // use the identity precompile to copy\n            // guaranteed not to fail, so pop the success\n            pop(staticcall(gas(), 4, _oldLoc, _len, _newLoc, _len))\n        }\n\n        written = unsafeBuildUnchecked(typeOf(memView), _newLoc, _len);\n    }\n\n    /**\n     * @notice          Copies the referenced memory to a new loc in memory, returning a `bytes` pointing to\n     *                  the new memory\n     * @dev             Shortcuts if the pointers are identical, otherwise compares type and digest.\n     * @param memView   The view\n     * @return          ret - The view pointing to the new memory\n     */\n    function clone(bytes29 memView) internal view returns (bytes memory ret) {\n        uint256 ptr;\n        uint256 _len = len(memView);\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            ptr := mload(0x40) // load unused memory pointer\n            ret := ptr\n        }\n        unsafeCopyTo(memView, ptr + 0x20);\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            mstore(0x40, add(add(ptr, _len), 0x20)) // write new unused pointer\n            mstore(ptr, _len) // write len of new array (in bytes)\n        }\n    }\n\n    /**\n     * @notice          Join the views in memory, return an unsafe reference to the memory.\n     * @dev             Super Dangerous direct memory access.\n     *\n     *                  This reference can be overwritten if anything else modifies memory (!!!).\n     *                  As such it MUST be consumed IMMEDIATELY.\n     *                  This function is private to prevent unsafe usage by callers.\n     * @param memViews  The views\n     * @return          unsafeView - The conjoined view pointing to the new memory\n     */\n    function unsafeJoin(bytes29[] memory memViews, uint256 _location) private view returns (bytes29 unsafeView) {\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            let ptr := mload(0x40)\n            // revert if we're writing in occupied memory\n            if gt(ptr, _location) {\n                revert(0x60, 0x20) // empty revert message\n            }\n        }\n\n        uint256 _offset = 0;\n        for (uint256 i = 0; i < memViews.length; i ++) {\n            bytes29 memView = memViews[i];\n            unsafeCopyTo(memView, _location + _offset);\n            _offset += len(memView);\n        }\n        unsafeView = unsafeBuildUnchecked(0, _location, _offset);\n    }\n\n    /**\n     * @notice          Produce the keccak256 digest of the concatenated contents of multiple views.\n     * @param memViews  The views\n     * @return          bytes32 - The keccak256 digest\n     */\n    function joinKeccak(bytes29[] memory memViews) internal view returns (bytes32) {\n        uint256 ptr;\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            ptr := mload(0x40) // load unused memory pointer\n        }\n        return keccak(unsafeJoin(memViews, ptr));\n    }\n\n    /**\n     * @notice          Produce the sha256 digest of the concatenated contents of multiple views.\n     * @param memViews  The views\n     * @return          bytes32 - The sha256 digest\n     */\n    function joinSha2(bytes29[] memory memViews) internal view returns (bytes32) {\n        uint256 ptr;\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            ptr := mload(0x40) // load unused memory pointer\n        }\n        return sha2(unsafeJoin(memViews, ptr));\n    }\n\n    /**\n     * @notice          copies all views, joins them into a new bytearray.\n     * @param memViews  The views\n     * @return          ret - The new byte array\n     */\n    function join(bytes29[] memory memViews) internal view returns (bytes memory ret) {\n        uint256 ptr;\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            ptr := mload(0x40) // load unused memory pointer\n        }\n\n        bytes29 _newView = unsafeJoin(memViews, ptr + 0x20);\n        uint256 _written = len(_newView);\n        uint256 _footprint = footprint(_newView);\n\n        assembly {\n            // solium-disable-previous-line security/no-inline-assembly\n            // store the legnth\n            mstore(ptr, _written)\n            // new pointer is old + 0x20 + the footprint of the body\n            mstore(0x40, add(add(ptr, _footprint), 0x20))\n            ret := ptr\n        }\n    }\n}\n"
    },
    "@summa-tx/memview-sol/contracts/SafeMath.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity >=0.5.10;\n\n/*\nThe MIT License (MIT)\n\nCopyright (c) 2016 Smart Contract Solutions, Inc.\n\nPermission is hereby granted, free of charge, to any person obtaining\na copy of this software and associated documentation files (the\n\"Software\"), to deal in the Software without restriction, including\nwithout limitation the rights to use, copy, modify, merge, publish,\ndistribute, sublicense, and/or sell copies of the Software, and to\npermit persons to whom the Software is furnished to do so, subject to\nthe following conditions:\n\nThe above copyright notice and this permission notice shall be included\nin all copies or substantial portions of the Software.\n\nTHE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS\nOR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF\nMERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.\nIN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY\nCLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,\nTORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE\nSOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.\n*/\n\n\n/**\n * @title SafeMath\n * @dev Math operations with safety checks that throw on error\n */\nlibrary SafeMath {\n\n    /**\n     * @dev Multiplies two numbers, throws on overflow.\n     */\n    function mul(uint256 _a, uint256 _b) internal pure returns (uint256 c) {\n        // Gas optimization: this is cheaper than asserting 'a' not being zero, but the\n        // benefit is lost if 'b' is also tested.\n        // See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522\n        if (_a == 0) {\n            return 0;\n        }\n\n        c = _a * _b;\n        require(c / _a == _b, \"Overflow during multiplication.\");\n        return c;\n    }\n\n    /**\n     * @dev Integer division of two numbers, truncating the quotient.\n     */\n    function div(uint256 _a, uint256 _b) internal pure returns (uint256) {\n        // assert(_b > 0); // Solidity automatically throws when dividing by 0\n        // uint256 c = _a / _b;\n        // assert(_a == _b * c + _a % _b); // There is no case in which this doesn't hold\n        return _a / _b;\n    }\n\n    /**\n     * @dev Subtracts two numbers, throws on overflow (i.e. if subtrahend is greater than minuend).\n     */\n    function sub(uint256 _a, uint256 _b) internal pure returns (uint256) {\n        require(_b <= _a, \"Underflow during subtraction.\");\n        return _a - _b;\n    }\n\n    /**\n     * @dev Adds two numbers, throws on overflow.\n     */\n    function add(uint256 _a, uint256 _b) internal pure returns (uint256 c) {\n        c = _a + _b;\n        require(c >= _a, \"Overflow during addition.\");\n        return c;\n    }\n}\n"
    },
    "@openzeppelin/contracts-upgradeable/utils/AddressUpgradeable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity >=0.6.2 <0.8.0;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary AddressUpgradeable {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * [IMPORTANT]\n     * ====\n     * It is unsafe to assume that an address for which this function returns\n     * false is an externally-owned account (EOA) and not a contract.\n     *\n     * Among others, `isContract` will return false for the following\n     * types of addresses:\n     *\n     *  - an externally-owned account\n     *  - a contract in construction\n     *  - an address where a contract will be created\n     *  - an address where a contract lived, but was destroyed\n     * ====\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies on extcodesize, which returns 0 for contracts in\n        // construction, since the code is only stored at the end of the\n        // constructor execution.\n\n        uint256 size;\n        // solhint-disable-next-line no-inline-assembly\n        assembly { size := extcodesize(account) }\n        return size > 0;\n    }\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        require(address(this).balance >= amount, \"Address: insufficient balance\");\n\n        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value\n        (bool success, ) = recipient.call{ value: amount }(\"\");\n        require(success, \"Address: unable to send value, recipient may have reverted\");\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain`call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason, it is bubbled up by this\n     * function (like regular Solidity function calls).\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n      return functionCall(target, data, \"Address: low-level call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with\n     * `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, value, \"Address: low-level call with value failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but\n     * with `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value, string memory errorMessage) internal returns (bytes memory) {\n        require(address(this).balance >= value, \"Address: insufficient balance for call\");\n        require(isContract(target), \"Address: call to non-contract\");\n\n        // solhint-disable-next-line avoid-low-level-calls\n        (bool success, bytes memory returndata) = target.call{ value: value }(data);\n        return _verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {\n        return functionStaticCall(target, data, \"Address: low-level static call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data, string memory errorMessage) internal view returns (bytes memory) {\n        require(isContract(target), \"Address: static call to non-contract\");\n\n        // solhint-disable-next-line avoid-low-level-calls\n        (bool success, bytes memory returndata) = target.staticcall(data);\n        return _verifyCallResult(success, returndata, errorMessage);\n    }\n\n    function _verifyCallResult(bool success, bytes memory returndata, string memory errorMessage) private pure returns(bytes memory) {\n        if (success) {\n            return returndata;\n        } else {\n            // Look for revert reason and bubble it up if present\n            if (returndata.length > 0) {\n                // The easiest way to bubble the revert reason is using memory via assembly\n\n                // solhint-disable-next-line no-inline-assembly\n                assembly {\n                    let returndata_size := mload(returndata)\n                    revert(add(32, returndata), returndata_size)\n                }\n            } else {\n                revert(errorMessage);\n            }\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts-upgradeable/utils/ContextUpgradeable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity >=0.6.0 <0.8.0;\nimport \"../proxy/Initializable.sol\";\n\n/*\n * @dev Provides information about the current execution context, including the\n * sender of the transaction and its data. While these are generally available\n * via msg.sender and msg.data, they should not be accessed in such a direct\n * manner, since when dealing with GSN meta-transactions the account sending and\n * paying for execution may not be the actual sender (as far as an application\n * is concerned).\n *\n * This contract is only required for intermediate, library-like contracts.\n */\nabstract contract ContextUpgradeable is Initializable {\n    function __Context_init() internal initializer {\n        __Context_init_unchained();\n    }\n\n    function __Context_init_unchained() internal initializer {\n    }\n    function _msgSender() internal view virtual returns (address payable) {\n        return msg.sender;\n    }\n\n    function _msgData() internal view virtual returns (bytes memory) {\n        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691\n        return msg.data;\n    }\n    uint256[50] private __gap;\n}\n"
    },
    "@openzeppelin/contracts/utils/Context.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity >=0.6.0 <0.8.0;\n\n/*\n * @dev Provides information about the current execution context, including the\n * sender of the transaction and its data. While these are generally available\n * via msg.sender and msg.data, they should not be accessed in such a direct\n * manner, since when dealing with GSN meta-transactions the account sending and\n * paying for execution may not be the actual sender (as far as an application\n * is concerned).\n *\n * This contract is only required for intermediate, library-like contracts.\n */\nabstract contract Context {\n    function _msgSender() internal view virtual returns (address payable) {\n        return msg.sender;\n    }\n\n    function _msgData() internal view virtual returns (bytes memory) {\n        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691\n        return msg.data;\n    }\n}\n"
    }
  },
  "settings": {
    "optimizer": {
      "enabled": true,
      "runs": 999999
    },
    "outputSelection": {
      "*": {
        "*": [
          "evm.bytecode",
          "evm.deployedBytecode",
          "devdoc",
          "userdoc",
          "metadata",
          "abi"
        ]
      }
    },
    "libraries": {}
  }
}}